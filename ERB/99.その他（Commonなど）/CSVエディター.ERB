@CSV_EDITOR
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 표시数
#DIM 素質の数値표시
#DIM 확장표시

CUSTOMDRAWLINE —
PRINTL □현재작성되어있는 캐릭터
FOR LCOUNT, 0,CHARANUM
	PRINTFORML 　[{LCOUNT}] %NAME:LCOUNT%
NEXT
CUSTOMDRAWLINE —
PRINTPLAIN 　SELECT CHARA　
PRINT [-1] 새로운 캐릭터를 작성한다
PRINT [-2] 프로젝트를 저장
PRINT [-3] 타이틀로 돌아간다
;LOADTEXT
DO
	INPUT
	SELECTCASE RESULT
		CASE 0 TO CHARANUM
			TARGET = RESULT
			BREAK
		CASE -1
			ADDVOIDCHARA
			TARGET = CHARANUM-1
			BREAK
		CASE -2
			SAVEDATA 0, ""
			PRINTW 저장했습니다
			RESTART
		CASE -3
			RETURN 0
		CASEELSE
			CONTINUE
	ENDSELECT
LOOP 1

$RESTART
PRINTL 
PRINTL 
CUSTOMDRAWLINE —
PRINTL □ 기본정보
CUSTOMDRAWLINE —
PRINTL 
PRINTBUTTON @"[캐릭터번호] {NO:TARGET} ", "캐릭터番号"
PRINTL 
PRINTBUTTON @"[이름] %NAME:TARGET% ", "名前"
PRINTBUTTON @"[호칭] %CALLNAME:TARGET% ", "呼び名"
;PRINTBUTTON @"[별명] %NICKNAME:TARGET% ", "あだ名"
;PRINTBUTTON @"[주인의 호칭] %MASTERNAME:TARGET%", "主人の呼び方"
PRINTL 
CUSTOMDRAWLINE —

PRINTL □ 기초(BASE)
CUSTOMDRAWLINE —
PRINTL 

FOR LCOUNT, 0, VARSIZE("MAXBASE")
	SIF BASENAME:LCOUNT == ""
		CONTINUE
	SIF LCOUNT == 2
		MAXBASE:2 = 1500
	SIF LCOUNT == 10
		MAXBASE:10 = 1500
	SIF LCOUNT == 11
		MAXBASE:11 = 1000
	SIF LCOUNT == 12
		MAXBASE:12 = 1000
	SIF LCOUNT == 13
		MAXBASE:13 = 1000
	PRINTBUTTONLC @"[%BASENAME:LCOUNT%]:{MAXBASE:LCOUNT} ", @"BASE_%BASENAME:LCOUNT%"
NEXT
SIF !LINEISEMPTY()
	PRINTL 
CUSTOMDRAWLINE —

PRINTL □ 경험(EXP)
CUSTOMDRAWLINE —

FOR LCOUNT, 0, VARSIZE("EXP")
	SIF EXPNAME:LCOUNT == ""
		CONTINUE
	PRINTBUTTONLC @"[%EXPNAME:LCOUNT%]:{EXP:LCOUNT}", @"EXP_%EXPNAME:LCOUNT%"
NEXT
SIF !LINEISEMPTY()
	PRINTL 
CUSTOMDRAWLINE —

PRINTL □ 능력(ABL)
CUSTOMDRAWLINE —
PRINTL 

FOR LCOUNT, 0, VARSIZE("ABL")
	SIF ABLNAME:LCOUNT == ""
		CONTINUE
	PRINTBUTTONLC @"[%ABLNAME:LCOUNT%]:{ABL:LCOUNT}", @"ABL_%ABLNAME:LCOUNT%"
NEXT
SIF !LINEISEMPTY()
	PRINTL 
CUSTOMDRAWLINE —

PRINTPLAIN □ 소질(TALENT) 
PRINTL 
CUSTOMDRAWLINE —
IF 素質の数値표시
	FOR LCOUNT, 0, VARSIZE("TALENT")
		SIF TALENTNAME:LCOUNT == "" || TALENT:LCOUNT == 0
			CONTINUE
		PRINTBUTTONLC @"[%TALENTNAME:LCOUNT%]:{TALENT:LCOUNT}", @"TALENT_%TALENTNAME:LCOUNT%"
	NEXT
ELSE
	SETCOLOR C_AQUA
	PRINTBUTTON "[변경한다]", "素質を변경する"
	RESETCOLOR
	PRINTL 
	PRINT 　
	표시数 = 0
	FOR LCOUNT, 0, VARSIZE("TALENT")
		SIF TALENT:LCOUNT == 0
			CONTINUE
		SIF TALENTNAME:LCOUNT == ""
			CONTINUE
		IF 표시数%8 == 0 && 표시数 != 0
			PRINTL 
			PRINT 　
		ENDIF
		PRINTFORM [%GET_TALENTNAME(LCOUNT,TALENT:TARGET:LCOUNT)%]
		표시数++
	NEXT
ENDIF
SIF !LINEISEMPTY()
	PRINTL 
CUSTOMDRAWLINE —

IF 확장표시
	PRINTL □ フラグ(CFLAG)
	FOR LCOUNT, 0, VARSIZE("CFLAG")
		SIF CFLAGNAME:LCOUNT == ""
			CONTINUE
		PRINTBUTTONLC @"[%CFLAGNAME:LCOUNT%]:{CFLAG:LCOUNT}", @"CFLAG_%CFLAGNAME:LCOUNT%"
	NEXT
	SIF !LINEISEMPTY()
		PRINTL 
	CUSTOMDRAWLINE —
ENDIF

IF 확장표시
	PRINTL □ 각인(MARK)
	FOR LCOUNT, 0, VARSIZE("MARK")
		SIF MARKNAME:LCOUNT == ""
			CONTINUE
		PRINTBUTTONLC @"[%MARKNAME:LCOUNT%]:{MARK:LCOUNT}", @"MARK_%MARKNAME:LCOUNT%"
	NEXT
	SIF !LINEISEMPTY()
		PRINTL 
	CUSTOMDRAWLINE —
ENDIF

IF 확장표시
	PRINTL □ 보석(JUEL)
	FOR LCOUNT, 0, VARSIZE("PALAM")
		SIF PALAMNAME:LCOUNT == ""
			CONTINUE
		PRINTBUTTONLC @"[%PALAMNAME:LCOUNT%]:{JUEL:LCOUNT}", @"JUEL_%PALAMNAME:LCOUNT%"
	NEXT
	SIF !LINEISEMPTY()
		PRINTL 
	CUSTOMDRAWLINE —
ENDIF

IF 확장표시
	PRINTL □ 장착물(EQUIP)
	FOR LCOUNT, 0, VARSIZE("EQUIP")
		SIF EQUIPNAME:LCOUNT == ""
			CONTINUE
		PRINTBUTTONLC @"[%EQUIPNAME:LCOUNT%]:{EQUIP:LCOUNT}", @"EQUIP_%EQUIPNAME:LCOUNT%"
	NEXT
	SIF !LINEISEMPTY()
		PRINTL 
	CUSTOMDRAWLINE —
ENDIF

IF 확장표시
	PRINTL □ CSTR
	FOR LCOUNT, 0, VARSIZE("CSTR")
		SIF CSTRNAME:LCOUNT == ""
			CONTINUE
		PRINTBUTTON @"　%CSTRNAME:LCOUNT%:%CSTR:LCOUNT%", @"CSTR_%CSTRNAME:LCOUNT%"
		PRINTL 
	NEXT
	CUSTOMDRAWLINE —
ENDIF

PRINTBUTTON "　[0] CSV를 출력한다 ", "0"
PRINTBUTTON "[1] 캐릭터 선택으로 돌아간다 ", "1"
PRINTBUTTON "[2] 기존의 CSV를 불러온다 ", "2"
PRINTBUTTON "[3] 소질을 수치로 표시한다", "3"
PRINTBUTTON "[4] 표시범위를 확장한다", "4"
PRINTBUTTON "[5] 프로젝트를 초기화한다", "5"

$LOOP
INPUTS
CLEARLINE 1
SELECTCASE RESULTS
	CASE "0"
		PRINTL 현재 작업상황의 CSV를 로그로 출력합니다。괜찮겠습니까？
		PRINTL [0] - 예
		PRINTL [1] - 아니오
		INPUT
		DO
			SELECTCASE RESULT
				CASE 0
					CLEARLINE LINECOUNT
					PRINTFORML 番号,{NO:TARGET}
					PRINTFORML 名前,%NAME:TARGET%
					IF CALLNAME:TARGET == ""
						PRINTFORML 呼び名,%NAME:TARGET%
					ELSE
						PRINTFORML 呼び名,%CALLNAME:TARGET%
					ENDIF
					SIF NICKNAME:TARGET != ""
						PRINTFORML あだ名,%NICKNAME:TARGET%
					SIF MASTERNAME:TARGET != ""
						PRINTFORML 主人の呼び方,%MASTERNAME:TARGET%
					PRINTL 
					PRINTFORML ;BASE
					FOR LCOUNT, 0, VARSIZE("MAXBASE")
						SIF MAXBASE:LCOUNT != 0
							PRINTFORML 基礎,{LCOUNT},{MAXBASE:LCOUNT};%BASENAME:LCOUNT%
					NEXT
					PRINTFORML ;Talent
					FOR LCOUNT, 0, VARSIZE("TALENT")
						IF TALENT:LCOUNT != 0
							IF 素質の数値표시
								PRINTFORML 素質,{LCOUNT},{TALENT:LCOUNT};%TALENTNAME:LCOUNT%
							ELSE
								PRINTFORML 素質,%TALENTNAME:LCOUNT%
							ENDIF
						ENDIF
					NEXT
					PRINTFORML ;ABL
					FOR LCOUNT, 0, VARSIZE("ABL")
						SIF ABL:LCOUNT != 0
							PRINTFORML 能力,{LCOUNT},{ABL:LCOUNT};%ABLNAME:LCOUNT%
					NEXT
					PRINTFORML ;EXP
					FOR LCOUNT, 0, VARSIZE("EXP")
						SIF EXP:LCOUNT != 0
							PRINTFORML 経験,{LCOUNT},{EXP:LCOUNT};%EXPNAME:LCOUNT%
					NEXT
					FOR LCOUNT, 0, VARSIZE("CFLAG")
						SIF CFLAG:LCOUNT != 0
							PRINTFORML フラグ,%CFLAGNAME:LCOUNT%,{CFLAG:LCOUNT}
					NEXT
					FOR LCOUNT, 0, VARSIZE("MARK")
						SIF MARK:LCOUNT != 0
							PRINTFORML 刻印,%MARKNAME:LCOUNT%,{MARK:LCOUNT}
					NEXT
					FOR LCOUNT, 0, VARSIZE("PALAM")
						SIF CFLAG:LCOUNT != 0
							PRINTFORML 珠,%PALAMNAME:LCOUNT%,{JUEL:LCOUNT}
					NEXT
					FOR LCOUNT, 0, VARSIZE("EQUIP")
						SIF EQUIP:LCOUNT != 0
							PRINTFORML 装着物,%EQUIPNAME:LCOUNT%,{EQUIP:LCOUNT}
					NEXT
					PRINTL 					
					FOR LCOUNT, 0, VARSIZE("CSTR")
						SIF CSTR:LCOUNT != ""
							PRINTFORML CSTR,%CSTRNAME:LCOUNT%,%CSTR:LCOUNT%
					NEXT
					PRINTFORML ;FLAG
					PRINTFORML フラグ,7,101;服装
					PRINTFORML フラグ,311,100;시작위치
					OUTPUTLOG @"Chara{NO:TARGET} %NAME:TARGET%.csv"
					PRINTW 로그로 출력되었습니다。CSV로서 이용하는 방법은 동봉된 read me를 확인해주세요
					RESTART
				CASE 1
					GOTO RESTART
			ENDSELECT
		LOOP 1
	CASE "1"
		RESTART
	CASE "2"
		PRINTL 현재 작업상황을 파기하고 기존의 CSV를 불러옵니다。괜찮겠습니까？
		PRINTL [0] - 예
		PRINTL [1] - 아니오
		INPUT
		DO
			SELECTCASE RESULT
				CASE 0
					CALL CHARA_LIST
					IF EXISTCSV(RESULT)
						ADDCHARA RESULT
						SWAPCHARA TARGET,CHARANUM-1
						DELCHARA CHARANUM-1
					ENDIF
					GOTO RESTART
				CASE 1
					GOTO RESTART
			ENDSELECT
		LOOP 1
	CASE "3"
		INVERTBIT 素質の数値표시, 0
	CASE "4"
		INVERTBIT 확장표시, 0
	CASE "5"
		PRINTL 현재의 작업상황을 파기하고CSV를 다시만듭니다。괜찮겠습니까？
		PRINTL [0] - 예
		PRINTL [1] - 아니오
		BINPUT
		IF RESULT == 0
			RESETDATA
			RESTART
		ENDIF
		GOTO RESTART

	CASE "캐릭터番号"
		PRINTL 캐릭터 번호를 입력해주세요
		INPUT
		NO:TARGET = RESULT
		GOTO RESTART
	CASE "名前"
		PRINTL 풀네임을 입력해주세요
		INPUTS
		NAME:TARGET = %RESULTS%
		GOTO RESTART
	CASE "呼び名"
		PRINTL 호칭을 입력해주세요
		INPUTS
		CALLNAME:TARGET = %RESULTS%
		GOTO RESTART
	CASE "あだ名"
		PRINTL 별명을 입력해주세요
		INPUTS
		NICKNAME:TARGET = %RESULTS%
		GOTO RESTART
	CASE "主人の呼び方"
		PRINTL 주인의 호칭을 입력해주세요
		INPUTS
		MASTERNAME:TARGET = %RESULTS%
		GOTO RESTART
	CASE "素質を변경する"
		$TALENT_RESTART
		CUSTOMDRAWLINE —
		FOR LCOUNT, 0, VARSIZE("TALENT")
			SIF TALENTNAME:LCOUNT == ""
				CONTINUE
			PRINTFORMLC [{LCOUNT}] %TALENTNAME:LCOUNT% \@ TALENT:LCOUNT ? ◯ # × \@
		NEXT
		SIF !LINEISEMPTY()
			PRINTL 
		CUSTOMDRAWLINE —
		PRINTL [-1] - 돌아간다
		INPUT
		IF RESULT == -1
			GOTO RESTART
		ELSE
			INVERTBIT TALENT:RESULT, 0
			GOTO TALENT_RESTART
		ENDIF
	CASE "相性を변경する"
		PRINTL 조수의 번호를 입력해주세요
		PRINT [10000] 일람표시
		PRINTL [10001] 전부 기정치로 되돌린다
		INPUT
		IF RESULT == 10000
			PRINTL □ 일람표시
			CALL CHARA_LIST
			SIF RESULT == -1
				GOTO RESTART
		ELSEIF RESULT == 10001
			VARSET RELATION:TARGET:0, 100
			GOTO RESTART
		ELSEIF !EXISTCSV(RESULT)
			PRINTFORMW {RESULT}에는 캐릭터가 등록되어있지 않습니다
			GOTO RESTART
		ENDIF
		LOCAL = RESULT
		PRINTFORML %CSVNAME(LOCAL)%와의 상성치를 입력해주세요
		INPUT
		RELATION:LOCAL = RESULT
		GOTO RESTART
	CASEELSE
		SPLIT RESULTS, "_", LOCALS
		SELECTCASE LOCALS
			CASE "BASE"
				PRINTFORML BASE:%LOCALS:1%의 수치를 입력해주세요
				INPUT
				MAXBASE:(LOCALS:1) = RESULT
			CASE "EXP"
				PRINTFORML EXP:%LOCALS:1%의 수치를 입력해주세요
				INPUT
				EXP:(LOCALS:1) = RESULT
			CASE "ABL"
				PRINTFORML ABL:%LOCALS:1%의 수치를 입력해주세요
				INPUT
				ABL:(LOCALS:1) = RESULT
			CASE "TALENT"
				PRINTFORML TALENT:%LOCALS:1%의 수치를 입력해주세요
				INPUT
				TALENT:(LOCALS:1) = RESULT
			CASE "フラグ"
				PRINTFORML CFLAG:%LOCALS:1%의 수치를 입력해주세요
				INPUT
				CFLAG:(LOCALS:1) = RESULT
			CASE "각인"
				PRINTFORML MARK:%LOCALS:1%의 수치를 입력해주세요
				INPUT
				MARK:(LOCALS:1) = RESULT
			CASE "珠"
				PRINTFORML JUEL:%LOCALS:1%의 수치를 입력해주세요
				INPUT
				JUEL:(LOCALS:1) = RESULT
			CASE "装着物"
				PRINTFORML EQUIP:%LOCALS:1%의 수치를 입력해주세요
				INPUT
				EQUIP:(LOCALS:1) = RESULT
			CASE "CSTR"
				PRINTFORML CSTR:%LOCALS:1%의 내용을 입력해주세요
				INPUTS
				CSTR:(LOCALS:1) = %RESULTS%
		ENDSELECT
ENDSELECT

GOTO RESTART

@CHARA_LIST2
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC 표시数
#DIM DYNAMIC 処理数
#DIM DYNAMIC ページ数

$INPUT_LOOP

FOR LCOUNT, 0, 10000
	SIF !EXISTCSV(LCOUNT)
		CONTINUE

	IF 표시数 >= 30
		次ページあり = 1
		BREAK
	ENDIF

	処理数++
	SIF 処理数 <= (ページ数*30)
		CONTINUE

	SIF 표시数%3 == 0 && 표시数 != 0
		PRINTL 

	LOCALS = [{LCOUNT, 4}] %CSVCALLNAME(LCOUNT)%
	PRINTFORM %LOCALS , 38 , LEFT%
	
	표시数++
NEXT

SIF !LINEISEMPTY()
	PRINTL 

CUSTOMDRAWLINE —
PRINTPLAINFORM 　PAGE.{ページ数+1}　
IF TFLAG:ページ数 > 0
	PRINTLC [10001] 이전 페이지
ELSE
	PRINTLC  
ENDIF
PRINT [10000] 취소
SIF 次ページあり
	PRINTC [10009] 다음 페이지
PRINTL 

DO
	INPUT
	SELECTCASE RESULT
		CASE 10000
			RETURN -1
		CASE 10001
			SIF ページ数
				ページ数--
			GOTO INPUT_LOOP
		CASE 10009
			SIF 次ページあり
				ページ数++
			GOTO INPUT_LOOP
		CASEELSE
			IF EXISTCSV(RESULT)
				CUSTOMDRAWLINE —
				RETURN RESULT
			ENDIF
	ENDSELECT
LOOP 1


@CHARA_LIST
#DIM 소환リスト_캐릭터
#DIM 소환リスト_現체력
#DIM 소환_ページ送り
#DIM 캐릭터_ID
#DIM DYNAMIC カウンタ
#DIM サブカウンタ

#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 次ページあり
#DIM DYNAMIC 표시数
#DIM DYNAMIC 処理数
#DIM DYNAMIC ページ数
#DIM DYNAMIC ページ枚数
#DIM DYNAMIC 現ページ

ページ枚数 ++

FOR 캐릭터_ID,0,5000
	SIF !EXISTCSV(캐릭터_ID)
		CONTINUE
	;リストに格納する番号が캐릭터最大値以上の時、終了
	IF カウンタ == 5000
		BREAK
	ENDIF
	
	;リストカウントUP＋リスト内に캐릭터を格納
	カウンタ ++
	サブカウンタ ++
	リスト内표시:カウンタ = 캐릭터_ID
	PRINTFORML {リスト内표시:カウンタ}
	IF サブカウンタ == 30
		ページ枚数 ++
		サブカウンタ = 0
	ENDIF
NEXT
ページ数 = 30


$INPUT_LOOP
CUSTOMDRAWLINE —
PRINTL 캐릭터 선택
CUSTOMDRAWLINE —
PRINTL 
FOR カウンタ, ページ数 - 29, 5000
	SIF カウンタ >= ページ数 + 1
		CONTINUE
	IF カウンタ != 1
		SIF (リスト内표시:カウンタ) == 0
			CONTINUE
	ENDIF
	LOCALS = [{リスト内표시:カウンタ, 4}] %CSVCALLNAME(リスト内표시:カウンタ)%
	PRINTFORM %LOCALS , 38 , LEFT%
NEXT
PRINTL


CUSTOMDRAWLINE —
PRINTPLAINFORM 　PAGE.{ページ数+1}　
IF ページ数 > 30
	PRINTLC [10001] 이전 페이지
ELSE
	PRINTLC  
ENDIF
PRINT [10000] 취소
SIF 現ページ != ページ枚数 - 1
	PRINTC [10009] 다음 페이지
PRINTL 

BINPUT
IF RESULT == 10000
	RETURN -1
ELSEIF RESULT == 10001
	SIF ページ数
		ページ数 -= 30
		現ページ --
	GOTO INPUT_LOOP
ELSEIF RESULT == 10009
		ページ数 += 30
		現ページ ++
	GOTO INPUT_LOOP
ELSE
	RETURN RESULT
ENDIF
