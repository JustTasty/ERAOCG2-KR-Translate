@CARDNAME_79780,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 아스모데우스의 악령
	CASE "略称"
		RESULTS = 악령아스모데우스
	CASE "カテゴリ"
		RESULTS:0 = 악령
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_79780(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 2
	CASE "攻撃力"
		RETURN 900
	CASE "守備力"
		RETURN 900
	CASE "성별"
		RETURN 雄性
	CASE "튜너"
		RETURN 1
	CASE "分割"
		RETURN 효과なし
ENDSELECT

RETURN 0

@CARD_EXPLANATION_79780(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드의 ①의 효과는 1턴에 1번만 사용할 수 있다。
CALL CARD_ABLE(1,마함起動,손패起動)
PRINTL ①：발동한 이카드를 몬스터로 몬스터 존에 수비표시로 특수소환하고、상대는 ８００ＬＰ회복한다。
PRINTL （악마족 빛속성 레벨２ 공격력８００ 수비력９００ 튜너 남성）
PRINTL 그 후、패에서 마법사족 빛속성 몬스터 1장을 덱 맨 아래로 되돌리고、 자신은 2장 드로우 할 수 있다。
PRINTL 이 턴、자신은 빛 속성 몬스터만 EX덱에서 특수 소환할 수 있다。
CALL CARD_ABLE(2,지속효과)
PRINTL ②：이 카드가 마법＆함정 존에 앞면 표시로 존재하는 한、
PRINTL 자신 필드의「카타르시스터」Ｓ몬스터는 레벨 ６이 되고、상대 몬스터의 효과를 받지 않는다。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_79780(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,79780)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,79780)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	CALL CARD_SS_CHECK(듀얼리스트,79780,ゾーン)
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_79780(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 조교대상

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%



IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79780,1)
	IF RESULT == 0
		CALL FULLRESET
		IF 종류 == 마함起動
			CALL CARD_SP_SUMMON(듀얼리스트,"마함",場所,"守備표시")
		ELSEIF 종류 == 손패起動
			CALL CARD_SP_SUMMON(듀얼리스트,"손패",場所,"守備표시")
		ENDIF
		PRINTFORML 
		PRINTFORML 카드를 발동시키자, 장소에 상반신만 반투명한 악마가 감돌기 시작했다.
		PRINTFORMW 당장 날뛰기 시작할 것 같을 정도의 신음 소리를 내고 있다...
		PRINTFORML 
		CALL LIFE_CHANGE(対面者,"増加","효과",800)
		
		IF 듀얼리스트 == "자신"
			CUSTOMDRAWLINE —
			PRINTFORML 【2장 드로우 하기 위해서 덱으로 되돌릴 카드를 선택할 수 있습니다】
			CUSTOMDRAWLINE —
		ENDIF
		CALL FULLRESET
		효과条件종족 = 마법사족
		효과条件속성 = 빛속성
		효과条件任意 = 1
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"손패",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL FULLRESET
			CALL CARD_ADD(듀얼리스트, "손패", "덱", 選択カード)
			CALL CARDDRAW_BASIC(듀얼리스트,2)
		ENDIF
		CALL FULLRESET
		EX소환可能속성 = 빛속성
		CAll CARD_EX_RESTRICTION(듀얼리스트)
	ENDIF
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	효과条件종류 = 싱크로
	효과条件カテゴリ = 카타르시스터
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"モ耐")
	;CALL FULLRESET
	;효과条件종류 = 싱크로
	;효과条件カテゴリ = 카타르시스터
	;CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"一星",6)
	CALL FULLRESET
	FOR カウンタ,0,5
		SIF GETVAR(@"%듀얼리스트%_필드:{カウンタ}") == -1
			CONTINUE
		TRYCALLFORM CARDNAME_{GETVAR(@"%듀얼리스트%_필드:{カウンタ}")},"カテゴリ"
		SIF !MATCH(RESULTS, "카타르시스터")
			CONTINUE
		TRYCALLFORM CARD_{GETVAR(@"%듀얼리스트%_필드:{カウンタ}")},"종류"
		SIF RESULT != 싱크로
			CONTINUE
		CALL SET_FIELD_INFO(듀얼리스트,"필드",カウンタ,"一星",6,1)
	NEXT
ENDIF



