@CARDNAME_62120,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 미캉코의 화총무
	CASE "略称"
		RESULTS = 화총무
	CASE "カテゴリ"
		RESULTS:0 = 미캉코
ENDSELECT


@CARD_62120(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 装備마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_62120(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의  카드는 1턴에 1장밖에 발동할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ①：자신의 패 / 묘지에서 "미캉코" 몬스터 1장을 골라 특수 소환하고,
PRINTL 이 카드를 장착한다.
PRINTL 그 후, 상대 묘지에서 몬스터 1장을 골라 효과를 무효로 하고 상대 필드에 특수 소환할 수 있다.
CALLFORM CARD_ABLE(2,지속효과)
PRINTL ②：장착 몬스터는 효과로는 파괴되지 않는다.

@CARDCAN_62120(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,62120)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,62120)
		RETURN 0
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 미캉코
	효과条件종류 = 의식以外
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_62120(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 選択カード


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備先 = RESULT	
装備者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,62120,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과範囲손패 = 1
		효과条件カテゴリ = 미캉코
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT
			CALL FULLRESET
			효과範囲필드 = 1
			효과条件固定 = カード소환先
			CALLFORM CARD_EQUIP_ADV(듀얼리스트,場所,ゾーン)

			CALL FULLRESET
			효과範囲상대묘지 = 1
			효과추가종류 = 効追無効
			효과条件任意 = 1
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"",0,1)
		ENDIF
	ENDIF
ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"装備",場所,"破耐")
ENDIF