;-------------------------------------------------
;実績を確認する
;-------------------------------------------------
@TROPHY_LIST
;貯蔵を確認する
#DIMS DYNAMIC 実績名
#DIM DYNAMIC リスト_표시
#DIM DYNAMIC カウンタ,2
#DIM DYNAMIC リストスタート
#DIM DYNAMIC ローカル１
#DIM DYNAMIC 最大値
#DIM DYNAMIC 選択カード,10
#DIM DYNAMIC 一時보존,10
#DIM DYNAMIC 消去カード
#DIM DYNAMIC 選択実績
#DIM DYNAMIC Continue数
#DIM DYNAMIC 実績_リスト,999
#DIM 現在モード
#DIM DYNAMIC 표시モード
#DIM DYNAMIC 実績リスト最大値
#DIM CONST 実績リストサイズ = 15
#DIMS DYNAMIC TROPHY_STR, 100000

표시モード = 0
;現在モード = 1


$INPUT_FILTER
CALL TROPHY_SORT(実績_リスト,現在モード,표시モード)
実績リスト最大値 = RESULT
$INPUT_LOOP

CUSTOMDRAWLINE ―
PRINTL
PRINT ┏━━━━━┓
PRINT ┏━━━━━┓
PRINT ┏━━━━━┓
PRINT ┏━━━━━┓
PRINT ┏━━━━━┓
;PRINTFORM %"",14%
PRINT ┏━━━━━┓
PRINTL
PRINT ┃
PRINTBUTTON "모두표시　", 8000
PRINT ┃
PRINT ┃
PRINTBUTTON "달성가능　", 8001
PRINT ┃
PRINT ┃
PRINTBUTTON "　달성됨　", 8002
PRINT ┃
PRINT ┃
PRINTBUTTON "　　　　　", 8003
PRINT ┃
PRINT ┃
PRINTBUTTON "　　　　　", 8004
PRINT ┃
;PRINTFORM %"",14%
PRINT ┃
IF 표시モード == 0
	SETCOLOR C_GREEN
	PRINTBUTTON "모두표시　", 8010
ELSEIF 표시モード == 1
	SETCOLOR C_YELLOW
	PRINTBUTTON "모두표시중", 8010
ENDIF
RESETCOLOR
PRINT ┃
PRINTL
IF 現在モード != 0
	PRINT ┣━━━━━┻
ELSE
	PRINT ┃　　　　　┗
ENDIF

IF 現在モード != 1
	PRINT ┻━━━━━┻
ELSE
	PRINT ┛　　　　　┗
ENDIF

IF 現在モード != 2
	PRINT ┻━━━━━┻
ELSE
	PRINT ┛　　　　　┗
ENDIF

IF 現在モード != 3
	PRINT ┻━━━━━┻
ELSE
	PRINT ┛　　　　　┗
ENDIF

IF 現在モード != 4
	PRINT ┻━━━━━┻
ELSE
	PRINT ┛　　　　　┗
ENDIF
;PRINT ━━━━━━━
PRINT ┻━━━━━╋━┓
PRINTL

PRINT ┃
PRINTFORM %"",80,LEFT%
PRINT ┃　┃
PRINTL
;CUSTOMDRAWLINE ―
FOR カウンタ,0 + リストスタート,実績リストサイズ + リストスタート
	PRINT ┃
	VARSET RESULT
	VARSET RESULTS
	IF 実績_リスト:カウンタ != 0
		VARSET RESULT
		TRYCALLFORM TROPHY_NO_{(実績_リスト:カウンタ)}("条件")
		SIF RESULT == 0
			SETCOLOR C_GRAY
		SIF 実績달성플래그:(実績_リスト:カウンタ) == 2
			SETCOLOR C_YELLOW
		CALLFORM TROPHY_NO_{(実績_リスト:カウンタ)}("名前")
		PRINTFORM [{(カウンタ + 1),3}] - %RESULTS,63,LEFT%
		IF 実績달성플래그:(実績_リスト:カウンタ) == 2
			PRINT  (달성됨)
		ELSE
			TRYCALLFORM TROPHY_NO_{(実績_リスト:カウンタ)}("条件")
			IF RESULT == 1
				PRINT  (달성가능)
			ELSE
				PRINTFORM %"",9,LEFT%
			ENDIF
		ENDIF
		RESETCOLOR
	ELSE
		PRINTFORM %"",80,LEFT%
	ENDIF
	
	PRINTPLAIN ┃
	IF (カウンタ - リストスタート) == 2
		PRINT －
	ELSEIF (カウンタ - リストスタート) == 3
		SIF リストスタート == 0
			SETCOLOR C_GRAY
		PRINTBUTTON "△", 8500
		RESETCOLOR
	ELSEIF (カウンタ - リストスタート) == 4
		SIF リストスタート == 0
			SETCOLOR C_GRAY
		PRINTBUTTON "△", 8500
		RESETCOLOR
	ELSEIF (カウンタ - リストスタート) == 7
		PRINT －

	ELSEIF (カウンタ - リストスタート) == 10
		SIF リストスタート >= 実績リスト最大値 - 実績リストサイズ
			SETCOLOR C_GRAY
		PRINTBUTTON "▽", 8501
		RESETCOLOR
	ELSEIF (カウンタ - リストスタート) == 11
		SIF リストスタート >= 実績リスト最大値 - 実績リストサイズ
			SETCOLOR C_GRAY
		PRINTBUTTON "▽", 8501
		RESETCOLOR
	ELSEIF (カウンタ - リストスタート) == 12
		PRINT －
	ELSE
		PRINT 　
	ENDIF
	PRINTPLAIN ┃
	PRINTL
NEXT
PRINT ┃
PRINTFORM %"",80,LEFT%
PRINT ┃　┃
PRINTL
PRINT ┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┻━┛
PRINTL


PRINTL
CUSTOMDRAWLINE ―
PRINTFORM %"[9999] - 돌아간다",50,LEFT%
;SIF デバッグシステム == 1
SETCOLOR C_BLACK
	PRINTFORM %"[9998] - 실적리스트를 작성",50,LEFT%
RESETCOLOR
PRINTFORML
CUSTOMDRAWLINE ―
BINPUT

IF RESULT == 9999
	RETURN 0
ELSEIF RESULT == 8000
	現在モード = 0
	GOTO INPUT_FILTER
ELSEIF RESULT == 8001
	IF 現在モード == 1
		現在モード = 0
	ELSE
		現在モード = 1
	ENDIF
	GOTO INPUT_FILTER

ELSEIF RESULT == 8002
	IF 現在モード == 2
		現在モード = 0
	ELSE
		現在モード = 2
	ENDIF
	GOTO INPUT_FILTER

ELSEIF RESULT == 8010
	IF 표시モード == 1
		표시モード = 0
	ELSE
		표시モード = 1
	ENDIF
	GOTO INPUT_FILTER

ELSEIF RESULT == 8500
	リストスタート -= 実績リストサイズ
	SIF リストスタート <= 0
		リストスタート = 0
	GOTO INPUT_LOOP
ELSEIF RESULT == 8501
	IF リストスタート < 実績リスト最大値 - 実績リストサイズ
		リストスタート += 実績リストサイズ
	ENDIF
	GOTO INPUT_LOOP
ELSEIF RESULT == 9998
	FOR カウンタ,1,101
		VARSET RESULTS
		VARSET 実績名
		TRYCALLFORM TROPHY_NO_{カウンタ},"名前"
		IF RESULTS != ""
			実績名 = %RESULTS%
		ENDIF
		TROPHY_STR:カウンタ = {カウンタ,5}:　%実績名,60,LEFT%
	NEXT

	SAVETEXT STRJOIN(TROPHY_STR, UNICODE(0xA)), @"実績早見リスト.txt"
	PRINTFORMW 「実績早見リスト.txt」로 보존했습니다
	GOTO INPUT_LOOP
ENDIF

選択実績 = 実績_リスト:(RESULT - 1)


	


CUSTOMDRAWLINE ―
CALLFORM TROPHY_NO_{選択実績}("名前")
PRINTFORML 실적명：%RESULTS%
CUSTOMDRAWLINE ―
CALLFORM TROPHY_NO_{選択実績}("詳細")
CUSTOMDRAWLINE ―

VARSET RESULT
IF 実績달성플래그:選択実績 >= 1
	WAIT
	GOTO INPUT_FILTER
ENDIF


VARSET RESULT
TRYCALLFORM TROPHY_NO_{選択実績}("条件")
IF RESULT == 0
	WAIT
	GOTO INPUT_FILTER
ENDIF


PRINTL 이 실적을 달성하시겠습니까？

PRINTL [0] - 예
PRINTL [1] - 아니오
BINPUT

IF RESULT == 0
	CALLFORM TROPHY_NO_{選択実績}("名前")
	PRINTFORML
	PRINTFORML %RESULTS%를 달성했습니다！
	PRINTFORMW

	CALLFORM TROPHY_NO_{選択実績}("報酬")
	実績달성플래그:選択実績 = 2
	実績_完了횟수:選択実績 += 1	
	
	GOTO INPUT_FILTER
ELSEIF RESULT == 1
	GOTO INPUT_FILTER
ENDIF




@TROPHY_SORT(実績_リスト,現在モード,표시モード)
#DIM DYNAMIC 実績_ID_C
#DIM DYNAMIC カウンタB
#DIM REF 実績_リスト
#DIM REF 現在モード
#DIM REF 표시モード

VARSET 実績_リスト
FOR 実績_ID_C,0,1000
	VARSET RESULT
	VARSET RESULTS
	CALL TROPHY_FILTER(実績_ID_C,現在モード,표시モード)
	IF RESULT == 1
		実績_リスト:カウンタB = 実績_ID_C
		カウンタB ++
	ENDIF
NEXT



RETURN カウンタB

@TROPHY_FILTER(実績_ID_C,現在モード,표시モード)
#DIM DYNAMIC 実績_ID_C
#DIM REF 現在モード
#DIM REF 표시モード

VARSET RESULTS
TRYCALLFORM TROPHY_NO_{実績_ID_C}("名前")
SIF RESULTS == ""
	RETURN 0


IF 現在モード == 1
	TRYCALLFORM TROPHY_NO_{実績_ID_C}("条件")
	SIF RESULT != 1
		RETURN 0
	SIF 実績달성플래그:実績_ID_C > 0
		RETURN 0
ELSEIF 現在モード == 2
	SIF 実績달성플래그:実績_ID_C == 0
		RETURN 0
	RETURN 1
ENDIF

IF 표시モード == 0
	SIF 実績달성플래그:実績_ID_C == 2
		RETURN 0
ENDIF

RETURN 1

