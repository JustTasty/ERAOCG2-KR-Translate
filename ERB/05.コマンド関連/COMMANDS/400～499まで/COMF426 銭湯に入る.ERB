;-------------------------------------------------
;공중목욕탕에 들어간다
;-------------------------------------------------
@COM426
#DIM DYNAMIC 回復率_체력
#DIM DYNAMIC 回復率_기력
#DIM DYNAMIC 回復率_정력


PRINTL 500円이 듭니다만 괜찮으시겠습니까？
PRINTL [1] - 예
PRINTL [0] - 아니오

BINPUT
SIF RESULT == 0
	RETURN 0


CALL MONEY_GET(-500)
回復率_체력 = 1000
回復率_기력 = 2000

IF TIME_PROGRESS(CFLAG:MASTER:314) <= 960 && !CFLAG:MASTER:수면 && TARGET > 0
	CALL RECOVER_PERMIL(MASTER,回復率_체력,"체력",1)
	CALL RECOVER_PERMIL(MASTER,回復率_기력,"기력",1)
	RESET_STAIN MASTER
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:LOCAL:현재위치 != CFLAG:MASTER:현재위치
			CONTINUE
		CALL RECOVER_PERMIL(LOCAL,回復率_체력,"체력",1)
		CALL RECOVER_PERMIL(LOCAL,回復率_기력,"기력",1)
		CALL SOURCE_CALC_환락(LOCAL, MASTER, 50)
		CALL SOURCE_CALC_접촉(LOCAL, MASTER, 50)
		CFLAG:LOCAL:쇠약 -= 60
		RESET_STAIN LOCAL
	NEXT
	TIME += 120 - 40 * TALENT:MASTER:회복속도
ELSEIF TIME_PROGRESS(CFLAG:MASTER:314) <= 960 && !CFLAG:MASTER:수면
	CALL RECOVER_PERMIL(MASTER,回復率_체력,"체력",1)
	CALL RECOVER_PERMIL(MASTER,回復率_기력,"기력",1)
	TIME += 120 - 40 * TALENT:MASTER:회복속도
	RESET_STAIN MASTER
ENDIF

RETURN 1


;-------------------------------------------------
;実行판정
;-------------------------------------------------
;공중목욕탕에 들어간다
@COM_ABLE426
SIF !TFLAG:100
	RETURN 0
;一括관리
SIF GLOBAL_COMABLE(426)
	RETURN RESULT
SIF MONEY < 500
	RETURN 0
SIF CFLAG:MASTER:현재위치 != 場所_銭湯
	RETURN 0
;SIF CFLAG:TARGET:목욕 > 300
;	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
RETURN 1
