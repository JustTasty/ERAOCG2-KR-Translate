@CARDNAME_93175,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = リギャラクシー
	CASE "略称"
		RESULTS = 은하再誕
	CASE "カテゴリ"
		RESULTS:0 = ギャラクシー
		RESULTS:1 = 게임 오리지널
ENDSELECT


@CARD_93175(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 미확인족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
	CASE "分割"
		RETURN 효과なし
ENDSELECT

RETURN 0

@CARD_EXPLANATION_93175(종류)
#DIM DYNAMIC 종류
PRINTL 이 카드명의 ①③의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
PRINTL 상대 필드에만 モンスターが존재하고、자신의 묘지에 「은하眼の光子룡」が存在する경우、이 카드의 발동은 패에서 も할 수 있다。
CALLFORM CARD_ABLE(1,마함起動,손패起動,フリー誘発)
PRINTL ①：자신의 묘지の「ギャラクシー」몬스터 １体を대상として발동할 수 있다。
PRINTL 그 몬스터를 공격 표시로 특수 소환하고、この카드를 장착 카드 취급으로 그 몬스터에 장착한다。
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ②:装備モンスターは원래의 공격력이 절반이 되며、자신ターンの 엔드 페이즈에 묘지へ送られる。
CALLFORM CARD_ABLE(3,마함起動)
PRINTL ③:この카드가 레벨８이하의 モンスターに装備되어있는 자신メインフェイズに발동할 수 있다。
PRINTL この 카드는 장착 몬스터와 同じ종족・레벨의 일반 몬스터와 なり、몬스터 존에 특수  소환한다。
PRINTL その後、このカード名を含む자신 필드의 몬스터 を素材としてX소환を行う。

@CARDCAN_93175(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 효과先
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備先 = RESULT	
装備者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,93175)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF 발동済_3(듀얼리스트,93175)
		RETURN 0
	TRYCALLFORM CARD_{GETVAR(@"%듀얼리스트%_필드:装備先")}, "종류"
	SIF MATCH(RESULT,엑시즈) || MATCH(RESULT,링크)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_星:装備先") > 8
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,92340,"마함")
	SIF RESULT == 1
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,93175)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,0,"묘지")
	SIF RESULT == 1
		RETURN 0
	IF ゾーン == "손패"
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件名前 = ギャラクシーアイズ・フォトン・ドラゴン
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
		CALL FULLRESET
		효과範囲필드 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 0
			RETURN 0
		CALL FULLRESET
		효과範囲상대필드 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = ギャラクシー
	효과条件종류 = モン全般
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻守変動
	SIF ゾーン != "마함"
		RETURN 0
	SIF 装備先 == 5
		RETURN 0
	RETURN 1
ELSEIF 종류 == ターン終了時強制
	SIF ゾーン != "마함"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_93175(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 설정レベル
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 装備先종족
#DIM DYNAMIC 装備先レベル
#DIM DYNAMIC 攻撃力修正
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備先 = RESULT	
装備者 = %RESULTS%

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,93175,3)
	IF RESULT == 0
		装備先종족 = GETVAR(@"%装備者%_MZ_種:装備先")
		装備先レベル = GETVAR(@"%装備者%_MZ_星:装備先")
		CALL FULLRESET
		효과추가종류:0 = 100 + 装備先종족
		효과추가종류:1 = 140 + 装備先レベル
		CALL CARD_SP_SUMMON(듀얼리스트, "마함", 場所)
		IF RESULT
			CALL FULLRESET
			효과条件종류 = 엑시즈
			強制必要素材名前 = リギャラクシー
			효과条件소환可 = 1
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				TRYCALLFORM CARDSUMMON_{RESULT:99}(듀얼리스트,選択カード)
				CALL XYZSUMMON(듀얼리스트, 選択カード)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,93175,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = ギャラクシー
		효과条件종류 = モン全般
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"攻撃표시")
		IF RESULT
			CALL FULLRESET
			효과範囲필드 = 1
			효과条件固定 = カード소환先
			CALLFORM CARD_EQUIP_ADV(듀얼리스트,場所,ゾーン)
		ENDIF
	ENDIF
ELSEIF 종류 == 攻守変動
	CALLFORM CARD_{GETVAR(@"%装備者%_필드:装備先")}("攻撃力")
	攻撃力修正 = -RESULT / 2
	CALL STATCHANGE_BASIC(듀얼리스트,"装備",場所,攻撃力修正)
ELSEIF 종류 == ターン終了時強制
	CALL CARD_DISPOSE(듀얼리스트,ゾーン,装備先)
ENDIF
