@CARDNAME_73187,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = アルティメット・エタニティ・ストリーム
	CASE "略称"
		RESULTS = 究極永劫光子流
	CASE "カテゴリ"
		RESULTS:0 = ギャラクシーアイズ
		RESULTS:1 = ギャラクシー
		RESULTS:2 = フォトン
		RESULTS:3 = 게임 오리지널
ENDSELECT


@CARD_73187(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"

		RETURN 装備마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_73187(종류)
#DIM DYNAMIC 종류

PRINTL 자신 필드의 「ギャラクシーアイズ」Xモンスターにのみ装備可能。
PRINTL 이 카드명의 ②의 효과는 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ①：装備モンスターは상대の 마법 / 함정 카드의 효과를 受けない。
CALLFORM CARD_ABLE(1,마함起動 ,フリー誘発)
PRINTL ②：자신 / 상대 턴에、자신의 묘지 / 제외 상태인 빛 속성 몬스터１体を대상として발동できる
PRINTL （「은하眼の光子룡」を대상として발동했을 경우、装備몬스터를 覚醒状態にする）。
PRINTL その카드를 덱으로 되돌리고、装備몬스터의 X소재를 １개 늘린다。
PRINTL その後、装備モンスターが覚醒状態の경우、装備몬스터의 X소재를 １つ増やし、
PRINTL 자신의 덱 / 묘지 / 제외 상태인 카드를 １장選んで패에 加える。

@CARDCAN_73187(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 装備者
CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,73187)
SIF RESULT == 1
	RETURN 0
IF (종류 == 마함起動 || 종류 == フリー誘発) && GETVAR(@"%듀얼리스트%_필드:装備先") != -1
	SIF ゾーン != "마함"
		RETURN 0
	SIF 발동済_2(듀얼리스트,73187)
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과範囲제외 = 1
	효과条件속성 = 빛속성
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	IF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = ギャラクシーアイズ
	효과条件종류 = 엑시즈
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_73187(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 選択カード名
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先


CALL 装備판정(듀얼리스트,場所,ゾーン)
装備先 = RESULT	
装備者 = %RESULTS%


IF (종류 == 마함起動 || 종류 == フリー誘発) && GETVAR(@"%듀얼리스트%_필드:装備先") != -1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,73187,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과範囲제외 = 1
		효과条件속성 = 빛속성
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			選択カードID = RESULT:1
			TRYCALLFORM CARDNAME_{選択カードID}, "名前"
			選択カード名 = %RESULTS%
			SIF 選択カード名 == "ギャラクシーアイズ・フォトン・ドラゴン"
				CALL SET_FIELD_INFO(듀얼리스트,"필드",装備先,"条達",2,1)
			CALL FULLRESET
			CALL SET_FIELD_INFO(듀얼리스트,"필드",装備先,"Ｘ",1)
			IF GETVAR(@"%듀얼리스트%_MZ_条達:装備先") != 0
				CALL SET_FIELD_INFO(듀얼리스트,"필드",装備先,"Ｘ",1)
				CALL FULLRESET
				효과範囲덱 = 1
				효과範囲묘지 = 1
				효과範囲제외 = 1
				CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,73187)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = ギャラクシーアイズ
		효과条件종류 = 엑시즈
		CALLFORM CARD_EQUIP_ADV(듀얼리스트,場所,ゾーン)
	ENDIF
ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"装備",場所,"魔耐")
	CALL STATCHANGE_EFFECT(듀얼리스트,"装備",場所,"함정耐")
ENDIF