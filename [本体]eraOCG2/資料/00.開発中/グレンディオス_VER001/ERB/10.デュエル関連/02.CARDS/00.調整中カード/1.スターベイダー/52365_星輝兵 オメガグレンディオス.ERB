@CARDNAME_52365,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 星輝兵 “Ω” グレンディオス
	CASE "略称"
		RESULTS = Ωグレンディオス
	CASE "カテゴリ"
		RESULTS:0 = グレンディオス
		RESULTS:1 = 星輝兵
		RESULTS:2 = 링크ジョーカー
		RESULTS:4 = ヴァンガード
ENDSELECT

@CARD_52365(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 3000
	CASE "성별"
		RETURN 無性
ENDSELECT
RETURN 0

@CARDSUMMON_52365(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
RETURN 0

@CARD_EXPLANATION_52365(종류)
#DIM DYNAMIC 종류

CALL CARD_ABLE(1,EX덱起動)
PRINTL 「星輝兵 “Ω” グレンディオス」は１ターンに１度、자신필드에 「星輝兵」ＥＸモンスターが存在しない場合に、
PRINTL 「星輝兵」 몬스터 １장の上に重ねてのみＸ소환できる。
PRINTL このカード名の②③④の효과はそれぞれ１ターンに１度しか使用できない。
CALL CARD_ABLE(0,지속효과)
; PRINTL ①：자신フィールドの通常モンスター全ては「Я」モンスターとしても扱う。
; PRINTL また、자신フィールドの「Я」モンスターは효과無効を得て、攻撃力が２８００になる。
PRINTL ①：자신フィールドの「Я」モンスターは효과無効を得て、攻撃力が２８００になる。
PRINTL このカードが필드에 表側표시で存在する限り、자신は「グレンディオス」モンスターしかＥＸ덱から특수소환できない。
CALL CARD_ABLE(2,필드起動)
PRINTL ②：자신メインフェイズに발동できる。
PRINTL 손패から「Я」 몬스터 １장을 특수소환する。
; PRINTL 손패から通常モンスター 또는 「Я」 몬스터 １장을 특수소환する。。
CALL CARD_ABLE(3,효과後誘発)
PRINTL ③：자신필드에 「Я」モンスターが소환・특수소환された場合に、相手フィールドの表側표시の 몬스터 １장을 대상として발동できる。
PRINTL そのモンスターを呪縛する。
SETCOLOR C_GRAY
PRINTL （呪縛状態のモンスターは裏側守備표시となり、攻撃回避を得る。
PRINTL 　ただし、相手필드에 呪縛状態のモンスターしか存在しない場合、자신フィールドのモンスターが相手プレイヤーに与える戦闘ダメージは半分になる）
RESETCOLOR
CALL CARD_ABLE(4,ターン開始時)
PRINTL ④：相手スタンバイフェイズに발동できる。
PRINTL 손패・フィールドの「Я」 몬스터 １장을 묘지に送り、
; PRINTL 손패の通常モンスター 또는 「Я」 몬스터 １장을 捨てて、
PRINTL 턴 종료시까지 、相手は呪縛状態のモンスターを素材にできず、표시形式を변경できない。

@CARDCAN_52365(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 存在カードID,20
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 条件達成

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,52365)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX덱起動
	SIF 발동済_1(듀얼리스트,52365)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_EX_CHECK(듀얼리스트,52365)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = EXモン全般
	효과条件カテゴリ = 星輝兵
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 星輝兵
	효과条件重ねてＸ = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,52365)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件カテゴリ = Я
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 소환誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_3(듀얼리스트,52365)
		RETURN 0
	SIF 最終소환決者 != 듀얼리스트
		RETURN 0
	SIF 最終소환타입 == ""
		RETURN 0
	IF GETVARS(@"%最終소환決者%_MZ_改名:最終소환場所") == ""
		TRYCALLFORM CARDNAME_{最終소환ID}("カテゴリ")
		SIF MATCH(RESULTS,"Я")
			条件達成++
	ENDIF
	SIF GETVARS(@"%最終소환決者%_MZ_追カテ:最終소환場所") == "Я"
		条件達成++
	SIF GETVARS(@"%最終소환決者%_MZ_追カテ2:最終소환場所") == "Я"
		条件達成++
	SIF 条件達成 < 1
		RETURN 0
	CALL FULLRESET
	효과範囲相手필드 = 1
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == ターン開始時
	SIF 발동済_4(듀얼리스트,52365)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲필드 = 1
	효과条件カテゴリ = Я
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_52365(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 選択カードゾーン
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 소환先
#DIM DYNAMIC 存在カードID,20
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 条件カード数
#DIM DYNAMIC 攻撃力補正
#DIM DYNAMIC 守備力補正
#DIM DYNAMIC 条件達成

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == EX덱起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52365,1)
	IF RESULT == 0
		CALL RESETCONDITION_MAT
		必要素材数 = 1
		素材条件カテゴリ = 星輝兵
		重ねてＸ = 1
		TRYCALLFORM XYZSUMMON(듀얼리스트,場所)
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52365,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件カテゴリ = Я
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 소환誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52365,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲相手필드 = 1
		효과条件표시 = 表側표시
		CALL CARD_REMOVAL_ADV(듀얼리스트,"単体除去",場所,"표시변경")
	ENDIF
ELSEIF 종류 == ターン開始時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52365,4)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲필드 = 1
		효과条件カテゴリ = Я
		효과条件종류 = モン全般
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"条達",1)
		ENDIF
	ENDIF
ELSEIF 종류 == 지속효과
	FOR カウンタ, 0, 5
		VARSET 条件達成
		選択カードID = GETVAR(@"%듀얼리스트%_필드:カウンタ")
		SIF 選択カードID == -1
			CONTINUE
		; TRYCALLFORM CARD_{選択カードID}("종류")
		; SIF RESULT == 通常モン
		; 	CALL CARD_SET_CATEGORY(듀얼리스트,"필드",カウンタ,"Я")
		IF GETVARS(@"%듀얼리스트%_MZ_改名:カウンタ") == ""
			TRYCALLFORM CARDNAME_{選択カードID}("カテゴリ")
			SIF MATCH(RESULTS,"Я")
				条件達成++
		ENDIF
		SIF GROUPMATCH(GETVARS(@"%듀얼리스트%_MZ_追カテ:カウンタ"),"Я")
			条件達成++
		SIF GROUPMATCH(GETVARS(@"%듀얼리스트%_MZ_追カテ2:カウンタ"),"Я")
			条件達成++
		IF GETVAR(@"%듀얼리스트%_MZ_変化元ID:カウンタ") > -1
			TRYCALLFORM CARDNAME_{GETVAR(@"%듀얼리스트%_MZ_変化元ID:カウンタ")}("カテゴリ")
			SIF MATCH(RESULTS,"Я")
				条件達成++
		ENDIF
		SIF 条件達成 < 1
			CONTINUE
        攻撃力補正 = 2800 - GETVAR(@"%듀얼리스트%_MZ_攻:カウンタ")
        守備力補正 = 2800 - GETVAR(@"%듀얼리스트%_MZ_守:カウンタ")
		CALL STATCHANGE_EFFECT(듀얼리스트,"自身",カウンタ,"無効",3)
		SIF 攻撃力補正 != 0
			CALL STATCHANGE_EFFECT(듀얼리스트,"自身",カウンタ,"永攻",攻撃力補正)
		SIF 守備力補正 != 0
			CALL STATCHANGE_EFFECT(듀얼리스트,"自身",カウンタ,"永守",守備力補正)
	NEXT
	; CALL FULLRESET
	; 효과条件固カテゴリ = Я
	; CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"戦無",3)

	CALL CARD_EX_RESTRICTION(듀얼리스트,"グレンディオス")

	CALL FULLRESET
	효과条件표시 = 裏側守備표시
	CALL STATCHANGE_EFFECT(듀얼리스트,"相手필드",場所,"攻避",3)
	IF GETVAR(@"%듀얼리스트%_MZ_条達:場所")
		CALL STATCHANGE_EFFECT(듀얼리스트,"相手필드",場所,"不材",3)
		CALL STATCHANGE_EFFECT(듀얼리스트,"相手필드",場所,"表固",3)
	ENDIF

	CALL FULLRESET
	효과範囲相手필드 = 1
	효과条件표시 = 裏側守備표시
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	条件カード数 = RESULT

	CALL FULLRESET
	효과範囲相手필드 = 1
	효과条件표시 = 表側표시
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT <= 0 && 条件カード数 >= 1
		CALL FULLRESET
		CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"直攻")
		CALL SET_PLAYER_INFO(듀얼리스트,"ダメージ半化",1,1)
	ENDIF
ENDIF
