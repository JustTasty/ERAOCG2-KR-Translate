@CARDNAME_52365,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 星輝兵 “Ω” グレンディオス
	CASE "略称"
		RESULTS = Ωグレンディオス
	CASE "カテゴリ"
		RESULTS:0 = グレンディオス
		RESULTS:1 = 星輝兵
		RESULTS:2 = リンクジョーカー
		RESULTS:4 = ヴァンガード
ENDSELECT

@CARD_52365(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "種類"
		RETURN エクシーズ
	CASE "属性"
		RETURN 闇属性
	CASE "種族"
		RETURN 機械族
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 3000
	CASE "性別"
		RETURN 無性
ENDSELECT
RETURN 0

@CARDSUMMON_52365(決闘者,召喚カード,EX参照)
#DIMS DYNAMIC 決闘者
#DIM DYNAMIC 召喚カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
RETURN 0

@CARD_EXPLANATION_52365(種類)
#DIM DYNAMIC 種類

CALL CARD_ABLE(1,EXデッキ起動)
PRINTL 「星輝兵 “Ω” グレンディオス」は１ターンに１度、自分フィールドに「星輝兵」ＥＸモンスターが存在しない場合に、
PRINTL 「星輝兵」モンスター１体の上に重ねてのみＸ召喚できる。
PRINTL このカード名の②③④の効果はそれぞれ１ターンに１度しか使用できない。
CALL CARD_ABLE(0,永続効果)
; PRINTL ①：自分フィールドの通常モンスター全ては「Я」モンスターとしても扱う。
; PRINTL また、自分フィールドの「Я」モンスターは効果無効を得て、攻撃力が２８００になる。
PRINTL ①：自分フィールドの「Я」モンスターは効果無効を得て、攻撃力が２８００になる。
PRINTL このカードがフィールドに表側表示で存在する限り、自分は「グレンディオス」モンスターしかＥＸデッキから特殊召喚できない。
CALL CARD_ABLE(2,戦場起動)
PRINTL ②：自分メインフェイズに発動できる。
PRINTL 手札から「Я」モンスター１体を特殊召喚する。
; PRINTL 手札から通常モンスターまたは「Я」モンスター１体を特殊召喚する。。
CALL CARD_ABLE(3,効果後誘発)
PRINTL ③：自分フィールドに「Я」モンスターが召喚・特殊召喚された場合に、相手フィールドの表側表示のモンスター１体を対象として発動できる。
PRINTL そのモンスターを呪縛する。
SETCOLOR C_GRAY
PRINTL （呪縛状態のモンスターは裏側守備表示となり、攻撃回避を得る。
PRINTL 　ただし、相手フィールドに呪縛状態のモンスターしか存在しない場合、自分フィールドのモンスターが相手プレイヤーに与える戦闘ダメージは半分になる）
RESETCOLOR
CALL CARD_ABLE(4,ターン開始時)
PRINTL ④：相手スタンバイフェイズに発動できる。
PRINTL 手札・フィールドの「Я」モンスター１体を墓地に送り、
; PRINTL 手札の通常モンスターまたは「Я」モンスター１体を捨てて、
PRINTL ターン終了時まで、相手は呪縛状態のモンスターを素材にできず、表示形式を変更できない。

@CARDCAN_52365(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所

#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 存在カードID,20
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 条件達成

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,52365)
SIF RESULT == 1
	RETURN 0

IF 種類 == EXデッキ起動
	SIF 発動済_1(決闘者,52365)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL CARD_EX_CHECK(決闘者,52365)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件種類 = EXモン全般
	効果条件カテゴリ = 星輝兵
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件カテゴリ = 星輝兵
	効果条件重ねてＸ = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 戦場起動
	SIF ゾーン != "戦場"
		RETURN 0
	SIF 発動済_2(決闘者,52365)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL FULLRESET
	効果範囲手札 = 1
	効果条件カテゴリ = Я
	効果条件召喚可 = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 召喚誘発
	SIF ゾーン != "戦場"
		RETURN 0
	SIF 発動済_3(決闘者,52365)
		RETURN 0
	SIF 最終召喚決者 != 決闘者
		RETURN 0
	SIF 最終召喚タイプ == ""
		RETURN 0
	IF GETVARS(@"%最終召喚決者%_MZ_改名:最終召喚場所") == ""
		TRYCALLFORM CARDNAME_{最終召喚ID}("カテゴリ")
		SIF MATCH(RESULTS,"Я")
			条件達成++
	ENDIF
	SIF GETVARS(@"%最終召喚決者%_MZ_追カテ:最終召喚場所") == "Я"
		条件達成++
	SIF GETVARS(@"%最終召喚決者%_MZ_追カテ2:最終召喚場所") == "Я"
		条件達成++
	SIF 条件達成 < 1
		RETURN 0
	CALL FULLRESET
	効果範囲相手戦場 = 1
	効果条件表示 = 表側表示
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == ターン開始時
	SIF 発動済_4(決闘者,52365)
		RETURN 0
	SIF ゾーン != "戦場"
		RETURN 0
	SIF ターンプレイヤー == 決闘者
		RETURN 0
	CALL FULLRESET
	効果範囲手札 = 1
	効果範囲戦場 = 1
	効果条件カテゴリ = Я
	効果条件種類 = モン全般
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 永続効果
	SIF ゾーン != "戦場"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_52365(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 選択カードゾーン
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 召喚先
#DIM DYNAMIC 存在カードID,20
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 条件カード数
#DIM DYNAMIC 攻撃力補正
#DIM DYNAMIC 守備力補正
#DIM DYNAMIC 条件達成

CALL 対面者判定(決闘者)
対面者 = %RESULTS%


IF 種類 == EXデッキ起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,52365,1)
	IF RESULT == 0
		CALL RESETCONDITION_MAT
		必要素材数 = 1
		素材条件カテゴリ = 星輝兵
		重ねてＸ = 1
		TRYCALLFORM XYZSUMMON(決闘者,場所)
	ENDIF
ELSEIF 種類 == 戦場起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,52365,2)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲手札 = 1
		効果条件カテゴリ = Я
		CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
	ENDIF
ELSEIF 種類 == 召喚誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,52365,3)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲相手戦場 = 1
		効果条件表示 = 表側表示
		CALL CARD_REMOVAL_ADV(決闘者,"単体除去",場所,"表示変更")
	ENDIF
ELSEIF 種類 == ターン開始時
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,52365,4)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲手札 = 1
		効果範囲戦場 = 1
		効果条件カテゴリ = Я
		効果条件種類 = モン全般
		CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL STATCHANGE_EFFECT(決闘者,"自身",場所,"条達",1)
		ENDIF
	ENDIF
ELSEIF 種類 == 永続効果
	FOR カウンタ, 0, 5
		VARSET 条件達成
		選択カードID = GETVAR(@"%決闘者%_戦場:カウンタ")
		SIF 選択カードID == -1
			CONTINUE
		; TRYCALLFORM CARD_{選択カードID}("種類")
		; SIF RESULT == 通常モン
		; 	CALL CARD_SET_CATEGORY(決闘者,"戦場",カウンタ,"Я")
		IF GETVARS(@"%決闘者%_MZ_改名:カウンタ") == ""
			TRYCALLFORM CARDNAME_{選択カードID}("カテゴリ")
			SIF MATCH(RESULTS,"Я")
				条件達成++
		ENDIF
		SIF GROUPMATCH(GETVARS(@"%決闘者%_MZ_追カテ:カウンタ"),"Я")
			条件達成++
		SIF GROUPMATCH(GETVARS(@"%決闘者%_MZ_追カテ2:カウンタ"),"Я")
			条件達成++
		IF GETVAR(@"%決闘者%_MZ_変化元ID:カウンタ") > -1
			TRYCALLFORM CARDNAME_{GETVAR(@"%決闘者%_MZ_変化元ID:カウンタ")}("カテゴリ")
			SIF MATCH(RESULTS,"Я")
				条件達成++
		ENDIF
		SIF 条件達成 < 1
			CONTINUE
        攻撃力補正 = 2800 - GETVAR(@"%決闘者%_MZ_攻:カウンタ")
        守備力補正 = 2800 - GETVAR(@"%決闘者%_MZ_守:カウンタ")
		CALL STATCHANGE_EFFECT(決闘者,"自身",カウンタ,"無効",3)
		SIF 攻撃力補正 != 0
			CALL STATCHANGE_EFFECT(決闘者,"自身",カウンタ,"永攻",攻撃力補正)
		SIF 守備力補正 != 0
			CALL STATCHANGE_EFFECT(決闘者,"自身",カウンタ,"永守",守備力補正)
	NEXT
	; CALL FULLRESET
	; 効果条件固カテゴリ = Я
	; CALL STATCHANGE_EFFECT(決闘者,"戦場",場所,"戦無",3)

	CALL CARD_EX_RESTRICTION(決闘者,"グレンディオス")

	CALL FULLRESET
	効果条件表示 = 裏側守備表示
	CALL STATCHANGE_EFFECT(決闘者,"相手戦場",場所,"攻避",3)
	IF GETVAR(@"%決闘者%_MZ_条達:場所")
		CALL STATCHANGE_EFFECT(決闘者,"相手戦場",場所,"不材",3)
		CALL STATCHANGE_EFFECT(決闘者,"相手戦場",場所,"表固",3)
	ENDIF

	CALL FULLRESET
	効果範囲相手戦場 = 1
	効果条件表示 = 裏側守備表示
	効果条件CNT = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	条件カード数 = RESULT

	CALL FULLRESET
	効果範囲相手戦場 = 1
	効果条件表示 = 表側表示
	効果条件CNT = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	IF RESULT <= 0 && 条件カード数 >= 1
		CALL FULLRESET
		CALL STATCHANGE_EFFECT(決闘者,"戦場",場所,"直攻")
		CALL SET_PLAYER_INFO(決闘者,"ダメージ半化",1,1)
	ENDIF
ENDIF
