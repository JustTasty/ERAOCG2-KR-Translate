@CARDNAME_4413,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = ソウルガイド
	CASE "略称"
		RESULTS = ソウルガイド
	CASE "カテゴリ"
		RESULTS:0 = ナイトメア・クラス
		RESULTS:4 = Shadowverse
ENDSELECT


@CARD_4413(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "種類"
		RETURN 効果モン
	CASE "属性"
		RETURN 闇属性
	CASE "種族"
		RETURN アンデット族
	CASE "レベル"
		RETURN 2
	CASE "攻撃力"
		RETURN 1200
	CASE "守備力"
		RETURN 800
	CASE "性別"
		RETURN 牝性
	CASE "追加日"
		RETURN 250616
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4413(種類)
#DIM DYNAMIC 種類

PRINTL このカード名の①②の効果はそれぞれ１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(1,手札起動)
PRINTL ①：自分フィールドに「ナイトメア・クラス」モンスターが存在する場合、このカードは手札から特殊召喚できる。
CALLFORM CARD_ABLE(2,召喚時,特殊召喚時)
PRINTL ②：このカードが召喚・特殊召喚に成功した場合に発動できる。
PRINTL 自分フィールドの他の「ナイトメア・クラス」モンスター１体を破壊し、自分は２枚ドローする。



@CARDCAN_4413(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,4413)
SIF RESULT == 1
	RETURN 0

IF 種類 == 手札起動
	SIF 発動済_1(決闘者,4413)
		RETURN 0
	CALL CARD_SS_CHECK(決闘者,4413,"手札")
	SIF RESULT == 1
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件カテゴリ = ナイトメア・クラス
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 召喚時 || 種類 == 特殊召喚時
	SIF 発動済_2(決闘者,4413)
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件除去種 = 墓地
	効果条件他所 = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
; ELSEIF 種類 == チェーン可否
; 	SIF !GROUPMATCH(チェーン可否種類,手札起動)
; 		RETURN 0
; 	RETURN 1
ENDIF

@CARDEFFECT_4413(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 守備力修正


IF 種類 == 手札起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,4413,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(決闘者,"手札",場所)	
	ENDIF
ELSEIF 種類 == 召喚時 || 種類 == 特殊召喚時
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,4413,2)
	IF RESULT == 0
		CALL FULLRESET
		効果条件行先フラグ = 破壊行き
		効果条件他所 = 1
		CALL CARD_ADD_GY_BASIC(決闘者,"戦場",ゾーン,場所)
		IF RESULT == 1
			CALL CARDDRAW_BASIC(決闘者,2)
		ENDIF
	ENDIF
ENDIF

