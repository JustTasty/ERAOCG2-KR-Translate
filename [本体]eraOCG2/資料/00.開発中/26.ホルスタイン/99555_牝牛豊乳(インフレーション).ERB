@CARDNAME_99555,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

;カード制作者：xonng
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 【豊乳】ホルスタインフレーション
	CASE "略称"
		RESULTS = インフレ【豊乳】
	CASE "カテゴリ"
		RESULTS:0 = ホルスタイン
		RESULTS:1 = E.R.A
ENDSELECT

@CARD_99555(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 通常罠
	CASE "追加日"
		RETURN 250325
ENDSELECT

RETURN 0

@CARD_EXPLANATION_99555(種類)
#DIM DYNAMIC 種類
#DIMS DYNAMIC 決闘者

CALL TEXT_DECORATION("ROGUE")
PRINTL このカード名の②の効果は１ターンに１度しか発動できない。
CALLFORM CARD_ABLE(0,魔罠起動,手札起動)
PRINTL ①：メインフェイズ及びバトルフェイズに発動できる。フィールドの牝性モンスターを１体選び、発情させる。
PRINTL その後、以下の効果から選択する。
PRINTL ●乳をひと回り大きくする。
PRINTL ●フィールドで一番乳の大きいモンスターと同じ大きさにする。
PRINTL (ホルスタインカードによって魔法罠ゾーンに置かれた自分のカードも参照する)
PRINTL 自分の魔法＆罠ゾーンにセットされたカードが存在しない場合、このカードは手札から捨てて発動できる。
CALLFORM CARD_ABLE(1,墓地起動)
PRINTL ②：自分のフィールドか墓地に獣族の通常モンスターが存在する場合、自分メインフェイズに発動できる。
PRINTL 墓地のこのカードを手札に加える。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_99555(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 対戦カード

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,99555)
SIF RESULT == 1
	RETURN 0

IF 種類 == 魔罠起動 || 種類 == 手札起動 
	SIF !(メインフェイズ == 1 || バトルフェイズ == 1 )
		RETURN 0
	
	IF ゾーン == "手札"
		;セットカードがあれば使えない
		CALL FULLRESET
		効果範囲魔罠 = 1
		効果条件表示 = 裏側表示
		効果条件CNT = 1
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT > 0
			RETURN 0
	ELSEIF ゾーン == "魔罠"
		;セットしたターンには使えない
		SIF GETVAR(@"%決闘者%_魔_表:場所") != 2
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	
	;フィールドに牝が居なければ使えない
	CALL FULLRESET
	効果範囲戦場 = 1
	効果範囲相手戦場 = 1
	効果条件性別 = 牝性
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT == 0
		RETURN 0
		
	RETURN 1
ELSEIF 種類 == 墓地起動
	
	SIF 発動済_2(決闘者,99555)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	SIF ゾーン != "墓地"
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果範囲墓地 = 1
	効果条件種類 = 通常モン
	効果条件種族 = 獣族
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	
	RETURN 1
ENDIF

@CARDEFFECT_99555(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC 参照場所
#DIM DYNAMIC 相手フラグ
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 選択カードID_保
#DIM DYNAMIC 参照カードID
#DIM DYNAMIC 牝牛対象
#DIMS DYNAMIC 牝牛名称
#DIM DYNAMIC 元乳サイズ
#DIM DYNAMIC 現乳サイズ
#DIM DYNAMIC 牝牛最大ID
#DIMS DYNAMIC 最大決闘者
#DIMS DYNAMIC 牝牛最大名称
#DIM DYNAMIC 牝牛最大サイズ
#DIMS DYNAMIC カテゴリ名称
#DIM DYNAMIC 絶頂回数


CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 魔罠起動 || 種類 == 手札起動
	;カード発動確認
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,99555,1)
	IF RESULT == 0
		
		CALL FULLRESET
		効果範囲戦場 = 1
		効果範囲相手戦場 = 1
		効果条件性別 = 牝性
		
		CALL CARD_CHOOSE_ADV(決闘者,ゾーン,場所)
	
		IF RESULT != -1
			選択カード = RESULT
			選択カードID = RESULT:99
			選択カードID_保 = RESULT:99
			
			IF 選択カード >= 2000
				選択カード = 選択カード - 2000
				相手フラグ = 1
			ENDIF
			
			;ホルスタインカードなら、魔法罠のカードIDを参照
			IF 相手フラグ == 0
				CALL HORUSTEIN_GET_DATA(決闘者,選択カード,選択カードID)
			ELSE
				CALL HORUSTEIN_GET_DATA(対面者,選択カード,選択カードID)
			ENDIF
			牝牛名称 = %RESULTS:0%
			参照場所 = RESULT:0
			参照カードID = RESULT:99
			
			SIF 参照場所 != -1
				選択カードID = 参照カードID
			
			牝牛最大サイズ = -5
			;フィールドの胸サイズチェック
			FOR カウンタ,0,5
				SIF GETVAR(@"%決闘者%_戦場:{カウンタ}") == -1
					CONTINUE
				CALL HORUSTEIN_GET_DATA(決闘者,カウンタ,GETVAR(@"%決闘者%_MZ_ID:{カウンタ}") )
				CALL HORUSTEIN_GET_BUST(RESULT:99, GETVAR(@"%決闘者%_MZ_胸追:カウンタ"))
				IF RESULT:2 > 牝牛最大サイズ 
					牝牛最大サイズ = RESULT:2
					牝牛最大ID = GETVAR(@"%決闘者%_MZ_ID:カウンタ")
					最大決闘者 = %決闘者%
					;PRINTFORML 牝牛最大ID{牝牛最大ID} 牝牛最大サイズ {牝牛最大サイズ} 胸追{GETVAR(@"%決闘者%_MZ_胸追:カウンタ")}
				ENDIF
			NEXT
			FOR カウンタ,0,5
				SIF GETVAR(@"%対面者%_戦場:{カウンタ}") == -1
					CONTINUE
				CALL HORUSTEIN_GET_DATA(対面者,カウンタ,GETVAR(@"%対面者%_MZ_ID:{カウンタ}") )
				CALL HORUSTEIN_GET_BUST(RESULT:99, GETVAR(@"%対面者%_MZ_胸追:カウンタ"))
				IF RESULT:2 > 牝牛最大サイズ 
					牝牛最大サイズ = RESULT:2
					牝牛最大ID = GETVAR(@"%対面者%_MZ_ID:カウンタ")
					最大決闘者 = %対面者%
				ELSE
							
				ENDIF
			NEXT
			
			;自分のホルスタイン魔法罠のチェック
			;モンスターゾーンに存在していなくても、胸の変化情報が個Cに乗っているため
			FOR カウンタ,0,5
				SIF GETVAR(@"%決闘者%_魔罠:{カウンタ}") == -1
					CONTINUE
				;乳の登録情報は、個別Cで管理
				IF GETVARS(@"%決闘者%_魔_追カテ:カウンタ") == "登録情報：ホルスタイン-美乳" || GETVARS(@"%決闘者%_魔_追カテ:カウンタ") == "登録情報：ホルスタイン-愛乳" || GETVARS(@"%決闘者%_魔_追カテ:カウンタ") == "登録情報：ホルスタイン-旨乳"|| GETVARS(@"%決闘者%_魔_追カテ:カウンタ") == "登録情報：ホルスタイン-EX乳"
					
					CALL HORUSTEIN_GET_BUST(GETVAR(@"%決闘者%_魔_ID:{カウンタ}") , GETVAR(@"%決闘者%_魔_個Ｃ:カウンタ"))
					IF RESULT:2 > 牝牛最大サイズ 
						牝牛最大サイズ = RESULT:2 
						牝牛最大ID = GETVAR(@"%決闘者%_魔_ID:カウンタ")
						最大決闘者 = %決闘者%
					ENDIF
				ENDIF
			NEXT
			
			;最後に見たカードがホルスタインカードなら、魔法罠のカードを参照
			CALL HORUSTEIN_GET_DATA(最大決闘者,選択カード,牝牛最大ID)
			
			参照場所 = RESULT:0
			参照カードID = RESULT:99
			
			SIF 参照場所 != -1
				牝牛最大ID = 参照カードID
			
			CALL HORUSTEIN_GET_DATA(決闘者,-1,牝牛最大ID)
			牝牛最大名称 = %RESULTS:0%
			
			CALL HORUSTEIN_PRINT_STATE(決闘者,-1,牝牛最大ID, 牝牛最大サイズ,2,1);2乳上書き(比較なし)、1画像アイコンのみ
			
			PRINTFORML 
			SETCOLOR C_GOLD
			PRINTFORM ★最大サイズ★ ：
			RESETCOLOR
			SETCOLOR C_PINK
			PRINTFORM  [%GET_TALENTNAME(121,牝牛最大サイズ)%] 
			RESETCOLOR
			PRINTFORML %牝牛最大名称%
			PRINTFORML 
			
			;現在のサイズを取得
			CALL HORUSTEIN_GET_BUST(選択カードID, GETVAR(@"%対象者（CS）%_MZ_胸追:選択カード"))
			元乳サイズ = RESULT:0
			現乳サイズ = RESULT:2
			
			CALL HORUSTEIN_GET_DATA(決闘者,-1,選択カードID)
			牝牛名称 = %RESULTS:0%
			牝牛対象 = RESULT:1
			
			PRINTFORM 　現在サイズ　 ：
			PRINTFORM  [%GET_TALENTNAME(121,現乳サイズ)%] 
			RESETCOLOR
			PRINTFORML %牝牛名称%
			PRINTFORMW 
			
			;CALL HORUSTEIN_PRINT_STATE(選択カードID, 現乳サイズ,,,2,1);乳上書き(比較なし)、1画像アイコンのみ
			
			;胸のサイズによって選択肢の表示有無
			IF 牝牛最大サイズ <= 現乳サイズ
				;サイズアップのみ
				PRINTFORML
				PRINTFORMW 選択したカードが一番大きい乳のため、「乳をひと回り大きくする」が選択されます。
				RESULT = 0
			ELSE
				;選択可能
				CALL YN_CHOICE(決闘者,"「乳をひと回り大きくする」","「フィールドで一番乳の大きいモンスターと同じ大きさにする。」")
			ENDIF

			;ホルスタインモンスターの場合、魔法の方に個別Cを乗せる
			IF INRANGE (選択カードID_保, 179556, 179559) 
				SIF 選択カードID_保 == 179556
					カテゴリ名称 = 登録情報：ホルスタイン-美乳
				SIF 選択カードID_保 == 179557
					カテゴリ名称 = 登録情報：ホルスタイン-愛乳
				SIF 選択カードID_保 == 179558
					カテゴリ名称 = 登録情報：ホルスタイン-旨乳
				SIF 選択カードID_保 == 179559
					カテゴリ名称 = 登録情報：ホルスタイン-EX乳
				
				FOR カウンタ,0,5
					IF (相手フラグ == 1 && GETVAR(@"相手_MZ_所有:選択カード") == 1 ) || (相手フラグ == 0 && GETVAR(@"自分_MZ_所有:選択カード") == 1 ) 
						SIF GETVARS(@"自分_魔_追カテ:カウンタ") != カテゴリ名称
							CONTINUE
						相手フラグ = 0
					ELSEIF (相手フラグ == 0 && GETVAR(@"自分_MZ_所有:選択カード") == 2 ) || (相手フラグ == 1 && GETVAR(@"相手_MZ_所有:選択カード") == 2 )
						;相手のカードなら、相手の場を見る
						SIF GETVARS(@"相手_魔_追カテ:カウンタ") != カテゴリ名称
							CONTINUE
						相手フラグ = 1
					ELSE
						CONTINUE
					ENDIF
					IF RESULT == 0
						CALL SET_FIELD_INFO("自分","魔罠",カウンタ,"個Ｃ",1,,相手フラグ)
						現乳サイズ += 1
					ELSE
						CALL SET_FIELD_INFO("自分","魔罠",カウンタ,"個Ｃ",牝牛最大サイズ - 元乳サイズ,上書きモード,相手フラグ)
						現乳サイズ = 牝牛最大サイズ
					ENDIF
					BREAK
				NEXT
				
			ELSE
				IF RESULT == 0
					;乳を大きくする
					CALL SET_FIELD_INFO(決闘者,"戦場",選択カード,"胸追",1,,相手フラグ)
					現乳サイズ += 1
				ELSE
					;フィールドで一番乳の大きいモンスターと同じ大きさにする。」
					CALL SET_FIELD_INFO(決闘者,"戦場",選択カード,"胸追",牝牛最大サイズ - 元乳サイズ,上書きモード,相手フラグ)
					現乳サイズ = 牝牛最大サイズ
				ENDIF
			ENDIF
			
			PRINTFORML 
			PRINTFORML ホルスの従者達が%牝牛名称%を取りみ、怪しげな呪文を唱え始めた…
			PRINTFORMW 
			
			;名前から胸のサイズの変動まで表示
			CALL HORUSTEIN_PRINT_STATE(決闘者,場所,選択カードID, 現乳サイズ-元乳サイズ)
			
			IF 牝牛対象 != -1
				CALL PRINT_STATE_CLOTH(牝牛対象)
			ENDIF
			PRINTL
			PRINTFORML
			PRINTL □ 健康状態 □——————————————————————————————————————————————————————————————————————————————————
			PRINTFORML 
			PRINTFORML ・母乳の量　…　検査対象外
			PRINTFORML ・母乳品質　…　検査対象外
			PRINTFORM ・乳 感 度　… 
			FONTSTYLE 4
			PRINTFORM  検査対象外 
			FONTSTYLE 0
			SETCOLOR C_PINK
			PRINTFORML  → 敏感
			RESETCOLOR
			PRINTFORML ・性　　欲　…　検査対象外
			PRINTFORML ・触　　診　… 　異常なし
			PRINTFORM 　　　　　　　　　　　　
			FONTSTYLE 8
			PRINTFORML 　評 価：― 
			FONTSTYLE 0
			PRINTFORML 
			PRINTL □ 特記事項 □——————————————————————————————————————————————————————————————————————————————————
			PRINTFORML 
			SETCOLOR C_PINK
			PRINTFORML ・発情しています。
			PRINTFORML 　サイズが大きくなった影響か、一時的に敏感になっているようです。強い刺激はお控えください。
			PRINTFORML 
			RESETCOLOR
			
			CALL HORUSTEIN_GET_BUST(選択カードID,現乳サイズ-元乳サイズ)
			PRINTFORM ・乳のサイズが [%GET_TALENTNAME(121,RESULT:0)%] から
			SETCOLOR C_H_PINK
			PRINTFORM  [%GET_TALENTNAME(121,RESULT:2)%] 
			RESETCOLOR
			PRINTFORML になっています。
			PRINTFORML 　このカードで付与される発情状態は、乳のサイズによって攻撃が失敗する確率が変動します。
			PRINTFORML 　以下、参考資料となります。ご参考ください。
			PRINTFORML 
			PRINTFORML 　[並み]以下　：５０％程度
			SETCOLOR C_D_GRAY
			PRINTFORML 　　[巨乳]　　：６０％程度
			SETCOLOR 0X222222
			PRINTFORML 　　[爆乳]　　：７０％程度
			SETCOLOR 0X111111
			PRINTFORML 　 [ウシ乳] 　：８０％程度
			RESETCOLOR
			PRINTFORML 
			;PRINTFORML 　　[魔乳]　　：９０％程度
			;PRINTFORML 
			;PRINTFORML 　 [規格外] 　：１００％
			;PRINTFORML 

			CUSTOMDRAWLINE —
			PRINTFORMW 
			
			IF INRANGE (RESULT:2 , 2, 4) 
				PRINTFORML
				PRINTFORML %牝牛名称%は大きくなった乳を重そうに押さえている…
				PRINTFORMW 
			ELSEIF RESULT:2 >= 5
				PRINTFORML
				PRINTFORML %牝牛名称%は乳がデカすぎて動けないようだ…
				PRINTFORMW 
			ENDIF
		
			SETCOLOR C_PINK
			CUSTOMDRAWLINE —
			PRINTFORML
			PRINTFORML %牝牛名称%は発情している…
			PRINTFORML 
			CUSTOMDRAWLINE —
			PRINTFORML
			RESETCOLOR
			
			
			;発情値設定
			SELECTCASE 現乳サイズ
				CASE IS < 0
					CALL STATCHANGE_EFFECT(対象者（CS）,"自身",選択カード,"発情",150)
					PRINTFORML %牝牛名称%は発情している…
				CASE 1 ;巨乳
					CALL STATCHANGE_EFFECT(対象者（CS）,"自身",選択カード,"発情",160)
				CASE 2 ;爆乳
					CALL STATCHANGE_EFFECT(対象者（CS）,"自身",選択カード,"発情",170)
				CASE 3 ;牛乳
					CALL STATCHANGE_EFFECT(対象者（CS）,"自身",選択カード,"発情",180)
				CASE 4 ;魔乳
					CALL STATCHANGE_EFFECT(対象者（CS）,"自身",選択カード,"発情",190)
				CASE 5 ;規格外
					CALL STATCHANGE_EFFECT(対象者（CS）,"自身",選択カード,"発情",200)
			CASEELSE
					CALL STATCHANGE_EFFECT(対象者（CS）,"自身",選択カード,"発情",200)
			ENDSELECT
			
			IF 牝牛対象 != -1 && (現乳サイズ-元乳サイズ) > 0
				;元のサイズより大きい分だけ強い刺激になる
				CALL EROTIC_MOVE("バスト調教",牝牛対象,-1, 100 + (現乳サイズ-元乳サイズ) * 50)
				
				PRINTFORML 
				絶頂回数 += RESULT:1
			ENDIF
			
			IF 絶頂回数 > 0
				
				SETCOLOR C_PINK
				PRINTFORML 
				PRINTFORML %牝牛名称%は大きくなったばかりで敏感な乳を揺らしてしまい
				PRINTFORML その僅かな刺激で{絶頂回数}回イってしまった…
				PRINTFORMW 
				RESETCOLOR
			ENDIF
			
		ENDIF
		;テスト時はコメント化
		CALL CARD_DISPOSE(決闘者,ゾーン,場所)
	ENDIF
ELSEIF 種類 == 墓地起動
	
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,99555,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所,手札行き)
	ENDIF
ENDIF
