@CARDNAME_79556,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

;カード制作者：xonng
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 【美乳】ホルスタイン-ビューティ
	CASE "略称"
		RESULTS = 牝牛登録【美乳】
	CASE "カテゴリ"
		RESULTS:0 = ホルス
		RESULTS:1 = ホルスタイン
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_79556(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 通常魔法
	CASE "追加日"
		RETURN 250325
ENDSELECT

RETURN 0

@CARD_EXPLANATION_79556(種類)
#DIM DYNAMIC 種類

CALL TEXT_DECORATION("ROGUE")
PRINTL このカードは１ターンに１度しか発動できない。
PRINTL このカードの①の効果はデュエル中１度しか適用できず、①の効果で選択したカードは「牝牛」の登録情報として永続魔法となる。
CALLFORM CARD_ABLE(1,魔罠起動,手札起動)
PRINTL ①：手札・デッキから「ホルスタイン」以外の牝性モンスターを１장選択し、そのモンスターを永続魔法カード扱いで自分の魔法＆罠ゾーンに表側表示で置く。
PRINTL その後、デッキ外から「【美乳】ホルスタイン」１장을 手札に加え、
PRINTL 自分のデッキ・手札・モンスターゾーン・墓地に存在する全ての同名カードを「【美乳】ホルスタイン」に書き換える。
PRINTL この効果の処理後、以下の効果から選択する。
PRINTL ●手札から「【美乳】ホルスタイン」１장을 特殊召喚する。
PRINTL ●デッキから「ホルスタイン-ミルクファーム」１枚を手札に加える。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_79556(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

;戦闘終了フラグを拾うため
IF 種類 == 永攻紋章起動 && (自分_決闘敗北フラグ > 0 || 相手_決闘敗北フラグ > 0)
	RETURN 1
ENDIF

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,79556)
SIF RESULT == 1
	RETURN 0

IF 種類 == 魔罠起動 || 種類 == 手札起動
	SIF 発動済_D1(決闘者,79556) 
		RETURN 0
	SIF 発動済_1(決闘者,79556) 
		RETURN 0
	IF 種類 == 魔罠起動
		SIF GETVAR(@"%決闘者%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	SIF 魔罠数(決闘者) >= 5
		RETURN 0
	
	CALL FULLRESET
	効果範囲手札 = 1
	効果範囲デッキ = 1
	効果条件性別 = 牝性
	効果条件固カテゴリ = ホルスタイン
	
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 永紋章起動
	RETURN 1
ELSEIF 種類 == 永攻紋章起動
	RETURN 1
ENDIF

@CARDEFFECT_79556(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 参照カードID
#DIM DYNAMIC 書換先カードID
#DIM DYNAMIC 所有者
#DIM DYNAMIC 参照場所
#DIM DYNAMIC 牝牛対象
#DIM DYNAMIC 牝牛所有
#DIMS DYNAMIC 牝牛名称
#DIMS DYNAMIC 牝牛略称
#DIM DYNAMIC 牝牛属性
#DIM DYNAMIC 牝牛レベル
#DIM DYNAMIC 牝牛攻撃力
#DIM DYNAMIC 牝牛守備力
#DIM DYNAMIC 牝牛チューナー
#DIM DYNAMIC 牝牛乳サイズ
#DIM DYNAMIC 牝牛ミルクml
#DIM DYNAMIC 牝牛ミルク売却額
#DIM DYNAMIC 条件達成
#DIMS DYNAMIC カテゴリ名称

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

書換先カードID = 179556
カテゴリ名称 = 登録情報：ホルスタイン-美乳

IF 種類 == 魔罠起動 || 種類 == 手札起動
	;カード発動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,79556,1)
	IF RESULT == 0 && 魔罠数(決闘者) < 5
		
		CALL FULLRESET
		効果範囲手札 = 1
		効果範囲デッキ = 1
		効果条件性別 = 牝性
		効果条件固カテゴリ = ホルスタイン
		効果追加カテ = %カテゴリ名称%
		CALL CARD_CHOOSE_ADV(決闘者,ゾーン,場所,"表側表示")
		選択カードID = RESULT:99
		IF RESULT != 9999
			;(デュエル中１度)
			CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,79556,20)
			
			;データの設定
			SKIPDISP 1
			CALL CARD_ENG_EMBLEM(決闘者,-1,選択カードID,1)
			CALL CARD_RESERV(決闘者,79556,永紋章予約)
			CALL CARD_RESERV(決闘者,79556,永攻紋章予約)
			SKIPDISP 0

			;ホルスタイン情報の取得(カードIDから各種情報を取得)
			CALL HORUSTEIN_GET_DATA2(決闘者,-1,179556)
			牝牛名称 = %RESULTS:0%
			牝牛略称 = %RESULTS:1%
			参照場所 = RESULT:0
			牝牛対象 = RESULT:1
			
			;耐性と発動無効の付与
			CALL SET_FIELD_INFO(決闘者,"魔罠",参照場所,"破耐",2)
			CALL SET_FIELD_INFO(決闘者,"魔罠",参照場所,"対耐",2)
			CALL SET_FIELD_INFO(決闘者,"魔罠",参照場所,"発無",2)
			
			;カードの書き換え
			;戦場のカード
			FOR カウンタ,0,5
				SIF GETVAR(@"%決闘者%_MZ_ID:カウンタ") != 選択カードID
					CONTINUE
				CALL CARD_RECONSTRUCT(決闘者,"戦場",カウンタ,書換先カードID)
			NEXT
			
			;手札のカード
			FOR カウンタ,0,現手札(決闘者)
				SIF GETVAR(@"%決闘者%_手札:カウンタ") != 選択カードID
					CONTINUE
				CALL CARD_RECONSTRUCT(決闘者,"手札",カウンタ,書換先カードID)
			NEXT
			
			;デッキのカード
			FOR カウンタ,0,現山札数(決闘者)
				SIF GETVAR(@"%決闘者%_デッキ:カウンタ") != 選択カードID
					CONTINUE
				CALL CARD_RECONSTRUCT(決闘者,"デッキ",カウンタ,書換先カードID)
			NEXT
			
			;墓地のカード
			FOR カウンタ,0,現墓地数(決闘者)
				SIF GETVAR(@"%決闘者%_墓地:カウンタ") != 選択カードID
					CONTINUE
				CALL CARD_RECONSTRUCT(決闘者,"墓地",カウンタ,書換先カードID)
			NEXT
			
			PRINTFORMW 登録証に名前が刻まれると、どこからともなくホルスの守護者が現れた！
			PRINTFORML %牝牛名称%は抵抗する間もなく三人の守護者に押さえつけられると、パチン、とピアスを取り付けられてしまう…
			PRINTFORML 
			PRINTFORML 「…モォー！…モォー！？」
			PRINTFORMW 
			PRINTFORML %牝牛名称%は声を上げようとするも、取り付けられたピアスの影響か、その声は鳴き声に変わってしまう。
			PRINTFORML なんとか動こうと身じろぎして抵抗しようとするが、ピアスがぼんやりと光ると、徐々に抵抗が弱くなっていく…
			PRINTFORMW 微かに抵抗する%牝牛名称%を무시하고 、守護者はそのガッチリとした手で乳を丁寧に牝牛に相応しい"商品"へと"調整"していく…
			SETCOLOR C_PINK
			PRINTFORML 
			PRINTFORML 「モォー…」「モ、モォー…❤」
			PRINTFORMW 
			RESETCOLOR
			PRINTFORML …調整が終わったようだ。
			PRINTFORML 先ほどまでと様子が変わり、声を上げるたびに熱を帯びた牝牛の鳴き声へと変わっていく…
			PRINTFORML 
			CALL HORUSTEIN_GET_BUST(選択カードID,1)
			PRINTFORM %牝牛名称%の乳は
			SETCOLOR C_PINK
			PRINTFORM  [%GET_TALENTNAME(121,RESULT:0)%] 
			RESETCOLOR
			PRINTFORM から
			SETCOLOR C_H_PINK
			PRINTFORM  [%GET_TALENTNAME(121,RESULT:2)%] 
			RESETCOLOR
			PRINTFORML になってしまった…
			PRINTFORMW
			
			PRINTFORML %牝牛名称%は押さえつけていた守護者が何時の間にか居なくなったのを確認すると、
			PRINTFORMW 胸が苦しいのか、服を次々と脱ぎ始めた…
			PRINTFORML 
			PRINTFORML %牝牛名称%は足元の衣装を拾い、それが当然というように牛柄ビキニに着替え始めた…
			
			;服装の変更(牛柄ビキニと角)
			IF 牝牛対象 != -1 
				;アクセサリ&帽子：つけ角・大(2枠目を変更)
				EQUIP:牝牛対象:2 = 21
				CFLAG:牝牛対象:(200 + 2) = 21
				;靴、靴下は触らない
				;ズボン～インナーまでは全剥き
				FOR カウンタ,6,13
					EQUIP:牝牛対象:カウンタ = 0
					CFLAG:牝牛対象:(200 + カウンタ) = 0
				NEXT
				;ワンピース：牛柄ビキニ
				EQUIP:牝牛対象:15 = 33
				CFLAG:牝牛対象:(200 + 15) = 33
				
				;調教
				CALL EROTIC_MOVE("バスト調教",牝牛対象,-1,300)
			ENDIF
			
			;書き換えたカードを手札に加える。
			CALL CARD_GENERATE_HAND(決闘者,書換先カードID)
			
			CALL FULLRESET
			効果範囲デッキ = 1
			効果条件ID = 79555
			CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
			
			IF RESULT == 1
				;デッキにある場合
				CALL YN_CHOICE(決闘者,"「ホルスタイン-ミルクファーム」を手札に加える","「" + 牝牛名称 + "」を手札から特殊召喚する")
			ELSE
				;デッキに存在しない場合、選択肢は表示しない
				PRINTFORML
				PRINTFORMW 「ホルスタイン-ミルクファーム」がデッキに存在しない為、「%牝牛名称%」を手札から特殊召喚する…が選択されます。
				RESULT = 1
			ENDIF
			
			IF RESULT == 0
				;デッキからミルクファームを手札に加える
				CALL FULLRESET
				効果範囲デッキ = 1
				効果条件ID = 79555
				CALL CARD_ADD_ADV(決闘者,ゾーン,場所)
			ELSE
				;手札から特殊召喚
				CALL FULLRESET
				;選択時に【〇乳】ホルスタイン…と表示されるとｷﾞｮｯっとするので、手札から探して特殊召喚する
				FOR カウンタ,0,現手札(決闘者)
					SIF GETVAR(@"%決闘者%_手札:カウンタ") != 書換先カードID
						CONTINUE
					CALL CARD_SP_SUMMON(決闘者,"手札",カウンタ)
					BREAK
				NEXT
				IF RESULT != 0
					PRINTFORMW %牝牛名称%の美しい乳がしなやかに揺れた…
					CALL HORUSTEIN_RAND_COW_TXT(1,0)
				ENDIF
				
			ENDIF
			
			PRINTFORML 
			CALL FULLRESET
			効果範囲魔罠 = 1
			効果範囲手札 = 1
			効果条件ID = 79555
			CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
			IF RESULT == 1
				;手札・フィールドにホルスタインファームが存在する場合
				SETCOLOR C_PINK
				PRINTFORML %牝牛名称%の美しい乳がしなやかに揺れると、何も言わずに乳をこちらに突き出してきた…
				PRINTFORML %牝牛名称%は搾られる準備が出来ているようだ…
				PRINTFORMW 「……っ❤ モー…❤」
				PRINTFORML 
				RESETCOLOR
			ENDIF
			
			PRINTFORML 
			;永紋章の表示をスキップ

			SETCOLOR C_YELLOW
			PRINTFORML 「【美乳】ホルスタイン」はフィールドに存在する場合「%牝牛名称%」として扱われます。
			RESETCOLOR
			PRINTFORMW
			
			;美乳はカウンターを一個乗せる(乳サイズ1UP)
			CALL SET_FIELD_INFO(決闘者,"魔罠",参照場所,"個Ｃ",1)
			
		ELSE
			PRINTFORMW 魔法＆罠ゾーンにカードを置けなかったため、登録できませんでした…
		ENDIF
		
	ENDIF
	
	SKIPDISP 1
	CALL CARD_DISPOSE(決闘者,ゾーン,場所)
	SKIPDISP 0
	
ELSEIF 種類 == 永紋章起動 || 種類 == 永攻紋章起動
	;紋章を使用し、手札・フィールドの牝牛の情報を書き換え
	CALL FULLRESET
	CALL HORUSTEIN_GET_DATA(決闘者,,179556)
	牝牛名称 = %RESULTS:0%
	牝牛略称 = %RESULTS:1%
	参照場所 = RESULT:0
	牝牛対象 = RESULT:1
	牝牛所有 = RESULT:2
	牝牛ミルクml = RESULT:4		;搾乳量(決闘中総量)
	参照カードID = RESULT:99
	牝牛乳サイズ = GETVAR(@"%決闘者%_魔_個Ｃ:参照場所")
	;参照先カードが吹っ飛んでたら処理しない
	IF 参照場所 != -1
		
		IF 決闘者 == "自分"
			所有者 = 1
		ELSEIF 決闘者 == "相手"
			所有者 = 2
		ENDIF
		
		;属性
		TRYCALLFORM CARD_{参照カードID},"属性"
		牝牛属性 = RESULT
		
		;レベルの取得
		TRYCALLFORM CARD_{参照カードID},"レベル"
		牝牛レベル = RESULT
		
		;攻守
		TRYCALLFORM CARD_{参照カードID},"攻撃力"
		牝牛攻撃力 = RESULT
		TRYCALLFORM CARD_{参照カードID},"守備力"
		牝牛守備力 = RESULT

		;チューナー
		TRYCALLFORM CARD_{参照カードID},"チューナー"
		牝牛チューナー = RESULT
		
		;手札のカードのレベル変更(手札は設定分Lvが下がる)
		IF 種類 == 永攻紋章起動
			CALL FULLRESET
			効果条件ID = 書換先カードID
			CALL STATCHANGE_HAND(決闘者,"全体",場所,"永星",12 - 牝牛レベル)
		ENDIF
		
		;自分フィールドのカードで、所有者が自分
		FOR カウンタ,0,5
			SIF GETVAR(@"%決闘者%_MZ_ID:カウンタ") != 書換先カードID
				CONTINUE
			SIF GETVAR(@"%決闘者%_MZ_所有:カウンタ") != 所有者
				CONTINUE

			IF 種類 == 永紋章起動 
				CALL CARD_SET_RENAME(決闘者,"戦場",カウンタ,牝牛略称)
				CALL STATCHANGE_EFFECT(決闘者,"自身",カウンタ,"変化元ID",参照カードID)
			ELSEIF 種類 == 永攻紋章起動 
				
				CALL STATCHANGE_OTHER(決闘者,"自身",カウンタ,"属",牝牛属性)
				CALL STATCHANGE_EFFECT(決闘者,"自身",カウンタ,"永星",牝牛レベル)
				CALL STATCHANGE_BASIC(決闘者,"自身",カウンタ,牝牛攻撃力,牝牛守備力)
				CALL CARD_SET_CATEGORY(決闘者,"戦場",カウンタ,"E.R.A")
				;カテゴリ2は未設定なら変更する
				SIF GETVARS(@"%決闘者%_MZ_追カテ2:カウンタ") == ""
					CALL CARD_SET_CATEGORY(決闘者,"戦場",カウンタ,"ホルスタイン",1)
				SIF 牝牛チューナー == 1
					CALL STATCHANGE_EFFECT(決闘者,"自身",カウンタ,"一Ｔ",2)
				
				;永続魔法扱いの自分に乗ってるカウンターの数だけ乳を盛る
				CALL STATCHANGE_OTHER(決闘者,"自身",カウンタ,"胸追",牝牛乳サイズ)
			ENDIF
		NEXT
		
		;相手フィールドのカードで、所有者が自分
		FOR カウンタ,0,5
			SIF GETVAR(@"%対面者%_MZ_ID:カウンタ") != 書換先カードID
				CONTINUE
			SIF GETVAR(@"%対面者%_MZ_所有:カウンタ") != 所有者
				CONTINUE

			IF 種類 == 永紋章起動 
				CALL CARD_SET_RENAME(対面者,"戦場",カウンタ,牝牛略称)
				CALL STATCHANGE_EFFECT(対面者,"自身",カウンタ,"変化元ID",参照カードID)
			ELSEIF 種類 == 永攻紋章起動 
				
				CALL STATCHANGE_OTHER(対面者,"自身",カウンタ,"属",牝牛属性)
				CALL STATCHANGE_EFFECT(対面者,"自身",カウンタ,"永星",牝牛レベル)
				CALL STATCHANGE_BASIC(対面者,"自身",カウンタ,牝牛攻撃力,牝牛守備力)
				CALL CARD_SET_CATEGORY(対面者,"戦場",カウンタ,"E.R.A")
				;カテゴリ2は未設定なら変更する
				SIF GETVARS(@"%対面者%_MZ_追カテ2:カウンタ") == ""
					CALL CARD_SET_CATEGORY(対面者,"戦場",カウンタ,"ホルスタイン",1)
				
				SIF 牝牛チューナー == 1
					CALL STATCHANGE_EFFECT(対面者,"自身",カウンタ,"一Ｔ",2)
				
				CALL STATCHANGE_OTHER(対面者,"自身",カウンタ,"胸追",牝牛乳サイズ)
				
			ENDIF
		NEXT
	ENDIF
	IF (自分_決闘敗北フラグ > 0 || 相手_決闘敗北フラグ > 0) && 牝牛ミルクml > 0
		
		;基本は魔罠見るけど、自分フィールドに存在する場合、更に別の効果で乳がデカくなってるかチェック
		FOR カウンタ,0,5
			SIF GETVAR(@"%決闘者%_MZ_ID:カウンタ") != 書換先カードID
				CONTINUE
			SIF GETVAR(@"%決闘者%_MZ_胸追:カウンタ") > 牝牛乳サイズ
				牝牛乳サイズ = GETVAR(@"%決闘者%_MZ_胸追:カウンタ")
		NEXT
		
		;0.8倍
		牝牛ミルク売却額 = 牝牛ミルクml *10
		TIMES 牝牛ミルク売却額 , 0.8
		CALL MONEY_GET(牝牛ミルク売却額)
		
		CUSTOMDRAWLINE —
		PRINTFORML
		IF 牝牛乳サイズ <= -1
			;貧乳の乳は高く売れる
			TIMES 牝牛ミルク売却額 , 2.5
			
			SETCOLOR C_L_BLUE
			PRINTFORM [%GET_TALENTNAME(121,牝牛乳サイズ)%] 
			RESETCOLOR
			PRINTFORML の乳は高く買取してもらえるようだ…
		ELSEIF 牝牛乳サイズ >= 5
			;規格外な
			TIMES 牝牛ミルク売却額 , 0.15
		
			SETCOLOR C_H_PINK
			PRINTFORM [%GET_TALENTNAME(121,牝牛乳サイズ)%] 
			RESETCOLOR
			PRINTFORML の乳は買取が難しいようだ…
		ELSE
			;それ以外なら普通の価格に
			TIMES 牝牛ミルク売却額 , 0.8
		
			SETCOLOR C_PINK
			PRINTFORML [%GET_TALENTNAME(121,牝牛乳サイズ)%] 
			RESETCOLOR
		ENDIF
		
		PRINTFORML %牝牛名称%から搾った{牝牛ミルクml}mlのミルクを{牝牛ミルク売却額}円で売却した…
		PRINTFORML
		CUSTOMDRAWLINE —
		
		;リセットも兼ねて、ミルクの情報を削除
		CALL SET_FIELD_INFO(決闘者,"魔罠",参照場所,"T1汎数",0,上書きモード)
		CALL SET_FIELD_INFO(決闘者,"魔罠",参照場所,"汎数",0,上書きモード)
	ENDIF
ENDIF


;ここから牝牛のカード処理-----

@CARDNAME_179556,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC カード名
#DIMS DYNAMIC 参照先
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カードID
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
			RESULTS = 【美乳】ホルスタイン
	CASE "略称"
			RESULTS = 【美乳】牝牛
	CASE "カテゴリ"
		RESULTS:0 = ホルス
		RESULTS:1 = ホルスタイン
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_179556(参照先)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 参照先
#DIMS DYNAMIC 参照先チェック
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カードID
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 通常モン
	CASE "属性"
		;イムセティと同じ
		RETURN 闇属性
	CASE "種族"
		RETURN 獣族
	CASE "レベル"
		RETURN 12
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
	CASE "性別"
		RETURN 牝性
	CASE "分割"
		RETURN デュアル
ENDSELECT

RETURN 0

@CARD_EXPLANATION_179556(種類)
#DIM DYNAMIC 種類

PRINTL このカードは通常モンスターとして扱い、魔法＆罠ゾーンに置かれたカードによってステータスが決定する。
PRINTL 自分フィールドに「ホルスタイン-ミルクファーム」が存在する場合、以下の効果を得る。
PRINTL 
PRINTL このカードの②の効果は１ターンに１度しか使用できない。
PRINTL ①：メインフェイズに発動できる。このカードの乳を計測する。
;CALLFORM CARD_ABLE(0,戦場起動)
PRINTL ②：バトルフェイズに発動できる。このカードの乳を搾り発情させる。
PRINTL その後、このターン中以下の効果を適用する。
PRINTL ●このカードはバトルフェイズ中に２回攻撃できる。
PRINTL ●このカードは一度だけ戦闘・効果では破壊されない。
CALLFORM CARD_ABLE(1,効果後誘発)
PRINTL ③：発情したこのカードの攻撃が中断された場合、発動できる。
PRINTL 攻撃対象のモンスターを「ホルスタイン」カードとして扱い、その攻守を半分にする。

@CARDCAN_179556(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 別途カードID

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,179556)
SIF RESULT == 1
	RETURN 0

IF 種類 == 戦場起動
	SIF GETVARS(@"%決闘者%_フィールド名") != "ミルクファーム"
		RETURN 0
	IF メインフェイズ == 1
		SIF ターンプレイヤー == "相手" ;ただ情報を表示するだけなので、NPCは使用しない(ターン1付けてないから多分連打するし…)
			RETURN 0
	ELSEIF バトルフェイズ == 1
		SIF 発動済_1(決闘者,179556) 
			RETURN 0
	ENDIF
	
	RETURN 1
ELSEIF 種類 == 攻撃後強制誘発;本当はミルクファームなきゃダメだけど、ただ致すだけだし良いか…
	SIF ゾーン != "戦場"
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	SIF GETVAR(@"%決闘者%_戦闘カード") != 場所
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_発情:場所") == 0
		RETURN 0
	SIF GETVAR(@"%決闘者%_攻撃無効_保") != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 効果後誘発
	SIF GETVARS(@"%決闘者%_フィールド名") != "ミルクファーム"
		RETURN 0
	;発情時に攻撃中断された場合
	SIF ゾーン != "戦場"
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	SIF GETVAR(@"%決闘者%_戦闘カード") != 場所
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_発情:場所") == 0
		RETURN 0	
	SIF GETVAR(@"%決闘者%_攻撃無効") != 1
		RETURN 0
	RETURN 1
	
ELSEIF 種類 == 効果後強制誘発
	SIF ゾーン != "戦場"
		RETURN 0
	;守備表示の場合
	SIF GETVAR(@"%決闘者%_MZ_表:場所") == 1 
		RETURN 0
	;自分が特殊状態：ドリップ(99554)の場合
	SIF GETVAR(@"%決闘者%_MZ_特殊状態:場所") != 99554
		RETURN 0
	
	CALL FULLRESET
	効果条件固カテゴリ = ホルスタイン
	CALL CARD_CHECK_FIN_DATA(決闘者,"効果発動")
	SIF RESULT == 1
		RETURN 1
	
	CALL FULLRESET
	効果条件固カテゴリ = ホルスタイン
	CALL CARD_CHECK_FIN_DATA(対面者,"効果発動")
	SIF RESULT == 1
		RETURN 1
	
	RETURN 0
ENDIF

@CARDEFFECT_179556(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 参照カードID
#DIM DYNAMIC 書換先カードID
#DIM DYNAMIC 参照場所
#DIM DYNAMIC 牝牛対象
#DIMS DYNAMIC 牝牛名称
#DIM DYNAMIC 攻撃対象
#DIM DYNAMIC 攻撃対象カードID
#DIMS DYNAMIC 相手名称
#DIMS DYNAMIC 搾手名称
#DIM DYNAMIC 搾手
#DIM DYNAMIC 絶頂回数
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC カテゴリ名称
#DIM DYNAMIC T1ミルクml
#DIM DYNAMIC ミルクml

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

書換先カードID = 179556
カテゴリ名称 = 登録情報：ホルスタイン-美乳

;ホルスタイン情報の取得
CALL HORUSTEIN_GET_DATA(決闘者,場所,179556)
牝牛名称 = %RESULTS:0%
参照場所 = RESULT:0
牝牛対象 = RESULT:1
T1ミルクml = RESULT:3	;搾乳量(1ターン中)
ミルクml = RESULT:4		;搾乳量(決闘中総量)
参照カードID = RESULT:99

;実行者によって文章の相手を切り替える
IF 決闘者 == "自分"
	搾手名称 = %CALLNAME:MASTER%
	搾手 = MASTER
ELSE
	;対戦相手
	CALL CARD_EROTICS(相手プレイヤー,決闘者)
	IF RESULT != -1
		搾手名称 = %CALLNAME:RESULT%
	ELSE
		搾手名称 = "対戦者"
	ENDIF
	搾手 = RESULT
ENDIF

;攻撃対象の名前を取得
IF 種類 == 攻撃後強制誘発 || 種類 == 効果後誘発
	攻撃対象 = 戦闘場所(対面者)
	;名称の設定
	IF 攻撃対象 == 99
		CALL CARD_EROTICS(相手プレイヤー,決闘者)
		IF RESULT != -1
			相手名称 = %CALLNAME:RESULT%
		ELSE
			相手名称 = "対戦相手"
		ENDIF
	ELSE
		攻撃対象カードID = GETVAR(@"%対面者%_MZ_ID:攻撃対象")
		TRYCALLFORM CARDNAME_{攻撃対象カードID},"略称"
		相手名称 = %RESULTS%
	ENDIF
ENDIF

IF 種類 == 戦場起動 && メインフェイズ == 1
	;ステータス表示させるだけなので、無効にさせない
	効果無効不能 = 1
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,書換先カードID)
	IF RESULT == 0 && 参照カードID != -1
		
		PRINTFORML 
		PRINTFORMW ホルスの従者達が%牝牛名称%を取り囲んでいる…
		
		;名前から胸のサイズの変動まで表示
		CALL HORUSTEIN_PRINT_STATE(決闘者,場所,179556, GETVAR(@"%決闘者%_MZ_胸追:場所"))
		

	ENDIF

ELSEIF 種類 == 戦場起動 && バトルフェイズ == 1
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,書換先カードID,1)
	
	IF RESULT == 0 && 参照カードID != -1
		;胸を搾る
		IF GETVAR(@"%決闘者%_MZ_個Ｃ:場所") == 0
			;1回目
			PRINTFORML %搾手名称%は%牝牛名称%の乳に手を伸ばすと、その輪郭が変わらないように指を滑らせていく…
			PRINTFORMW 肌から乳へと変化していく感触を楽しみながら、徐々にその中心へと搾るような動きで胸を意識させる。
			PRINTFORM %牝牛名称%から
			SETCOLOR C_PINK
			PRINTFORM 「モー…❤」
			RESETCOLOR
			PRINTFORML と声が漏れるころには、%牝牛名称%の乳はピンっと張り、その先端は搾って欲しそうに艶めいていた…
			PRINTFORML %搾手名称%はキュッと指を搾ると、%牝牛名称%は喜ぶように白い液体を滴らせた…
			SETCOLOR C_PINK
			PRINTFORMW 「もぉっ❤ んっぁっ❤ ―…❤」
			RESETCOLOR
			IF 牝牛対象 != -1
				CALL EROTIC_MOVE("バスト調教",牝牛対象,搾手,300)
			ENDIF
			
			CALL STATCHANGE_EFFECT(決闘者,"自身",場所,"発情",175)
			CALL SET_FIELD_INFO(決闘者,"戦場",場所,"個Ｃ",1,上書きモード)
		ELSE
			;2回目
			PRINTFORML %搾手名称%は%牝牛名称%の乳に再び手を伸ばすと、その乳により白く濡れた指を滑らせた…
			PRINTFORML 既に一度搾られているというのに、指が触れただけでも尖らせた先端からはポタポタと漏れてしまっている…
			SETCOLOR C_PINK
			PRINTFORML 「モー…❤ モー…❤」
			RESETCOLOR
			PRINTFORMW 優しく撫でるような刺激では物足りないのか、%牝牛名称%は%搾手名称%の手に胸を押し付け始めた…
			PRINTFORML 
			PRINTFORML %搾手名称%は期待を裏切るかのように丁寧に優しくなぞると、先端をパチンっと弾き、高まった性感を一気に解放させた！
			PRINTFORML %牝牛名称%の乳は突然の刺激に耐えきれず、壊れた蛇口のように白い液体を零れさせていく…
			SETCOLOR C_PINK
			PRINTFORMW 「…ぁっ！？ ぉぉおっ～～～❤❤」
			RESETCOLOR
			IF 牝牛対象 != -1
				CALL EROTIC_MOVE("バスト調教",牝牛対象,搾手,300)
			ENDIF
			
			CALL STATCHANGE_EFFECT(決闘者,"自身",場所,"発情",185)
			CALL SET_FIELD_INFO(決闘者,"戦場",場所,"個Ｃ",0,上書きモード)
		ENDIF
		;二回攻撃、一度だけ破壊されない
		CALL SET_FIELD_INFO(決闘者,"戦場",場所,"戦",1)
		CALL SET_FIELD_INFO(決闘者,"戦場",場所,"一破",1)
		
		CALL HORUSTEIN_GET_MILK(決闘者,参照カードID,GETVAR(@"%決闘者%_MZ_胸追:場所"))
		
		;発情
		SETCOLOR C_PINK
		PRINTFORML 
		PRINTFORML %牝牛名称%は乳を搾られた影響か、酷く発情している…
		PRINTFORML
		RESETCOLOR

		IF ターンプレイヤー != 決闘者
			;相手ターンに発動した場合
			PRINTFORML %牝牛名称%は乳を搾られた余韻に浸っている…

			;2回搾られた場合(特に効果無いのに、何故二回も搾った？)
			;PRINTFORML %牝牛名称%は何故乳を搾られたか解らず、緩んだ口元は開いたままになってしまっている…
			
			;絶頂後の牝牛鳴き声
			CALL HORUSTEIN_RAND_COW_TXT(2,2)
		ENDIF
		
		;美乳
		;PRINTFORMW 「モっ…モォ…❤」
		;愛乳
		;PRINTFORM 「も…もぉー…❤」
		;旨乳
		;PRINTFORM 「モぉっ❤ ォゥ…❤」
		;EX乳
		;PRINTFORM 「…ぉぉ❤ …モォォ❤」
		
		PRINTFORMW
	ENDIF

ELSEIF 種類 == 攻撃後強制誘発
	;発情時に攻撃が中断された場合
	
	;ひとりえっち(ここは一回触ってるので短めに。効果処理もあるしね)
	PRINTFORML …発情した%牝牛名称%は%相手名称%にふらふらと近寄るが、視線を足元に向けると途中で足を止めてしまった。
	PRINTFORML ふぅふぅと呼吸を荒げている%牝牛名称%は、自身の乳から目が離せなくなってしまう…
	PRINTFORMW %牝牛名称%は%搾手名称%に搾って貰った記憶を呼び覚ますように、乳をゆっくりと擦り始めた…
	
	;牝牛の鳴き声を鳴らす
	CALL HORUSTEIN_RAND_COW_TXT(1)
	
	IF 牝牛対象 != -1
		CALL EROTIC_MOVE("バスト調教",牝牛対象,-1,500)
	ENDIF
	絶頂回数 += RESULT:1
	IF 絶頂回数 > 0
		PRINTFORML %牝牛名称%は{絶頂回数}回絶頂したようだ。
		PRINTFORMW 余程気持ち良かったのか、%牝牛名称%の周りには小さな白い斑点が飛び散ってしまっている…
	ENDIF
	
	PRINTFORML 
ELSEIF 種類 == 効果後誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,書換先カードID)
	
	IF RESULT == 0
		IF 攻撃対象 == 5 
			;直接攻撃失敗時
			;再び手を伸ばすと、相手フィールドに他に戦闘を行えるモンスターがいない為か、タガが外れたように…
			
			PRINTFORMW %牝牛名称%は相手フィールドに戦闘を行えるモンスター居ないことを確認すると、さらなる快楽を求めて指を動かし始めた…
			絶頂回数 += 2
			SETCOLOR C_PINK
			
			;牝牛の鳴き声を鳴らす
			CALL HORUSTEIN_RAND_COW_TXT(絶頂回数)
			
			IF 牝牛対象 != -1
				REPEAT 絶頂回数
					CALL EROTIC_MOVE("バスト調教",牝牛対象,-1,200)
				REND
			ENDIF
			
			RESETCOLOR
			PRINTFORML 鳴き声が響き終わると、%牝牛名称%は「ぱちゃんっ」と音を立てて白い水たまりに倒れ込んだ…
			PRINTFORMW 好き放題気持ちよくなった%牝牛名称%は、満足そうな笑みを浮かべている…
			;絶頂後の牝牛鳴き声
			CALL HORUSTEIN_RAND_COW_TXT(2,2)
			PRINTFORMW 
		ELSE
			;モンスター攻撃時に失敗
			PRINTFORML 発情した%牝牛名称%は%相手名称%を押し倒し、そのまま馬乗りの姿勢になった！
			PRINTFORM %牝牛名称%は
			SETCOLOR C_PINK
			;美乳
			PRINTFORM 「モー…❤」
			;愛乳
			;PRINTFORM 「もぉー…❤」
			;旨乳
			;PRINTFORM 「モォゥ…❤」
			;EX乳
			;PRINTFORM 「モォォ❤」
			RESETCOLOR
			
			IF GETVAR(@"%決闘者%_MZ_服状:場所") >= 2
				;全裸の場合
				PRINTFORML と鳴くと、生乳をぴたっと%相手名称%に押し付けた…
				PRINTFORMW %牝牛名称%は下着を付けていないため、乳の感覚がダイレクトに伝わってくる…
				PRINTFORML 押し付けられた乳はしっとりと濡れており、先端からはとぷとぷと白い液体が滴っている。
				PRINTFORML %牝牛名称%は自身から零れた液体をなぞると、%相手名称%に塗りつけるように模様を残した…
				PRINTFORMW %相手名称%は母乳によってマーキングされてしまった…
				
				SETCOLOR C_GRAY
				PRINTFORMW ※全裸のため、攻守を下げる効果は適用されません
				RESETCOLOR
			ELSE
				PRINTFORML と鳴くと、乳をぴたっと%相手名称%に押し付けた…
				PRINTFORMW 黒と白の如何にも牛柄な下着の裏は"ホルス"による契約書が刻まれている…
				PRINTFORML 
				PRINTFORMW ミルクファームの契約が履行され、%相手名称%の攻守は半分になった！
				PRINTFORMW %相手名称%は母乳によってマーキングされてしまった…
				
				CALL SET_FIELD_INFO(対面者,"戦場",攻撃対象,"永攻",-(GETVAR(@"%対面者%_MZ_攻:攻撃対象")) / 2)
				CALL SET_FIELD_INFO(対面者,"戦場",攻撃対象,"永守",-(GETVAR(@"%対面者%_MZ_守:攻撃対象")) / 2)
			ENDIF
			
			CALL CARD_SET_CATEGORY(対面者,"戦場",攻撃対象,"ホルスタイン")
		ENDIF
		
	ENDIF
ELSEIF 種類 == 効果後強制誘発
	;ドリップ状態(守備表示時、ホルスタイン以外のカードが発動する度に搾られる)
	
	SETCOLOR C_PINK
	CUSTOMDRAWLINE —
	PRINTFORML
	PRINTFORML %牝牛名称%はドリップ装置に繋がれている…
	PRINTFORML 
	CUSTOMDRAWLINE —
	RESETCOLOR
	
	PRINTFORML
	PRINTFORML %牝牛名称%の胸に取り付けられた装置が僅かに振動し、零れ落ちたモノがミルクサーバーに溜まっていく…
	PRINTFORML
	
	;短め鳴き
	CALL HORUSTEIN_RAND_COW_TXT(1,0,10)
	PRINTFORML 
	
	;搾乳する(通常よりも少量)
	CALL HORUSTEIN_GET_MILK(決闘者,参照カードID,GETVAR(@"%決闘者%_MZ_胸追:場所")-5)
	PRINTFORML 
	
	;乳を吸ってLP回復
	CALL LIFE_CHANGE(決闘者,"増加","効果",100)
ENDIF

