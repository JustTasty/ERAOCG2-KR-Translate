@CARDNAME_79555,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

;カード制作者：xonng
SELECTCASE 参照先
	CASE "名前"
		RESULTS = ホルスタイン-ミルクファーム
	CASE "略称"
		RESULTS = ミルクファーム
	CASE "カテゴリ"
		RESULTS:0 = ホルス
		RESULTS:1 = ホルスタイン
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_79555(参照先)

#DIMS DYNAMIC 参照先
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カードIDチェック
#DIM DYNAMIC 魔法罠数チェック

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN フィールド
	CASE "追加日"
		RETURN 250325
ENDSELECT

RETURN 0

@CARD_EXPLANATION_79555(種類)
#DIM DYNAMIC 種類

CALL TEXT_DECORATION("ROGUE")
PRINTL 自分フィールドは「ホルスタイン-ミルクファーム」となる。
PRINTL このカードの効果の発動に対して、相手はカードの効果を発動できない。
CALLFORM CARD_ABLE(1,魔罠起動)
PRINTL ①：バトルフェイズ、自分メインフェイズに発動できる。
PRINTL 手札を１枚捨て、捨てたカードによって以下の効果を適用する。
PRINTL ●通常モンスター：そのカードが獣族牝性モンスターの場合、１０００ＬＰ回復する。
PRINTL ●効果モンスター：墓地の「ホルスタイン」モンスターを１体選んで特殊召喚する。
PRINTL ●魔法カード：デッキから「ホルスタイン」通常魔法カードを１枚選んで手札に加える。
PRINTL ●罠カード：デッキから「ホルスタイン」罠カードを１枚選んで手札に加える。


CALL TEXT_DECORATION("ROGUE")

@CARDCAN_79555(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,79555)
SIF RESULT == 1
	RETURN 0

IF 種類 == 手札起動
	IF 種類 == 魔罠起動
		SIF GETVAR(@"%決闘者%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	RETURN 1
ELSEIF 種類 == 魔罠起動 
	SIF !(メインフェイズ == 1 && ターンプレイヤー == 決闘者) && バトルフェイズ != 1
		RETURN 0
	;他、各種効果の発動可否をチェックする(下記の内、一つでもOKなら発動可能)
	;通常モンスター(手札の通常)
	CALL FULLRESET
	効果範囲手札 = 1
	効果条件種類 = 通常モン
	;効果条件種族 = 獣族
	;効果条件性別 = 牝性
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT == 1
		RETURN 1
	
	;効果モンスター(手札の効果+墓地のホルスタインモンスター)
	CALL FULLRESET
	効果範囲手札 = 1
	効果条件種類:0 = 効果モン
	効果条件種類:1 = 儀式
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	IF RESULT == 1
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件種族 = 獣族
		効果条件カテゴリ = ホルスタイン
		効果条件召喚可 = 1
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT == 1
			RETURN 1
	ENDIF
	
	;魔法カード(手札の魔法+デッキのホルスタイン魔法カード)
	CALL FULLRESET
	効果範囲手札 = 1
	効果条件種類 = 魔法全般
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	IF RESULT == 1
		CALL FULLRESET
		効果範囲デッキ = 1
		効果条件種類 = 通常魔法
		効果条件カテゴリ = ホルスタイン
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT == 1
			RETURN 1
	ENDIF
	
	;罠(手札の罠+デッキのホルスタイン罠カード)
	CALL FULLRESET
	効果範囲手札 = 1
	効果条件種類 = 罠全般
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	IF RESULT == 1
		CALL FULLRESET
		効果範囲デッキ = 1
		効果条件種類 = 罠全般
		効果条件カテゴリ = ホルスタイン
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT == 1
			RETURN 1
	ENDIF
	
	RETURN 0
ELSEIF 種類 == 永続効果
	SIF ゾーン != "魔罠"
		RETURN 0	
	RETURN 1
ELSEIF 種類 == チェーン可否
	SIF !GROUPMATCH(チェーン可否種類, 魔罠起動)
		RETURN 0

	RETURN 1
ENDIF

@CARDEFFECT_79555(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 効果条件_1
#DIM DYNAMIC 効果条件_2
#DIM DYNAMIC 効果条件_3
#DIM DYNAMIC 効果条件_4
#DIM DYNAMIC 選択カード種類
#DIMS DYNAMIC カテゴリ名称
#DIMS DYNAMIC 牝牛名称
#DIM DYNAMIC 牝牛対象
#DIM DYNAMIC 牝牛胸追
#DIM DYNAMIC 参照場所;魔法罠に置かれたホルスタインの参照先
#DIM DYNAMIC 参照カードID;魔法罠に置かれたホルスタインの参照先

IF  種類 == 手札起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,79555)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所,魔罠行き)
	ENDIF
ELSEIF 種類 == 魔罠起動	
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,79555)
	IF RESULT == 0
;＞＞条件設定開始―――――――――――――――――――――――――――――――――
		;通常モンスター(手札の通常)
		CALL FULLRESET
		効果範囲手札 = 1
		効果条件種類 = 通常モン
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT == 1
			効果条件_1 = 1

		;効果モンスター(手札の効果+ホルスタインモンスターのチェック)
		CALL FULLRESET
		効果範囲手札 = 1
		効果条件種類:0 = 効果モン
		効果条件種類:1 = 儀式
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			効果範囲墓地 = 1
			効果条件種族 = 獣族
			効果条件カテゴリ = ホルスタイン
			CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
			SIF RESULT == 1
				効果条件_2 = 1
		ENDIF
		
		;魔法カード(手札の魔法+デッキのホルスタイン魔法カード)
		CALL FULLRESET
		効果範囲手札 = 1
		効果条件種類 = 魔法全般
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			効果範囲デッキ = 1
			効果条件種類 = 通常魔法
			効果条件カテゴリ = ホルスタイン
			CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
			SIF RESULT == 1
				効果条件_3 = 1
		ENDIF
		
		;罠(手札の罠+デッキのホルスタイン罠カード)
		CALL FULLRESET
		効果範囲手札 = 1
		効果条件種類 = 罠全般
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			効果範囲デッキ = 1
			効果条件種類 = 罠全般
			効果条件カテゴリ = ホルスタイン
			CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
			SIF RESULT == 1
				効果条件_4 = 1
		ENDIF
;＞＞条件設定終了―――――――――――――――――――――――――――――――――

		カウンタ = 0
		;0から始まる連番でなければならない
		CALL FULLRESET
		効果範囲手札 = 1
		IF 効果条件_1
			効果条件種類:カウンタ = 通常モン
			カウンタ++
		ENDIF
		IF 効果条件_2
			効果条件種類:カウンタ = 効果モン
			カウンタ++
			効果条件種類:カウンタ = 儀式
			カウンタ++
		ENDIF
		IF 効果条件_3
			効果条件種類:カウンタ = 通常魔法
			効果条件種類:(カウンタ+1) = 装備魔法
			効果条件種類:(カウンタ+2) = 速攻魔法
			効果条件種類:(カウンタ+3) = 永続魔法
			効果条件種類:(カウンタ+4) = フィールド
			カウンタ += 5
		ENDIF
		IF 効果条件_4
			効果条件種類:カウンタ = 通常罠
			効果条件種類:(カウンタ+1) = 永続罠
			カウンタ++
		ENDIF
		CALL CARD_ADD_GY_BASIC(決闘者,"手札",ゾーン,場所)
		IF RESULT == 1
			;★デバッグ用チェッカー
			;PRINTFORMW 効果1：{効果条件_1} 効果2：{効果条件_2} 効果3：{効果条件_3} 効果4：{効果条件_4}
			選択カードID = RESULT:1
			CALLFORM CARD_{選択カードID},"種類"
			選択カード種類 = RESULT

			SELECTCASE 選択カード種類
			;●通常モンスター：そのカードが獣族牝性モンスターの場合、LPを1000ポイント回復する。
			CASE 通常モン
				CALLFORM CARD_{選択カードID},"性別"
				IF RESULT == 牝性
					CALLFORM CARD_{選択カードID},"種族"
					IF RESULT == 獣族
						;ランダム効果でも良いかも、LP回復、ドロー、召喚権追加みたいな
						CALL HORUSTEIN_GET_DATA(決闘者,,選択カードID)
						牝牛名称 = %RESULTS:0%
						参照場所 = RESULT:0
						参照カードID = RESULT:99
						牝牛胸追 = GETVAR(@"%決闘者%_魔_個Ｃ:参照場所")						
						PRINTFORML %牝牛名称%から搾ったミルクを飲み干した！
						;ホルスタインモンスターの場合、きっちり搾乳する
						IF INRANGE (選択カードID, 179556, 179558) 
							;搾乳する
							CALL HORUSTEIN_GET_MILK(決闘者,参照カードID,牝牛胸追);★
						ENDIF						
						;乳を吸ってLP回復
						CALL LIFE_CHANGE(決闘者,"増加","効果",1000)
					ELSE
						;獣じゃないなら乳を揉むだけとか？						
					ENDIF
				ENDIF
			;●効果モンスター：墓地の「ホルスタイン」モンスターを特殊召喚する。
			CASE 効果モン,儀式
				CALL FULLRESET
				効果範囲墓地 = 1
				効果条件カテゴリ  = ホルスタイン
				CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
				
				IF RESULT != -1
					選択カードID = RESULT:1
					CALL HORUSTEIN_GET_DATA(決闘者,カード召喚先,選択カードID);★
					牝牛名称 = %RESULTS:0%
					;◆小さな胸でも違和感のない描写にする
					SELECTCASE 選択カードID
					CASE 179556
						;美乳
						PRINTFORMW %牝牛名称%の美しい乳がしなやかに揺れた…
						CALL HORUSTEIN_RAND_COW_TXT(1,0)
					CASE 179557
						;愛乳
						PRINTFORMW %牝牛名称%の可愛らしい乳首がぷるんと震えた…
						CALL HORUSTEIN_RAND_COW_TXT(1,1)
					CASE 179558
						;旨乳
						PRINTFORMW %牝牛名称%の思わず吸い付きたくなる乳がしっとりと艶めいている…
						CALL HORUSTEIN_RAND_COW_TXT(1,2)
					CASE 179559
						;EXは明確にデカイ
						PRINTFORMW %牝牛名称%の頭より大きな乳がどぷんっと音を立てて震えた…
						CALL HORUSTEIN_RAND_COW_TXT(1,3)
					ENDSELECT
				ENDIF
				PRINTFORMW 
			;●魔法カード：「ホルスタイン」魔法カードを手札に加える。
			CASE 20 TO 29
				CALL FULLRESET
				効果条件カテゴリ = ホルスタイン
				効果条件種類 = 魔法全般
				CALL CARD_ADD_BASIC(決闘者,"デッキ",ゾーン,場所)
			;●罠カード：「ホルスタイン」罠カードを手札に加える。
			CASE 30 TO 39	
				CALL FULLRESET
				効果条件カテゴリ = ホルスタイン
				効果条件種類 = 罠全般
				CALL CARD_ADD_BASIC(決闘者,"デッキ",ゾーン,場所)
			ENDSELECT
		ENDIF
	ENDIF

ELSEIF 種類 == 永続効果
	CALL SET_FIELD_MAGIC_INFO(決闘者, "ミルクファーム")
ENDIF