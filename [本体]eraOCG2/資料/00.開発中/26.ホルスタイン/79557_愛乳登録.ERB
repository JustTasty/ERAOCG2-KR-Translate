@CARDNAME_79557,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

;カード制作者：xonng
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 【愛乳】ホルスタイン-プリティ
	CASE "略称"
		RESULTS = 암소登録【愛乳】
	CASE "カテゴリ"
		RESULTS:0 = ホルス
		RESULTS:1 = ホルスタイン
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_79557(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "추가日"
		RETURN 250325
ENDSELECT

RETURN 0

@CARD_EXPLANATION_79557(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL このカードは１ターンに１度しか발동できない。
PRINTL このカードの①の효과はデュエル中１度しか適用できず、①の효과で選択したカードは「암소」の登録情報として지속마법となる。
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ①：손패・덱から「ホルスタイン」以外の牝性モンスターを１장選択し、そのモンスターを지속마법カード扱いで자신の마법＆함정ゾーンに表側표시で置く。
PRINTL その後、덱外から「【愛乳】ホルスタイン」１장을 손패に加え、
PRINTL 자신の덱・손패・モンスターゾーン・묘지に存在する全ての同名カードを「【愛乳】ホルスタイン」に書き換える。
PRINTL この효과の処理後、以下の효과から選択する。
PRINTL ●손패から「【愛乳】ホルスタイン」１장을 특수소환する。
PRINTL ●덱から「ホルスタイン」通常마법を１枚選んで손패に加える。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_79557(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

;戦闘終了플래그を拾うため
IF 종류 == 永攻紋章起動 && (자신_듀얼패배플래그 > 0 || 상대_듀얼패배플래그 > 0)
	RETURN 1
ENDIF

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,79557)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_D1(듀얼리스트,79557) 
		RETURN 0
	SIF 발동済_1(듀얼리스트,79557) 
		RETURN 0
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲덱 = 1
	효과条件성별 = 牝性
	효과条件固カテゴリ = ホルスタイン
	
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 永紋章起動
	RETURN 1
ELSEIF 종류 == 永攻紋章起動
	RETURN 1
ENDIF

@CARDEFFECT_79557(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 参照カードID
#DIM DYNAMIC 書換先カードID
#DIM DYNAMIC 所有者
#DIM DYNAMIC 参照場所
#DIM DYNAMIC 암소대상
#DIM DYNAMIC 암소所有
#DIMS DYNAMIC 암소名称
#DIMS DYNAMIC 암소略称
#DIM DYNAMIC 암소속성
#DIM DYNAMIC 암소レベル
#DIM DYNAMIC 암소攻撃力
#DIM DYNAMIC 암소守備力
#DIM DYNAMIC 암소튜너
#DIM DYNAMIC 암소乳サイズ
#DIM DYNAMIC 암소ミルクml
#DIM DYNAMIC 암소ミルク売却額
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 貧乳可否
#DIMS DYNAMIC カテゴリ名称

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

書換先カードID = 179557
カテゴリ名称 = 登録情報：ホルスタイン-愛乳

IF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79557,1)
	IF RESULT == 0 && 마함数(듀얼리스트) < 5
		
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲덱 = 1
		효과条件성별 = 牝性
		효과条件固カテゴリ = ホルスタイン
		효과추가カテ = %カテゴリ名称%
		CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")
		
		選択カードID = RESULT:1
		
		IF RESULT != 0
			;마함に置く(デュエル中１度)
			CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79557,20)
			
			;ホルスタイン情報の취득(カードIDから各種情報を취득)
			CALL HORUSTEIN_GET_DATA(듀얼리스트,-1,書換先カードID)
			암소名称 = %RESULTS:0%
			암소略称 = %RESULTS:1%
			参照場所 = RESULT:0
			암소대상 = RESULT:1
			参照カードID = RESULT:99
			CALL HORUSTEIN_GET_BUST(参照カードID)
			암소乳サイズ = RESULT:2
			
			;耐性と발동無効の付与
			CALL SET_FIELD_INFO(듀얼리스트,"마함",参照場所,"破耐",2)
			CALL SET_FIELD_INFO(듀얼리스트,"마함",参照場所,"対耐",2)
			CALL SET_FIELD_INFO(듀얼리스트,"마함",参照場所,"発無",2)
			
			;カードの書き換え
			;필드のカード
			FOR カウンタ,0,5
				SIF GETVAR(@"%듀얼리스트%_MZ_ID:カウンタ") != 選択カードID
					CONTINUE
				CALL CARD_RECONSTRUCT(듀얼리스트,"필드",カウンタ,書換先カードID)
			NEXT
			
			;손패のカード
			FOR カウンタ,0,現손패(듀얼리스트)
				SIF GETVAR(@"%듀얼리스트%_손패:カウンタ") != 選択カードID
					CONTINUE
				CALL CARD_RECONSTRUCT(듀얼리스트,"손패",カウンタ,書換先カードID)
			NEXT
			
			;덱のカード
			FOR カウンタ,0,現山札数(듀얼리스트)
				SIF GETVAR(@"%듀얼리스트%_덱:カウンタ") != 選択カードID
					CONTINUE
				CALL CARD_RECONSTRUCT(듀얼리스트,"덱",カウンタ,書換先カードID)
			NEXT
			
			;묘지のカード
			FOR カウンタ,0,現묘지数(듀얼리스트)
				SIF GETVAR(@"%듀얼리스트%_묘지:カウンタ") != 選択カードID
					CONTINUE
				CALL CARD_RECONSTRUCT(듀얼리스트,"묘지",カウンタ,書換先カードID)
			NEXT
			
			PRINTFORMW 登録証に名前が刻まれると、どこからともなくホルスの守護者が現れた！
			PRINTFORML %암소名称%は抵抗する間もなく三人の守護者に押さえつけられると、カウベルの付いた首輪を取り付けられてしまう…
			PRINTFORML 従者の一人がカウベルを指ではじくと、カララン…と%암소名称%の頭に音が鳴り響いていく…
			PRINTFORML 
			PRINTFORML 「…もっ…もぉー…？」
			PRINTFORMW 
			PRINTFORML %암소名称%はぼんやりとしてた頭を起すように、必死に声を上げようとするが、その声は암소の鳴き声に変わってしまう。
			PRINTFORMW 困惑する%암소名称%を무시하고 、守護者はそのガッチリとした手で乳を丁寧に암소に相応しい"商品"へと"調整"していく…
			
			IF 암소乳サイズ >= 0
				
				SETCOLOR C_GRAY
				PRINTFORML 守護者の一人がこちらを見ている… 
				PRINTFORML
				RESETCOLOR
				CALL YN_CHOICE(듀얼리스트,"「貧乳」にする","乳のサイズを변경しない")
				
				貧乳可否 = RESULT
			ENDIF
			
			SETCOLOR C_PINK
			PRINTFORML 
			PRINTFORML 「もぉー…」「もぉ、もぉー…❤」
			PRINTFORMW 
			RESETCOLOR
			PRINTFORML …調整が終わったようだ。
			PRINTFORML 先ほどまでと様子が変わり、声を上げるたびに熱を帯びた암소の鳴き声へと変わっていく…
			PRINTFORML 
			IF 암소乳サイズ <= -1 || 貧乳可否 == 1
				;元々貧乳未満の場合、変わらなかった…
				
				PRINTFORM %암소名称%の乳は
				IF 암소乳サイズ <= -1
					SETCOLOR C_L_BLUE
				ELSE
					SETCOLOR C_PINK
				ENDIF
				PRINTFORM  [%GET_TALENTNAME(121,암소乳サイズ)%] 
				RESETCOLOR
				PRINTFORM から……特に変動はなかったようだ……
				PRINTFORMW
				
				;着替え
				PRINTFORML %암소名称%は押さえつけていた守護者が何時の間にか居なくなったのを確認すると、
				PRINTFORMW 火照る身体が熱いのか、服を次々と脱ぎ始めた…
			ELSE
				;貧乳より大きい場合、貧乳にする
				PRINTFORM %암소名称%の乳は
				SETCOLOR C_PINK
				PRINTFORM  [%GET_TALENTNAME(121,암소乳サイズ)%] 
				RESETCOLOR
				PRINTFORM から
				SETCOLOR C_L_BLUE
				PRINTFORM  [%GET_TALENTNAME(121,-1)%] 
				RESETCOLOR
				PRINTFORML になってしまった…
				PRINTFORMW
				
				;着替え
				PRINTFORML %암소名称%は押さえつけていた守護者から解放されたが
				PRINTFORMW 少し身体を動かしただけで、サイズの合わなくなった服が体からズリ落ちてしまった…
				
				;元々が小柄体型じゃなければ표시(実際には변경しない)
				IF 암소대상 != -1 && TALENT:암소대상:100 != -1
					PRINTFORML
					PRINTFORMW %암소名称%は[小柄体型]になってしまったようだ…
				ENDIF
			ENDIF
			
			PRINTFORML 
			PRINTFORML %암소名称%は足元に用意してあった衣装を拾い、それが当然というように牛柄マイクロビキニに着替え始めた…
			
			;服装の변경(牛柄ビキニと角)
			IF 암소대상 != -1 
				;アクセサリ&帽子：つけ角・小(2枠目を변경)
				EQUIP:암소대상:2 = 20
				CFLAG:암소대상:(200 + 2) = 20
				;靴、靴下は触らない
				;ズボン～インナーまでは全剥き
				FOR カウンタ,6,13
					EQUIP:암소대상:カウンタ = 0
					CFLAG:암소대상:(200 + カウンタ) = 0
				NEXT
				;ワンピース：牛柄ビキニ(マイクロビキニは無かった)
				EQUIP:암소대상:15 = 33
				CFLAG:암소대상:(200 + 15) = 33
				
				;조교
				CALL EROTIC_MOVE("バスト조교",암소대상,-1,300)
			ENDIF
			
			;書き換えたカードを손패に加える。
			CALL CARD_GENERATE_HAND(듀얼리스트,書換先カードID)
			
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = ホルスタイン
			효과条件종류 = 通常마법
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			
			IF RESULT == 1
				;덱にある場合
				CALL YN_CHOICE(듀얼리스트,"「ホルスタイン」通常마법カードを손패に加える","「" + 암소名称 + "」を손패から특수소환する")
			ELSE
				;덱に存在しない場合、選択肢は표시しない
				PRINTFORML
				PRINTFORMW 「ホルスタイン」通常마법カードが덱に存在しない為、「%암소名称%」を손패から특수소환する…が選択されます。
				RESULT = 1
			ENDIF
			
			IF RESULT == 0
				;덱から「ホルスタイン」通常마법を손패に加える
				CALL FULLRESET
				효과範囲덱 = 1
				효과条件カテゴリ = ホルスタイン
				효과条件종류 = 通常마법
				CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
			ELSE
				;손패から특수소환
				CALL FULLRESET
				;選択時に【〇乳】ホルスタイン…と표시されるとｷﾞｮｯっとするので、손패から探して특수소환する
				FOR カウンタ,0,現손패(듀얼리스트)
					SIF GETVAR(@"%듀얼리스트%_손패:カウンタ") != 書換先カードID
						CONTINUE
					CALL CARD_SP_SUMMON(듀얼리스트,"손패",カウンタ)
					BREAK
				NEXT
				IF RESULT != 0
					PRINTFORMW %암소名称%の可愛らしい유두がぷるんと震えた…
					CALL HORUSTEIN_RAND_COW_TXT(1,1)
				ENDIF
				
			ENDIF
			
			PRINTFORML 
			CALL FULLRESET
			효과範囲마함 = 1
			효과範囲손패 = 1
			효과条件ID = 79555
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 1
				;손패・필드에 ホルスタインファームが存在する場合
				SETCOLOR C_PINK
				PRINTFORML %암소名称%の可愛らしい유두がぷるんと震えると、何も言わずに乳をこちらに突き出してきた…
				PRINTFORML %암소名称%は搾られる準備が出来ているようだ…
				PRINTFORMW 「……もっ❤ もぉー…❤」
				PRINTFORML 
				RESETCOLOR
			ENDIF
			
			PRINTFORML 
			;永紋章の표시をスキップ
			SKIPDISP 1
			CALL CARD_RESERV(듀얼리스트,79557,永紋章予約)
			CALL CARD_RESERV(듀얼리스트,79557,永攻紋章予約)
			SKIPDISP 0
			SETCOLOR C_YELLOW
			PRINTFORML 「【愛乳】ホルスタイン」は필드에 存在する場合「%암소名称%」として扱われます。
			RESETCOLOR
			PRINTFORMW
			
			IF 암소乳サイズ <= -1 || 貧乳可否 == 1
				;貧乳未満なら貧乳のまま
				암소乳サイズ = 0
			ELSE
				;貧乳にする
				암소乳サイズ = -(암소乳サイズ)-1
			ENDIF
			;愛乳は카운터を貧乳になるように調整してスタート(乳サイズ：DOWN)
			CALL SET_FIELD_INFO(듀얼리스트,"마함",参照場所,"個Ｃ",암소乳サイズ)
			
		ELSE
			PRINTFORMW 마법＆함정ゾーンにカードを置けなかったため、登録できませんでした…
		ENDIF
		
	ENDIF
	
	SKIPDISP 1
	CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
	SKIPDISP 0
	
ELSEIF 종류 == 永紋章起動 || 종류 == 永攻紋章起動
	;紋章を使用し、손패・フィールドの암소の情報を書き換え
	CALL FULLRESET
	CALL HORUSTEIN_GET_DATA(듀얼리스트,,179557)
	암소名称 = %RESULTS:0%
	암소略称 = %RESULTS:1%
	参照場所 = RESULT:0
	암소대상 = RESULT:1
	암소所有 = RESULT:2
	암소ミルクml = RESULT:4		;搾乳量(듀얼中総量)
	参照カードID = RESULT:99
	암소乳サイズ = GETVAR(@"%듀얼리스트%_魔_個Ｃ:参照場所")
	
	;参照先カードが吹っ飛んでたら処理しない
	IF 参照場所 != -1
		
		IF 듀얼리스트 == "자신"
			所有者 = 1
		ELSEIF 듀얼리스트 == "상대"
			所有者 = 2
		ENDIF
		
		;속성
		TRYCALLFORM CARD_{参照カードID},"속성"
		암소속성 = RESULT
		
		;レベルの취득
		TRYCALLFORM CARD_{参照カードID},"レベル"
		암소レベル = RESULT
		
		;攻守
		TRYCALLFORM CARD_{参照カードID},"攻撃力"
		암소攻撃力 = RESULT
		TRYCALLFORM CARD_{参照カードID},"守備力"
		암소守備力 = RESULT

		;愛乳の攻守は0に近いほど高くなる
		IF 암소攻撃力 >= 3000
			암소攻撃力 = 0
		ELSE 
			암소攻撃力 = 3000 - 암소攻撃力
		ENDIF

		IF 암소守備力 >= 3000
			암소守備力 = 0
		ELSE 
			암소守備力 = 3000 - 암소守備力
		ENDIF

		;튜너
		TRYCALLFORM CARD_{参照カードID},"튜너"
		암소튜너 = RESULT
		
		;손패のカードのレベル변경(손패は설정分Lvが下がる)
		IF 종류 == 永攻紋章起動
			CALL FULLRESET
			효과条件ID = 書換先カードID
			CALL STATCHANGE_HAND(듀얼리스트,"全体",場所,"永星",12 - 암소レベル)
		ENDIF
		
		;자신フィールドのカードで、所有者が자신
		FOR カウンタ,0,5
			SIF GETVAR(@"%듀얼리스트%_MZ_ID:カウンタ") != 書換先カードID
				CONTINUE
			SIF GETVAR(@"%듀얼리스트%_MZ_所有:カウンタ") != 所有者
				CONTINUE

			IF 종류 == 永紋章起動 
				CALL CARD_SET_RENAME(듀얼리스트,"필드",カウンタ,암소略称)
				CALL STATCHANGE_EFFECT(듀얼리스트,"自身",カウンタ,"変化元ID",参照カードID)
			ELSEIF 종류 == 永攻紋章起動 
				
				CALL STATCHANGE_OTHER(듀얼리스트,"自身",カウンタ,"属",암소속성)
				CALL STATCHANGE_EFFECT(듀얼리스트,"自身",カウンタ,"永星",암소レベル)
				CALL STATCHANGE_BASIC(듀얼리스트,"自身",カウンタ,암소攻撃力,암소守備力)
				CALL CARD_SET_CATEGORY(듀얼리스트,"필드",カウンタ,"E.R.A")
				;カテゴリ2は未설정なら변경する
				SIF GETVARS(@"%듀얼리스트%_MZ_追カテ2:カウンタ") == ""
					CALL CARD_SET_CATEGORY(듀얼리스트,"필드",カウンタ,"ホルスタイン",1)
				SIF 암소튜너 == 1
					CALL STATCHANGE_EFFECT(듀얼리스트,"自身",カウンタ,"一Ｔ",2)
				
				;지속마법扱いの자신に乗ってる카운터の数だけ乳を盛る
				CALL STATCHANGE_OTHER(듀얼리스트,"自身",カウンタ,"가슴追",암소乳サイズ)
			ENDIF
		NEXT
		
		;상대フィールドのカードで、所有者が자신
		FOR カウンタ,0,5
			SIF GETVAR(@"%対面者%_MZ_ID:カウンタ") != 書換先カードID
				CONTINUE
			SIF GETVAR(@"%対面者%_MZ_所有:カウンタ") != 所有者
				CONTINUE

			IF 종류 == 永紋章起動 
				CALL CARD_SET_RENAME(対面者,"필드",カウンタ,암소略称)
				CALL STATCHANGE_EFFECT(対面者,"自身",カウンタ,"変化元ID",参照カードID)
			ELSEIF 종류 == 永攻紋章起動 
				
				CALL STATCHANGE_OTHER(対面者,"自身",カウンタ,"属",암소속성)
				CALL STATCHANGE_EFFECT(対面者,"自身",カウンタ,"永星",암소レベル)
				CALL STATCHANGE_BASIC(対面者,"自身",カウンタ,암소攻撃力,암소守備力)
				CALL CARD_SET_CATEGORY(対面者,"필드",カウンタ,"E.R.A")
				;カテゴリ2は未설정なら변경する
				SIF GETVARS(@"%対面者%_MZ_追カテ2:カウンタ") == ""
					CALL CARD_SET_CATEGORY(対面者,"필드",カウンタ,"ホルスタイン",1)
				
				SIF 암소튜너 == 1
					CALL STATCHANGE_EFFECT(対面者,"自身",カウンタ,"一Ｔ",2)
				
				CALL STATCHANGE_OTHER(対面者,"自身",カウンタ,"가슴追",암소乳サイズ)
				
			ENDIF
		NEXT
	ENDIF
	IF (자신_듀얼패배플래그 > 0 || 상대_듀얼패배플래그 > 0) && 암소ミルクml > 0
	
		;基本は마함見るけど、자신필드에 存在する場合、更に別の효과で乳がデカくなってるかチェック
		FOR カウンタ,0,5
			SIF GETVAR(@"%듀얼리스트%_MZ_ID:カウンタ") != 書換先カードID
				CONTINUE
			SIF GETVAR(@"%듀얼리스트%_MZ_가슴追:カウンタ") > 암소乳サイズ
				암소乳サイズ = GETVAR(@"%듀얼리스트%_MZ_가슴追:カウンタ")
		NEXT
		
		CALL HORUSTEIN_GET_BUST(参照カードID,암소乳サイズ)
		암소乳サイズ = RESULT:2
		암소ミルク売却額 = 암소ミルクml *10
		
		CUSTOMDRAWLINE —
		PRINTFORML
		IF 암소乳サイズ <= -1
			;貧乳の乳は高く売れる
			TIMES 암소ミルク売却額 , 2.5
			
			SETCOLOR C_L_BLUE
			PRINTFORM [%GET_TALENTNAME(121,암소乳サイズ)%] 
			RESETCOLOR
			PRINTFORML の乳は高く買取してもらえるようだ…
		ELSEIF 암소乳サイズ >= 5
			;規格外な
			TIMES 암소ミルク売却額 , 0.15
		
			SETCOLOR C_H_PINK
			PRINTFORM [%GET_TALENTNAME(121,암소乳サイズ)%] 
			RESETCOLOR
			PRINTFORML の乳は買取が難しいようだ…
		ELSE
			;それ以外なら普通の価格に
			TIMES 암소ミルク売却額 , 0.8
		
			SETCOLOR C_PINK
			PRINTFORML [%GET_TALENTNAME(121,암소乳サイズ)%] 
			RESETCOLOR
		ENDIF
		
		PRINTFORML %암소名称%から搾った{암소ミルクml}mlのミルクを{암소ミルク売却額}円で売却した…
		PRINTFORML
		CUSTOMDRAWLINE —
		
		CALL MONEY_GET(암소ミルク売却額)


		;リセットも兼ねて、ミルクの情報を削除
		CALL SET_FIELD_INFO(듀얼리스트,"마함",参照場所,"T1汎数",0,上書きモード)
		CALL SET_FIELD_INFO(듀얼리스트,"마함",参照場所,"汎数",0,上書きモード)
	ENDIF
ENDIF


;ここから암소のカード処理-----

@CARDNAME_179557,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC カード名
#DIMS DYNAMIC 参照先
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カードID
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
			RESULTS = 【愛乳】ホルスタイン
	CASE "略称"
			RESULTS = 【愛乳】암소
	CASE "カテゴリ"
		RESULTS:0 = ホルス
		RESULTS:1 = ホルスタイン
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_179557(参照先)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 参照先
#DIMS DYNAMIC 参照先チェック
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カードID
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常モン
	CASE "속성"
		;ケベンセヌフと同じ
		RETURN 땅속성
	CASE "종족"
		RETURN 야수족
	CASE "レベル"
		RETURN 12
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
	CASE "성별"
		RETURN 牝性
	CASE "分割"
		RETURN デュアル
ENDSELECT

RETURN 0

@CARD_EXPLANATION_179557(종류)
#DIM DYNAMIC 종류

PRINTL このカードは通常モンスターとして扱い、마법＆함정ゾーンに置かれたカードによってステータスが決定する。
PRINTL 자신필드에 「ホルスタイン-ミルクファーム」が存在する場合、以下の효과を得る。
PRINTL 
PRINTL このカードの②の효과は１ターンに１度しか使用できない。
PRINTL ①：メインフェイズに발동できる。このカードの乳を計測する。
;CALLFORM CARD_ABLE(0,필드起動)
PRINTL ②：バトルフェイズに발동できる。このカードの乳を搾り発情させる。
PRINTL その後、以下の효과を適用する。
PRINTL ●このターン、このカードの攻撃は貫通し、守備표시のまま攻撃できる。
PRINTL ●１ターンに１度「愛乳」モンスターが戦闘・효과で破壊された場合、묘지から「ホルスタイン」 몬스터 １장을 골라 특수소환できる。
CALLFORM CARD_ABLE(1,효과後誘発)
PRINTL ③：このカードの攻撃が中断された場合に발동できる。
PRINTL 攻撃대상のモンスターを「ホルスタイン」カードとして扱い、そのモンスターの효과を無効にし、守備표시にする。

@CARDCAN_179557(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 別途カードID

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,179557)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動
	SIF GETVARS(@"%듀얼리스트%_フィールド名") != "ミルクファーム"
		RETURN 0
	IF メインフェイズ == 1
		SIF ターンプレイヤー == "상대" ;ただ情報を표시するだけなので、NPCは使用しない(ターン1付けてないから多分連打するし…)
			RETURN 0
	ELSEIF バトルフェイズ == 1
		SIF 발동済_1(듀얼리스트,179557) 
			RETURN 0
	ENDIF
	
	RETURN 1
ELSEIF 종류 == 攻撃後強制誘発;本当はミルクファームなきゃダメだけど、ただ致すだけだし良いか…
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_戦闘カード") != 場所
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_発情:場所") == 0
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_攻撃無効_保") != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF GETVARS(@"%듀얼리스트%_フィールド名") != "ミルクファーム"
		RETURN 0
	;発情時に攻撃中断された場合
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_戦闘カード") != 場所
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_発情:場所") == 0
		RETURN 0	
	SIF GETVAR(@"%듀얼리스트%_攻撃無効") != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 破壊→묘지 || 종류 == 戦闘→묘지
	CALL CARD_SS_CHECK(듀얼리스트,0,"묘지")
	SIF RESULT == 1
		RETURN 0
	
	;バトルフェイズに乳を搾った
	SIF !발동済_1(듀얼리스트,179557) 
		RETURN 0
	
	;戦闘・효과破壊時の효과を使っていない
	SIF 발동済_2(듀얼리스트,179557) 
		RETURN 0
	
	;특수소환可否
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ  = ホルスタイン
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	;守備표시の場合
	SIF GETVAR(@"%듀얼리스트%_MZ_表:場所") == 1 
		RETURN 0
	;자신が특수状態：ドリップ(99554)の場合
	SIF GETVAR(@"%듀얼리스트%_MZ_특수状態:場所") != 99554
		RETURN 0
	
	CALL FULLRESET
	효과条件固カテゴリ = ホルスタイン
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"효과발동")
	SIF RESULT == 1
		RETURN 1
	
	CALL FULLRESET
	효과条件固カテゴリ = ホルスタイン
	CALL CARD_CHECK_FIN_DATA(対面者,"효과발동")
	SIF RESULT == 1
		RETURN 1
	
	RETURN 0
ENDIF

@CARDEFFECT_179557(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 参照カードID
#DIM DYNAMIC 書換先カードID
#DIM DYNAMIC 参照場所
#DIM DYNAMIC 암소대상
#DIMS DYNAMIC 암소名称
#DIM DYNAMIC 攻撃대상
#DIM DYNAMIC 攻撃대상カードID
#DIMS DYNAMIC 상대名称
#DIMS DYNAMIC 搾手名称
#DIM DYNAMIC 搾手
#DIM DYNAMIC 절정횟수
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC カテゴリ名称
#DIM DYNAMIC 現乳サイズ
#DIM DYNAMIC T1ミルクml
#DIM DYNAMIC ミルクml

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

書換先カードID = 179557
カテゴリ名称 = 登録情報：ホルスタイン-愛乳

;ホルスタイン情報の취득
CALL HORUSTEIN_GET_DATA(듀얼리스트,場所,179557)
암소名称 = %RESULTS:0%
参照場所 = RESULT:0
암소대상 = RESULT:1
T1ミルクml = RESULT:3	;搾乳量(1ターン中)
ミルクml = RESULT:4		;搾乳量(듀얼中総量)
参照カードID = RESULT:99

;乳サイズ취득
CALL HORUSTEIN_GET_BUST(参照カードID,GETVAR(@"%듀얼리스트%_MZ_가슴追:場所"))
現乳サイズ = RESULT:2 

;実行者によって文章の상대を切り替える
IF 듀얼리스트 == "자신"
	搾手名称 = %CALLNAME:MASTER%
	搾手 = MASTER
ELSE
	;対戦상대
	CALL CARD_EROTICS(상대プレイヤー,듀얼리스트)
	IF RESULT != -1
		搾手名称 = %CALLNAME:RESULT%
	ELSE
		搾手名称 = "対戦者"
	ENDIF
	搾手 = RESULT
ENDIF

;攻撃대상の名前を취득
IF 종류 == 攻撃後強制誘発 || 종류 == 효과後誘発
	攻撃대상 = 戦闘場所(対面者)
	;名称の설정
	IF 攻撃대상 == 99
		CALL CARD_EROTICS(상대プレイヤー,듀얼리스트)
		IF RESULT != -1
			상대名称 = %CALLNAME:RESULT%
		ELSE
			상대名称 = "対戦상대"
		ENDIF
	ELSE
		攻撃대상カードID = GETVAR(@"%対面者%_MZ_ID:攻撃대상")
		TRYCALLFORM CARDNAME_{攻撃대상カードID},"略称"
		상대名称 = %RESULTS%
	ENDIF
ENDIF

IF 종류 == 필드起動 && メインフェイズ == 1
	;ステータス표시させるだけなので、無効にさせない
	효과無効不能 = 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,書換先カードID)
	IF RESULT == 0 && 参照カードID != -1
		
		PRINTFORML 
		PRINTFORMW ホルスの従者達が%암소名称%を取り囲んでいる…
		
		;名前から가슴のサイズの変動まで표시
		CALL HORUSTEIN_PRINT_STATE(듀얼리스트,場所,179557, GETVAR(@"%듀얼리스트%_MZ_가슴追:場所"))
		

	ENDIF
ELSEIF 종류 == 필드起動 && バトルフェイズ == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,書換先カードID,1)
	
	IF RESULT == 0 && 参照カードID != -1
		;가슴を搾る
		IF GETVAR(@"%듀얼리스트%_MZ_個Ｃ:場所") == 0
			;1回目
			PRINTFORML %搾手名称%は%암소名称%の乳に手を伸ばすと、その輪郭が変わらないように指を滑らせていく…
			PRINTFORMW 肌から乳へと変化していく感触を楽しみながら、徐々にその中心へと搾るような動きで가슴を意識させる。
			PRINTFORM %암소名称%から
			SETCOLOR C_PINK
			PRINTFORM 「モー…❤」
			RESETCOLOR
			PRINTFORML と声が漏れるころには、%암소名称%の乳はピンっと張り、その先端は搾って欲しそうに艶めいていた…
			PRINTFORML %搾手名称%はキュッと指を搾ると、%암소名称%は喜ぶように白い液体を滴らせた…
			SETCOLOR C_PINK
			PRINTFORMW 「もぉっ❤ んっぁっ❤ ―…❤」
			RESETCOLOR
			IF 암소대상 != -1
				CALL EROTIC_MOVE("バスト조교",암소대상,搾手,300)
			ENDIF
			
			CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"発情",175)
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"個Ｃ",1,上書きモード)
		ELSE
			;2回目
			PRINTFORML %搾手名称%は%암소名称%の乳に再び手を伸ばすと、その乳により白く濡れた指を滑らせた…
			PRINTFORML 既に一度搾られているというのに、指が触れただけでも尖らせた先端からはポタポタと漏れてしまっている…
			SETCOLOR C_PINK
			PRINTFORML 「モー…❤ モー…❤」
			RESETCOLOR
			PRINTFORMW 優しく撫でるような刺激では物足りないのか、%암소名称%は%搾手名称%の手に가슴を押し付け始めた…
			PRINTFORML 
			PRINTFORML %搾手名称%は期待を裏切るかのように丁寧に優しくなぞると、先端をパチンっと弾き、高まった性感を一気に解放させた！
			PRINTFORML %암소名称%の乳は突然の刺激に耐えきれず、壊れた蛇口のように白い液体を零れさせていく…
			SETCOLOR C_PINK
			PRINTFORMW 「…ぁっ！？ ぉぉおっ～～～❤❤」
			RESETCOLOR
			IF 암소대상 != -1
				CALL EROTIC_MOVE("バスト조교",암소대상,搾手,300)
			ENDIF
			
			CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"発情",185)
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"個Ｃ",0,上書きモード)
		ENDIF
		;守備표시で攻撃、貫通攻撃の付与
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"貫通",1)
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"守戦",1)

		;戦闘・효과破壊の適用は발동済2で판정
		
		CALL HORUSTEIN_GET_MILK(듀얼리스트,参照カードID,GETVAR(@"%듀얼리스트%_MZ_가슴追:場所"))
		
		;発情
		SETCOLOR C_PINK
		PRINTFORML 
		PRINTFORML %암소名称%は乳を搾られた影響か、酷く発情している…
		PRINTFORML
		RESETCOLOR

		IF ターンプレイヤー != 듀얼리스트
			;상대ターンに발동した場合
			PRINTFORML %암소名称%は乳を搾られた余韻に浸っている…

			;암소鳴き声：2回、ひらがな鳴き
			CALL HORUSTEIN_RAND_COW_TXT(2,1)
		ENDIF
		
		PRINTFORMW
	ENDIF

ELSEIF 종류 == 攻撃後強制誘発
	;発情時に攻撃が中断された場合
	
	;ひとりえっち(ここは一回触ってるので短めに。효과処理もあるしね)
	PRINTFORML …発情した%암소名称%は%상대名称%にふらふらと近寄るが、視線を足元に向けると途中で足を止めてしまった。
	PRINTFORML ふぅふぅと呼吸を荒げている%암소名称%は、自身の乳から目が離せなくなってしまう…
	PRINTFORMW %암소名称%は%搾手名称%に搾って貰った記憶を呼び覚ますように、乳をゆっくりと擦り始めた…
	
	;암소の鳴き声を鳴らす
	CALL HORUSTEIN_RAND_COW_TXT(1)
	
	IF 암소대상 != -1
		CALL EROTIC_MOVE("バスト조교",암소대상,-1,500)
	ENDIF
	절정횟수 += RESULT:1
	IF 절정횟수 > 0
		PRINTFORML %암소名称%は{절정횟수}回절정したようだ。
		PRINTFORMW 余程気持ち良かったのか、%암소名称%の周りには小さな白い斑点が飛び散ってしまっている…
	ENDIF
	
	PRINTFORML 
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,書換先カードID)
	
	IF RESULT == 0
		IF 攻撃대상 == 5 
			;直接攻撃失敗時
			;再び手を伸ばすと、상대필드에 他に戦闘を行えるモンスターがいない為か、タガが外れたように…
			
			PRINTFORMW %암소名称%は상대필드에 戦闘を行えるモンスター居ないことを確認すると、さらなる快楽を求めて指を動かし始めた…
			절정횟수 += 2
			SETCOLOR C_PINK
			
			;암소の鳴き声を鳴らす
			CALL HORUSTEIN_RAND_COW_TXT(절정횟수)
			
			IF 암소대상 != -1
				REPEAT 절정횟수
					CALL EROTIC_MOVE("バスト조교",암소대상,-1,200)
				REND
			ENDIF
			
			RESETCOLOR
			PRINTFORML 鳴き声が響き終わると、%암소名称%は「ぱちゃんっ」と音を立てて白い水たまりに倒れ込んだ…
			PRINTFORMW 好き放題気持ちよくなった%암소名称%は、満足そうな笑みを浮かべている…
			;절정後の암소鳴き声
			CALL HORUSTEIN_RAND_COW_TXT(2,2)
			PRINTFORMW 
		ELSE
			;モンスター攻撃時に失敗
			PRINTFORML 発情した%암소名称%は%상대名称%に抱き着いた！
			PRINTFORML …小さすぎる牛柄のビキニはふらふらと揺れ、隠さなければならない物がチラチラと覗いている。
			PRINTFORM %암소名称%は
			SETCOLOR C_PINK
			;愛乳
			PRINTFORM 「もぉー…❤」
			RESETCOLOR
			
			IF 現乳サイズ >= 0
				;貧乳じゃない場合
				PRINTFORML と鳴くと、そのまま母乳に濡れた乳をぴたっと%상대名称%に押し付けた…
				PRINTFORML 
				PRINTFORMW カウベルは%암소名称%の乳に挟まれ、くぐもった音を響かせている…
				
				SETCOLOR C_GRAY
				PRINTFORMW ※貧乳ではないため、守備표시にする효과は適用されません
				RESETCOLOR
				
				PRINTFORMW %상대名称%は母乳によってマーキングされ、효과は無効になってしまったようだ…
				
				;효과無効
				CALL SET_FIELD_INFO(対面者,"필드",攻撃대상,"無効",2)
			ELSE
				;貧乳の場合
				PRINTFORMW と鳴くと、%암소名称%の乳なのか骨なのか分からないモノを押し付けている…
				PRINTFORML 
				PRINTFORMW カウベルのカララン…と鳴り響くと、%상대名称%の효과は無効になり、守備표시にされてしまった！！
				PRINTFORMW %상대名称%は少量ではあるが母乳が塗りつけられ、マーキングされてしまったようだ…
				
				;守備표시にして
				CALL SET_FIELD_INFO(対面者,"필드",攻撃대상,"表",2,上書きモード)
				;효과無効
				CALL SET_FIELD_INFO(対面者,"필드",攻撃대상,"無効",2)
			ENDIF
			
			CALL CARD_SET_CATEGORY(対面者,"필드",攻撃대상,"ホルスタイン")
		ENDIF
		
	ENDIF
ELSEIF 종류 == 破壊→묘지 || 종류 == 戦闘→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,書換先カードID,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ  = ホルスタイン
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 효과後強制誘発
	;ドリップ状態(守備표시時、ホルスタイン以外のカードが발동する度に搾られる)
	
	SETCOLOR C_PINK
	CUSTOMDRAWLINE —
	PRINTFORML
	PRINTFORML %암소名称%はドリップ装置に繋がれている…
	PRINTFORML 
	CUSTOMDRAWLINE —
	RESETCOLOR
	
	PRINTFORML
	PRINTFORML %암소名称%の가슴に取り付けられた装置が僅かに振動し、零れ落ちたモノがミルクサーバーに溜まっていく…
	PRINTFORML
	
	;短め鳴き
	CALL HORUSTEIN_RAND_COW_TXT(1,0,10)
	PRINTFORML 
	
	;搾乳する(通常よりも少量)
	CALL HORUSTEIN_GET_MILK(듀얼리스트,参照カードID,GETVAR(@"%듀얼리스트%_MZ_가슴追:場所")-5)
	PRINTFORML 
	
	;乳を吸ってLP回復
	CALL LIFE_CHANGE(듀얼리스트,"増加","효과",100)
ENDIF
