@CARDNAME_6210,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = アビスベル＝ジャシン帝
	CASE "略称"
		RESULTS = ジャシン帝
	CASE "カテゴリ"
		RESULTS:0 = アビス
		RESULTS:1 = 邪神
ENDSELECT


@CARD_6210(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	;효과モン/通常モン/의식/융합/싱크로/엑시즈/링크/초차원
	CASE "종류"
		RETURN 효과モン
	;어둠속성/빛속성/땅속성/물속성/화염속성/바람속성
	CASE "속성"
		RETURN 어둠속성

	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1500
	CASE "守備力"
		RETURN 2300
	CASE "성별"
		RETURN 雄性
ENDSELECT
RETURN 0

@CARD_EXPLANATION_6210(종류)
#DIM DYNAMIC 종류

PRINTL このカード名の①②③の효과はそれぞれ１ターンに１度しか使用できない。
PRINTL ①：덱から「アビスベル＝ジャシン帝」以外の「アビス」 몬스터 １장을 묘지へ送って발동できる。
PRINTL このカードを손패から특수소환する。
PRINTL この효과の발동後、턴 종료시까지 자신は「アビス」モンスターしか특수소환できない。
PRINTL ②：このカードが既にモンスターゾーンに存在する状態で、
PRINTL 자신필드에 他の「アビス」モンスターが소환・특수소환された場合に발동できる。
PRINTL 덱から「アビス」마법・함정카드 １枚を손패に加える。
PRINTL ③：자신の묘지の「アビス」 몬스터 １장을 대상として발동できる。
PRINTL そのモンスターを攻撃표시で특수소환する。
PRINTL この효과で특수소환したモンスターは、このターンのエンドフェイズに제외される。
PRINTL ④：このカードがフィールドを離れる場合、かわりに손패を1枚捨てることができる。

@CARDCAN_6210(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,6210)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,6210)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,6210,ゾーン)
	SIF RESULT == 1
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0

	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = アビス
	효과条件固名前 = アビスベル＝ジャシン帝
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF 발동済_2(듀얼리스트,6210)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_最召場所") == 場所
		RETURN 0
	CALL FULLRESET
	효과条件カテゴリ = アビス
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"특수소환")
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 발동済_3(듀얼리스트,6210)
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0	
	SIF メインフェイズ != 1
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = アビス
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 身代효과
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件표시 = 20
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	SIF 破壊대상ゾーン != "필드"
		RETURN 0
	SIF 破壊대상場所 != 場所
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_6210(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動 
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,6210,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = アビス
		효과条件固名前 = アビスベル＝ジャシン帝
		효과条件종류 = モン全般
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"덱",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
			CALL CARD_SS_RESTRICTION(듀얼리스트,"アビス")
		ENDIF
	ENDIF
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,6210,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = アビス
		효과条件종류 = 마함全般
		CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,6210,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = アビス
		효과条件종류 = モン全般
		효과추가종류 = 10 + 덱行き
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"攻撃표시")
	ENDIF
ELSEIF 종류 == 身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,6210)
	IF RESULT == 0
		CALL FULLRESET
		효과条件표시 = 20
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		SIF RESULT == 1
			RETURN 1
	ENDIF
	RETURN 0
ENDIF