@CARDNAME_99660,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 만지기머신즈 서클
	CASE "略称"
		RESULTS = 만지기 서클
	CASE "カテゴリ"
		RESULTS:0 = 만지기머신
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_99660(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
	CASE "추가日"
		RETURN 250804
ENDSELECT

@CARD_EXPLANATION_99660(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
CALL CARD_ABLE(0,攻撃時誘発)
PRINTL ①：몬스터의 공격 선언시에 발동할 수 있다。
PRINTL 서로의 플레이어는 이하의 효과를 각각 적용한다。
PRINTL ●자신：덱에서 공격력 １０００ 이하의 미확인족 몬스터 １장을 특수 소환한다。
PRINTL 　　　　공격 몬스터가 상대 몬스터인 경우、추가로 공격 대상을 그 몬스터로 옮긴다。
PRINTL ●상대：덱에서 암컷 몬스터 １장을 수비 표시로 특수 소환한다。
PRINTL 　　　　이 효과로 특수 소환한 몬스터는 이 턴、효과는 무효화 된다。
CALL CARD_ABLE(0,묘지起動,フリー誘発)
PRINTL ②：묘지의 이 카드를 제외하고 발동할 수 있다。
PRINTL 자신의 마법＆함정 존의 "만지기머신" 몬스터 １장을 특수 소환한다。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_99660(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,99660)
SIF RESULT == 1
	RETURN 0

IF 종류 == 攻撃時誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 2
		RETURN 0
	SIF モンスター数(対面者) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종족 = 미확인족
	효과条件攻撃力 = 1000
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 묘지起動 || 종류 == フリー誘発
	SIF ゾーン != "묘지"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件カテゴリ = 만지기머신
	효과条件소환可 = 1	
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_99660(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 조교대상
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99660)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件종족 = 미확인족
		효과条件攻撃力 = 1000
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		SIF ターンプレイヤー != 듀얼리스트
			CALL COVER_ATTACK(듀얼리스트,カード소환先)
		
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件성별 = 牝性
		효과추가종류 = 効追一無
		CALL CARD_SP_SUMMON_ADV(対面者,ゾーン,場所,"守備표시")
	ENDIF

ELSEIF 종류 == 묘지起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99660)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件カテゴリ = 만지기머신
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF


