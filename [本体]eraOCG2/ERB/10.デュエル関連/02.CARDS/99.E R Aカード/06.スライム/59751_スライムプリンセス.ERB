@CARDNAME_59751,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 음란한 슬라임 공주
	CASE "略称"
		RESULTS = 슬라임 공주
	CASE "カテゴリ"
		RESULTS:0 = 슬라임
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_59751(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 융합
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 원소정령족
	CASE "レベル"
		RETURN 7
	CASE "攻撃力"
		RETURN 2400
	CASE "守備力"
		RETURN 2000
	CASE "성별"
		RETURN 牝性
ENDSELECT
RETURN 0

@CARD_EXPLANATION_59751(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL "슬라임" 몬스터 + 레벨 5이상의 몬스터 1체
PRINTL 이 카드명의  ②의 효과는 1턴에 1번밖에 사용할 수 없다。
CALL CARD_ABLE(1,EX소환時強制)
PRINTL ①：융합 소환한 이 카드의 이름은, 2번째의 소재에 사용한 여성 몬스터 1장과 동명 카드로도 ​​취급한다。
CALL CARD_ABLE(2,필드起動)
PRINTL ②：자신 메인 페이즈에 발동할 수 있다。
PRINTL 상대 필드의 "묘상"이외의 카드 1장을 묘지로 보낸다。
PRINTL 그 후, 상대의 여성 몬스터 1장을 소유자의 마법 & 함정 존에 "묘상" 카테고리를 부여하여 앞면 표시로 둘 수 있다。
CALL CARD_ABLE(0,지속효과)
PRINTL ③：공격 표시의 이 카드는 전투로는 파괴되지 않는다。
CALL TEXT_DECORATION("ROGUE")

@CARDSUMMON_AA_59751



@CARDSUMMON_59751(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要素材数 = 2
素材条件素材ID起動 = 2

;素材１장 目＝슬라임
IF EX参照 == 0 || EX参照 == 1
	素材条件カテゴリ = 슬라임
;素材２장目＝レベル５以上
ELSEIF EX参照 >= 10
	CALLFORM 素材条件レベル_ADV(5, "以上")
ENDIF


IF EX参照 == 1
	;PRINTL サモンまでは言ってるよ
	CALL FUSCAN(듀얼리스트,소환カード,59751) 
	RETURN RESULT
ENDIF

@CARDCAN_59751(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,59751)
SIF RESULT == 1
	RETURN 0



IF 종류 == EX소환時強制
	;場にいないかチェック
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	;②を발동済みかチェック
	SIF 발동済_2(듀얼리스트,59751)
		RETURN 0
	;コントロ-ラ-のターンじゃないかチェック
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	;メインフェイズじゃないかチェック
	SIF メインフェイズ != 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	효과条件固カテゴリ = 묘상
	효과条件除去種 = リリース
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	;それ以外ならOK
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_表:場所") == 3
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_59751(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 出処
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件合致数
#DIM DYNAMIC 選択カード,10
#DIM DYNAMIC 選択カードB,10
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 조교대상
#DIM DYNAMIC 이동カード名
#DIM DYNAMIC 取込カードID

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == EX소환時強制
	;汎数に기록した素材があるかチェック
	IF GETVAR(@"%듀얼리스트%_MZ_汎数:場所") >= 0
		;汎数読み込み
		取込カードID = GETVAR(@"%듀얼리스트%_MZ_汎数:場所")
		;기록した素材の성별をチェック
		TRYCALLFORM CARD_{取込カードID}, "성별"
		;♀かふたなりなら続行
		IF GROUPMATCH(RESULT,1,3)
			;変化元IDに汎数を打ち込み
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"変化元ID",取込カードID,上書きモード)
			;フレ-バ-、캐릭터リスト読み込み
			CALL CARD_EROTICS(取込カードID)
			SIF RESULT != -1
				PRINTFORMW 슬라임 공주는 %NAME:RESULT%의 유전자를 이어 받고 있다…！
		ENDIF
	ENDIF
	;使い終わった汎数を削除
	CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"汎数",0,上書きモード)
ELSEIF 종류 == 필드起動
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,59751,2)
	IF RESULT == 0
		;상대 필드의 카드 １장을 묘지에 
		CALL FULLRESET
		효과条件固カテゴリ = 묘상
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"リリース")
		IF RESULT == 1
			;使用者の마함が埋まり切ってない場合
			;♀몬스터를 묘상に
			IF 마함数(対面者) < 5
				CALL FULLRESET
				효과範囲상대필드 = 1
				효과条件성별 = 牝性
				효과条件任意 = 1
				효과条件除去種 = 選択
				효과추가カテ = 묘상
				CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시",1)
				IF RESULT == 1
					;フレ-バ-
					조교대상 = RESULT:1
					CALL CARD_EROTICS(조교대상)
					IF RESULT != -1
						조교대상 = RESULT
						SETCOLOR C_PINK
						PRINTFORML
						PRINTFORMW 슬라임 공주는 %CALLNAME:조교대상% 덮쳐 넘어뜨리고, 전신을 범하며 묘상으로 만들어 버렸다…！
						PRINTFORML
						RESETCOLOR
						CALL EROTIC_MOVE("묘상捕縛",조교대상, -1, 500)
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 지속효과
	;戦闘耐性付与
	CALL FULLRESET
	SIF GETVAR(@"%듀얼리스트%_MZ_表:場所")  == 1
		CALL STATCHANGE_EFFECT(듀얼리스트,"자신",場所,"戦耐")
ENDIF

