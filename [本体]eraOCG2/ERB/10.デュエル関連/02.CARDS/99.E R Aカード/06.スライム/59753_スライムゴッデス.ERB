@CARDNAME_59753,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 음란한 슬라임 여신
	CASE "略称"
		RESULTS = 슬라임 여신
	CASE "カテゴリ"
		RESULTS:0 = 슬라임
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_59753(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 융합
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 원소정령족
	CASE "レベル"
		RETURN 11
	CASE "攻撃力"
		RETURN 3200
	CASE "守備力"
		RETURN 2800
	CASE "성별"
		RETURN 牝性
ENDSELECT
RETURN 0

@CARD_EXPLANATION_59753(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 레벨 7이상의 "슬라임" 몬스터 ＋ 몬스터 3장
PRINTL 이 카드명의  ①의 효과는 1턴에 1번밖에 사용할 수 없다
CALL CARD_ABLE(1,EX소환時)
PRINTL ①：이 카드가 융합 소환으로 소환되었을 경우에 발동할 수 있다。
PRINTL 상대 필드 / 묘지의 여성 몬스터를 원하는 만큼 상대의 마법 & 함정 존에 "묘상」 카테고리를 부여하여 앞면 표시로 둔다
CALL CARD_ABLE(10,필드起動)
PRINTL ②：자신 / 상대 필드에 존재하는 "묘상" 카드 매수에 따라, 이하의 효과를 얻는다。
PRINTL ● 0장：1턴에 1번, 자신 메인 페이즈에 발동할 수 있다。
PRINTL 이 카드의 융합 소환시의 효과를 사용한다。
PRINTL ● 1장 이상：자신 필드의 "슬라임」 몬스터의 공격력은,
PRINTL 자신 / 상대 필드의 "묘상」 카드 의 수 X 30０ 올린다。
PRINTL ● 2장 이상：자신 필드의 "슬라임" 몬스터는 수비 표시 몬스터를 공격했을 경우, 그 수비력을 공격력이 넘은 만큼만 상대에게 전투 데미지를 준다。
PRINTL ● 3장 이상：자신 필드의 "슬라임" 몬스터는 효과의 대상이 되지 않는다。
PRINTL ● 4장 이상：1턴에 1번, 자신 / 상대 필드에 존재하는 "묘상" 카드 1장을 묘지로 보내고 발동할 수 있다。
PRINTL 묘지에서 "슬라임」 몬스터 1장을 특수 소환한다。
PRINTL ● 5장 이상：상대는 마법 / 함정 카드의 효과를 발동할 수 없다。
CALL TEXT_DECORATION("ROGUE")

@CARDSUMMON_AA_59753



@CARDSUMMON_59753(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要素材数 = 4

IF EX参照 == 0 || EX参照 == 1
	素材条件カテゴリ = 슬라임
	CALLFORM 素材条件レベル_ADV(7, "以上")
ELSEIF EX参照 >= 10
	素材条件종류 = モン全般
ENDIF


IF EX参照 == 1
	;PRINTL サモンまでは言ってるよ
	CALL FUSCAN(듀얼리스트,소환カード,59753) 
	RETURN RESULT
ENDIF

@CARDCAN_59753(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,59753)
SIF RESULT == 1
	RETURN 0


IF 종류 == EX소환時
	SIF 발동済_1(듀얼리스트,59753)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF 마함数(対面者) >= 5
		RETURN 0
	CALL FULLRESET
	효과条件성별 = 牝性
	효과範囲상대필드 = 1
	효과範囲상대묘지 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	효과条件カテゴリ = 묘상
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT == 0
		SIF 발동済_1(듀얼리스트,59753)
			RETURN 0
		RETURN 1
	ELSEIF RESULT >= 4
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 슬라임
		효과条件소환可 = 2
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
		SIF GETVAR(@"%듀얼리스트%_MZ_効済:場所") != 1
			RETURN 1
	ENDIF
	RETURN 0
ELSEIF 종류 == 攻守変動 || 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_表:場所") == 3
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_59753(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 出処
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件合致数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 조교대상
#DIM DYNAMIC 묘상数

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == EX소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,59753,1)
	IF RESULT == 0
		FOR カウンタ,0,5
			CALL FULLRESET
			효과範囲상대필드 = 1
			효과範囲상대묘지 = 1
			효과条件성별 = 牝性
			효과条件任意 = 1
			효과추가カテ = 묘상
			CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시",1)
			IF RESULT == 1
				조교대상 = RESULT:1
				CALL CARD_EROTICS(조교대상)
				IF RESULT != -1
					조교대상 = RESULT
					SETCOLOR C_PINK
					PRINTFORML
					PRINTFORMW  %CALLNAME:조교대상%은 전신 슬라임 절임으로 묘상이 되어 버렸다…！
					PRINTFORML
					RESETCOLOR
					CALL EROTIC_MOVE("묘상捕縛",조교대상, -1, 500)
				ENDIF
			ELSEIF RESULT == 0
				BREAK
			ENDIF
			SIF 마함数(対面者) >= 5
				BREAK
		NEXT
	ENDIF
ELSEIF 종류 == 필드起動
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,59753)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		효과条件カテゴリ = 묘상
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 0
			CALLFORM CARDEFFECT_59753(듀얼리스트,EX소환時,"효과処理",場所)
		ELSEIF RESULT >= 4
			CALL FULLRESET
			효과範囲필드 = 1
			IF モンスター数(듀얼리스트) < 5
				효과範囲마함 = 1
				효과範囲상대필드 = 1
				효과範囲상대마함 = 1
			ENDIF
			효과条件カテゴリ = 묘상
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				조교대상 = RESULT:1
				CALL FULLRESET
				효과範囲묘지 = 1
				효과条件カテゴリ = 슬라임
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
				IF RESULT == 1
					CALL SET_FIELD_INFO(듀얼리스트,ゾーン,場所,"効済",1,1)
					CALL CARD_EROTICS(조교대상)
					IF RESULT != -1
						조교대상 = RESULT
						SETCOLOR C_PINK
						PRINTFORML
						PRINTFORMW 묘상의 %CALLNAME:조교대상%를 모체로 하여, 슬라임이 부활했다！
						PRINTFORML
						RESETCOLOR
						CALL EROTIC_MOVE("疑似출산",조교대상, -1, 500)
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	효과条件カテゴリ = 묘상
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	묘상数 = RESULT


	CALL FULLRESET
	효과条件カテゴリ = 슬라임
	SIF 묘상数 >= 2
		CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"貫通")
	SIF 묘상数 >= 3
		CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"対耐")
	IF 묘상数 >= 5
		CALL SET_PLAYER_INFO(듀얼리스트,"魔無効",3,1)
		CALL SET_PLAYER_INFO(듀얼리스트,"함정無効",3,1)
	ENDIF

ELSEIF 종류 == 攻守変動

	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	효과条件カテゴリ = 묘상
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	묘상数 = RESULT

	CALL FULLRESET
	효과条件カテゴリ = 슬라임
	SIF 묘상数 >= 1
		CALL STATCHANGE_BASIC(듀얼리스트,"필드",場所,묘상数 * 300)
ENDIF

