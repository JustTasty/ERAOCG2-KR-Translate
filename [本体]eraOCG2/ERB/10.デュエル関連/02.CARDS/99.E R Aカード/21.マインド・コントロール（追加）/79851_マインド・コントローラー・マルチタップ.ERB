@CARDNAME_79851,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 마인드컨트롤러 - 멀티탭
	CASE "略称"
		RESULTS = MC 멀티탭
	CASE "カテゴリ"
		RESULTS:0 = MC
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_79851(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 速攻마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_79851(종류)
#DIM DYNAMIC 종류
CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드명의  카드의①②의 효과는 1턴에 1번, 어느쪽이든 1개 밖에 사용할 수 없다.
CALL CARD_ABLE(1,마함起動,손패起動,フリー誘発)
PRINTL ①：자신 필드에『마인드컨트롤러 - 토큰』1장을 수비 표시로 특수 소환한다.
PRINTL （기계족 광 레벨 1 공 0／수 100 소재불가）
PRINTL 그 후, 상대 묘지의 『MC』카드 1장을 대상으로 할 수 있다.
PRINTL 그러한 경우, 그 카드를 상대의 덱에 2장 복제하고, 상대는 1장 드로우한다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②：자신 메인 페이즈에 묘지의 이 카드를 제외하고, 자신의 마법 / 함정 존의『MC』몬스터 카드 1장을 대상으로 하고 발동할 수 있다.
PRINTL 그 몬스터를 특수 소환한다.
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_79851(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,79851)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,79851)
		RETURN 0
	IF ゾーン == "손패"
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	CALL CARD_SS_CHECK(듀얼리스트,179851,"")
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_1(듀얼리스트,79851)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件표시 = 表側표시
	효과条件カテゴリ = MC
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_79851(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 조교대상
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードID

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79851,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		CALL CARD_GENERATE(듀얼리스트,179851,"守備표시")
		CALL FULLRESET
		효과範囲상대묘지 = 1
		효과条件カテゴリ = MC
		효과条件任意 = 1
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			選択カード %= 100
			選択カードID = GETVAR(@"%対面者%_묘지:選択カード")
			SETCOLOR C_PINK
			CALL CARD_GENERATE_DECK(対面者,選択カードID)
			CALL CARD_GENERATE_DECK(対面者,選択カードID)
			RESETCOLOR
			CALL SHUFFLE(対面者)
			CALL CARDDRAW_BASIC(対面者,1)
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79851,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件カテゴリ = MC
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF


@CARDNAME_179851,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 마인드컨트롤러 - 토큰
	CASE "略称"
		RESULTS = MC - 토큰
	CASE "カテゴリ"
		RESULTS:0 = MC
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_179851(参照先)
;名前専用
#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常モン
	CASE "トークン"
		RETURN 1
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 100
ENDSELECT

@CARD_EXPLANATION_179851(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 「마인드컨트롤러 - 멀티탭」의 효과로 생성된다.
PRINTL 이 토큰은 소재로 할 수 없다.
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_179851(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,179851)
SIF RESULT == 1
	RETURN 0

IF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_179851(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード


IF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"자신",場所,"不材")
ENDIF
