@CARDNAME_99696,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = Live♥Do 데스★트로이
	CASE "略称"
		RESULTS = LD 데스★트로이
	CASE "カテゴリ"
		RESULTS:0 = Live♥Do
		RESULTS:1 = AliVe♡Twin
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_99696(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
ENDSELECT

RETURN 0

@CARD_EXPLANATION_99696(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드명의  ①의 효과는 1턴에 1번밖에 사용할 수 없다.。
CALL CARD_ABLE(1,효과後誘発)
PRINTL ①：상대 몬스터의 직접 공격으로 자신이 데미지를 받았을 경우에 발동할 수 있다。
PRINTL 공격한 그 몬스터를 파괴하고 １０００ 데미지를 준다。
PRINTL 이 효과를 발동한 턴 자신은 「Live☆Twin」 몬스터 밖에 특수 소환할 수 없다。
CALL CARD_ABLE(2,攻撃時強制誘発)
PRINTL ②：이 카드가 묘지에 존재할 경우, 상대 몬스터에 직접 공격 선언시 발동한다。
PRINTL 상대는 ２００ＬＰ 잃고, 자신은 ２００ＬＰ회복한다。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_99696(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,99696)
SIF RESULT == 1
	RETURN 0

IF 종류 == 효과後誘発
	SIF 발동済_1(듀얼리스트,99696)
		RETURN 0
	SIF バトルフェイズ != 1
		RETURN 0
	CALL CARD_CHECK_FIN_BIT(듀얼리스트,"被弾種",2)
	SIF RESULT != 1
		RETURN 0
	IF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
	ELSEIF ゾーン == "손패"
		SIF GETVARS(@"%듀얼리스트%_フィールド名") != "AV T 채널"
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	RETURN 1
ELSEIF 종류 == 攻撃時強制誘発
	SIF ゾーン != "묘지"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 5
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_99696(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%



IF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99696,1)
	IF RESULT == 0
		選択カード = 戦闘場所(듀얼리스트,1)
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",選択カード,"묘지")
		IF RESULT == 1
			PRINTFORML
			PRINTFORML 《 터치 금지！》
			PRINTFORMW 공격한 몬스터는 악성 바이러스에 감염되어 파괴되었다！
			PRINTFORML
			CALL LIFE_CHANGE(듀얼리스트,"減少","효과",1000,1)
			CALL CARD_SS_RESTRICTION(듀얼리스트,"Live☆Twin")
			CALL CARD_EX_RESTRICTION(듀얼리스트,"Live☆Twin")
		ENDIF
	ENDIF
ELSEIF 종류 == 攻撃時強制誘発
	PRINTL AliVe♡Twin 채널 시청료 징수！
	CALL LIFE_CHANGE(対面者,"減少","コスト",200)
	CALL LIFE_CHANGE(듀얼리스트,"増加","효과",200)
ENDIF

