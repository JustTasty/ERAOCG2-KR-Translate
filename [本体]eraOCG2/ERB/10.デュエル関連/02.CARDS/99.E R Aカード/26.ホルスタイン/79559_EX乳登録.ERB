@CARDNAME_79559,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

;カード制作者：xonng
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 【EX유】호루스타인─엑스트라
	CASE "略称"
		RESULTS = 암소등록【EX유】
	CASE "カテゴリ"
		RESULTS:0 = 호루스
		RESULTS:1 = 호루스타인
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_79559(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "추가日"
		RETURN 250325
ENDSELECT

RETURN 0

@CARD_EXPLANATION_79559(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드는 １턴에 １번밖에 발동할 수 없으며, 이 카드의 ①의 효과는 듀얼 중 １번밖에 적용할 수 없다。
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ①：덱 / 패 / 필드에서 "호루스타인" 이외의 암컷 몬스터를 １장 선택해、
PRINTL 자신의 덱 / 패 /필드 / 묘지에 존재하는 모든 같은 이름의 카드를 "【EX유】호루스타인" 으로 재정의해、덱에 추가한다。
PRINTL 이 효과의 처리 후、이하의 효과에서 선택한다。
PRINTL ●덱 / 묘지에서 "호루스타인" 몬스터 １장을 특수 소환한다。
PRINTL ●덱에서 "호루스타인" 카드를 １장 골라 패에 넣는다。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_79559(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

;戦闘終了플래그を拾うためNEGATE前に記載
IF 종류 == 永攻紋章起動 && (자신_듀얼패배플래그 > 0 || 상대_듀얼패배플래그 > 0 )
	RETURN 1
ENDIF

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,79559)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_D1(듀얼리스트,79559) 
		RETURN 0
	SIF 발동済_1(듀얼리스트,79559) 
		RETURN 0
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
		
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲덱 = 1
	효과範囲EX덱 = 1
	효과範囲필드 = 1
	효과条件성별 = 牝性
	효과条件固カテゴリ = 호루스타인
	
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 永紋章起動
	RETURN 1
ELSEIF 종류 == 永攻紋章起動
	RETURN 1
ENDIF

@CARDEFFECT_79559(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 参照カードID
#DIM DYNAMIC 書換先カードID
#DIM DYNAMIC 所有者
#DIM DYNAMIC 参照場所
#DIM DYNAMIC 암소대상
#DIM DYNAMIC 암소所有
#DIMS DYNAMIC 암소名称
#DIMS DYNAMIC 암소略称
#DIM DYNAMIC 암소속성
#DIM DYNAMIC 암소ミルクml
#DIM DYNAMIC 암소ミルク売却額
#DIM DYNAMIC 条件달성

#DIM DYNAMIC 紋章カードID
#DIM DYNAMIC 乳サイズ보정
#DIM DYNAMIC 紋章場所
#DIM DYNAMIC 암소鳴き声
#DIM DYNAMIC 암소절정声
#DIM DYNAMIC 암소弛緩声
#DIMS DYNAMIC 乳소환
#DIMS DYNAMIC 乳名

#DIM DYNAMIC 암소乳サイズ

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

紋章カードID = 79559
IF 듀얼리스트 == "자신"
	書換先カードID = 紋章カードID + 100000
ELSE
	書換先カードID = 紋章カードID + 200000
ENDIF

IF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79559,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲덱 = 1
		효과範囲EX덱 = 1
		효과範囲필드 = 1
		효과条件성별 = 牝性
		효과条件固カテゴリ = 호루스타인
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		選択カードID = RESULT:99
		IF RESULT != 9999
			;(デュエル中１번)
			CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,紋章カードID,20)
			
			;永紋章の표시をスキップ
			SKIPDISP 1
			;データの설정(※初期値に-1が入っている+入力値は加減算されるので、上書きで初期化する)
			CALL CARD_ENG_EMBLEM(듀얼리스트,-1,選択カードID,1,上書きモード);No１：カードID
			CALL CARD_ENG_EMBLEM(듀얼리스트,-1,0,2,上書きモード);No２：乳サイズ보정
			CALL CARD_ENG_EMBLEM(듀얼리스트,-1,0,3,上書きモード);No３：ミルクml
			CALL CARD_RESERV(듀얼리스트,紋章カードID,永紋章予約)
			CALL CARD_RESERV(듀얼리스트,紋章カードID,永攻紋章予約)
			CALL CARD_GET_EMBLEM_VALUES(듀얼리스트,紋章カードID)
			紋章場所 = RESULT
			SKIPDISP 0
			
			;◆ホルスタイン情報の취득(紋章カードIDから各種情報を취득)
			CALL HORUSTEIN_GET_DATA(듀얼리스트,紋章カードID)
			암소名称 = %RESULTS:0%
			암소略称 = %RESULTS:1%
			乳소환 = %RESULTS:3%
			紋章場所 = RESULT:0 
			암소대상 = RESULT:1 
			암소鳴き声 = RESULT:2 
			암소절정声 = RESULT:3 
			암소弛緩声 = RESULT:4 
			암소ミルクml = RESULT:97
			参照カードID = RESULT:99
			
			CALL HORUSTEIN_GET_BUST(参照カードID)
			암소乳サイズ = RESULT:2
			
			;◆カードの書き換え(フィールド・손패・덱・묘지)
			CALL HORUSTEIN_SET_RECONSTRUCT(듀얼리스트,選択カードID,書換先カードID)
			
			PRINTFORMW 등록증에 이름이 새겨지니、어디선가 호루스의 수호자가 나타났다！
			PRINTFORML %암소名称%는 저항할 새도없이 세명의 수호자에게 밀려져、
			PRINTFORML 피어스에 카우벨이 붙은 목걸이가 채워지고、몸에 관리태그가 찍혀버렸다…
			PRINTFORML 
			PRINTFORML "…음멋！…음머엇！？"
			PRINTFORMW 
			PRINTFORML %암소名称%는 목소리를 냈지만、그 소리는 암소의 울음소리로밖에 들리지 않았다。
			PRINTFORML …어떻게든 저항하려 하고있는것 이겠지。하지만 몸은 이미 암소가 되어버린것인지、아양떠는듯한 울음소리밖에 낼수없게된것같다…
			PRINTFORMW 무언가를 항의하는 %암소名称%를 무시하고、수호자는 그 튼튼한 손으로 가슴을 받치기 시작했다…
			SETCOLOR C_PINK
			PRINTFORML 
			PRINTFORML "음머-…！？…음멋❤""음머엇❤ 음머❤"
			PRINTFORMW 
			RESETCOLOR
			PRINTFORML …수호자에게 가슴이 받쳐진것으로、그 가슴은 "상응하는 가슴"으로 바뀌어간다…
			PRINTFORML 

			;◆乳サイズ変動
			IF 암소乳サイズ >= 4
				;魔乳以上の場合、1サイズアップする
				CALL HORUSTEIN_SET_BUST(듀얼리스트,紋章カードID,参照カードID,1)
				
				;着替え
				PRINTFORMW %암소名称%는 너무 큰 자신의 가슴을 확인하니、그 가슴을 해방하는듯이 옷을 벗기 시작했다…
			ELSE
				;魔乳未満の경우、規格外にする
				CALL HORUSTEIN_SET_BUST(듀얼리스트,紋章カードID,参照カードID,(+5 -암소乳サイズ))
				
				;着替え
				PRINTFORML 수호자는 %암소名称%의 한번에 커져버린 가슴을 놓치는듯이 옷을 벗겨주기 시작했다…
				PRINTFORMW %암소名称%는 몸의 중심이 바뀌어버린 영향인지、걷기 힘들어보인다…

			ENDIF
			
			PRINTFORML 
			PRINTFORML %암소名称%는 발밑에 준비되어있던 옷을 주…우려 했지만、가슴이 너무 커서 줍지 못했다…
			PRINTFORML 수호자는 %암소名称%를 둘러싸고、발밑에 있던 소묘양 비키니로 갈아입혔다…
			
			;◆服装の변경(牛柄ビキニと角)
			CALL HORUSTEIN_SET_CLOTHES(듀얼리스트,選択カードID,"守護者")
			
			PRINTFORML 
			PRINTFORMW %암소名称%는 소모양 비키니로 갈아입었지만、지금도 가슴이 흘러넘칠것같다…
			
			IF 암소대상 != -1 
				;着替えによる조교
				CALL EROTIC_MOVE("バスト조교",암소대상,-1,300)
			ENDIF
				
			CALL FULLRESET
			효과範囲덱 = 1
			효과範囲묘지 = 1
			효과条件カテゴリ = 호루스타인
			효과条件종류 = モン全般
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			
			IF RESULT == 1
				条件달성++
				PRINTFORML [1] - 덱 / 묘지에서 "호루스타인" 몬스터를 특수 소환한다。
			ELSE
				SETCOLOR C_GRAY
				PRINTFORML     - 덱 / 묘지에서 "호루스타인" 몬스터를 특수 소환한다。
				RESETCOLOR
			ENDIF
			
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 호루스타인
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			
			IF RESULT == 1
				条件달성++
				PRINTFORML [2] - 덱에서 "호루스타인" 카드를 패에 넣는다。
			ELSE
				SETCOLOR C_GRAY
				PRINTFORML     - 덱에서 "호루스타인" 카드를 패에 넣는다。
				RESETCOLOR
			ENDIF
			
			IF 条件달성 == 0
				PRINTFORML
				PRINTFORMW [0] - 특수 소환 가능한 카드、패에 넣을 수 있는 카드가 존재하지 않습니다。
				RESULT = 0
			ELSE
				BINPUT
			ENDIF
			
			IF RESULT == 1
				;덱 / 묘지에서 "호루스타인" 몬스터를 특수 소환한다
				CALL FULLRESET
				효과範囲덱 = 1
				효과範囲묘지 = 1
				효과条件カテゴリ = 호루스타인
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
				
				;◆소환演出
				IF RESULT != -1
					選択カードID = RESULT:1
					CALL HORUSTEIN_GET_DATA(듀얼리스트,選択カードID)
					乳소환 = %RESULTS:3%
					암소鳴き声 = RESULT:2 
					
					PRINTFORMW %乳소환%
					CALL HORUSTEIN_RAND_COW_TXT(1,암소鳴き声)
					PRINTFORMW 
				ENDIF
			ELSEIF RESULT == 2
				;덱에서 「호루스타인」 카드를 패에 넣는다
				CALL FULLRESET
				효과範囲덱 = 1
				효과条件カテゴリ = 호루스타인
				CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
			ENDIF
			
			PRINTFORML 
			SETCOLOR C_YELLOW
			PRINTFORML "【EX유】호루스타인"은 필드에 존재하는 경우"%암소名称%"로 취급됩니다。
			RESETCOLOR
			PRINTFORMW
			
		ENDIF
		
	ENDIF
	
	SKIPDISP 1
	CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
	SKIPDISP 0
	
ELSEIF 종류 == 永紋章起動 || 종류 == 永攻紋章起動
	;紋章を使用し、フィールドの암소の情報を更新(속성の付与、가슴追の설정など)
	CALL HORUSTEIN_FIELD_UPDATE(종류,書換先カードID)
	
	IF (자신_듀얼패배플래그 > 0 || 상대_듀얼패배플래그 > 0)
		;ミルクの精算処理
		CALL HORUSTEIN_SELL_MILK()
	ENDIF
ENDIF
	

;ここから암소のカード処理-----

@CARDNAME_179559,参照先,参照듀얼리스트 = "자신"
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先
#DIMS DYNAMIC 参照듀얼리스트
#DIMS DYNAMIC 암소名称
#DIMS DYNAMIC 암소略称
#DIMS DYNAMIC 乳名
#DIM DYNAMIC 参照カードID
VARSET RESULT
VARSET RESULTS

;◆ホルスタイン情報の취득(紋章カードIDから各種情報を취득)
CALL HORUSTEIN_GET_DATA(参照듀얼리스트,79559)
암소名称 = %RESULTS:0%
암소略称 = %RESULTS:1%
乳名 = %RESULTS:2%
参照カードID = RESULT:99

SELECTCASE 参照先
	CASE "名前"
			RESULTS = 【%乳名%】%암소名称%
	CASE "略称"
			RESULTS = %암소略称%
	CASE "カテゴリ"
		
		TRYCALLFORM CARDNAME_{参照カードID}, "カテゴリ"
		RESULTS:6 = %RESULTS:3%
		RESULTS:5 = %RESULTS:2%
		RESULTS:4 = %RESULTS:1%
		RESULTS:3 = %RESULTS:0%
		RESULTS:0 = 호루스
		RESULTS:1 = 호루스타인
		RESULTS:2 = E.R.A
				
	CASEELSE
		RESULTS =
ENDSELECT


@CARD_179559(参照先,듀얼리스트 = "자신")
#DIMS DYNAMIC 参照先
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 参照カードID
VARSET RESULT
VARSET RESULTS

IF GROUPMATCH(参照先,"レベル","攻撃力","守備力","튜너","連動ID")
	;◆ホルスタイン情報の취득(紋章カードIDから各種情報を취득)
	CALL HORUSTEIN_GET_DATA(듀얼리스트,79559)
	参照カードID = RESULT:99
ENDIF

SELECTCASE 参照先
	CASE "종류"
		RETURN 링크
	CASE "속성"
		;두아무테프と同じ
		RETURN 물속성
	CASE "종족"
		RETURN 야수족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		TRYCALLFORM CARD_{参照カードID},"攻撃力"
		RETURN RESULT
	CASE "守備力"
		TRYCALLFORM CARD_{参照カードID},"守備力"
		RETURN RESULT
	CASE "성별"
		RETURN 牝性
	CASE "連動ID"
		RETURN 参照カードID
		
ENDSELECT

RETURN 0

@CARDSUMMON_179559(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要링크数 = 3
素材条件カテゴリ = 호루스타인

IF EX参照 == 1
	IF 듀얼리스트 == "자신"
		CALL LINCAN(듀얼리스트,소환カード,179559) 
	ELSE
		CALL LINCAN(듀얼리스트,소환カード,279559) 
	ENDIF
	RETURN RESULT
ENDIF

@CARD_EXPLANATION_179559(종류)
#DIM DYNAMIC 종류

PRINTL "호루스타인" 몬스터 ３장
PRINTL 이 카드의 ③의 효과는 １턴에 １번밖에 사용할 수 없다。
PRINTL ①：L소환성공시에 발동할 수 있다。묘지에서 "호루스타인" 몬스터를 ２장까지 골라、수비표시로 특수 소환한다。
PRINTL 이 효과로 특수 소환한 몬스터는、이 턴 소재로 할 수 없다。
PRINTL ②：자신 메인 페이즈에 발동할 수 있다。이 카드의 젖을 계측한다。
;CALLFORM CARD_ABLE(0,필드起動)
PRINTL ③：배틀 페이즈에 발동할 수 있다。이 카드의 젖을 뽑아 발정시킨다。
PRINTL 그 후、이하의 효과를 적용한다。
PRINTL ●이 카드의 공격력 / 수비력은 필드의 "호루스타인" 몬스터의 공격력 / 수비력의 합계와 같은 수치가 된다。
PRINTL ●이 카드는 필드의 수비표시인 "호루스타인" 몬스터의 수 만큼 공격할 수 있다。
CALLFORM CARD_ABLE(1,효과後誘発)
PRINTL ④：발정한 이 카드의 공격이 중단된 경우엔 발동할 수 있다。
PRINTL 공격 대상인 몬스터를 "호루스타인"카드로 취급해、수비표시가 된다。이후、그 몬스터는 공격 대상이 되지 않는다。
PRINTL （이 효과가 적용된 몬스터밖에 필드에 존재하지 않는 상태에서의 공격은 직접공격이 된다。）

@CARDCAN_179559(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 書換先カードID

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 듀얼리스트 == "자신"
	書換先カードID = 179559
ELSE
	書換先カードID = 279559
ENDIF

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,179559)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX소환時
	;묘지에 호루스타인が居なければ使えない
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 호루스타인
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF GETVARS(@"%듀얼리스트%_フィールド名") != "밀크 팜"
		RETURN 0
	IF メインフェイズ == 1
		SIF 書換先カードID > 200000 ;ただ情報を표시するだけなので、NPCは使用しない(ターン1付けてないから多分連打するし…)
			RETURN 0
		SIF ターンプレイヤー != "자신"
			RETURN 0
	ELSEIF バトルフェイズ == 1
		SIF 발동済_1(듀얼리스트,書換先カードID) 
			RETURN 0
	ENDIF
	
	RETURN 1
ELSEIF 종류 == 攻撃後強制誘発;本当は밀크 팜なきゃダメだけど、ただ致すだけだし良いか…
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_戦闘カード") != 場所
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_発情:場所") == 0
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_攻撃無効_保") != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF GETVARS(@"%듀얼리스트%_フィールド名") != "밀크 팜"
		RETURN 0
	;発情時に攻撃中断された場合
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_戦闘カード") != 場所
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_発情:場所") == 0
		RETURN 0	
	SIF GETVAR(@"%듀얼리스트%_攻撃無効") != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻守変動 
	SIF ゾーン != "필드"
		RETURN 0
 	SIF !발동済_1(듀얼리스트,書換先カードID) 
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	;守備표시の場合
	SIF GETVAR(@"%듀얼리스트%_MZ_表:場所") == 1 
		RETURN 0
	;자신이 특수状態：ドリップ(99554)の場合
	SIF GETVAR(@"%듀얼리스트%_MZ_특수状態:場所") != 99554
		RETURN 0
	
	CALL FULLRESET
	효과条件固カテゴリ = 호루스타인
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"효과발동")
	SIF RESULT == 1
		RETURN 1
	
	CALL FULLRESET
	효과条件固カテゴリ = 호루스타인
	CALL CARD_CHECK_FIN_DATA(対面者,"효과발동")
	SIF RESULT == 1
		RETURN 1
	
	RETURN 0
ENDIF

@CARDEFFECT_179559(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 参照カードID
#DIM DYNAMIC 書換先カードID
#DIM DYNAMIC 攻撃대상
#DIM DYNAMIC 攻撃대상カードID
#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 상대名称
#DIMS DYNAMIC 搾手名称
#DIM DYNAMIC 搾手
#DIM DYNAMIC 절정횟수
#DIM DYNAMIC カウンタ

#DIM DYNAMIC 紋章カードID
#DIM DYNAMIC 紋章場所
#DIMS DYNAMIC 乳소환		;소환時の구상
#DIMS DYNAMIC 암소名称
#DIM DYNAMIC 암소대상
#DIM DYNAMIC 암소鳴き声
#DIM DYNAMIC 암소절정声
#DIM DYNAMIC 암소弛緩声
#DIM DYNAMIC 乳サイズ보정
#DIM DYNAMIC 암소乳サイズ

#DIM DYNAMIC ステ合計

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

紋章カードID = 79559
IF 듀얼리스트 == "자신"
	書換先カードID = 179559
ELSE
	書換先カードID = 279559
ENDIF
;◆ホルスタイン情報の취득(紋章カードIDから各種情報を취득)
CALL HORUSTEIN_GET_DATA(듀얼리스트,紋章カードID)
암소名称 = %RESULTS:0%
紋章場所 = RESULT:0
암소대상 = RESULT:1
암소鳴き声 = RESULT:2 
암소절정声 = RESULT:3 
암소弛緩声 = RESULT:4 
乳サイズ보정 = RESULT:98
参照カードID = RESULT:99

;実行者によって文章の상대を切り替える
IF 듀얼리스트 == "자신"
	搾手名称 = %CALLNAME:MASTER%
	搾手 = MASTER
ELSE
	;대상상대
	CALL CARD_EROTICS(상대プレイヤー,"자신")
	IF RESULT != -1
		搾手名称 = %CALLNAME:RESULT%
	ELSE
		搾手名称 = "대상者"
	ENDIF
	搾手 = RESULT
ENDIF

;공격 대상의 名前を취득
IF 종류 == 攻撃後強制誘発 || 종류 == 효과後誘発
	攻撃대상 = 戦闘場所(対面者)
	;名称の설정
	IF 攻撃대상 == 99
		CALL CARD_EROTICS(상대プレイヤー,듀얼리스트)
		IF RESULT != -1
			상대名称 = %CALLNAME:RESULT%
		ELSE
			상대名称 = "대상상대"
		ENDIF
	ELSE
		攻撃대상カードID = GETVAR(@"%対面者%_MZ_ID:攻撃대상")
		TRYCALLFORM CARDNAME_{攻撃대상カードID},"略称"
		상대名称 = %RESULTS%
	ENDIF
ENDIF

IF 종류 == EX소환時
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99554,1)
	IF RESULT == 0
		;묘지에서 「호루스타인」2장을 수비 표시로 특수소환(素材불가)
		
		FOR カウンタ,0,2
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件カテゴリ = 호루스타인
			효과추가종류 = 21	;素材불가
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"守備표시")
			
			;◆소환演出
			IF RESULT != -1
				選択カードID = RESULT:1
				CALL HORUSTEIN_GET_DATA(듀얼리스트,選択カードID)
				乳소환 = %RESULTS:3%
				암소鳴き声 = RESULT:2 
				
				PRINTFORMW %乳소환%
					CALL HORUSTEIN_RAND_COW_TXT(1,암소鳴き声,,1)
			ENDIF
		NEXT
		PRINTFORMW 
	ENDIF
ELSEIF 종류 == 필드起動 && メインフェイズ == 1
	;ステータス표시させるだけなので、無効にさせない
	효과無効不能 = 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,書換先カードID)
	IF RESULT == 0 && 参照カードID != -1
		
		PRINTFORML 
		PRINTFORMW 호루스의 종자들이 %암소名称%를 둘러싸고있다…
		
		;名前から가슴のサイズの変動まで표시
		CALL HORUSTEIN_PRINT_STATE(듀얼리스트,場所,書換先カードID)
	ENDIF

ELSEIF 종류 == 필드起動 && バトルフェイズ == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,書換先カードID,1)
	
	IF RESULT == 0 && 参照カードID != -1
		;가슴を搾る
		IF GETVAR(@"%듀얼리스트%_MZ_個Ｃ:場所") == 1
			;◆バトルフェイズ：乳搾りパターン１
			CALL HORUSTEIN_KOJO_BATTLE_MILK(듀얼리스트,書換先カードID,1)
			
			CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"発情",175)
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"個Ｃ",2,上書きモード)
		ELSE
			;◆バトルフェイズ：乳搾りパターン２
			CALL HORUSTEIN_KOJO_BATTLE_MILK(듀얼리스트,書換先カードID,2)
			
			CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"発情",185)
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"個Ｃ",1,上書きモード)
		ENDIF
		
		CALL HORUSTEIN_GET_MILK(듀얼리스트,書換先カードID)
		
		;発情
		SETCOLOR C_PINK
		PRINTFORML 
		PRINTFORML %암소名称%는 젖을 짜낸 영향인지、심하게 발정하고있다…
		PRINTFORML
		RESETCOLOR

		IF ターンプレイヤー != 듀얼리스트
			;상대 턴에 발동한 場合
			PRINTFORML %암소名称%는 젖을 짜여진 여운에 잠겨있다…

			;2回、うるさい鳴き
			CALL HORUSTEIN_RAND_COW_TXT(2,암소弛緩声)
		ENDIF
		
		PRINTFORMW
	ENDIF

ELSEIF 종류 == 攻撃後強制誘発
	;発情時に攻撃が中断された場合
	
	;ひとりえっち(ここは一回触ってるので短めに。효과処理もあるしね)
	PRINTFORML …발정한 %암소名称%는 %상대名称%에게 흔들흔들하며 가까이 가지만、시선을 발밑으로 향하니 도중에 다리를 멈춰버렸다。
	PRINTFORML 후우후우 하며 거친 호흡을 하는 %암소名称%는、자신의 젖에서 눈을 떼지 못하고있다…
	PRINTFORMW %암소名称%는 %搾手名称%에게 짜여진 기억을 기억해내는듯이、젖을 천천히 문지르기 시작했다…
	
	;1回、うるさい鳴き
	CALL HORUSTEIN_RAND_COW_TXT(1,암소鳴き声)
	
	IF 암소대상 != -1
		CALL EROTIC_MOVE("バスト조교",암소대상,-1,500)
	ENDIF
	절정횟수 += RESULT:1
	IF 절정횟수 > 0
		PRINTFORML %암소名称%는 {절정횟수}번 절정한것같다。
		PRINTFORMW 여운이 기분좋았던것인지、%암소名称%의 주변엔 작고 하얀 반점이 흩날리고있다…
	ENDIF
	
	PRINTFORML 
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,書換先カードID)
	
	IF RESULT == 0
		IF 攻撃대상 == 5 
			;直接攻撃失敗時
			;再び手を伸ばすと、상대 필드에 他に戦闘を行える 몬스터가 いない為か、タガが外れたように…
			
			PRINTFORMW %암소名称%는 상대 필드에 전투를 할 수 있는 몬스터가 없는것을 확인하고、더욱깊은 쾌락을 탐구해 손가락을 움직이기 시작했다…
			절정횟수 += 2
			SETCOLOR C_PINK
			
			;암소の鳴き声を鳴らす
			CALL HORUSTEIN_RAND_COW_TXT(절정횟수,암소절정声)
			
			IF 암소대상 != -1
				REPEAT 절정횟수
					CALL EROTIC_MOVE("バスト조교",암소대상,-1,200)
				REND
			ENDIF
			
			RESETCOLOR
			PRINTFORML 울음소리가 끝나니、%암소名称%는 "철퍽" 하는 소리를 내며 하얀 물웅덩이에 쓰러졌다…
			PRINTFORMW 마음껏 기분 좋아진 %암소名称%는、만족한것같은 미소를 띄우고있다…
			;절정後の암소鳴き声
			CALL HORUSTEIN_RAND_COW_TXT(2,암소弛緩声)
			PRINTFORMW 
		ELSE
			;モンスター攻撃時に失敗
			PRINTFORML %상대名称%는 발정한 %암소名称%의 가슴에 짓눌렸다！
			PRINTFORM %암소名称%는
			CALL HORUSTEIN_RAND_COW_TXT(2,암소弛緩声,10,2)
			PRINTFORML 하며 울고、규격외로 너무 커다란 가슴으로、그대로 %상대名称%을 끼워버렸다！
			PRINTFORMW 짓이겨지지 않으려고 %상대名称%는、너무 커다란 그 가슴을 "들어올려"버렸다…
			PRINTFORML 
			PRINTFORMW %상대名称%는 짓눌리지 않으려 %암소名称%의 가슴을 받치고있다…
			
			;守備표시にして、공격 대상から外れる
			CALL SET_FIELD_INFO(対面者,"필드",攻撃대상,"攻避",2)
			CALL SET_FIELD_INFO(対面者,"필드",攻撃대상,"表",2,上書きモード)
			CALL SET_FIELD_INFO(対面者,"필드",攻撃대상,"リ数",1)
			CALL CARD_SET_CATEGORY(対面者,"필드",攻撃대상,"호루스타인")

			ステ合計 += GETVAR(@"%対面者%_MZ_攻:攻撃대상")
			ステ合計 += GETVAR(@"%対面者%_MZ_守:攻撃대상")
			ステ合計 /= 1000
			
			;攻守の합계가 1000毎に乳サイズ1UP
			IF ステ合計 > 0
				PRINTFORM %암소名称%의 가슴은 더욱 커진것같다…
				SETCOLOR C_PINK
				PRINTFORMW ({ステ合計}랭크 Up)
				RESETCOLOR
				CALL HORUSTEIN_GET_BUST(参照カードID,乳サイズ보정)
				CALL HORUSTEIN_PRINT_STATE(듀얼리스트,場所,書換先カードID, RESULT:2+ステ合計,2,1);2乳上書き(比較なし)、1画像アイコンのみ
				
				PRINTFORML 
				;サイズアップ分鳴く、うるさい鳴き
				CALL HORUSTEIN_RAND_COW_TXT(ステ合計,3)
				PRINTFORMW 
			ENDIF
		ENDIF
		
	ENDIF
ELSEIF 종류 == 攻守変動
	;攻守の합계가 1000毎に乳サイズ1UP
	CALL HORUSTEIN_GET_EX_SUM(듀얼리스트,場所)
ELSEIF 종류 == 효과後強制誘発
	;ドリップ状態(守備표시時、ホルスタイン以外のカードが발동する度に搾られる)
	;ドリップ状態の処理
	CALL HORUSTEIN_DRIP(듀얼리스트,書換先カードID)
	ENDIF
	

;ここから암소(상대)のカード処理-----
;CARDNAMEとCARDだけ処理を変える。(상대が○○登録で설정したカードの情報を표시する)

@CARDNAME_279559,参照先
#DIMS DYNAMIC 参照先
TRYCALLFORM CARDNAME_179559(参照先,"상대")
RESULTS = %RESULTS%

@CARD_279559(参照先)
#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS
TRYCALLFORM CARD_179559(参照先,"상대")
RETURN RESULT


@CARDSUMMON_279559(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照

TRYCALLFORM CARDSUMMON_179559(듀얼리스트,소환カード,EX参照)

@CARD_EXPLANATION_279559(종류)
#DIM DYNAMIC 종류

TRYCALLFORM CARD_EXPLANATION_179559(종류)

@CARDCAN_279559(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

TRYCALLFORM CARDCAN_179559(듀얼리스트,종류,ゾーン,場所)
RETURN RESULT

@CARDEFFECT_279559(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

TRYCALLFORM CARDEFFECT_179559(듀얼리스트,종류,ゾーン,場所)