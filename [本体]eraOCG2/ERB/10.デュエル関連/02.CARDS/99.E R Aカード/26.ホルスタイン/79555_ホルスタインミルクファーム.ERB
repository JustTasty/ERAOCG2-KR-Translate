@CARDNAME_79555,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

;カード制作者：xonng
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 호루스타인─밀크 팜
	CASE "略称"
		RESULTS = 밀크 팜
	CASE "カテゴリ"
		RESULTS:0 = 호루스
		RESULTS:1 = 호루스타인
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_79555(参照先)

#DIMS DYNAMIC 参照先
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カードIDチェック
#DIM DYNAMIC 마법함정数チェック

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN フィールド
	CASE "추가日"
		RETURN 250325
ENDSELECT

RETURN 0

@CARD_EXPLANATION_79555(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 자신 필드는 "호루스타인─밀크 팜"이 된다。
PRINTL 이 카드의 효과의 발동에 대해서、상대는 카드의 효과를 발동할 수 없다。
CALLFORM CARD_ABLE(1,마함起動)
PRINTL ①：배틀 페이즈、자신 메인 페이즈에 발동할 수 있다。
PRINTL 패를 １장 버리고、버린 카드에 따라 이하의 효과를 적용한다。
PRINTL ●통상몬스터：그 카드가 야수족 암컷 몬스터인 경우、１０００ＬＰ 회복한다。
PRINTL ●효과몬스터：묘지의 "호루스타인" 몬스터를 １장 골라 특수 소환한다。
PRINTL ●마법카드：덱에서 "호루스타인" 통상 마법카드를 １장 골라 패에 넣는다。
PRINTL ●함정카드：덱에서 "호루스타인" 함정카드 １장 고르고 패에 넣는다。


CALL TEXT_DECORATION("ROGUE")

@CARDCAN_79555(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,79555)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動 
	SIF !(メインフェイズ == 1 && ターンプレイヤー == 듀얼리스트) && バトルフェイズ != 1
		RETURN 0
	;他、各種효과의 발동可否をチェックする(下記の内、一つでもOKなら발동可能)
	;일반 몬스터(패의 通常)
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종류 = 通常モン
	;효과条件종족 = 야수족
	;효과条件성별 = 牝性
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 1
	
	;효과モンスター(패의 효과+묘지의 호루스타인モンスター)
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종류:0 = 효과モン
	효과条件종류:1 = 의식
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT == 1
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件종족 = 야수족
		효과条件カテゴリ = 호루스타인
		효과条件소환可 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			RETURN 1
	ENDIF
	
	;마법 카드(패의 마법+덱의 호루스타인마법 카드)
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종류:0 = 通常마법
	효과条件종류:1 = 装備마법
	효과条件종류:2 = 速攻마법
	효과条件종류:3 = 지속마법
	효과条件종류:4 = フィールド
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT == 1
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件종류 = 通常마법
		효과条件カテゴリ = 호루스타인
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			RETURN 1
	ENDIF
	
	;함정(패의 함정+덱의 호루스타인 함정 카드 )
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종류:0 = 通常함정
	효과条件종류:1 = 지속함정
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT == 1
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件종류 = 함정全般
		효과条件カテゴリ = 호루스타인
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			RETURN 1
	ENDIF
	
	RETURN 0
ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	RETURN 1
ELSEIF 종류 == チェーン可否
	SIF !GROUPMATCH(チェーン可否종류, 마함起動)
		RETURN 0
	
	RETURN 1
ENDIF

@CARDEFFECT_79555(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 효과条件BIT
#DIM DYNAMIC 選択カード종류
#DIMS DYNAMIC カテゴリ名称
#DIMS DYNAMIC 암소名称
#DIM DYNAMIC 암소대상
#DIM DYNAMIC 암소가슴追

#DIM DYNAMIC 紋章カードID
#DIM DYNAMIC 紋章場所
#DIM DYNAMIC 암소鳴き声
#DIM DYNAMIC 암소절정声
#DIM DYNAMIC 암소弛緩声
#DIMS DYNAMIC 乳소환

IF  종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79555)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF
ELSEIF 종류 == 마함起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79555)
	IF RESULT == 0
;＞＞条件설정開始―――――――――――――――――――――――――――――――――
		;일반 몬스터(패의 通常)
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件종류 = 通常モン
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			SETBIT 효과条件BIT, 0
	
		;효과モンスター(패의 효과+호루스타인 몬스터의 チェック)
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件종류:0 = 효과モン
		효과条件종류:1 = 의식
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件종족 = 야수족
			효과条件カテゴリ = 호루스타인
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				SETBIT 효과条件BIT, 1
		ENDIF
	
		;마법 카드(패의 마법+덱의 호루스타인마법 카드)
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件종류:0 = 通常마법
		효과条件종류:1 = 装備마법
		효과条件종류:2 = 速攻마법
		효과条件종류:3 = 지속마법
		효과条件종류:4 = フィールド
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件종류 = 通常마법
			효과条件カテゴリ = 호루스타인
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				SETBIT 효과条件BIT, 2
		ENDIF
		
		;함정(패의 함정+덱의 호루스타인 함정 카드 )
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件종류:0 = 通常함정
		효과条件종류:1 = 지속함정
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件종류 = 함정全般
			효과条件カテゴリ = 호루스타인
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				SETBIT 효과条件BIT, 3
		ENDIF
;＞＞条件설정終了―――――――――――――――――――――――――――――――――
	;발동条件を満たしており、패에서 捨てられるカード
	カウンタ = 0
	;0から始まる連番でなければならない
	CALL FULLRESET
	효과範囲손패 = 1
	IF GETBIT(효과条件BIT,0) == 1
		효과条件종류:カウンタ = 通常モン
		カウンタ++
	ENDIF
	IF GETBIT(효과条件BIT,1) == 1
		효과条件종류:カウンタ = 효과モン
		カウンタ++
		효과条件종류:カウンタ = 의식
		カウンタ++
	ENDIF
	IF GETBIT(효과条件BIT,2) == 1
		효과条件종류:カウンタ = 通常마법
		효과条件종류:(カウンタ+1) = 装備마법
		효과条件종류:(カウンタ+2) = 速攻마법
		효과条件종류:(カウンタ+3) = 지속마법
		효과条件종류:(カウンタ+4) = フィールド
		カウンタ += 5
	ENDIF
	IF GETBIT(효과条件BIT,3) == 1
		효과条件종류:カウンタ = 通常함정
		효과条件종류:(カウンタ+1) = 지속함정
	ENDIF
	CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
	IF RESULT == 1
			;★デバッグ用チェッカ-
			;PRINTFORMW 효과1：{효과条件_1} 효과2：{효과条件_2} 효과3：{효과条件_3} 효과4：{효과条件_4}
			選択カードID = RESULT:1
			CALLFORM CARD_{選択カードID},"종류"
			選択カード종류 = RESULT
	
	SELECTCASE 選択カード종류
			;●일반 몬스터：그 카드가 야수족牝性 몬스터의 경우、LPを1000ポイント회복する。
			CASE 通常モン
				효과条件BIT = 0
				CALLFORM CARD_{選択カードID},"성별"
				SIF RESULT == 牝性
					SETBIT 효과条件BIT, 0
				CALLFORM CARD_{選択カードID},"종족"
				SIF RESULT == 야수족
					SETBIT 효과条件BIT, 1

				;牝性かつ、야수족
				IF GETBIT(효과条件BIT,0) == 1 && GETBIT(효과条件BIT,1) == 1
					CALL HORUSTEIN_GET_DATA(듀얼리스트,選択カードID)
					암소名称 = %RESULTS:0%
					紋章場所 = RESULT:0		;(ホルスタインモンスターでない경우、-1が入る
					PRINTFORML %암소名称%에게서 짜낸 우유를 마셨다！
					SIF 紋章場所 != -1
						CALL HORUSTEIN_GET_MILK(듀얼리스트,選択カードID);★						
					;乳を吸ってLP회복
					CALL LIFE_CHANGE(듀얼리스트,"増加","효과",1000)
				ENDIF
			;●효과モンスター：묘지의 "호루스타인" 몬스터를 특수 소환한다。
			CASE 효과モン,의식
				CALL FULLRESET
				효과範囲묘지 = 1
				효과条件カテゴリ  = 호루스타인
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
				
				;◆소환演出
				IF RESULT != -1
					選択カードID = RESULT:1
					CALL HORUSTEIN_GET_DATA(듀얼리스트,選択カードID);★
					乳소환 = %RESULTS:3%
					암소鳴き声 = RESULT:2 
					
					PRINTFORMW %乳소환%
					CALL HORUSTEIN_RAND_COW_TXT(1,암소鳴き声)
					PRINTFORMW 
				ENDIF
			;●마법 카드："호루스타인"마법 카드를 패에 넣는다。
			CASE 20 TO 29
				CALL FULLRESET
				효과条件カテゴリ = 호루스타인
				효과条件종류 = 마법全般
				CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
			;● 함정 카드 ："호루스타인"함정 카드를 패에 넣는다。
			CASE 30 TO 39	
				CALL FULLRESET
				효과条件カテゴリ = 호루스타인
				효과条件종류 = 함정全般
				CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
			ENDSELECT
		ENDIF
	ENDIF

ELSEIF 종류 == 지속효과
	CALL SET_FIELD_MAGIC_INFO(듀얼리스트, "밀크 팜")
ENDIF