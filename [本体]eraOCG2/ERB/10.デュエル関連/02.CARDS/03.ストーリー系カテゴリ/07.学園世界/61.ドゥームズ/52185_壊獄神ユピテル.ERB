@CARDNAME_52185,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 괴감옥신 유피테르
	CASE "略称"
		RESULTS = 괴감옥신 유피테르
	CASE "カテゴリ"
		RESULTS:0 = 감옥신
ENDSELECT


@CARD_52185(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN エクシーズ
	CASE "属性"
		RETURN 風属性
	CASE "種族"
		RETURN 機械族
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 3500
	CASE "守備力"
		RETURN 2500
	CASE "特殊制限"
		RETURN 1
	CASE "追加日"
		RETURN 250809
ENDSELECT
RETURN 0


@CARDSUMMON_52185(決闘者,召喚カード,EX参照)
#DIMS DYNAMIC 決闘者
#DIM DYNAMIC 召喚カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 3
素材条件レベル = 10
;追加効果用
指定素材種類 = エクシーズ
IF EX参照 == 1
	CALL XYZCAN(決闘者,召喚カード,52185) 
	RETURN RESULT
ENDIF

@CARD_EXPLANATION_52185(種類)
#DIM DYNAMIC 種類
PRINTL レベル１０モンスター×３
CALLFORM CARD_ABLE(0,EXデッキ起動)
PRINTL 自分は「괴감옥신 유피테르」を１ターンに１度しか特殊召喚できない。
PRINTL 「괴감옥신 유피테르」は１ターンに１度、装備カードを３枚以上装備している自分のモンスター上に重ねてＸ召喚する事もできる。
CALLFORM CARD_ABLE(1,EX召喚時)
PRINTL ①：このカードがＸ召喚した場合に発動できる。
PRINTL このカードに装備可能な装備魔法カードを任意の数だけ自分の墓地から装備する。
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ②：Ｘモンスターを素材としているこのカードの攻撃力は３０００アップする。
CALLFORM CARD_ABLE(10,戦場起動)
PRINTL ③：１ターンに１度、このカードのＸ素材を１つ取り除いて発動できる。
PRINTL 自分の墓地から「둠즈」モンスター１体を特殊召喚する。
PRINTL その後、フィールドのカード１枚を破壊できる。

@CARDCAN_52185(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件達成

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,52185)
SIF RESULT == 1
	RETURN 0

IF 種類 == EXデッキ起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL CARD_EX_CHECK(決闘者,52185)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件重ねてＸ = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	FOR カウンタ,0,5
		CALL FULLRESET
		効果条件CNT = 1
		CALL CARD_EQUIP_CHECK(決闘者,カウンタ,"戦場")
		IF RESULT >= 3
			RETURN 1
		ENDIF
	NEXT
	RETURN 0
ELSEIF 種類 == EX召喚時
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件種類 = 装備魔法
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 攻守変動
	SIF ゾーン != "戦場"
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_条達:場所") == 0
		RETURN 0
	RETURN 1
ELSEIF 種類 == 戦場起動
	SIF ゾーン != "戦場"
		RETURN 0
	SIF 効果発動済(決闘者,場所,ゾーン)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_Ｘ:場所") <= -1
		RETURN 0
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件カテゴリ = 둠즈
	効果条件召喚可 = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_52185(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 選択カード,5
#DIM DYNAMIC 素材カード
#DIM DYNAMIC 装備可能数
#DIMS DYNAMIC 出処
#DIM DYNAMIC 選択結果


CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == EXデッキ起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,52185,0)
	IF RESULT == 0
		;装備カード３枚以上装備しているモンスターの場所を調べる
		FOR カウンタ,0,5
			CALL FULLRESET
			効果条件CNT = 1
			CALL CARD_EQUIP_CHECK(決闘者,カウンタ,"戦場")
			IF RESULT >= 3
				選択カード:カウンタB = カウンタ
				カウンタB ++
			ENDIF
		NEXT
		CALL FULLRESET
		効果範囲戦場 = 1
		FOR カウンタ,0,カウンタB
			効果条件固定:カウンタ = 選択カード:カウンタ
		NEXT
		CALL CARD_CHOOSE_ADV(決闘者,ゾーン,場所)
		素材カード = RESULT
		IF 素材カード != 9999
			CALL RESETCONDITION_MAT
			必要素材数 = 1
			素材条件固定 = 素材カード
			TRYCALLFORM CARD_{GETVAR(@"%決闘者%_戦場:素材カード")},"種類"
			SIF RESULT == エクシーズ
				効果追加種類 = 効追条達
			重ねてＸ = 1
			TRYCALLFORM XYZSUMMON(決闘者,場所)
		ENDIF
	ENDIF
ELSEIF 種類 == EX召喚時
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,52185,1)
	IF RESULT == 0
		CALL FULLRESET
		;魔罠ゾーンの空き数カウント
		装備可能数 = 5 - 魔罠数(決闘者)
		;墓地の装備魔法の枚数カウント
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件種類 = 装備魔法
		効果条件CNT = 1
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT != 0 && RESULT < 装備可能数
			装備可能数 = RESULT
		FOR カウンタ,0,装備可能数
			CALL FULLRESET
			IF 決闘者 == "自分"
				PRINTFORML \@ カウンタ == 0 ? 유피테르に # さらに\@装備魔法を装備させますか？
				CALL YN_CHOICE(決闘者)
				選択結果 = RESULT
			ENDIF
			IF 選択結果 == 0
				CALL FULLRESET
				効果範囲墓地 = 1
				効果条件種類 = 装備魔法
				CALL CARD_CHOOSE_ADV(決闘者,ゾーン,場所)
				選択カード = RESULT
				IF 選択カード != 9999
					CALL CARD_SELECT(決闘者,選択カード)
					選択カード = RESULT
					出処 = %参照先（CS）%
					CALL FULLRESET
					効果条件固定 = カード召喚先
					CALLFORM CARD_EQUIP_ADV(決闘者,選択カード,出処)
				ENDIF
			ELSE
				BREAK
			ENDIF
		NEXT
	ENDIF
ELSEIF 種類 == 攻守変動
	CALL STATCHANGE_BASIC(決闘者,"自身",場所,3000)
ELSEIF 種類 == 戦場起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,52185,10,X消費1)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件種類 = モン全般
		効果条件カテゴリ = 둠즈
		CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
		IF 決闘者 == "自分"
			PRINTL フィールドのモンスターを破壊しますか？
			CALL YN_CHOICE(決闘者)
			選択結果 = RESULT
		ELSE
			CALL FULLRESET
			効果範囲相手戦場 = 1
			CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
			SIF RESULT == 1
				選択結果 = 0
		ENDIF
		IF 選択結果 == 0
			CALL CARD_REMOVAL_BASIC(決闘者,"両戦場単体",場所,"墓地")
		ENDIF
	ENDIF
ENDIF