@CARDNAME_1967,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 둠즈ＶＩＩ－엘라라
	CASE "略称"
		RESULTS = 둠즈VII 엘라라
	CASE "カテゴリ"
		RESULTS:0 = 둠즈
ENDSELECT


@CARD_1967(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1400
	CASE "守備力"
		RETURN 1200
	CASE "성별"
		RETURN 牝性
	CASE "추가日"
		RETURN 250809
ENDSELECT

RETURN 0

@CARD_EXPLANATION_1967(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,소환時,특수소환時,破壊→묘지)
PRINTL ①：이 카드가 소환 / 특수 소환에 성공한 경우、또는 효과로 파괴된 경우에 발동할 수 있다。
PRINTL 덱에서 "둠즈" 마법 / 함정 카드 １장을 자신 필드에 세트한다。
CALLFORM CARD_ABLE(2,필드起動,フリー誘発)
PRINTL ②：자신 / 상대턴에、이 카드가 장착 카드를 장착하고 있는 경우에 발동할 수 있다。
PRINTL 이 카드의 레벨과 같은 수치의 랭크를 가진 기계족 / 바람속성 Ｘ몬스터 １장을 Ｘ소환 취급으로 ＥＸ덱에서 특수 소환하고、
PRINTL 이 카드에 장착되어있는 카드의 수＋１만큼 그 몬스터의 Ｘ소재를 늘린다。
PRINTL 그 후、이 카드 및 이 카드의 장착 카드를 전부 묘지로 보낸다。

@CARDCAN_1967(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 対面者


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,1967)
SIF RESULT == 1
	RETURN 0

IF 종류 == 소환時 || 종류 == 특수소환時 || 종류 == 破壊→묘지
	SIF 발동済_1(듀얼리스트,1967)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 둠즈
	효과条件종류 = 마함全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF 발동済_2(듀얼리스트,1967)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	CALL CARD_EQUIP_CHECK(듀얼리스트,場所,ゾーン)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 엑시즈
	효과条件종족 = 기계족
	효과条件속성 = 바람속성
	효과条件소환可 = 1
	효과条件特不無視 = 1
	효과条件レベル = GETVAR(@"%듀얼리스트%_MZ_星:場所")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_1967(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 装備数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIMS DYNAMIC 出処

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 소환時 || 종류 == 특수소환時 || 종류 == 破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1967,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 둠즈
		효과条件종류 = 마함全般
		CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"裏側표시")
	ENDIF
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1967,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件CNT = 1
		CALL CARD_EQUIP_CHECK(듀얼리스트,場所,ゾーン)
		SIF RESULT > 0
			装備数 = RESULT
		;
		;DOOMDURGの③효과の処理用
		CALL FULLRESET
		효과条件名前 = 둠즈 커맨드『Ｄ.Ｏ.Ｏ.Ｍ.Ｄ.Ｕ.Ｒ.Ｇ.』
		CALL CARD_EQUIP_CHECK(듀얼리스트,場所,ゾーン)
		SIF RESULT == 1
			条件달성 = 1
		;
		CALL FULLRESET
		효과条件종류 = 엑시즈
		효과条件종족 = 기계족
		효과条件속성 = 바람속성
		효과条件レベル = GETVAR(@"%듀얼리스트%_MZ_星:場所")
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		選択カード_ID = RESULT:99
		IF 選択カード != 9999
			CALL FULLRESET
			효과条件特不無視 = 1
			CALL CARD_EX_SUMMON(듀얼리스트,"EX덱",選択カード)
			CALL SET_FIELD_INFO(듀얼리스트,"필드",カード소환先,"Ｘ",装備数 + 1)
			CALL FULLRESET
			CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
			IF 条件달성 == 1
				SETCOLOR C_YELLOW
				PRINTL 【둠즈 커맨드『Ｄ.Ｏ.Ｏ.Ｍ.Ｄ.Ｕ.Ｒ.Ｇ.』가 장착됩니다】
				RESETCOLOR
				CALL FULLRESET
				효과範囲묘지 = 1
				효과条件名前 = 둠즈 커맨드『Ｄ.Ｏ.Ｏ.Ｍ.Ｄ.Ｕ.Ｒ.Ｇ.』
				CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
				選択カード = RESULT
				IF 選択カード != 9999
					CALL CARD_SELECT(듀얼리스트,選択カード)
					選択カード = RESULT
					出処 = %参照先（CS）%
					CALL FULLRESET
					효과条件固定 = カード소환先
					CALLFORM CARD_EQUIP_ADV(듀얼리스트,選択カード,出処)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

