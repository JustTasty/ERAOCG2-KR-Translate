@CARDNAME_24384,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 둠즈ＸＩＩ－드라스티아
	CASE "略称"
		RESULTS = 둠즈XII 드라스
	CASE "カテゴリ"
		RESULTS:0 = 둠즈
ENDSELECT


@CARD_24384(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 効果モン
	CASE "属性"
		RETURN 闇属性
	CASE "種族"
		RETURN ドラゴン族
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 2400
	CASE "守備力"
		RETURN 1500
	CASE "性別"
		RETURN 雄性
	CASE "追加日"
		RETURN 250809
ENDSELECT

RETURN 0

@CARD_EXPLANATION_24384(種類)
#DIM DYNAMIC 種類

PRINTL このカード名の①②の効果はそれぞれ１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(1,手札起動)
PRINTL ①：自分フィールドの「둠즈」カード１枚を対象として発動できる。
PRINTL そのカードを破壊し、このカードを手札から特殊召喚する。
PRINTL その後、デッキから装備魔法カード１枚をこのカードに装備できる。
CALLFORM CARD_ABLE(2,戦場起動,フリー誘発)
PRINTL ②：自分・相手ターンに、このカードが装備カードを装備している場合に発動できる。
PRINTL このカードのレベルと同じ数値のランクを持つ機械族・風属性Ｘモンスター１体をＸ召喚扱いでＥＸデッキから特殊召喚し、
PRINTL このカードに装備されているカードの数＋１だけそのモンスターのＸ素材を増やす。
PRINTL その後、このカード及びこのカードの装備カードを全て墓地へ送る。

@CARDCAN_24384(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件達成
#DIMS DYNAMIC 対面者


CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,24384)
SIF RESULT == 1
	RETURN 0

IF 種類 == 手札起動
	SIF 発動済_1(決闘者,24384)
		RETURN 0
	SIF ゾーン != "手札"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL CARD_SS_CHECK(決闘者,24384,"手札",1)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果範囲魔罠 = 1
	効果条件カテゴリ = 둠즈
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 戦場起動 || 種類 == フリー誘発
	SIF 発動済_2(決闘者,24384)
		RETURN 0
	SIF ゾーン != "戦場"
		RETURN 0
	SIF モンスター数(決闘者) >= 5
		RETURN 0
	CALL FULLRESET
	CALL CARD_EQUIP_CHECK(決闘者,場所,ゾーン)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	効果範囲EXデッキ = 1
	効果条件種類 = エクシーズ
	効果条件種族 = 機械族
	効果条件属性 = 風属性
	効果条件召喚可 = 1
	効果条件特不無視 = 1
	効果条件レベル = GETVAR(@"%決闘者%_MZ_星:場所")
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_24384(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件達成
#DIM DYNAMIC 装備数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 選択結果
#DIMS DYNAMIC 出処

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 手札起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,24384,1)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲戦場 = 1
		SIF モンスター数(決闘者) < 5
			効果範囲魔罠 = 1
		効果条件行先フラグ = 破壊行き
		効果条件カテゴリ = 둠즈
		CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(決闘者, "手札", 場所)
			CALL FULLRESET
			効果範囲デッキ = 1
			効果条件種類 = 装備魔法
			CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
			IF RESULT == 1 && 魔罠数(決闘者) < 5
				CALL FULLRESET
				IF 決闘者 == "自分"
					PRINTL 드라스티아に装備魔法を装備させますか？
					CALL YN_CHOICE(決闘者)
					選択結果 = RESULT
				ENDIF
				IF 選択結果 == 0
					CALL FULLRESET
					効果範囲デッキ = 1
					効果条件種類 = 装備魔法
					CALL CARD_CHOOSE_ADV(決闘者,ゾーン,場所)
					選択カード = RESULT
					IF 選択カード != 9999
						CALL CARD_SELECT(決闘者,選択カード)
						選択カード = RESULT
						出処 = %参照先（CS）%
						CALL FULLRESET
						効果条件固定 = カード召喚先
						CALLFORM CARD_EQUIP_ADV(決闘者,選択カード,出処)
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ELSEIF 種類 == 戦場起動 || 種類 == フリー誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,24384,2)
	IF RESULT == 0
		CALL FULLRESET
		効果条件CNT = 1
		CALL CARD_EQUIP_CHECK(決闘者,場所,ゾーン)
		SIF RESULT > 0
			装備数 = RESULT
		;
		;DOOMDURGの③効果の処理用
		CALL FULLRESET
		効果条件名前 = 終刻起動『Ｄ.Ｏ.Ｏ.Ｍ.Ｄ.Ｕ.Ｒ.Ｇ.』
		CALL CARD_EQUIP_CHECK(決闘者,場所,ゾーン)
		SIF RESULT == 1
			条件達成 = 1
		;
		CALL FULLRESET
		効果条件種類 = エクシーズ
		効果条件種族 = 機械族
		効果条件属性 = 風属性
		効果条件レベル = GETVAR(@"%決闘者%_MZ_星:場所")
		CALL CARD_CHOOSE_BASIC(決闘者,"EXデッキ",ゾーン,場所)
		選択カード = RESULT
		選択カード_ID = RESULT:99
		IF 選択カード != 9999
			CALL FULLRESET
			効果条件特不無視 = 1
			CALL CARD_EX_SUMMON(決闘者,"EXデッキ",選択カード)
			CALL SET_FIELD_INFO(決闘者,"戦場",カード召喚先,"Ｘ",装備数 + 1)
			CALL FULLRESET
			CALL CARD_DISPOSE(決闘者,ゾーン,場所)
			IF 条件達成 == 1
				SETCOLOR C_YELLOW
				PRINTL 【終刻起動『Ｄ.Ｏ.Ｏ.Ｍ.Ｄ.Ｕ.Ｒ.Ｇ.』が装備されます】
				RESETCOLOR
				CALL FULLRESET
				効果範囲墓地 = 1
				効果条件名前 = 終刻起動『Ｄ.Ｏ.Ｏ.Ｍ.Ｄ.Ｕ.Ｒ.Ｇ.』
				CALL CARD_CHOOSE_ADV(決闘者,ゾーン,場所)
				選択カード = RESULT
				IF 選択カード != 9999
					CALL CARD_SELECT(決闘者,選択カード)
					選択カード = RESULT
					出処 = %参照先（CS）%
					CALL FULLRESET
					効果条件固定 = カード召喚先
					CALLFORM CARD_EQUIP_ADV(決闘者,選択カード,出処)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

