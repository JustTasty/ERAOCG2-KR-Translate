@CARDNAME_92183,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 둠즈 디스트럭션
	CASE "略称"
		RESULTS = 둠즈 디스트럭션
	CASE "カテゴリ"
		RESULTS:0 = 둠즈
ENDSELECT

@CARD_92183(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
	CASE "추가日"
		RETURN 250809
ENDSELECT
RETURN 0

@CARD_EXPLANATION_92183(종류)
#DIM DYNAMIC 종류

PRINTL このカード名の①②の효과はそれぞれ１ターンに１度しか使用できない。
CALL CARD_ABLE(1,마함起動,フリー誘発)
PRINTL ①：자신フィールドの「둠즈」Ｘモンスター１体を대상としてこのカードを발동できる。
PRINTL このカードを以下の효과を持つ装備カード扱いでそのモンスターに装備する。
CALLFORM CARD_ABLE(0,효과後誘発)
PRINTL ●상대が효과で덱からカードを손패に加えた場合に발동できる。
PRINTL 상대の손패をランダムに１枚덱に戻す。
CALL CARD_ABLE(2,破壊→묘지)
PRINTL ②：このカードが효과で破壊された場合、
PRINTL フィールドの表側표시モンスター１体を대상として발동できる。
PRINTL 덱から「둠즈」モンスター１体を装備마법カード扱いで대상のモンスターに装備する。

@CARDCAN_92183(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ数
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 装備者

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,92183)
SIF RESULT == 1
	RETURN 0

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

IF 종류 == 마함起動 || 종류 == フリー誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF 발동済_1(듀얼리스트,92183)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 둠즈
	효과条件종류 = 엑시즈
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 破壊→묘지
	SIF 발동済_2(듀얼리스트,92183)
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲상대필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 둠즈
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	CALL CARD_CHECK_FIN_BIT(듀얼리스트,"離이동元",4,1)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"손패취득+D",1)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_92183(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードB
#DIM DYNAMIC 選択カードB_ID
#DIM DYNAMIC 修正値
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 装備者
#DIMS DYNAMIC 대상者
#DIMS DYNAMIC 出処

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

IF 종류 == 마함起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,92183,1)
	IF RESULT == 0
		효과範囲필드 = 1
		효과条件カテゴリ = 둠즈
		효과条件종류 = 엑시즈
		효과条件표시 = 表側표시
		CALLFORM CARD_EQUIP_ADV(듀얼리스트,場所,ゾーン)
	ENDIF
ELSEIF 종류 == 破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,92183,0)
	IF RESULT == 0
		CALL FULLRESET
		SETCOLOR C_YELLOW
		PRINTL 【装備대상を選んでください】
		RESETCOLOR
		효과範囲필드 = 1
		효과範囲상대필드 = 1
		효과条件표시 = 表側표시
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL CARD_SELECT(듀얼리스트,選択カード)
			選択カード = RESULT
			대상者 = %대상者（CS）%
			出処 = %参照先（CS）%
			SETCOLOR C_YELLOW
			PRINTL 【装備する「둠즈」モンスターを選んでください】
			RESETCOLOR
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 둠즈
			효과条件종류 = モン全般
			CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
			選択カードB = RESULT
			選択カードB_ID = RESULT:99
			IF 選択カードB != 9999
				CALL CARD_SELECT(듀얼리스트,選択カードB)
				選択カードB = RESULT
				CALL FULLRESET
				IF 대상者 == "상대"
					出処 = 상대덱
				ELSE
					出処 = 덱
				ENDIF
				CALL CARD_EQUIP_MONSTER(대상者,選択カード,出処,選択カードB)
				TRYCALLFORM CARDNAME_{選択カードB_ID},"名前"
				SETCOLOR C_YELLOW
				PRINTFORMW 【%RESULTS%が装備されました】
				RESETCOLOR
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 효과後誘発
	CALL CARD_REMOVAL_BASIC(듀얼리스트,"손패破壊",場所,"덱")
ENDIF