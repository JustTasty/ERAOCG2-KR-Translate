@CARDNAME_72183,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 둠즈 커맨드『Ｄ.Ｏ.Ｏ.Ｍ.Ｄ.Ｕ.Ｒ.Ｇ.』
	CASE "略称"
		RESULTS = D.O.O.M.D.U.R.G.
	CASE "カテゴリ"
		RESULTS:0 = 둠즈
ENDSELECT


@CARD_72183(参照先)
;名前専用
#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 装備魔法
	CASE "追加日"
		RETURN 250809
ENDSELECT

RETURN 0

@CARD_EXPLANATION_72183(種類)
#DIM DYNAMIC 種類

CALLFORM CARD_ABLE(1,ターン開始時強制)
PRINTL ①：自分・相手のスタンバイフェイズに発動する。
PRINTL 相手に５００ダメージを与える。
CALLFORM CARD_ABLE(2,永続効果)
PRINTL ②：このカードが「둠즈」モンスターまたは機械族・風属性Ｘモンスターに装備されている場合、
PRINTL 装備モンスターは相手の効果の対象にならず、このカードは以下の効果を得る。
CALLFORM CARD_ABLE(10,魔罠起動,フリー誘発)
PRINTL ●自分・相手ターンに１度、発動できる。
PRINTL 自分フィールドの装備モンスター以外の表側表示カード１枚を破壊する。
PRINTL 装備モンスターはこのターン、攻撃力が装備モンスターのレベル・ランク×１００アップし、
PRINTL 直接攻撃でき、戦闘を行ったダメージステップ終了時に破壊される。
;둠즈のメインモンスター（エララ、アマルテ、ドラステ）側に処理を書いています
PRINTL ③：Ｘモンスター以外の「둠즈」モンスターに装備されているこのカードが装備モンスターの効果で墓地へ送られた場合、
PRINTL 自分フィールドの機械族・風属性Ｘモンスター１体を対象として発動する。
PRINTL このカードをそのモンスターに装備する。

@CARDCAN_72183(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 条件達成, 3

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL 装備判定(決闘者,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,72183)
SIF RESULT == 1
	RETURN 0

IF ゾーン == "魔罠" && (種類 == 永続効果 || (種類 == 魔罠起動 || 種類 == フリー誘発) && GETVAR(@"%決闘者%_魔_表:場所") == 1)
	SIF GETVAR(@"%装備者%_戦場:装備先") == -1
		RETURN 0
	;「●」の効果
	IF 種類 == 魔罠起動 || 種類 == フリー誘発
		SIF 効果発動済(決闘者,場所,ゾーン)
			RETURN 0
	ENDIF
	;装備モンスターチェック
	TRYCALLFORM CARD_{GETVAR(@"%装備者%_戦場:装備先")}, "種族"
	SIF RESULT == 機械族
		条件達成:0 = 1
	TRYCALLFORM CARD_{GETVAR(@"%装備者%_戦場:装備先")}, "属性"
	SIF RESULT == 風属性
		条件達成:1 = 1
	TRYCALLFORM CARD_{GETVAR(@"%装備者%_戦場:装備先")}, "種類"
	SIF RESULT == エクシーズ
		条件達成:2 = 1

	;「둠즈」か機械・風エクシーズならOK
	TRYCALLFORM CARDNAME_{GETVAR(@"%装備者%_戦場:装備先")}, "カテゴリ"
	IF RESULTS == "둠즈"
		RETURN 1
	ELSEIF 条件達成:0 == 1 && 条件達成:1 == 1 && 条件達成:2 == 1
		RETURN 1
	ENDIF
	RETURN 0
;装備処理
ELSEIF 種類 == 魔罠起動 || 種類 == 手札起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果範囲相手戦場 = 1
	効果条件表示 = 表側表示
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
;①の効果
ELSEIF 種類 == ターン開始時強制
	SIF ゾーン != "魔罠"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_72183(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 装備者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 装備先
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正


CALL 装備判定(決闘者,場所,ゾーン)
装備先 = RESULT	
装備者 = %RESULTS%


IF (種類 == 魔罠起動 || 種類 == フリー誘発) && GETVAR(@"%決闘者%_魔_表:場所") == 1
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,72183,10)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲戦場 = 1
		効果範囲魔罠 = 1
		効果条件表示 = 表側表示
		効果条件行先フラグ = 破壊行き
		効果条件他所 = 1
		CALL CARD_ADD_GY_ADV(決闘者,"戦場",装備先)
		IF RESULT == 1
			攻撃力修正 = GETVAR(@"%決闘者%_MZ_星:装備先") * 100
			CALL SET_FIELD_INFO(決闘者,"戦場",装備先,"一攻",攻撃力修正)
			CALL STATCHANGE_EFFECT(決闘者,"装備",場所,"直攻",1)
			CALL SET_FIELD_INFO(決闘者,"戦場",装備先,"終戦",破壊行き)
		ENDIF
	ENDIF
ELSEIF 種類 == 魔罠起動 || 種類 == 手札起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,72183)
	IF RESULT == 0
		;手札から発動後、魔法トラップゾーンにカード移動
		CALL FULLRESET
		効果範囲戦場 = 1
		効果範囲相手戦場 = 1
		CALLFORM CARD_EQUIP_ADV(決闘者,場所,ゾーン)
	ENDIF
ELSEIF 種類 == 永続効果
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(決闘者,"装備",場所,"対耐")
ELSEIF 種類 == ターン開始時強制
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,72183,1)
	IF RESULT == 0
		CALL LIFE_CHANGE(決闘者,"減少","効果",500,1)
	ENDIF
ENDIF
