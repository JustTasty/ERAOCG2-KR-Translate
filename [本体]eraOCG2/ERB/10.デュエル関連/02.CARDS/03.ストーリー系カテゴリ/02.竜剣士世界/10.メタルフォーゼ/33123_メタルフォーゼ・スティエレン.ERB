@CARDNAME_33123,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 메탈포제・スティエレン
	CASE "略称"
		RESULTS = MFスティエレン
	CASE "カテゴリ"
		RESULTS:0 = 메탈포제
ENDSELECT


@CARD_33123(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常モン
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 사이킥족
	CASE "レベル"
		RETURN 2
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 2100
	CASE "성별"
		RETURN 雄性
	CASE "分割"
		RETURN 펜듈럼
	CASE "추가日"
		RETURN 250630
ENDSELECT
RETURN 0

@CARD_EXPLANATION_33123(종류)
#DIM DYNAMIC 종류


CALL TEXT_DECORATION("P_MAGIC")
PRINTL このカード名のP효과は１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(1,마함起動)
PRINTL ①：このカード以外の자신フィールドのカード１枚を대상として발동できる。
PRINTL そのカードを破壊し、덱から「메탈포제」마법・함정カード１枚を選んで자신フィールドにセットする。

CALL TEXT_DECORATION("P_MAGIC_2")
PRINTL 黒鉄の機体に秘められた魂が覚醒する時、鋼鉄は秘金属へと昇華し、
PRINTL 人機一体の勇士となる。その身に刻まれし魂鋼を燃焼させろ！――錬装융합！！

@CARDCAN_33123(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,33123)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動
	SIF 발동済_1(듀얼리스트,33123)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_33123(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択횟수
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,33123)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF
ELSEIF 종류 == 마함起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,33123,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件他所 = 1
		효과条件行先플래그 = 破壊行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과条件カテゴリ = 메탈포제
			효과条件종류 = 마함全般
			CALL CARD_SET_BASIC(듀얼리스트,"덱",ゾーン,場所,"裏側표시")
		ENDIF
	ENDIF
ENDIF





