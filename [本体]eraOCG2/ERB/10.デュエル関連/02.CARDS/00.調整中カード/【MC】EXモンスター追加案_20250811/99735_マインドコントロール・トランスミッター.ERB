@CARDNAME_99735,参照先
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 마인드컨트롤 트랜스미터
	CASE "略称"
		RESULTS = MC트랜스미터
	CASE "カテゴリ"
		RESULTS:0 = MC
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_99735(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 効果モン
	CASE "属性"
		RETURN 光属性
	CASE "種族"
		RETURN 機械族
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 2200
	CASE "チューナー"
		RETURN 1
ENDSELECT
RETURN 0

@CARD_EXPLANATION_99735(種類)
#DIM DYNAMIC 種類

CALL TEXT_DECORATION("ROGUE")
PRINTL このカード名の①②③の効果はそれぞれ１ターンに１度しか使用できない。
CALL CARD_ABLE(1,手札起動)
PRINTL ①：自分メインフェイズに自分フィールドの魔法・罠カードを１枚手札に戻して発動できる。
PRINTL 手札のこのカードを特殊召喚する。
CALL CARD_ABLE(2,S素材時効果,X素材時効果,L素材時効果,F素材時効果)
PRINTL ②：このカードがEXカードの素材として墓地へ送られた場合に発動できる。
PRINTL 相手デッキにランダムなマインド・コントロールカード３枚を加える。
CALLFORM CARD_ABLE(3,墓地起動)
PRINTL ③：自分メインフェイズに墓地のこのカードを除外して発動できる。
PRINTL 相手は手札のカードを全てデッキに戻してシャッフルし、戻した枚数分ドローする。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_99735(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,99735)
SIF RESULT == 1
	RETURN 0

IF 種類 == 手札起動
	SIF 発動済_1(決闘者,99735)
		RETURN 0
	CALL CARD_SS_CHECK(決闘者,99735,"手札")
	SIF RESULT == 1
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL FULLRESET
	効果範囲魔罠 = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == S素材時効果 || 種類 == L素材時効果 || 種類 == X素材時効果 || 種類 == F素材時効果
	SIF 発動済_2(決闘者,99735)
		RETURN 0
	RETURN 1
ELSEIF 種類 == 墓地起動
	SIF 発動済_3(決闘者,99735)
		RETURN 0
	SIF ゾーン != "墓地"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_99735(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC ドロー枚数

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 手札起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,99735,1)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲魔罠 = 1
		CALL CARD_ADD_BASIC(決闘者,"魔罠",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(決闘者,"手札",場所)
		ENDIF
	ENDIF
	RETURN 0
ELSEIF 種類 == S素材時効果 || 種類 == L素材時効果 || 種類 == X素材時効果 || 種類 == F素材時効果
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,99735,2)
	IF RESULT == 0
		CALL FULLRESET
		REPEAT 3
			SELECTCASE RAND:9
				CASE 0
					CALL CARD_GENERATE_DECK(対面者,79840)
				CASE 1
					CALL CARD_GENERATE_DECK(対面者,79841)
				CASE 2
					CALL CARD_GENERATE_DECK(対面者,79842)
				CASE 3
					CALL CARD_GENERATE_DECK(対面者,79843)
				CASE 4
					CALL CARD_GENERATE_DECK(対面者,79844)
				CASE 5
					CALL CARD_GENERATE_DECK(対面者,79845)
				CASE 6
					CALL CARD_GENERATE_DECK(対面者,79846)
				CASE 7
					CALL CARD_GENERATE_DECK(対面者,79847)
				CASE 8
					CALL CARD_GENERATE_DECK(対面者,79848)
			ENDSELECT
		REND
		SETCOLOR C_PINK
		PRINTFORML 마인드컨트롤 트랜스미터が奇怪な音を立てると、
		PRINTFORMW 相手のデッキに催眠カードが加わった…。
		RESETCOLOR
	ENDIF
ELSEIF 種類 == 墓地起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,99735,3)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所)
		FOR カウンタ,0,紙束最大値
			SIF GETVAR(@"%対面者%_手札:カウンタ") == -1
				CONTINUE
			CALL CARD_DISPOSE(対面者,"手札",カウンタ,デッキ行き)
			ドロー枚数 ++
		NEXT
		CALL SHUFFLE(対面者)
		CALL CARDDRAW_BASIC(対面者,ドロー枚数,1)
		SETCOLOR C_PINK
		PRINTFORML 마인드컨트롤 트랜스미터が奇怪な音を立てて壊れると、
		PRINTFORMW 相手の手札が入れ替わった…。
		RESETCOLOR
	ENDIF
ENDIF
