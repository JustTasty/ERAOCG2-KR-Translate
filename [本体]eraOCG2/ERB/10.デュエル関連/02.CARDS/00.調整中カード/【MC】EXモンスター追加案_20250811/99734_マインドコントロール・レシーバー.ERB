@CARDNAME_99734,参照先
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 마인드컨트롤 리시버
	CASE "略称"
		RESULTS = MC 리시버
	CASE "カテゴリ"
		RESULTS:0 = MC
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_99734(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 1800
	CASE "튜너"
		RETURN 1
ENDSELECT
RETURN 0

@CARD_EXPLANATION_99734(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드명의 ①②③의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①：자신 메인 페이즈에 발동할 수 있다。
PRINT 패의 이 카드를 특수 소환한다。그 후、
SETCOLOR C_D_GRAY
PRINT 상대 덱에 무작위로 마인드 컨트롤 카드 ２장을 넣고、
RESETCOLOR
PRINTL 상대와 자신은 덱에서 １장 드로우한다。
CALLFORM CARD_ABLE(2,소환時,특수소환時)
PRINTL ②：이 카드가 소환 / 특수 소환에 성공한 경우에 발동할 수 있다。
PRINTL 자신 묘지 / 손패 / EX덱에서 EX 이외의 "MC"몬스터 １체를 특수 소환한다。
CALLFORM CARD_ABLE(3,묘지起動)
PRINTL ③：자신 메인 페이즈에 묘지의 이 카드를 제외하고 발동할 수 있다。
PRINTL 상대 묘지의 "MC"카드 ３장을 상대의 덱 위에 놓는다。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_99734(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,99734)
SIF RESULT == 1
	RETURN 0
IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,99734)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,99734,"손패")
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,99734)
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲묘지 = 1
	효과範囲EX덱 = 1
	효과条件カテゴリ = MC
	효과条件종류 = EX以外全般
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_3(듀얼리스트,99734)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲상대묘지 = 1
	효과条件カテゴリ = MC
	효과条件종류 = 마법全般
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 3
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_99734(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 설정レベル

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99734,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,"손패",場所)

		CALL FULLRESET
		REPEAT 2
			SELECTCASE RAND:9
				CASE 0
					CALL CARD_GENERATE_DECK(対面者,79840)
				CASE 1
					CALL CARD_GENERATE_DECK(対面者,79841)
				CASE 2
					CALL CARD_GENERATE_DECK(対面者,79842)
				CASE 3
					CALL CARD_GENERATE_DECK(対面者,79843)
				CASE 4
					CALL CARD_GENERATE_DECK(対面者,79844)
				CASE 5
					CALL CARD_GENERATE_DECK(対面者,79845)
				CASE 6
					CALL CARD_GENERATE_DECK(対面者,79846)
				CASE 7
					CALL CARD_GENERATE_DECK(対面者,79847)
				CASE 8
					CALL CARD_GENERATE_DECK(対面者,79848)
			ENDSELECT
		REND
		SETCOLOR C_PINK
		PRINTFORMW 마인드컨트롤 리시버가 작게 소리를 내자 상대의 덱에 최면 카드가 ２장이 생성되었다…。
		RESETCOLOR
		CALL SHUFFLE(対面者)
		CALL CARDDRAW_BASIC(対面者,1)
		CALL CARDDRAW_BASIC(듀얼리스트,1)
	ENDIF
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99734,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲묘지 = 1
		효과範囲EX덱 = 1
		효과条件カテゴリ = MC
		효과条件종류 = EX以外全般
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99734,3)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		FOR カウンタ,0,3
			CALL FULLRESET
			효과条件カテゴリ = MC
			효과条件종류 = 마법全般
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"상대묘지",ゾーン,場所)
			IF RESULT != 9999
				CALL CARD_ADD_DECK_SPC(対面者,"묘지",RESULT)
			ENDIF
		NEXT
	ENDIF
ENDIF
