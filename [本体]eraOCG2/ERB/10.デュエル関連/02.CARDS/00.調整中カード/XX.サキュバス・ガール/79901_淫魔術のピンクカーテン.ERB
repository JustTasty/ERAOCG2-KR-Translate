@CARDNAME_79901,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

;黒魔術のカーテン+黒魔術の継承
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 음마술의 핑크 커튼 －음마술의 아기씨계승－
	CASE "略称"
		RESULTS = 음마술도천막
	CASE "カテゴリ"
	    RESULTS:0 = 매지션 걸
		RESULTS:1 = 서큐버스 걸
		RESULTS:2 = E.R.A
ENDSELECT

@CARD_79901(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "生成カード"
		RETURN 宝液ID
ENDSELECT
RETURN 0

@CARDSUMMON_79901(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
RETURN 0

@CARD_EXPLANATION_79901(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드명의 효과는 １턴에 １번밖에 사용할 수 없다。
CALL CARD_ABLE(1,마함起動,손패起動)
PRINTL ①：ＬＰ를 １０００지불하고、융합 몬스터 카드에 따라 정해진 １장째의 융합 소재 몬스터만을 자신의 덱에서 묘지로 보내고、
PRINTL 자신 필드에 같은 이름의 카드가 존재하지 않는 "서큐버스 걸" 융합 몬스터 １장을 ＥＸ덱에서 특수 소환한다。
PRINTL 그 후、묘지에 "현자의 보액" 토큰을 １장 생성한다。
PRINTL 이 턴、자신은 "서큐버스 걸" 몬스터밖에 ＥＸ덱에서 특수 소환할 수 없다。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_79901(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 選択カード名
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,79901)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,79901)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과条件固カテゴリ = 서큐버스 걸
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"EX履歴")
	SIF RESULT != 0
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 1000 && GETVAR(@"%듀얼리스트%_コスト無効") < 1
		RETURN 0
	;後続のEX덱チェック内の소환可 = 1とちょっと被ってるけど
	;その後続のEX덱チェックの処理が省けるためここで確認しておく
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	FOR カウンタ, 0, 現EX山札数(듀얼리스트)
		選択カードID = GETVAR(@"%듀얼리스트%_EX덱:カウンタ")
		SIF 選択カードID < 1
			CONTINUE
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件ID = 選択カードID
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			CONTINUE
		CALL CARD_EX_CHECK(듀얼리스트,選択カードID)
		SIF RESULT == 1
			CONTINUE
		TRYCALLFORM CARDNAME_{選択カードID}("カテゴリ")
		SIF !MATCH(RESULTS,"서큐버스 걸")
			CONTINUE
		CALL RESETCONDITION_MAT
		TRYCALLFORM CARDSUMMON_{選択カードID}(듀얼리스트,選択カードID,0)
		選択カード名 = %素材条件名前%
		SIF 選択カード名 == ""
			CONTINUE
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件名前 = %選択カード名%
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			条件달성++
			BREAK
		ENDIF
	NEXT
	SIF 条件달성 < 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_79901(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 選択カード名
#DIM DYNAMIC 選択カード候補,VARSIZE("효과条件ID")
#DIM DYNAMIC カウンタB

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79901,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",1000)
		FOR カウンタ, 0, 現EX山札数(듀얼리스트)
			選択カードID = GETVAR(@"%듀얼리스트%_EX덱:カウンタ")
			SIF 選択カードID < 1
				CONTINUE
			SIF MATCH(選択カード候補,選択カードID)
				CONTINUE
			CALL FULLRESET
			효과範囲필드 = 1
			효과範囲마함 = 1
			효과条件ID = 選択カードID
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				CONTINUE
			CALL CARD_EX_CHECK(듀얼리스트,選択カードID)
			SIF RESULT == 1
				CONTINUE
			TRYCALLFORM CARDNAME_{選択カードID}("カテゴリ")
			SIF !MATCH(RESULTS,"서큐버스 걸")
				CONTINUE
			CALL RESETCONDITION_MAT
			TRYCALLFORM CARDSUMMON_{選択カードID}(듀얼리스트,選択カードID,0)
			選択カード名 = %素材条件名前%
			SIF 選択カード名 == ""
				CONTINUE
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件名前 = %選択カード名%
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 1
				選択カード候補:カウンタB = 選択カードID
				カウンタB++
				SIF カウンタB >= VARSIZE("효과条件ID")
					BREAK
			ENDIF
		NEXT
		CALL FULLRESET
		효과条件종류 = 융합
		효과条件カテゴリ = 서큐버스 걸
		FOR カウンタ, 0, カウンタB
			효과条件ID:カウンタ = 選択カード候補:カウンタ
		NEXT
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL RESETCONDITION_MAT
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード,0)
			選択カード名 = %素材条件名前%
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件名前 = %選択カード名%
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				CALL CARD_EX_SUMMON(듀얼리스트,"EX덱",選択カード)
				IF RESULT == 1
					CALL CARD_GENERATE_GY(듀얼리스트,宝液ID)
				ENDIF
			ENDIF
		ENDIF
		CALL CARD_EX_RESTRICTION(듀얼리스트,"서큐버스 걸")
	ENDIF
ENDIF