@CARDNAME_59853,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 레몬 서큐버스 걸
	CASE "略称"
		RESULTS = 레몬 Ｓ Ｇ
	CASE "カテゴリ"
	    RESULTS:0 = 매지션 걸
		RESULTS:1 = 서큐버스 걸
		RESULTS:2 = E.R.A
ENDSELECT

@CARD_59853(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 融合
	CASE "属性"
		RETURN 光属性
	CASE "種族"
		RESULT:1 = 悪魔族
		RETURN 魔法使い族
	CASE "レベル"
		RETURN 2
	CASE "攻撃力"
		RETURN 800
	CASE "守備力"
		RETURN 600
	CASE "性別"
		RETURN 牝性
	CASE "生成カード"
		RETURN 宝液ID
	CASE "連動ID"
		RETURN 1005
ENDSELECT
RETURN 0

@CARD_EXPLANATION_59853(種類)
#DIM DYNAMIC 種類

CALL TEXT_DECORATION("ROGUE")
SETCOLOR C_H_PINK
PRINTFORML 「레몬・매지션 걸」❤「決闘者 %CALLNAME:MASTER%」
RESETCOLOR
;効果番号1はシルクベッドルームの召喚履歴に利用
CALL CARD_ABLE(0,攻守変動)
PRINTL ①：このカードの攻撃力は、墓地の「서큐버스 걸」魔法カードの数×２００アップする。
CALL CARD_ABLE(2,戦場起動)
PRINTL ②：自分メインフェイズに発動できる。
PRINTL 墓地の「서큐버스 걸」魔法カードを１枚除外し、
PRINTL ＥＸデッキから自分フィールドに同名カードが存在しない「서큐버스 걸」融合モンスター１体を相手に見せて、
PRINTL デッキ・墓地からそのカードの１体目の融合素材モンスター１体を攻撃力・守備力を０にして、効果無効状態で特殊召喚する。
CALL CARD_ABLE(0,ターン終了時強制)
PRINTL ③：自分エンドフェイズに、このカードが元々の持ち主のフィールドに存在する場合に発動する。
PRINT 自分は
SETCOLOR C_H_PINK
PRINT ❤契約の対価❤として
RESETCOLOR
PRINTL ３００ＬＰを失い、墓地に「賢者の宝液」トークン１枚を生成する。

CALL TEXT_DECORATION("ROGUE")

@CARDSUMMON_AA_59853

@CARDSUMMON_59853(決闘者,召喚カード,EX参照)
#DIMS DYNAMIC 決闘者
#DIM DYNAMIC 召喚カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要素材数 = 2

IF EX参照 == 0 || EX参照 == 1
	素材条件名前 = 레몬・매지션 걸
ELSEIF EX参照 >= 10
	素材条件名前 = 決闘者 %CALLNAME:MASTER%
ENDIF

IF EX参照 == 1
	CALL FUSCAN(決闘者,召喚カード,59853) 
	RETURN RESULT
ENDIF

@CARDCAN_59853(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 選択カード名
#DIM DYNAMIC 条件達成

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,59853)
SIF RESULT == 1
	RETURN 0

IF 種類 == 攻守変動
	SIF ゾーン != "戦場"
		RETURN 0
	RETURN 1
ELSEIF 種類 == 戦場起動
	SIF 発動済_2(決闘者,59853)
		RETURN 0
	SIF ゾーン != "戦場"
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件カテゴリ = 서큐버스 걸
	効果条件種類 = 魔法全般
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	;後続のEXデッキチェック内の召喚可 = 1とちょっと被ってるけど
	;その後続のEXデッキチェックの処理が省けるためここで確認しておく
	SIF モンスター数(決闘者) >= 5
		RETURN 0
	FOR カウンタ, 0, 現EX山札数(決闘者)
		選択カードID = GETVAR(@"%決闘者%_EXデッキ:カウンタ")
		SIF 選択カードID < 1
			CONTINUE
		CALL FULLRESET
		効果範囲戦場 = 1
		効果範囲魔罠 = 1
		効果条件ID = 選択カードID
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT == 1
			CONTINUE
		TRYCALLFORM CARDNAME_{選択カードID}("カテゴリ")
		SIF !MATCH(RESULTS,"서큐버스 걸")
			CONTINUE
		TRYCALLFORM CARD_{選択カードID}("種類")
		SIF RESULT != 融合
			CONTINUE
		CALL RESETCONDITION_MAT
		TRYCALLFORM CARDSUMMON_{選択カードID}(決闘者,選択カードID,0)
		選択カード名 = %素材条件名前%
		SIF 選択カード名 == ""
			CONTINUE
		CALL FULLRESET
		効果範囲デッキ = 1
		効果範囲墓地 = 1
		効果条件名前 = %選択カード名%
		効果条件召喚可 = 1
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		IF RESULT == 1
			条件達成++
			BREAK
		ENDIF
	NEXT
	SIF 条件達成 < 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == ターン終了時強制
	SIF ゾーン != "戦場"
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	IF 決闘者 == "自分"
		SIF GETVAR(@"%決闘者%_MZ_所有:場所") != 1
			RETURN 0
	ELSE
		SIF GETVAR(@"%決闘者%_MZ_所有:場所") != 2
			RETURN 0
	ENDIF
	RETURN 1
ENDIF



@CARDEFFECT_59853(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 選択カード名
#DIM DYNAMIC 選択カード候補,VARSIZE("効果条件ID")
#DIM DYNAMIC カウンタB

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 攻守変動
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件カテゴリ = 서큐버스 걸
	効果条件種類 = 魔法全般
	効果条件CNT = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT > 0
		CALL STATCHANGE_BASIC(決闘者,"自身",場所,RESULT * 200)
ELSEIF 種類 == 戦場起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,59853,2)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件カテゴリ = 서큐버스 걸
		効果条件種類 = 魔法全般
		効果条件行先フラグ = 除外行き
		CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
		IF RESULT == 1
			FOR カウンタ, 0, 現EX山札数(決闘者)
				選択カードID = GETVAR(@"%決闘者%_EXデッキ:カウンタ")
				SIF 選択カードID < 1
					CONTINUE
				SIF MATCH(選択カード候補,選択カードID)
					CONTINUE
				CALL FULLRESET
				効果範囲戦場 = 1
				効果範囲魔罠 = 1
				効果条件ID = 選択カードID
				CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
				SIF RESULT == 1
					CONTINUE
				TRYCALLFORM CARDNAME_{選択カードID}("カテゴリ")
				SIF !MATCH(RESULTS,"서큐버스 걸")
					CONTINUE
				TRYCALLFORM CARD_{選択カードID}("種類")
				SIF RESULT != 融合
					CONTINUE
				CALL RESETCONDITION_MAT
				TRYCALLFORM CARDSUMMON_{選択カードID}(決闘者,選択カードID,0)
				選択カード名 = %素材条件名前%
				SIF 選択カード名 == ""
					CONTINUE
				CALL FULLRESET
				効果範囲デッキ = 1
				効果範囲墓地 = 1
				効果条件名前 = %選択カード名%
				効果条件召喚可 = 1
				CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
				IF RESULT == 1
					選択カード候補:カウンタB = 選択カードID
					カウンタB++
					SIF カウンタB >= VARSIZE("効果条件ID")
						BREAK
				ENDIF
			NEXT
			CALL FULLRESET
			効果条件種類 = 融合
			効果条件カテゴリ = 서큐버스 걸
			FOR カウンタ, 0, カウンタB
				効果条件ID:カウンタ = 選択カード候補:カウンタ
			NEXT
			CALL CARD_CHOOSE_BASIC(決闘者,"EXデッキ",ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				CALL RESETCONDITION_MAT
				TRYCALLFORM CARDSUMMON_{GETVAR(@"%決闘者%_EXデッキ:{選択カード}")}(決闘者,選択カード,0)
				選択カード名 = %素材条件名前%
				CALL FULLRESET
				効果範囲デッキ = 1
				効果範囲墓地 = 1
				効果条件名前 = %選択カード名%
				効果追加種類:0 = 効追攻守０
				効果追加種類:1 = 効追無効
				CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 種類 == ターン終了時強制
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,59853)
	IF RESULT == 0
		CALL LIFE_CHANGE(決闘者,"減少","コスト",500)
		CALL CARD_GENERATE_GY(決闘者,宝液ID)
	ENDIF
ENDIF
