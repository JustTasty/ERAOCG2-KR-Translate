@CARDNAME_59853,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 레몬 서큐버스 걸
	CASE "略称"
		RESULTS = 레몬 Ｓ Ｇ
	CASE "カテゴリ"
	    RESULTS:0 = 매지션 걸
		RESULTS:1 = 서큐버스 걸
		RESULTS:2 = E.R.A
ENDSELECT

@CARD_59853(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 융합
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RESULT:1 = 악마족
		RETURN 마법사족
	CASE "レベル"
		RETURN 2
	CASE "攻撃力"
		RETURN 800
	CASE "守備力"
		RETURN 600
	CASE "성별"
		RETURN 牝性
	CASE "生成カード"
		RETURN 宝液ID
	CASE "連動ID"
		RETURN 1005
ENDSELECT
RETURN 0

@CARD_EXPLANATION_59853(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
SETCOLOR C_H_PINK
PRINTFORML 「레몬・매지션 걸」❤「듀얼리스트 %CALLNAME:MASTER%」
RESETCOLOR
;효과番号1はシルクベッドルームの소환履歴に利用
CALL CARD_ABLE(0,攻守変動)
PRINTL ①：このカードの攻撃力は、묘지の「서큐버스 걸」마법カードの数×２００アップする。
CALL CARD_ABLE(2,필드起動)
PRINTL ②：자신メインフェイズに발동できる。
PRINTL 묘지の「서큐버스 걸」마법カードを１枚제외し、
PRINTL ＥＸ덱から자신フィールドに同名カードが存在しない「서큐버스 걸」융합モンスター１体を상대に見せて、
PRINTL 덱・묘지からそのカードの１体目の융합素材モンスター１体を攻撃力・守備力を０にして、효과無効状態で특수소환する。
CALL CARD_ABLE(0,ターン終了時強制)
PRINTL ③：자신エンドフェイズに、このカードが元々の持ち主のフィールドに存在する場合に발동する。
PRINT 자신は
SETCOLOR C_H_PINK
PRINT ❤契約の対価❤として
RESETCOLOR
PRINTL ３００ＬＰを失い、묘지に「현자의 보액」トークン１枚を生成する。

CALL TEXT_DECORATION("ROGUE")

@CARDSUMMON_AA_59853

@CARDSUMMON_59853(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要素材数 = 2

IF EX参照 == 0 || EX参照 == 1
	素材条件名前 = 레몬・매지션 걸
ELSEIF EX参照 >= 10
	素材条件名前 = 듀얼리스트 %CALLNAME:MASTER%
ENDIF

IF EX参照 == 1
	CALL FUSCAN(듀얼리스트,소환カード,59853) 
	RETURN RESULT
ENDIF

@CARDCAN_59853(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 選択カード名
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,59853)
SIF RESULT == 1
	RETURN 0

IF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,59853)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 서큐버스 걸
	효과条件종류 = 마법全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	;後続のEX덱チェック内の소환可 = 1とちょっと被ってるけど
	;その後続のEX덱チェックの処理が省けるためここで確認しておく
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	FOR カウンタ, 0, 現EX山札数(듀얼리스트)
		選択カードID = GETVAR(@"%듀얼리스트%_EX덱:カウンタ")
		SIF 選択カードID < 1
			CONTINUE
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件ID = 選択カードID
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			CONTINUE
		TRYCALLFORM CARDNAME_{選択カードID}("カテゴリ")
		SIF !MATCH(RESULTS,"서큐버스 걸")
			CONTINUE
		TRYCALLFORM CARD_{選択カードID}("종류")
		SIF RESULT != 융합
			CONTINUE
		CALL RESETCONDITION_MAT
		TRYCALLFORM CARDSUMMON_{選択カードID}(듀얼리스트,選択カードID,0)
		選択カード名 = %素材条件名前%
		SIF 選択カード名 == ""
			CONTINUE
		CALL FULLRESET
		효과範囲덱 = 1
		효과範囲묘지 = 1
		효과条件名前 = %選択カード名%
		효과条件소환可 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			条件달성++
			BREAK
		ENDIF
	NEXT
	SIF 条件달성 < 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == ターン終了時強制
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	IF 듀얼리스트 == "자신"
		SIF GETVAR(@"%듀얼리스트%_MZ_所有:場所") != 1
			RETURN 0
	ELSE
		SIF GETVAR(@"%듀얼리스트%_MZ_所有:場所") != 2
			RETURN 0
	ENDIF
	RETURN 1
ENDIF



@CARDEFFECT_59853(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 選択カード名
#DIM DYNAMIC 選択カード候補,VARSIZE("효과条件ID")
#DIM DYNAMIC カウンタB

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 攻守変動
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 서큐버스 걸
	효과条件종류 = 마법全般
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT > 0
		CALL STATCHANGE_BASIC(듀얼리스트,"自身",場所,RESULT * 200)
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,59853,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 서큐버스 걸
		효과条件종류 = 마법全般
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			FOR カウンタ, 0, 現EX山札数(듀얼리스트)
				選択カードID = GETVAR(@"%듀얼리스트%_EX덱:カウンタ")
				SIF 選択カードID < 1
					CONTINUE
				SIF MATCH(選択カード候補,選択カードID)
					CONTINUE
				CALL FULLRESET
				효과範囲필드 = 1
				효과範囲마함 = 1
				효과条件ID = 選択カードID
				CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
				SIF RESULT == 1
					CONTINUE
				TRYCALLFORM CARDNAME_{選択カードID}("カテゴリ")
				SIF !MATCH(RESULTS,"서큐버스 걸")
					CONTINUE
				TRYCALLFORM CARD_{選択カードID}("종류")
				SIF RESULT != 융합
					CONTINUE
				CALL RESETCONDITION_MAT
				TRYCALLFORM CARDSUMMON_{選択カードID}(듀얼리스트,選択カードID,0)
				選択カード名 = %素材条件名前%
				SIF 選択カード名 == ""
					CONTINUE
				CALL FULLRESET
				효과範囲덱 = 1
				효과範囲묘지 = 1
				효과条件名前 = %選択カード名%
				효과条件소환可 = 1
				CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
				IF RESULT == 1
					選択カード候補:カウンタB = 選択カードID
					カウンタB++
					SIF カウンタB >= VARSIZE("효과条件ID")
						BREAK
				ENDIF
			NEXT
			CALL FULLRESET
			효과条件종류 = 융합
			효과条件カテゴリ = 서큐버스 걸
			FOR カウンタ, 0, カウンタB
				효과条件ID:カウンタ = 選択カード候補:カウンタ
			NEXT
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				CALL RESETCONDITION_MAT
				TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード,0)
				選択カード名 = %素材条件名前%
				CALL FULLRESET
				효과範囲덱 = 1
				효과範囲묘지 = 1
				효과条件名前 = %選択カード名%
				효과추가종류:0 = 効追攻守０
				효과추가종류:1 = 効追無効
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == ターン終了時強制
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,59853)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",500)
		CALL CARD_GENERATE_GY(듀얼리스트,宝液ID)
	ENDIF
ENDIF
