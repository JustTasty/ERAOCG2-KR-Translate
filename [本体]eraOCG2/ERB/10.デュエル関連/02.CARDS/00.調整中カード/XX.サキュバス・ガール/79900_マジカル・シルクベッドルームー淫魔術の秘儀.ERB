@CARDNAME_79900,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

;マジカルシルクハット(名前だけ)
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 매지컬 실크 베드룸－음마술의 비의
	CASE "略称"
		RESULTS = 음마도의 계약장
	CASE "カテゴリ"
	    RESULTS:0 = 매지션 걸
		RESULTS:1 = 서큐버스 걸
		RESULTS:2 = E.R.A
ENDSELECT

@CARD_79900(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN フィールド
ENDSELECT
RETURN 0

@CARDSUMMON_79900(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
RETURN 0

@CARD_EXPLANATION_79900(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 자신 필드는 "음마도의 계약장"이 된다。
PRINTL 이 카드명의 ①의 효과는 １턴에 １번밖에 사용할 수 없다。
CALL CARD_ABLE(1,손패起動,마함起動)
PRINTL ①：이 카드의 발동시의 효과 처리로서、
PRINTL 덱에서 "매지션 걸" 몬스터 １장 또는 이 카드명 이외의 "서큐버스 걸"마법 / 함정 카드 １장을 패에 넣을수 있다。
PRINTL 이 효과를 발동하는 턴、자신은 "서큐버스 걸" 몬스터밖에 ＥＸ덱에서 특수 소환할 수 없다。
CALL CARD_ABLE(0,손패起動,마함起動)
PRINTL ②：자신 메인 페이즈에 발동할 수 있다。
PRINTL 융합 몬스터 카드에 따라 정해진 １장째의 융합 소재 몬스터만을 자신의 패 / 필드에서 묘지로 보내고、
PRINTL 자신 필드에 같은 이름의 카드가 존재하지 않는 "서큐버스 걸" 융합 몬스터 １장을 ＥＸ덱에서 특수 소환한다。
PRINTL 그 후、묘지에 "현자의 보액" 토큰을 １장 생성한다。
PRINTL 이 효과로는 같은 카드명의 몬스터는 １턴에 １번밖에 특수 소환할 수 없으며、
PRINTL 이 효과를 발동하는 턴、자신은「서큐버스 걸」몬스터밖에 ＥＸ덱에서 특수 소환할 수 없다。
CALL CARD_ABLE(0,身代효과)
PRINTL ③：이 카드가 효과에 의해 파괴되는 경우、대신에 묘지의 "매지션 걸" 마법 카드 ２장을 제외할 수 있다。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_79900(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 選択カード名
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,79900)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과条件固カテゴリ = 서큐버스 걸
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"EX履歴")
	SIF RESULT != 0
		RETURN 0
	;後続のEX덱チェック内の소환可 = 1とちょっと被ってるけど
	;その後続のEX덱チェックの処理が省けるためここで確認しておく
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	FOR カウンタ, 0, 現EX山札数(듀얼리스트)
		選択カードID = GETVAR(@"%듀얼리스트%_EX덱:カウンタ")
		SIF 選択カードID < 1
			CONTINUE
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件ID = 選択カードID
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			CONTINUE
		CALL CARD_EX_CHECK(듀얼리스트,選択カードID)
		SIF RESULT == 1
			CONTINUE
		TRYCALLFORM CARDNAME_{選択カードID}("カテゴリ")
		SIF !MATCH(RESULTS,"서큐버스 걸")
			CONTINUE
		;この효과による소환履歴は발동효과１で記録
		SIF 발동済_1(듀얼리스트,選択カードID)
			CONTINUE
		CALL RESETCONDITION_MAT
		TRYCALLFORM CARDSUMMON_{選択カードID}(듀얼리스트,選択カードID,0)
		選択カード名 = %素材条件名前%
		SIF 選択カード名 == ""
			CONTINUE
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲필드 = 1
		효과条件名前 = %選択カード名%
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			条件달성++
			BREAK
		ENDIF
	NEXT
	SIF 条件달성 < 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,79900)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과条件固カテゴリ = 서큐버스 걸
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"EX履歴")
	SIF RESULT != 0
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	RETURN 1
ELSEIF 종류 == 身代효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	SIF 破壊종류 != "효과"
		RETURN 0
	SIF 破壊대상ゾーン != "마함"
		RETURN 0
	SIF 破壊대상場所 != 場所
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 서큐버스 걸
	효과条件종류 = 마법全般
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_79900(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 選択カード名
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 条件Ａ
#DIM DYNAMIC 条件Ｂ
#DIM DYNAMIC 選択효과
#DIM DYNAMIC 選択カード候補,VARSIZE("효과条件ID")

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79900)
	IF RESULT == 0
		FOR カウンタ, 0, 現EX山札数(듀얼리스트)
			選択カードID = GETVAR(@"%듀얼리스트%_EX덱:カウンタ")
			SIF 選択カードID < 1
				CONTINUE
			SIF MATCH(選択カード候補,選択カードID)
				CONTINUE
			CALL FULLRESET
			효과範囲필드 = 1
			효과範囲마함 = 1
			효과条件ID = 選択カードID
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				CONTINUE
			CALL CARD_EX_CHECK(듀얼리스트,選択カードID)
			SIF RESULT == 1
				CONTINUE
			TRYCALLFORM CARDNAME_{選択カードID}("カテゴリ")
			SIF !MATCH(RESULTS,"서큐버스 걸")
				CONTINUE
			;この효과による소환履歴は발동효과１で記録
			SIF 발동済_1(듀얼리스트,選択カードID)
				CONTINUE
			CALL RESETCONDITION_MAT
			TRYCALLFORM CARDSUMMON_{選択カードID}(듀얼리스트,選択カードID,0)
			選択カード名 = %素材条件名前%
			SIF 選択カード名 == ""
				CONTINUE
			CALL FULLRESET
			효과範囲손패 = 1
			효과範囲필드 = 1
			효과条件名前 = %選択カード名%
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 1
				選択カード候補:カウンタB = 選択カードID
				カウンタB++
				SIF カウンタB >= VARSIZE("효과条件ID")
					BREAK
			ENDIF
		NEXT
		CALL FULLRESET
		효과条件종류 = 융합
		효과条件カテゴリ = 서큐버스 걸
		FOR カウンタ, 0, カウンタB
			효과条件ID:カウンタ = 選択カード候補:カウンタ
		NEXT
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			選択カードID = RESULT:99
			CALL RESETCONDITION_MAT
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード,0)
			選択カード名 = %素材条件名前%
			CALL FULLRESET
			효과範囲손패 = 1
			효과範囲필드 = 1
			효과条件名前 = %選択カード名%
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				CALL CARD_EFFECT_USE(듀얼리스트,選択カードID,1)
				CALL CARD_EX_SUMMON(듀얼리스트,"EX덱",選択カード)
				IF RESULT == 1
					CALL CARD_GENERATE_GY(듀얼리스트,宝液ID)
				ENDIF
			ENDIF
		ENDIF
		CALL CARD_EX_RESTRICTION(듀얼리스트,"서큐버스 걸")
	ENDIF
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79900,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件スーパーカテゴリ = 매지션 걸
		효과条件スーパー종류 = モン全般
		효과条件カテゴリ = 서큐버스 걸
		효과条件종류 = 마함全般
		효과条件固名前 = 매지컬 실크 베드룸－음마술의 비의
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件スーパーカテゴリ = 매지션 걸
			효과条件スーパー종류 = モン全般
			효과条件カテゴリ = 서큐버스 걸
			효과条件종류 = 마함全般
			효과条件固名前 = 매지컬 실크 베드룸－음마술의 비의
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
		CALL CARD_EX_RESTRICTION(듀얼리스트,"서큐버스 걸")
	ENDIF
ELSEIF 종류 == 身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79900)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 서큐버스 걸
		효과条件종류 = 마법全般
		효과条件行先플래그 = 제외行き
		효과条件複数 = 2
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			RETURN 1
		ENDIF
	ENDIF
	RETURN 0	
ELSEIF 종류 == 지속효과
	CALL SET_FIELD_MAGIC_INFO(듀얼리스트, "淫魔導の契りの間")
ENDIF
