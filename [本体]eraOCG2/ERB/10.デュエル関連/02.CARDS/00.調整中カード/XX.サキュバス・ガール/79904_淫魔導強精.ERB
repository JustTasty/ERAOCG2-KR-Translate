@CARDNAME_79904,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

;マジック・エキスパンド
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 서큐버스 매직 익스팬드
	CASE "略称"
		RESULTS = 음마도강정
	CASE "カテゴリ"
	    RESULTS:0 = 매지션 걸
		RESULTS:1 = 서큐버스 걸
		RESULTS:2 = E.R.A
ENDSELECT

@CARD_79904(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 速攻魔法
ENDSELECT
RETURN 0

@CARDSUMMON_79904(決闘者,召喚カード,EX参照)
#DIMS DYNAMIC 決闘者
#DIM DYNAMIC 召喚カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
RETURN 0

@CARD_EXPLANATION_79904(種類)
#DIM DYNAMIC 種類

CALL TEXT_DECORATION("ROGUE")
PRINTL このカード名の①②の効果は１ターンにいずれか１つしか使用できない。
CALL CARD_ABLE(1,魔罠起動,手札起動,フリー誘発)
PRINTL ①：自分フィールドの「서큐버스 걸」モンスターの数によって以下の効果を適用する。
PRINTL ●１体以上：墓地の「서큐버스 걸」魔法カードの数×１００ＬＰを回復する。
PRINTL ●２体以上：自分フィールドの「서큐버스 걸」モンスターの数×３００ＬＰを回復する。
PRINTL ●３体以上：ターン終了時まで、自分はカードの効果によってＬＰを失わなくなり、払う必要もなくなる。
CALL CARD_ABLE(2,効果後誘発)
PRINTL ②：自分の「서큐버스 걸」モンスターが効果を発動した場合に発動できる。
PRINTL 墓地のこのカードを自分フィールドにセットする。
PRINTL この効果でセットしたこのカードは、フィールドから離れた場合に除外される。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_79904(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

#DIM DYNAMIC 条件達成

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,79904)
SIF RESULT == 1
	RETURN 0

IF 種類 == 魔罠起動 || 種類 == 手札起動 || 種類 == フリー誘発
	SIF 発動済_1(決闘者,79904)
		RETURN 0
	SIF 発動済_2(決闘者,79904)
		RETURN 0
	IF ゾーン == "手札"
		SIF ターンプレイヤー != 決闘者
			RETURN 0
	ELSEIF ゾーン == "魔罠"
		SIF GETVAR(@"%決闘者%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件カテゴリ = 서큐버스 걸
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件カテゴリ = 서큐버스 걸
	効果条件種類 = 魔法全般
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 効果後誘発
	SIF 発動済_1(決闘者,79904)
		RETURN 0
	SIF 発動済_2(決闘者,79904)
		RETURN 0
	SIF ゾーン != "墓地"
		RETURN 0
	CALL FULLRESET
	効果条件カテゴリ = 서큐버스 걸
	CALL CARD_CHECK_FIN_DATA(決闘者,"効果発動")
	SIF RESULT != 1
		RETURN 0
	SIF 魔罠数(決闘者) >= 5
		RETURN 0
    RETURN 1
ENDIF

@CARDEFFECT_79904(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 魔罠起動 || 種類 == 手札起動 || 種類 == フリー誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,79904,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所)
		CALL FULLRESET
		効果範囲戦場 = 1
		効果条件カテゴリ = 서큐버스 걸
		効果条件CNT = 1
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		カウンタ = RESULT
		IF カウンタ
			CALL FULLRESET
			効果範囲墓地 = 1
			効果条件カテゴリ = 서큐버스 걸
			効果条件CNT = 1
			CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
			CALL LIFE_CHANGE(決闘者,"増加","効果",RESULT * 100)
			IF カウンタ > 1
				CALL LIFE_CHANGE(決闘者,"増加","効果",カウンタ * 300)
				IF カウンタ > 2
					PRINTFORML %決闘者%はこのターンＬＰを失わず、払う必要もなくなりました。
					CALL SET_PLAYER_INFO(決闘者,"コスト無効",1)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ELSEIF 種類 == 墓地起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,79904,2)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件名前 = 서큐버스 매직 익스팬드
		CALL CARD_SET_ADV(決闘者,ゾーン,場所)
	ENDIF
ENDIF