@CARDNAME_59850,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 블랙 서큐버스 걸
	CASE "略称"
		RESULTS = Ｂ Ｓ Ｇ
	CASE "カテゴリ"
	    RESULTS:0 = 블랙 매지션 걸
	    RESULTS:1 = 매지션 걸
		RESULTS:2 = 서큐버스 걸
		RESULTS:3 = E.R.A
ENDSELECT

@CARD_59850(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 融合
	CASE "属性"
		RETURN 闇属性
	CASE "種族"
		RESULT:1 = 悪魔族
		RETURN 魔法使い族
	CASE "レベル"
		RETURN 6
	CASE "攻撃力"
		RETURN 2000
	CASE "守備力"
		RETURN 1700
	CASE "性別"
		RETURN 牝性
	CASE "生成カード"
		RETURN 宝液ID
	CASE "連動ID"
		RETURN 1000
ENDSELECT
RETURN 0

@CARD_EXPLANATION_59850(種類)
#DIM DYNAMIC 種類

CALL TEXT_DECORATION("ROGUE")
SETCOLOR C_H_PINK
PRINTFORML 「블랙 매지션 걸」❤「決闘者 %CALLNAME:MASTER%」
;効果番号1はシルクベッドルームの召喚履歴に利用
RESETCOLOR
PRINTL このカード名の②の効果は１ターンに１度しか使用できない。
CALL CARD_ABLE(0,攻守変動)
PRINTL ①：このカードの攻撃力は、墓地の「서큐버스 걸」魔法カードの数×３００アップする。
CALL CARD_ABLE(2,発動誘発)
PRINTL ②：相手の魔法カードの効果が発動した場合に発動できる。
PRINTL 墓地の「서큐버스 걸」魔法カードを２枚除外し、その効果を無効にして破壊する。
CALL CARD_ABLE(0,ターン終了時強制)
PRINTL ③：自分エンドフェイズに、このカードが元々の持ち主のフィールドに存在する場合に発動する。
PRINT 自分は
SETCOLOR C_H_PINK
PRINT ❤契約の対価❤として
RESETCOLOR
PRINTL ５００ＬＰを失い、墓地に「賢者の宝液」トークン１枚を生成する。
PRINTL その後、デッキから「서큐버스 걸」魔法・罠カードを手札に加えることができ、
PRINTL この効果でカードを手札に加えた場合、手札から「서큐버스 걸」魔法・罠カード１枚を自分フィールドにセットできる。
CALL TEXT_DECORATION("ROGUE")

@CARDSUMMON_AA_59850

@CARDSUMMON_59850(決闘者,召喚カード,EX参照)
#DIMS DYNAMIC 決闘者
#DIM DYNAMIC 召喚カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要素材数 = 2

IF EX参照 == 0 || EX参照 == 1
	素材条件名前 = 블랙 매지션 걸
ELSEIF EX参照 >= 10
	素材条件名前 = 決闘者 %CALLNAME:MASTER%
ENDIF

IF EX参照 == 1
	CALL FUSCAN(決闘者,召喚カード,59850) 
	RETURN RESULT
ENDIF

@CARDCAN_59850(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,59850)
SIF RESULT == 1
	RETURN 0

IF 種類 == 攻守変動
	SIF ゾーン != "戦場"
		RETURN 0
	RETURN 1
ELSEIF 種類 == 発動誘発
	SIF 発動済_2(決闘者,59850)
		RETURN 0
	SIF ゾーン != "戦場"
		RETURN 0
	SIF GETVAR(@"%決闘者%_除外NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	効果条件種類 = 魔法全般
	CALL CARD_CHECK_START_DATA(決闘者,1)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件カテゴリ = 서큐버스 걸
	効果条件種類 = 魔罠全般
	効果条件CNT = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0
	RETURN 1
ELSEIF 種類 == ターン終了時強制
	SIF ゾーン != "戦場"
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	IF 決闘者 == "自分"
		SIF GETVAR(@"%決闘者%_MZ_所有:場所") != 1
			RETURN 0
	ELSE
		SIF GETVAR(@"%決闘者%_MZ_所有:場所") != 2
			RETURN 0
	ENDIF
	RETURN 1
ENDIF



@CARDEFFECT_59850(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 選択カード

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 攻守変動
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件カテゴリ = 서큐버스 걸
	効果条件種類 = 魔法全般
	効果条件CNT = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT > 0
		CALL STATCHANGE_BASIC(決闘者,"自身",場所,RESULT * 300)
ELSEIF 種類 == 発動誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,59850,2)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件カテゴリ = 서큐버스 걸
		効果条件種類 = 魔法全般
		効果条件行先フラグ = 除外行き
		効果条件複数 = 2
		CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(決闘者,"発動無効",場所,"墓地")
			RETURN 1
		ENDIF
	ENDIF
ELSEIF 種類 == ターン終了時強制
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,59850)
	IF RESULT == 0
		CALL LIFE_CHANGE(決闘者,"減少","コスト",500)
		CALL CARD_GENERATE_GY(決闘者,宝液ID)
		IF GETVAR(@"ライフ_%決闘者%") > 0
			CALL FULLRESET
			効果範囲デッキ = 1
			効果条件カテゴリ = 서큐버스 걸
			効果条件種類 = 魔罠全般
			効果条件任意 = 1
			CALL CARD_ADD_ADV(決闘者,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				効果範囲手札 = 1
				効果条件カテゴリ = 서큐버스 걸
				効果条件種類 = 魔罠全般
				効果条件任意 = 1
				CALL CARD_SET_ADV(決闘者,ゾーン,場所,"裏側表示")
			ENDIF
		ENDIF
	ENDIF
ENDIF

;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
; トークン
@CARDNAME_159850(参照先)
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 賢者の宝液
	CASE "略称"
		RESULTS = 賢者の宝液
	CASE "カテゴリ"
		RESULTS:0 = 서큐버스 걸
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_159850(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 通常魔法
	CASE "トークン"
		RETURN 1
ENDSELECT
RETURN 0

@CARD_EXPLANATION_159850(種類)
#DIM DYNAMIC 種類

CALL TEXT_DECORATION("ROGUE")
PRINTL このカード名の効果は１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(0,手札起動,魔罠起動)
PRINTL ①：「서큐버스 걸」モンスター１体を対象として発動できる。
PRINTL ターン終了時までそのモンスターの攻撃力を７００アップする。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_159850(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,159850)
SIF RESULT == 1
	RETURN 0

IF 種類 == 魔罠起動 || 種類 == 手札起動
	SIF 発動済_1(決闘者,159850)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件カテゴリ = 서큐버스 걸
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_159850(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

#DIM DYNAMIC 選択カード

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 魔罠起動 || 種類 == 手札起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,159850,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_DISPOSE(決闘者,ゾーン,場所)
		CALL FULLRESET
		効果条件カテゴリ = 서큐버스 걸
		CALL CARD_CHOOSE_BASIC(決闘者,"戦場",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL SET_FIELD_INFO(決闘者,"戦場",選択カード,"一攻",700)
		ENDIF
	ENDIF
ENDIF
