@CARDNAME_99321,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 심메트리아의 구속
	CASE "略称"
		RESULTS = Sym구속
	CASE "カテゴリ"
		RESULTS:0 = 심메트리아
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_99321(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
	CASE "추가日"
		RETURN 250721
ENDSELECT

RETURN 0

@CARD_EXPLANATION_99321(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
CALLFORM CARD_ABLE(0,마함起動 ,フリー誘発)
PRINTL ①：자신 필드에 존재하는 "타천칭" 이외의 카드를 １장을 묘지로 보내고 이 카드를 발동할 수 있다。
PRINTL 상대 필드 / 묘지의 몬스터 １장을 "심메트리아 슬레이브" 로서、
PRINTL 지속 마법 카드 취급으로 자신의 마법＆함정 존에 앞면 표시로 둔다。
PRINTL ②：묘지의 이 카드를 제외하고 발동할 수 있다。자신의 덱의 위에서 카드를 ３장 묘지로 보낸다。
PRINTL 그 후、"타천칭" 카드에 개별 카운터를 ４개 놓는다。
CALL TEXT_DECORATION("ROGUE")


@CARDCAN_99321(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,99321)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == フリー誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
		RETURN 0
	;ここで条件지정
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件CNT = 1
	효과条件固カテゴリ = 타천칭
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과範囲상대묘지 = 1
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF ゾーン != "묘지"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_99321(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カード추가先

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99321)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		;지정した카드를 묘지에 送る
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件固カテゴリ = 타천칭
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			;상대의 카드를 마법함정 존에 置く효과の処理
			CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
			;카드를 置く位置の情報を취득
			カード추가先 = 現마함(듀얼리스트)
			;상대의 카드를 置く
			CALL FULLRESET
			효과範囲상대필드 = 1
			효과範囲상대묘지 = 1
			효과条件표시 = 10
			효과条件종류 = モン全般
			효과条件除去種 = 強奪
			CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")
			;名前を「Sym슬레이브」に변경（処理上はこの表記）
			CALL CARD_SET_RENAME(듀얼리스트,"마함",カード추가先,"Sym슬레이브")
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99321)
	IF RESULT == 0
		;카드를 묘지에 送る
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL CARD_DECK_DISPOSE(듀얼리스트,"덱",3)
		;카운터を乗せる
		CALL CARD_COUNTER_SYMMETRIA(듀얼리스트,종류,ゾーン,場所,4)
	ENDIF
ENDIF