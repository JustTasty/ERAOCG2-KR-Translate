@CARDNAME_59903,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 심메트리아 세라프
	CASE "略称"
		RESULTS = Sym세라프
	CASE "カテゴリ"
		RESULTS:0 = 심메트리아
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_59903(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 400
	CASE "성별"
		RETURN 牝性
	CASE "추가日"
		RETURN 250721
ENDSELECT

RETURN 0

@CARD_EXPLANATION_59903(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL このカード名の(1)(2)(3)の효과はそれぞれ１ターンに１度しか使用できない。
CALL CARD_ABLE(0,身代효과)
PRINTL ①：자신フィールドの「타천칭」カードが戦闘・효과で破壊される場合、代わりに손패のこのカードを捨てる事ができる。
CALL CARD_ABLE(2,묘지起動)
PRINTL ②：このカードが묘지に存在する場合に、
PRINTL 자신の손패・フィールドのカード１枚を묘지へ送って발동できる。このカードを특수소환する。
PRINTL その後、묘지から「심메트리아 세라프」以外の「심메트리아」カード１枚を손패に加える。
CALLFORM CARD_ABLE(3,필드起動,フリー誘発)
PRINTL ③：자신・상대ターンに、자신フィールドに「타천칭」カードが存在する場合、
PRINTL フィールドのこのカードを묘지へ送って발동できる。
PRINTL 상대は以下の효과から１つを選ぶ。その後、その효과を適用する。
PRINTL ●このターン상대は마법・함정カードを발동できない。
PRINTL ●상대に公開オナニーをさせる。その後200ダメージを与え、「타천칭」カードに個別카운터を１つ乗せる。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_59903(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,59903)
SIF RESULT == 1
	RETURN 0

;身代わり효과
IF 종류 == 身代효과
	SIF ゾーン != "손패"
		RETURN 0
	TRYCALLFORM CARDNAME_{破壊대상ID}("カテゴリ")
	SIF !MATCH(RESULTS,"타천칭")
		RETURN 0
	SIF !GROUPMATCH(破壊종류,"효과","戦闘")
		RETURN 0
	RETURN 1

;묘지からのss효과
ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,59903)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,59903,"묘지")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과範囲손패 = 1
	효과条件他所 = 1
	효과条件固カテゴリ = 타천칭
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所,1281)
	SIF RESULT < 1
		RETURN 0
	RETURN 1

;フリー誘発효과
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF 발동済_3(듀얼리스트,59903)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 타천칭
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_59903(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 選択後メッセージ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 카운터数
#DIM DYNAMIC ダメージ量
#DIM DYNAMIC 효과分岐
#DIM DYNAMIC 조교대상

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,59903)
SIF RESULT == 1
	RETURN 0

IF 종류 == 身代효과
	;身代わり효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,59903)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		RETURN 1
	ENDIF
	RETURN 0

ELSEIF 종류 == 묘지起動
	;묘지から자신を특수소환する효과
	;このカードのみ条件と효과が違う
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,59903,2)
	IF RESULT == 0
		;손패またはフィールドのカードを묘지へ
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과範囲손패 = 1
		효과条件固カテゴリ = 타천칭
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			;自身を특수소환
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,"묘지",場所)
			;テーマカードを回収
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件カテゴリ = 심메트리아
			효과条件固名前 = 심메트리아 세라프
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	;ダメージか固有のボーナス효과かを상대に選択させる효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,59903,3)
	IF RESULT == 0
		;자신を묘지に送る
		CALL FULLRESET
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		;どの효과が選択されるか決定
		CALL FULLRESET
		CALL CALC_PROBABILITY_SYMMETRIA(듀얼리스트,종류,ゾーン,場所)
		효과分岐 = RESULT

		IF 효과分岐 == 1 || 효과分岐 == 2
			;RESULTが1なら確率によってボーナス효과が選択された
			;RESULTが2ならダメージ的にボーナス효과を選択するしかなかった
			;それぞれに違うメッセージを用意する予定があったが、今のところは結果は同じ
			;마법함정無効
			CALL SET_PLAYER_INFO(듀얼리스트,"마함無効",1,1)
		ELSEIF 효과分岐 == 3
			;RESULTが3なら使途が居るので両方の효과を適用させる
			;モンスターが個別で持つボーナス효과の処理
			;마법함정無効
			CALL SET_PLAYER_INFO(듀얼리스트,"마함無効",1,1)
			;奉仕処理
			CALL CARD_EROTICS(상대プレイヤー,듀얼리스트)
			IF RESULT != -1
				조교대상 = RESULT
				PRINTFORML
				SETCOLOR C_PINK 
				PRINTFORML %CALLNAME:조교대상%は公開オナニーを強要されている……
				RESETCOLOR
				CALL EROTIC_MOVE("自慰",조교대상,MASTER,500)
			ENDIF
			;ダメージを与え카운터を置く효과
			CALL FULLRESET
			ダメージ量 = 200
			CALL CARD_DAMAGE_SYMMETRIA(듀얼리스트,종류,ゾーン,場所,ダメージ量)
		ELSE
			;それ以外ならダメージを与える효과が選択されている。
			;奉仕処理
			CALL CARD_EROTICS(상대プレイヤー,듀얼리스트)
			IF RESULT != -1
				조교대상 = RESULT
				PRINTFORML
				SETCOLOR C_PINK 
				PRINTFORML %CALLNAME:조교대상%は公開オナニーを強要されている……
				RESETCOLOR
				CALL EROTIC_MOVE("自慰",조교대상,MASTER,500)
			ENDIF
			;ダメージを与え카운터を置く효과の処理
			CALL FULLRESET
			ダメージ量 = 200
			CALL CARD_DAMAGE_SYMMETRIA(듀얼리스트,종류,ゾーン,場所,ダメージ量)
		ENDIF
	ENDIF
ENDIF



