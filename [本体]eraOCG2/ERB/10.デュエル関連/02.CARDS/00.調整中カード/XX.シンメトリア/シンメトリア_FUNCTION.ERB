@CALC_PROBABILITY_SYMMETRIA(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 選択後メッセージ
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC ボーナス効果選択率
#DIM DYNAMIC カウンター数
#DIM DYNAMIC ボーナス効果
#DIM DYNAMIC 効果分岐
#DIM DYNAMIC 調教対象

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

ボーナス効果選択率 = 0
効果分岐 = 0
選択後メッセージ = 奉仕することを選んだ。

;場のカウンターの数をチェック
CALL FULLRESET
効果範囲魔罠 = 1
効果条件カテゴリ = 타천칭
効果条件個Ｃ所持 = 1
効果条件CNT = 1
CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
カウンター数 = RESULT:9
SIF カウンター数 >= 19
	カウンター数 = 19
SIF カウンター数 >= 1
	カウンター数+=1
;カウンター数と相手のLPを元に確率を算出
;相手のLPが「服従の타천칭で与えるダメージ量+1200」以下の場合、わずかながらボーナス効果を選ばれる可能性がある。
;「服従の타천칭で与えるダメージ量+300」以下なら確実に選択される（即死が見えているため）。

SIF GETVAR(@"ライフ_%対面者%")  <= (カウンター数*100)+1200
	ボーナス効果選択率 = 5
SIF GETVAR(@"ライフ_%対面者%")  <= (カウンター数*100)+600
	ボーナス効果選択率 = 10
SIF GETVAR(@"ライフ_%対面者%")  <= (カウンター数*100)+300
	ボーナス効果選択率 = 101
;使途が場に居るなら確実にボーナス効果が確定
CALL FULLRESET
効果範囲戦場 = 1
効果条件名前 = 타천칭의 사도
CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
IF RESULT == 1
	ボーナス効果選択率 = 102
ENDIF

;ダイスを振って効果を確定させる
ボーナス効果 = RAND:99 + 1

[IF_DEBUG]
	PRINTFORMW 100面ダイス:{ボーナス効果}
	PRINTFORMW ボーナス効果選択率:{ボーナス効果選択率}
[ENDIF]

;分岐パターンは４つ
IF ボーナス効果選択率 >= ボーナス効果 && 100 >= ボーナス効果選択率
	;１つめ　確率によってボーナス効果が選ばれたパターン
	選択後メッセージ = 苦悶の表情で１つ目の効果を選択した。
	効果分岐 = 1
ELSEIF ボーナス効果選択率 == 101
	;２つめ　ダメージがギリギリなのでボーナス効果を選ぶしか無かった
	効果分岐 = 2
ELSEIF ボーナス効果選択率 == 102
	;３つめ　타천칭의 사도の効果で確実に効果が通る状況だった
	選択後メッセージ = 타천칭의 사도の存在によって完全に屈服している……
	効果分岐 = 3
ENDIF
;それ以外の状況の場合はダメージ効果が選択される

;選択に応じてメッセージを表示
PRINTFORMW %対面者%は%選択後メッセージ%

RETURN 効果分岐
;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
; ダメージを与えカウンターを置く効果の共通化処理
@CARD_DAMAGE_SYMMETRIA(決闘者,種類,ゾーン,場所,ダメージ量)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC ダメージ量
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 調教対象

;ダメージを与える
CALL LIFE_CHANGE(決闘者,"減少","効果",ダメージ量,1)

;快感ダメージも付与
CALL CARD_EROTICS(相手プレイヤー,決闘者)
IF RESULT != -1
	調教対象 = RESULT
	PRINTFORML
	SETCOLOR C_PINK 
	PRINTFORML %CALLNAME:調教対象%はさらに快感ダメージを受けた！
	RESETCOLOR
	CALL EROTIC_MOVE("調教部屋",調教対象,,ダメージ量)
ENDIF

;カウンターを置く
PRINTFORMW 「타천칭」カードにカウンターを置いてください
CALL FULLRESET
効果条件カテゴリ = 타천칭
CALL CARD_CHOOSE_BASIC(決闘者,"魔罠",ゾーン,場所)
選択カード = RESULT
SIF 選択カード != 9999
	CALL SET_FIELD_INFO(決闘者,"魔罠",選択カード,"個Ｃ",1)

RETURN 0
;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
; ダメージだけを与える効果
@CARD_DAMAGE_59904(決闘者,種類,ゾーン,場所,ダメージ量)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC ダメージ量
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 調教対象

;ダメージを与える
CALL LIFE_CHANGE(決闘者,"減少","効果",ダメージ量,1)

;快感ダメージも付与
CALL CARD_EROTICS(相手プレイヤー,決闘者)
IF RESULT != -1
	調教対象 = RESULT
	PRINTFORML
	SETCOLOR C_PINK 
	PRINTFORML %CALLNAME:調教対象%はさらに快感ダメージを受けた！
	RESETCOLOR
	CALL EROTIC_MOVE("調教部屋",調教対象,,ダメージ量)
ENDIF

RETURN 0
;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
; 駄天秤によるセクハラ効果（本番無し）
@CARD_HOSTAGE_FORT_59904(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 調教対象

CALL FULLRESET
効果範囲戦場 = 1
効果範囲魔罠 = 1
効果条件名前 = Symスレイヴ
効果条件種類 = モン全般
効果条件CNT = 1
CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
SIF RESULT >= 1
	PRINTFORMW 심메트리아達は%CALLNAME:MASTER%に捕えた女たちで遊ぶ事を提案した。

CALL FULLRESET
FOR カウンタ,0,5
	SIF GETVAR(@"%決闘者%_魔罠:カウンタ") == -1
		CONTINUE
	SIF GETVAR(@"%決闘者%_魔_リ数:カウンタ") >= 10
		CONTINUE
	CALL CARD_EROTICS_FIELD(決闘者,"魔罠",カウンタ)
	IF RESULT != -1
		調教対象 = RESULT
		SELECTCASE RAND:5
			CASE 0
				PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:調教対象%の唇をこじ開けて舌を絡めた。
				CALL EROTIC_MOVE("ディープキス",調教対象,-1,500)
			CASE 1
				PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:調教対象%の全身をいやらしく撫でまわした。
				CALL EROTIC_MOVE("全身愛撫",調教対象,-1,500)
			CASE 2
				PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:調教対象%に口で自分を満足させるよう命令した。
				CALL EROTIC_MOVE("フェラチオ",調教対象,-1,500)
			CASE 3
				PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:調教対象%に胸で奉仕するよう命令した。
				CALL EROTIC_MOVE("パイズリ",調教対象,-1,500)
			CASE 4
				PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:調教対象%を晒しものにしている。
				CALL EROTIC_MOVE("晒しもの",調教対象,-1,500)
		ENDSELECT
		CALL SET_FIELD_INFO(決闘者,"魔罠",カウンタ,"リ数",1)
	ENDIF
NEXT

RETURN 0

;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
; 駄天秤によるセクハラ効果（本番あり）
@CARD_HOSTAGE_MONSTER_59904(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 調教対象

CALL FULLRESET
効果範囲戦場 = 1
効果範囲魔罠 = 1
効果条件名前 = Symスレイヴ
効果条件種類 = モン全般
効果条件CNT = 1
CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
SIF RESULT >= 1
	PRINTFORMW 심메트리아達は%CALLNAME:MASTER%に捕えた女たちで遊ぶ事を提案した。

CALL FULLRESET
FOR カウンタ,0,5
	SIF GETVAR(@"%決闘者%_魔罠:カウンタ") == -1
		CONTINUE
	SIF GETVAR(@"%決闘者%_魔_リ数:カウンタ") >= 10
		CONTINUE
	CALL CARD_EROTICS_FIELD(決闘者,"魔罠",カウンタ)
	IF RESULT != -1
		調教対象 = RESULT
		SELECTCASE RAND:4
			CASE 0
				PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:調教対象%に騎乗位で奉仕するよう命令した。
				CALL EROTIC_MOVE("騎乗位",調教対象,-1,500)
			CASE 1
				PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:調教対象%を激しく犯している。
				CALL EROTIC_MOVE("前穴凌辱",調教対象,-1,500)
			CASE 2
				PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:調教対象%の後ろの穴を激しく犯している。
				CALL EROTIC_MOVE("後穴凌辱",調教対象,-1,500)
			CASE 3
				PRINTFORMW %CALLNAME:MASTER%は심메트리아達と一緒に%CALLNAME:調教対象%を輪姦した。
				CALL EROTIC_MOVE("全身凌辱",調教対象,-1,500)
		ENDSELECT
		CALL SET_FIELD_INFO(決闘者,"魔罠",カウンタ,"リ数",1)
	ENDIF
NEXT

RETURN 0

;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
; カウンターだけを乗せる効果
@CARD_COUNTER_SYMMETRIA(決闘者,種類,ゾーン,場所,カウンター個数)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンター個数
#DIM DYNAMIC 選択カード

;カウンターを置く
PRINTFORMW 「타천칭」カードにカウンターを置いてください
CALL FULLRESET
効果条件カテゴリ = 타천칭
CALL CARD_CHOOSE_BASIC(決闘者,"魔罠",ゾーン,場所)
選択カード = RESULT
SIF 選択カード != 9999
	CALL SET_FIELD_INFO(決闘者,"魔罠",選択カード,"個Ｃ",カウンター個数)

RETURN 0