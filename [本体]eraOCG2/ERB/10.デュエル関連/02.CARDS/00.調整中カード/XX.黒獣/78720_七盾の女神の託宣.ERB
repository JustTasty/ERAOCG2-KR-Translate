@CARDNAME_78720,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 七盾の女神の託宣
	CASE "略称"
		RESULTS = 七盾の女神の託宣
	CASE "カテゴリ"
		RESULTS:0 = 七盾同盟
		RESULTS:1 = 黒獣
ENDSELECT

@CARD_78720(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "種類"
		RETURN 速攻魔法
ENDSELECT
RETURN 0

@CARDSUMMON_78720(決闘者,召喚カード,EX参照)
#DIMS DYNAMIC 決闘者
#DIM DYNAMIC 召喚カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
RETURN 0

@CARD_EXPLANATION_78720(種類)
#DIM DYNAMIC 種類

PRINTL このカード名の①の効果は１ターンに１度しか使用できない。
CALL CARD_ABLE(1,魔罠起動,手札起動,フリー誘発)
PRINTL ①：以下の効果から１つを選択して発動できる。
PRINTL ●自分フィールドに「七盾同盟の象徴 セレスティン」が存在しない場合に選択できる。
PRINTL デッキから「七盾同盟の象徴 セレスティン」１体を自分の魔法＆罠ゾーンに表側表示で置く。
PRINTL ●デッキから「七盾同盟」Ｐモンスター１体をＥＸデッキに送る。
; CALL CARD_ABLE(2,身代効果)
CALL CARD_ABLE(0,身代効果)
PRINTL ②：自分フィールドの「七盾同盟の象徴 セレスティン」が戦闘・効果によって破壊される場合、代わりに墓地のこのカードを除外できる。

@CARDCAN_78720(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

#DIMS DYNAMIC 対象ゾーン
#DIM DYNAMIC 条件達成

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,78720)
SIF RESULT == 1
	RETURN 0

IF 種類 == 魔罠起動 || 種類 == 手札起動 || 種類 == フリー誘発
	IF ゾーン == "手札"
		IF ターンプレイヤー != 決闘者
			RETURN 0
		ENDIF
	ELSEIF ゾーン == "魔罠"
		SIF GETVAR(@"%決闘者%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	SIF 発動済_1(決闘者,78720)
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果範囲魔罠 = 1
	効果条件名前 = 七盾同盟の象徴 セレスティン
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	IF RESULT != 1
		CALL FULLRESET
		効果範囲デッキ = 1
		効果条件名前 = 七盾同盟の象徴 セレスティン
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT == 1 && 魔罠数(決闘者) < 5
			条件達成++
	ENDIF
	CALL FULLRESET
	効果範囲デッキ = 1
	効果条件カテゴリ = 七盾同盟
	効果条件分割 = ペンデュラム
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT == 1
		条件達成++
	SIF 条件達成 < 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 身代効果
	; SIF 発動済_2(決闘者,78720)
	; 	RETURN 0
	SIF ゾーン != "墓地"
		RETURN 0
	SIF GETVAR(@"%決闘者%_除外NG置換") >= 1
		RETURN 0
	IF 破壊対象ゾーン == "戦場"
		対象ゾーン = MZ
	ELSE
		対象ゾーン = 魔
	ENDIF
	IF 対象ゾーン != ""
		SIF 破壊対象ID != 4716 && GETVAR(@"%決闘者%_%対象ゾーン%_変化元ID:破壊対象場所") != 4716
			RETURN 0
	ENDIF
	SIF !GROUPMATCH(破壊種類,"効果","戦闘")
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_78720(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

#DIM DYNAMIC 条件Ａ
#DIM DYNAMIC 条件Ｂ
#DIM DYNAMIC 選択効果

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 魔罠起動 || 種類 == 手札起動 || 種類 == フリー誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,78720,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所)
		CALL FULLRESET
		効果範囲戦場 = 1
		効果範囲魔罠 = 1
		効果条件名前 = 七盾同盟の象徴 セレスティン
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		IF RESULT != 1
			CALL FULLRESET
			効果範囲デッキ = 1
			効果条件名前 = 七盾同盟の象徴 セレスティン
			CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
			SIF RESULT == 1 && 魔罠数(決闘者) < 5
				条件Ａ++
		ENDIF
		CALL FULLRESET
		効果範囲デッキ = 1
		効果条件カテゴリ = 七盾同盟
		効果条件分割 = ペンデュラム
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT == 1
			条件Ｂ++
		IF 条件Ａ != 0 && 条件Ｂ != 0
			CALL YN_CHOICE(決闘者,"●デッキから「七盾同盟の象徴 セレスティン」１体を自分の魔法＆罠ゾーンに表側表示で置く。","●デッキから「七盾同盟」Ｐモンスター１体をＥＸデッキに送る。")
			選択効果 = RESULT
		ELSEIF 条件Ｂ == 1 && 条件Ａ == 0
			選択効果 = いいえ
		ELSEIF 条件Ａ == 1 && 条件Ｂ == 0
			選択効果 = はい
		ENDIF
		IF 選択効果 == はい
			CALL FULLRESET
			効果範囲デッキ = 1
			効果条件名前 = 七盾同盟の象徴 セレスティン
			CALL CARD_SET_ADV(決闘者,ゾーン,場所,"表側表示")
		ELSEIF 選択効果 == いいえ
			CALL FULLRESET
			効果範囲デッキ = 1
			効果条件カテゴリ = 七盾同盟
			効果条件分割 = ペンデュラム
			効果条件行先フラグ = EXデッキ行き
			CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 種類 == 身代効果
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,78720,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所)
		RETURN 1
	ENDIF
	RETURN 0
ENDIF