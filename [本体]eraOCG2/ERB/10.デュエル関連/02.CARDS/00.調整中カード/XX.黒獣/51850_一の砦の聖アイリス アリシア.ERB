@CARDNAME_51850,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 一の砦の聖アイリス アリシア
	CASE "略称"
		RESULTS = 聖アリシア
	CASE "カテゴリ"
		RESULTS:0 = 七盾同盟
		RESULTS:1 = 黒獣
ENDSELECT

@CARD_51850(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "種類"
		RETURN エクシーズ
	CASE "属性"
		RETURN 光属性
	CASE "種族"
		RETURN 戦士族
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 1800
	CASE "守備力"
		RETURN 1000
	CASE "性別"
		RETURN 牝性
	CASE "分割"
		RETURN ペンデュラム
	CASE "連動ID"
		RETURN 4710
	CASE "追加日"
		RETURN 250707
ENDSELECT
RETURN 0

@CARDSUMMON_51850(決闘者,召喚カード,EX参照)
#DIMS DYNAMIC 決闘者
#DIM DYNAMIC 召喚カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 2
素材条件レベル = 1
素材条件種族 = 戦士族

IF EX参照 == 1
	CALL XYZCAN(決闘者,召喚カード,51850) 
	RETURN RESULT
ENDIF

@CARD_EXPLANATION_51850(種類)
#DIM DYNAMIC 種類

CALL TEXT_DECORATION("P_MAGIC")
PRINTL このカード名のＰ効果は１ターンに１度しか使用できない。
CALL CARD_ABLE(1,発動誘発)
PRINTL ①：このカード名以外のモンスターの効果が発動した場合に、
PRINTL 自分フィールドに「七盾同盟の象徴 セレスティン」または「二の砦の無垢姫 プリム」が存在するなら発動できる。
PRINTL このカードを特殊召喚する。
PRINTL その後、このカードのＸ素材を１つ増やすことで、発動した効果を無効にできる。
CALL TEXT_DECORATION("P_MAGIC_2")
PRINTL レベル１戦士族モンスター×２
CALL CARD_ABLE(2,EXデッキ起動)
PRINTL このカード名のカードは１ターンに１度、
PRINTL 自分フィールドに「七盾同盟の象徴 セレスティン」または「二の砦の無垢姫 プリム」が存在する場合に、
PRINTL 自分フィールドの「一の砦の姫騎士 アリシア」の上に重ねてＸ召喚できる。
PRINTL このカード名の①②のモンスター効果はそれぞれ１ターンに１度しか使用できない。
CALL CARD_ABLE(3,EX召喚時,特殊召喚時)
PRINTL ①：このカードが特殊召喚した場合に発動できる。
PRINTL このカードのＸ素材を１つ取り除き、このターン中このカードは覚醒状態になり、
PRINTL 攻撃力が自分フィールドに存在する「七盾同盟」モンスターのレベルまたはランクの種類×２００アップする。
CALL CARD_ABLE(4,効果後誘発)
PRINTL ②：このカード名以外のモンスターが特殊召喚した場合に、
PRINTL 自分フィールドに「七盾同盟の象徴 セレスティン」または「二の砦の無垢姫 プリム」が存在するなら発動できる。
PRINTL このカードを魔法＆罠ゾーンに表側表示で置き、
PRINTL ＥＸデッキ・魔法＆罠ゾーンのこのカード名以外でかつＥＸモンスター以外の「七盾同盟」Ｐモンスター１体を特殊召喚する。


@CARDCAN_51850(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,51850)
SIF RESULT == 1
	RETURN 0

IF 種類 == 手札起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	RETURN 1
ELSEIF 種類 == 発動誘発
	SIF ゾーン != "魔罠"
		RETURN 0
	SIF 発動済_1(決闘者,51850)
		RETURN 0
	SIF GETVAR(@"%決闘者%_魔_表:場所") > 1
		RETURN 0
	CALL FULLRESET
	効果条件種類 = モン全般
	効果条件固名前 = 一の砦の聖アイリス アリシア
	CALL CARD_CHECK_START_DATA(決闘者,2)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果範囲魔罠 = 1
	効果条件名前:0 = 七盾同盟の象徴 セレスティン
	効果条件名前:1 = 二の砦の無垢姫 プリム
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL CARD_SS_CHECK(決闘者,51850,"魔罠")
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == EXデッキ起動
	SIF 発動済_2(決闘者,51850)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果範囲魔罠 = 1
	効果条件名前:0 = 七盾同盟の象徴 セレスティン
	効果条件名前:1 = 二の砦の無垢姫 プリム
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件名前 = 一の砦の姫騎士 アリシア
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == EX召喚時 || 種類 == 特殊召喚時
	SIF 発動済_3(決闘者,51850)
		RETURN 0
	SIF 魔罠数(決闘者) >= 5
		RETURN 0
	SIF ゾーン != "戦場"
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_Ｘ:場所") < 0
		RETURN 0
	RETURN 1
ELSEIF 種類 == 効果後誘発
	SIF 発動済_4(決闘者,51850)
		RETURN 0
	SIF ゾーン != "戦場"
		RETURN 0
	CALL FULLRESET
	効果条件種類 = モン全般
	効果条件固名前 = 一の砦の聖アイリス アリシア
	CALL CARD_CHECK_FIN_DATA(決闘者,"特殊召喚",2)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果範囲魔罠 = 1
	効果条件名前:0 = 七盾同盟の象徴 セレスティン
	効果条件名前:1 = 二の砦の無垢姫 プリム
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	SIF 魔罠数(決闘者) >= 5
		RETURN 0
	CALL FULLRESET
	効果範囲魔罠 = 1
	効果範囲EXデッキ = 1
	効果条件カテゴリ = 七盾同盟
	効果条件分割 = ペンデュラム
	効果条件種類 = EX以外全般
	効果条件召喚可 = 2
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 攻守変動
	SIF ゾーン != "戦場"
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_条達:場所") < 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_51850(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードID

#DIM DYNAMIC 条件達成
#DIM DYNAMIC 候補除外レベル,12
#DIM DYNAMIC カウンタB

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 手札起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,51850)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_DISPOSE(決闘者,ゾーン,場所,魔罠行き)
	ENDIF
ELSEIF 種類 == 発動誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,51850,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(決闘者,ゾーン,場所)
		IF RESULT == 1
			IF 決闘者 == "自分"
				PRINTL このカードのＸ素材を１つ増やし、発動した効果を無効にしますか？
				CALL YN_CHOICE(決闘者)
			ELSE
				SIF 効後_実行者:(効後_カウンタ - 2) == 決闘者
					RESULT = いいえ
			ENDIF
			IF RESULT == はい
				CALL SET_FIELD_INFO(決闘者,"戦場",カード召喚先,"Ｘ",1)
				CALL FULLRESET
				CALL CARD_REMOVAL_BASIC(決闘者,"発動無効",場所,"無効")
				RETURN 1
			ENDIF
		ENDIF
	ENDIF
ELSEIF 種類 == EXデッキ起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,51850,2)
	IF RESULT == 0
		CALL RESETCONDITION_MAT
		必要素材数 = 1
		効果範囲戦場 = 1
		素材条件名前 = 一の砦の姫騎士 アリシア
		重ねてＸ = 1
		TRYCALLFORM XYZSUMMON(決闘者,場所)
	ENDIF
ELSEIF 種類 == EX召喚時 || 種類 == 特殊召喚時
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,51850,3,X消費1)
	IF RESULT == 0
		CALL SET_FIELD_INFO(決闘者,ゾーン,場所,"条達",1,上書きモード)
	ENDIF
ELSEIF 種類 == 効果後誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,51850,4)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_DISPOSE(決闘者,ゾーン,場所,魔罠行き)
		IF RESULT == 1
			CALL FULLRESET
			効果範囲魔罠 = 1
			効果範囲EXデッキ = 1
			効果条件種類 = EX以外全般
			効果条件固名前 = 一の砦の聖アイリス アリシア
			効果条件分割 = ペンデュラム
			効果条件カテゴリ = 七盾同盟
			CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 種類 == 攻守変動
	FOR カウンタ, 0, 6
		;MZ
		選択カードID = GETVAR(@"%決闘者%_戦場:カウンタ")
		IF 選択カード != -1
			VARSET 条件達成
			TRYCALLFORM CARDNAME_{選択カードID}, "カテゴリ"
			SIF MATCH(RESULTS, "七盾同盟")
				条件達成 ++
			IF GETVAR(@"%決闘者%_MZ_変化元ID:カウンタ") > -1
				TRYCALLFORM CARDNAME_{GETVAR(@"%決闘者%_MZ_変化元ID:カウンタ")}, "カテゴリ"
				SIF MATCH(RESULTS, "七盾同盟")
					条件達成 ++
			ENDIF
			;改名されてるとカテゴリをすべて失い、追加カテゴリのみ
			IF GETVARS(@"%決闘者%_MZ_改名:カウンタ") != ""
				VARSET 条件達成
			ENDIF
			SIF GETVARS(@"%決闘者%_MZ_追カテ:カウンタ") == "七盾同盟"
				条件達成++
			SIF GETVARS(@"%決闘者%_MZ_追カテ2:カウンタ") == "七盾同盟"
				条件達成++
			IF 条件達成 > 0 && GETVAR(@"%決闘者%_MZ_星:カウンタ") - 1 > 0
				候補除外レベル:(GETVAR(@"%決闘者%_MZ_星:カウンタ") - 1) = 1
			ENDIF
		ENDIF
		;魔罠
		選択カードID = GETVAR(@"%決闘者%_魔罠:カウンタ")
		IF 選択カード != -1
			VARSET 条件達成
			TRYCALLFORM CARDNAME_{選択カードID}, "カテゴリ"
			SIF MATCH(RESULTS, "七盾同盟")
				条件達成++
			;改名されてるとカテゴリをすべて失い、追加カテゴリのみ
			IF GETVARS(@"%決闘者%_魔_改名:カウンタ") != ""
				VARSET 条件達成
			ENDIF
			SIF GETVARS(@"%決闘者%_魔_追カテ:カウンタ") == "七盾同盟"
				条件達成++
			SIF GETVARS(@"%決闘者%_魔_追カテ2:カウンタ") == "七盾同盟"
				条件達成++
			IF 条件達成 > 0
				TRYCALLFORM CARD_{選択カードID}("レベル")
				SIF RESULT > 0
					候補除外レベル:(RESULT - 1) = 1
			ENDIF
		ENDIF
	NEXT
	カウンタ = MATCH(候補除外レベル,1)
	IF カウンタ > 0
		CALL FULLRESET
		CALL STATCHANGE_BASIC(決闘者,"自身",場所,カウンタ*200)
	ENDIF
	RETURN 0
ENDIF