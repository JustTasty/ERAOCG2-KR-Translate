@CARDNAME_4717,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 칠흑의여왕 올가
	CASE "略称"
		RESULTS = 올가
	CASE "カテゴリ"
		RESULTS:0 = 쿠로이누
ENDSELECT

@CARD_4717(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 마법사족
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 1800
	CASE "守備力"
		RETURN 2800
	CASE "성별"
		RETURN 牝性
	CASE "分割"
		RETURN 펜듈럼
	CASE "単一存在"
		RETURN 1
	CASE "추가日"
		RETURN 250707
ENDSELECT
RETURN 0

@CARD_EXPLANATION_4717(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("P_MAGIC")
PRINTL 자신 필드에 어둠속성以外のモンスター카드가存在する場合、이 카드는 지속마법扱いで발동できない。
PRINTL 이 카드명의 ②のＰ효과は１턴에 １번밖에 사용할 수 없다。
CALL CARD_ABLE(0,攻守変動)
PRINTL ①：자신의 어둠속성モンスターは공격력이 ３００올린다。
CALL CARD_ABLE(1,마함起動)
PRINTL ②：자신 메인 페이즈에 발동할 수 있다。
PRINTL 이 카드를 ＥＸ덱に送り、덱에서 レベル４以下の어둠속성몬스터 １장을 묘지に送る。
PRINTL この효과の발동後、このデュエル中に자신은 어둠속성以外のモンスター의 효과를 발동できない。
CALL TEXT_DECORATION("P_MAGIC_2")
PRINTL 이 카드명의 カードは자신 필드에 １体しか表側표시で存在できない。
PRINTL 이 카드명의 モンスター효과はそれぞれ１턴에 １번밖에 사용할 수 없다。
CALL CARD_ABLE(2,ターン開始時)
PRINTL ①：お互いのスタンバイフェイズに、상대 필드の몬스터 １장을 대상으로 하고 발동할 수 있다。
PRINTL 자신 필드의 어둠속성몬스터 １장을 묘지로 보내고、ＥＸ덱에서 このモンスターカードを자신의 마법＆함정ゾーンに表側표시で置く。
PRINTL 그 후、대상のカード를 파괴한다。

@CARDCAN_4717(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4717)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	CALLFORM 효과条件속성_ADV(어둠속성,"以外")
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件名前 = 칠흑의여왕 올가
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF 발동済_1(듀얼리스트,4717)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件속성 = 어둠속성
	효과条件종류 = モン全般
	CALLFORM 효과条件レベル_ADV(4,"以下")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻守変動
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") > 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF !GROUPMATCH(ゾーン,"필드","마함")
	RETURN 1
ELSEIF 종류 == ターン開始時
	SIF 발동済_2(듀얼리스트,4717)
		RETURN 0
	SIF ゾーン != "EX덱"
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件除去種 = 選択
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件속성 = 어둠속성
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,4717,"EX덱",1)
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_4717(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4717)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF
ELSEIF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4717,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,EX덱行き)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件속성 = 어둠속성
			효과条件종류 = モン全般
			CALLFORM 효과条件レベル_ADV(4,"以下")
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
		CALL SET_PLAYER_INFO(듀얼리스트,"빛속성효과無効",10)
		CALL SET_PLAYER_INFO(듀얼리스트,"땅속성효과無効",10)
		CALL SET_PLAYER_INFO(듀얼리스트,"물속성효과無効",10)
		CALL SET_PLAYER_INFO(듀얼리스트,"화염속성효과無効",10)
		CALL SET_PLAYER_INFO(듀얼리스트,"바람속성효과無効",10)
	ENDIF
ELSEIF 종류 == 攻守変動
	CALL FULLRESET
	효과条件속성 = 어둠속성
	CALL STATCHANGE_BASIC(듀얼리스트,"필드",場所,300)
ELSEIF 종류 == ターン開始時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4717,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲상대필드 = 1
		CALL CARD_REMOVAL_ADV(듀얼리스트,"単体除去",場所,"選択")
		選択カード = RESULT:1
		IF RESULT == 1
			CALL FULLRESET
			효과範囲필드 = 1
			효과条件속성 = 어둠속성
			효과条件종류 = モン全般
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				효과範囲EX덱 = 1
				효과条件名前 = 칠흑의여왕 올가
				CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")
				IF RESULT == 1
					CALL CARD_SELECT(듀얼리스트,選択カード)
					CALL CARD_DISPOSE(対面者,"필드",選択カード（CS）,破壊行き)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF