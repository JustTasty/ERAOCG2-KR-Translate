@CARDNAME_52367,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 滅星輝兵 “Ø” グレンディオス
	CASE "略称"
		RESULTS = Øグレンディオス
	CASE "カテゴリ"
		RESULTS:0 = グレンディオス
		RESULTS:1 = 星輝兵
		RESULTS:2 = リンクジョーカー
		RESULTS:4 = ヴァンガード
ENDSELECT


@CARD_52367(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "種類"
		RETURN 超次元
	CASE "属性"
		RETURN 闇属性
	CASE "種族"
		RETURN 機械族 
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 1500
	CASE "守備力"
		RETURN 0
	CASE "性別"
		RETURN 無性
	CASE "特殊不可"
		RETURN 1
ENDSELECT

RETURN 0


@CARDSUMMON_52367(決闘者,召喚カード,EX参照)
#DIMS DYNAMIC 決闘者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 召喚カード
#DIM DYNAMIC EX参照
;素材条件


CALL RESETCONDITION_MAT
必要素材数 = 1
素材条件カテゴリ = グレンディオス

IF EX参照 == 1
	CALL STRIDECAN(決闘者,召喚カード,52367) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_52367(種類)
#DIM DYNAMIC 種類

PRINTL 『超越』「グレンディオス」モンスター
SETCOLOR C_GRAY
PRINTL （自分の手札からモンスター１体を除外して発動できる。
PRINTL 　EXデッキのこのカードを、自分フィールドの指定されたモンスター１体の上に重ねて『超越』する。
PRINTL  『超越』したこのカードの名前は重ねたモンスターと同じ名前として扱い、
PRINTL 　重ねたモンスターの攻撃力・守備力分、このカードの攻撃力・守備力をアップする。
PRINTL  『超越』は１ターンに１度しか行えず、先行１ターン目には使用できない）
RESETCOLOR
PRINTL このカード名の②③の効果は１ターンに１度しか使用できない。
CALL CARD_ABLE(0,永続効果)
PRINTL ①：自分フィールドの「Я」モンスターは効果無効・攻撃不可を得る。
CALLFORM CARD_ABLE(1,EX召喚時)
PRINTL ②：このカードが超越した場合に発動できる。
PRINTL ＬＰを半分払い、１枚ドローする。
CALLFORM CARD_ABLE(2,戦場起動)
PRINTL ③：自分メインフェイズ１に発動できる。
PRINTL EXデッキの「滅星輝兵 “Ø” グレンディオス」１体を除外し、自分フィールドの「Я」モンスター１体を墓地に送り、
PRINTL 相手は墓地からＥＸモンスター以外のモンスターを２体まで手札に戻し、手札からモンスター２体を呪縛状態で特殊召喚する。
SETCOLOR C_GRAY
PRINTL （呪縛状態のモンスターは裏側守備表示となり、攻撃回避を得る。
PRINTL 　ただし、相手フィールドに呪縛状態のモンスターしか存在しない場合、自分フィールドのモンスターが相手プレイヤーに与える戦闘ダメージは半分になる）
RESETCOLOR

@CARDCAN_52367(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件達成

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,52367)
SIF RESULT == 1
	RETURN 0

IF 種類 == 永続効果
	SIF ゾーン != "戦場"
		RETURN 0
	RETURN 1
ELSEIF 種類 == EX召喚時
	SIF 発動済_1(決闘者,52367)
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1
ELSEIF 種類 == 戦場起動
	SIF 発動済_2(決闘者,52367)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF メインフェイズ２ == 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL FULLRESET
	効果範囲EXデッキ = 1
	効果条件名前 = 滅星輝兵 “Ø” グレンディオス
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件カテゴリ = Я
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	SIF モンスター数(対面者) >= 5
		RETURN 0
	RETURN 1 
ENDIF

@CARDEFFECT_52367(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件カード数

CALL 対面者判定(決闘者)
対面者 = %RESULTS%


IF 種類 == 永続効果
	CALL FULLRESET
	効果条件カテゴリ = Я
	CALL STATCHANGE_EFFECT(決闘者,"戦場",場所,"戦無",3)
	CALL STATCHANGE_EFFECT(決闘者,"戦場",場所,"無効",3)

	CALL FULLRESET
	効果条件表示 = 裏側守備表示
	CALL STATCHANGE_EFFECT(決闘者,"相手戦場",場所,"攻避",3)
	CALL FULLRESET
	効果範囲相手戦場 = 1
	効果条件表示 = 裏側守備表示
	効果条件CNT = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	条件カード数 = RESULT
	CALL FULLRESET
	効果範囲相手戦場 = 1
	効果条件表示 = 表側表示
	効果条件CNT = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	IF RESULT <= 0 && 条件カード数 >= 1
		CALL FULLRESET
		CALL STATCHANGE_EFFECT(決闘者,"戦場",場所,"直攻")
		CALL SET_PLAYER_INFO(決闘者,"ダメージ半化",1,1)
	ENDIF
ELSEIF 種類 == EX召喚時
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,52367,1)
	IF RESULT == 0
		CALL LIFE_CHANGE(決闘者,"減少","コスト",(GETVAR(@"ライフ_%決闘者%")  / 2))
		CALL CARDDRAW_BASIC(決闘者,1)
	ENDIF
ELSEIF 種類 == 戦場起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,52367,2)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲EXデッキ = 1
		効果条件名前 = 滅星輝兵 “Ø” グレンディオス
		効果条件行先フラグ = 除外行き
		CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			効果範囲戦場 = 1
			効果条件カテゴリ = Я
			CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				効果範囲墓地 = 1
				効果条件種類 = EX以外全般
				効果条件複数 = 98
				CALL CARD_ADD_ADV(対面者,ゾーン,場所)
				CALL FULLRESET
				効果範囲手札 = 1
				効果条件複数 = 2
				CALL CARD_SP_SUMMON_ADV(対面者,ゾーン,場所,"裏側守備表示")
			ENDIF
		ENDIF
	ENDIF
ENDIF



