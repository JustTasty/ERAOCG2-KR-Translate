@CARDNAME_25165,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 베어루크티－미크빌리스
	CASE "略称"
		RESULTS = BeR미크빌리스
	CASE "カテゴリ"
		RESULTS:0 = 베어루크티
ENDSELECT

@CARD_25165(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 야수족
	CASE "レベル"
		RETURN 7
	CASE "攻撃力"
		RETURN 700
	CASE "守備力"
		RETURN 1400
	CASE "추가日"
		RETURN 250818
ENDSELECT

RETURN 0

@CARD_EXPLANATION_25165(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,필드起動)
PRINTL ①：자신 / 상대의 메인 페이즈에、패에서 다른 몬스터 카드 １장을 릴리스하고 발동할 수 있다。
PRINTL 이 카드를 패에서 특수 소환한다。
PRINTL 이 효과로 "베어루크티" 몬스터를 릴리스 한 경우、카드를 １장 드로우한다。
PRINTL 이 효과의 발동 후 턴 종료시까지 자신은 L / X 몬스터를 특수 소환할 수 없다。
CALLFORM CARD_ABLE(2,특수소환時)
PRINTL ②：이 카드가 특수 소환에 성공했을 경우에 발동할 수 있다。
PRINTL 패에서 이 카드명 이외의 "베어루크티" 몬스터 １장을 특수 소환한다。


@CARDCAN_25165(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 효과使用불가,10
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,25165)
SIF RESULT == 1
	RETURN 0
IF 종류 == 손패起動 || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,25165)
		RETURN 0
	SIF ゾーン != "손패"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	CALL FULLRESET
	SIF GROUPMATCH(GETVARS(@"%듀얼리스트%_フィールド名"), "빅디퍼","베어트론")
		효과範囲묘지 = 1
	효과範囲손패 = 1
	효과条件종류 = モン全般
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,25165,"손패")
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,25165)
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件カテゴリ = 베어루크티
	효과条件固名前 = 베어루크티－미크빌리스
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_25165(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 효과使用불가,10
#DIMS DYNAMIC 選択名前
#DIM DYNAMIC 選択カード_ID

IF 종류 == 손패起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,25165,1)
	IF RESULT == 0
		CALL FULLRESET
		SIF GROUPMATCH(GETVARS(@"%듀얼리스트%_フィールド名"), "빅디퍼","베어트론")
			효과範囲묘지 = 1
		효과範囲손패 = 1
		효과条件종류 = モン全般
		효과条件他所 = 1
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			選択カード_ID = RESULT:1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,"손패",場所)
			
			TRYCALLFORM CARDNAME_{選択カード_ID}("カテゴリ")
			IF MATCH(RESULTS,"베어루크티")
				CALL CARDDRAW_BASIC(듀얼리스트,1)
			ENDIF
		ENDIF
		SS소환可能종류 = レベル持ち
		CALL CARD_SS_RESTRICTION(듀얼리스트)
	ENDIF
ELSEIF 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,25165,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件カテゴリ = 베어루크티
		효과条件固名前 = 베어루크티－미크빌리스
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF

