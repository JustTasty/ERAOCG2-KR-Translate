@CARDNAME_51062,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 벤데드 배스터드
	CASE "略称"
		RESULTS = VD.Battlelord
	CASE "カテゴリ"
		RESULTS:0 = 벤데드
ENDSELECT


@CARD_51062(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 의식
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 언데드족 
	CASE "レベル"
		RETURN 7
	CASE "攻撃力"
		RETURN 2700
	CASE "守備力"
		RETURN 0
	CASE "추가日"
		RETURN 250707
ENDSELECT

RETURN 0
@CARDSUMMON_51062(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 出処
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照

CALL RESETCONDITION_MAT
必要レベル数 = 7

IF EX参照 == 1
	CALL RITCAN(듀얼리스트,소환カード,51062) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_51062(종류)
#DIM DYNAMIC 종류

PRINTL "벤데드" 의식 마법 카드에 의해 강림。
PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALL CARD_ABLE(1,필드起動)
PRINTL ①：자신의 묘지에서 "벤데드"카드 １장을 제외하고、카드의 종류（몬스터 / 마법 / 함정）을 선언하고 발동할 수 있다。
PRINTL 이 몬스터가 필드에 존재하는 한、이 턴、상대는 선언한 종류의 카드의 효과를 발동할 수 없다。
CALL CARD_ABLE(2,戦破壊→묘지)
PRINTL ②：필드의 이 카드가 전투 / 효과로 파괴된 경우에 발동할 수 있다。
PRINTL 덱에서 의식몬스터 １장을 패에 넣고、덱에서 "벤데드"몬스터 １장을 묘지로 보낸다。


@CARDCAN_51062(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 발동불가,3

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,51062)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	
	;バスタード
	SIF 발동済_1(듀얼리스트,51062)
		발동불가:2 = 1
	SIF メインフェイズ != 1
		발동불가:2 = 1
	SIF ターンプレイヤー != 듀얼리스트
		발동불가:2 = 1
	SIF 종류 == フリー誘発
		발동불가:2 = 1
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종족 = 언데드족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		발동불가:2 = 1
	
	;レヴナント
	SIF GETBIT(GETVAR(@"%듀얼리스트%_MZ_汎数:場所"),1) != 1
		발동불가:0 = 1
	SIF 발동済_3(듀얼리스트,51062)
		발동불가:0 = 1
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件召種 = 召種全특수
	효과条件除去種 = 제외
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		발동불가:0 = 1

	;ヘルハン
	SIF GETBIT(GETVAR(@"%듀얼리스트%_MZ_汎数:場所"),2) != 1
		발동불가:1 = 1
	SIF 발동済_4(듀얼리스트,51062)
		발동불가:1 = 1
	CALL FULLRESET
	효과範囲상대마함 = 1
	효과条件除去種 = 제외
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		발동불가:1 = 1

	SIF 발동불가:0 == 1 && 발동불가:1 == 1 && 발동불가:2 == 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 戦破壊→묘지
	SIF 발동済_2(듀얼리스트,51062)
		RETURN 0
	SIF !GROUPMATCH(묘지送り종류,"戦闘破壊","破壊")
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종류 = 의식
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 벤데드
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_T1汎数:場所") < 1
		RETURN 0
	RETURN 1

;ストリゲスの付与효과
ELSEIF 종류 == 攻撃後誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	SIF GETBIT(GETVAR(@"%듀얼리스트%_MZ_汎数:場所"),3) != 1
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1
ENDIF

@CARDEFFECT_51062(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 발동불가,3
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動 || 종류 == フリー誘発
	;バスタード
	SIF 발동済_1(듀얼리스트,51062)
		발동불가:2 = 1
	SIF メインフェイズ != 1
		발동불가:2 = 1
	SIF ターンプレイヤー != 듀얼리스트
		발동불가:2 = 1
	SIF 종류 == フリー誘発
		발동불가:2 = 1
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종족 = 언데드족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		발동불가:2 = 1
	
	;レヴナント
	SIF GETBIT(GETVAR(@"%듀얼리스트%_MZ_汎数:場所"),1) != 1
		발동불가:0 = 1
	SIF 발동済_3(듀얼리스트,51062)
		발동불가:0 = 1
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件召種 = 召種全특수
	효과条件除去種 = 제외
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		발동불가:0 = 1

	;ヘルハン
	SIF GETBIT(GETVAR(@"%듀얼리스트%_MZ_汎数:場所"),2) != 1
		발동불가:1 = 1
	SIF 발동済_4(듀얼리스트,51062)
		발동불가:1 = 1
	CALL FULLRESET
	효과範囲상대마함 = 1
	효과条件除去種 = 제외
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		발동불가:1 = 1

	IF 듀얼리스트 == "자신"
		PRINTL 使用する효과を選択してください
		PRINTL 
		SIF 발동불가:0 != 1
			PRINTL [0] - ●レヴナント
		SIF 발동불가:1 != 1
			PRINTL [1] - ●ヘルハウンド
		SIF 발동불가:2 != 1
			PRINTL [2] - ●バスタード
	ELSE
		SIF 발동불가:1 != 1
			RESULT = 1
		SIF 발동불가:0 != 1
			RESULT = 0
		SIF 발동불가:2 != 1
			RESULT = 2
	ENDIF

	IF RESULT == 0
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51062,3)
		IF RESULT == 0
			CALL FULLRESET
			효과条件召種 = 召種全특수
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"제외")
		ENDIF

	ELSEIF RESULT == 1
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51062,4)
		IF RESULT == 0
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"마함単体",場所,"제외")
		ENDIF

	ELSEIF RESULT == 2
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51062,1)
		IF RESULT == 0
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件종족 = 언데드족
			효과条件行先플래그 = 제외行き
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				IF 듀얼리스트 == "자신"
					PRINTL 선언할 종류를 골라주세요
					PRINTL
					PRINTL [1] - 몬스터
					PRINTL [2] - 마법
					PRINTL [3] - 함정
					BINPUT
				ELSE
					RESULT = 1
				ENDIF

				CALL SET_FIELD_INFO(듀얼리스트,ゾーン,場所,"T1汎数",RESULT,1)
			ENDIF
		ENDIF
	ENDIF


ELSEIF 종류 == 戦破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51062,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件종류 = 의식
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 벤데드
			효과条件종류 = モン全般
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF

ELSEIF 종류 == 지속효과
	SELECTCASE GETVAR(@"%듀얼리스트%_MZ_T1汎数:場所")
		CASE 1
			CALL SET_PLAYER_INFO(듀얼리스트,"モン효과無効",3,1)
		CASE 2
			CALL SET_PLAYER_INFO(듀얼리스트,"魔無効",3,1)
		CASE 3
			CALL SET_PLAYER_INFO(듀얼리스트,"함정無効",3,1)
	ENDSELECT

ELSEIF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51062)
	IF RESULT == 0
		CALL URARA_CHANCE(듀얼리스트,処理中_효과ID,"雑多")
		IF RESULT == 0
			CALL CARDDRAW_BASIC(듀얼리스트,2)
			CALL FULLRESET
			효과範囲손패 = 1
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ENDIF