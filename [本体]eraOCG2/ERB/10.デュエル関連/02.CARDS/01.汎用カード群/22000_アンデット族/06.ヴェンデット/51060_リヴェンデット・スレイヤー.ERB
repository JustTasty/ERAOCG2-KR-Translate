@CARDNAME_51060,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 리벤데드 슬레이어
	CASE "略称"
		RESULTS = RVD.Slayer
	CASE "カテゴリ"
		RESULTS:0 = 벤데드
ENDSELECT


@CARD_51060(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 의식
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 언데드족 
	CASE "レベル"
		RETURN 6
	CASE "攻撃力"
		RETURN 2400
	CASE "守備力"
		RETURN 0
	CASE "성별"
		RETURN 雄性
	CASE "추가日"
		RETURN 250707
ENDSELECT

RETURN 0
@CARDSUMMON_51060(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 出処

#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照

CALL RESETCONDITION_MAT
必要レベル数 = 6

IF EX参照 == 1
	CALL RITCAN(듀얼리스트,소환カード,51060) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_51060(종류)
#DIM DYNAMIC 종류

PRINTL 「벤데드」의식 마법カードにより降臨。
PRINTL このカード名の②の효과は１ターンに１度しか使用できない。
CALL CARD_ABLE(0,攻撃時誘発)
PRINTL ①：このカードが상대モンスターと戦闘を行う攻撃宣言時に발동できる。
PRINTL 자신の묘지から언데드족モンスター１体を제외して발동できる。
PRINTL このカードの攻撃力は３００アップする。
CALL CARD_ABLE(2,戦破壊→묘지)
PRINTL ②：フィールドのこのカードが묘지へ送られた場合に발동できる。
PRINTL 덱から의식 마법カード１枚を손패に加え、덱から「벤데드」モンスター１体を묘지へ送る。



@CARDCAN_51060(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 발동불가,2

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,51060)
SIF RESULT == 1
	RETURN 0

IF 종류 == 攻撃時誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종족 = 언데드족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 戦破壊→묘지
	SIF 발동済_2(듀얼리스트,51060)
		RETURN 0
	SIF !GROUPMATCH(묘지送り종류,"戦闘破壊","破壊","リリース")
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 의식 마법
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 벤데드
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0

	;レヴナント
	SIF GETBIT(GETVAR(@"%듀얼리스트%_MZ_汎数:場所"),1) != 1
		발동불가:0 = 1
	SIF 발동済_1(듀얼리스트,51060)
		발동불가:0 = 1
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件召種 = 召種全특수
	효과条件除去種 = 제외
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		발동불가:0 = 1

	;ヘルハン
	SIF GETBIT(GETVAR(@"%듀얼리스트%_MZ_汎数:場所"),2) != 1
		발동불가:1 = 1
	SIF 발동済_3(듀얼리스트,51060)
		발동불가:1 = 1
	CALL FULLRESET
	효과範囲상대마함 = 1
	효과条件除去種 = 제외
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		발동불가:1 = 1

	SIF 발동불가:0 == 1 && 발동불가:1 == 1
		RETURN 0
	RETURN 1

;ストリゲスの付与효과
ELSEIF 종류 == 攻撃後誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	SIF GETBIT(GETVAR(@"%듀얼리스트%_MZ_汎数:場所"),3) != 1
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1
ENDIF

@CARDEFFECT_51060(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 발동불가,2
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51060)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件종족 = 언데드족
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL SET_FIELD_INFO(듀얼리스트,ゾーン,場所,"永攻",300)
		ENDIF
	ENDIF

ELSEIF 종류 == 戦破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51060,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 의식 마법
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 벤데드
			효과条件종류 = モン全般
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF


ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF GETBIT(GETVAR(@"%듀얼리스트%_MZ_汎数:場所"),1) != 1
		발동불가:0 = 1
	SIF 발동済_1(듀얼리스트,51060)
		발동불가:0 = 1
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件召種 = 召種全특수
	효과条件除去種 = 제외
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		발동불가:0 = 1

	SIF GETBIT(GETVAR(@"%듀얼리스트%_MZ_汎数:場所"),2) != 1
		발동불가:1 = 1
	SIF 발동済_3(듀얼리스트,51060)
		발동불가:1 = 1
	CALL FULLRESET
	효과範囲상대마함 = 1
	효과条件除去種 = 제외
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		발동불가:1 = 1

	IF 발동불가:0 != 1 && 발동불가:1 != 1
		PRINTL 使用する효과を選択してください
		CALL YN_CHOICE(듀얼리스트,"●벤데드 레버넌트","●벤데드 헬하운드")
	ELSEIF 발동불가:0 != 1
		RESULT = 0
	ELSEIF 발동불가:1 != 1
		RESULT = 1
	ENDIF

	IF RESULT == 0
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51060,1)
		IF RESULT == 0
			CALL FULLRESET
			효과条件召種 = 召種全특수
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"제외")
		ENDIF
	ELSEIF RESULT == 1
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51060,3)
		IF RESULT == 0
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"마함単体",場所,"제외")
		ENDIF
	ENDIF

ELSEIF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51060)
	IF RESULT == 0
		CALL URARA_CHANCE(듀얼리스트,処理中_효과ID,"雑多")
		IF RESULT == 0
			CALL CARDDRAW_BASIC(듀얼리스트,2)
			CALL FULLRESET
			효과範囲손패 = 1
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ENDIF