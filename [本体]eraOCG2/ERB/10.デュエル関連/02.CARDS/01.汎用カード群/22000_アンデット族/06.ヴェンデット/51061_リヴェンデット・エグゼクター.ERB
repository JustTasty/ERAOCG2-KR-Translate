@CARDNAME_51061,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 리벤데드 이그제커터
	CASE "略称"
		RESULTS = RVD.Executor
	CASE "カテゴリ"
		RESULTS:0 = 벤데드
ENDSELECT


@CARD_51061(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 儀式
	CASE "属性"
		RETURN 闇属性
	CASE "種族"
		RETURN アンデット族 
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 0
	CASE "性別"
		RETURN 雄性
	CASE "追加日"
		RETURN 250707
ENDSELECT

RETURN 0
@CARDSUMMON_51061(決闘者,召喚カード,EX参照)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 出処
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 召喚カード
#DIM DYNAMIC EX参照

CALL RESETCONDITION_MAT
必要レベル数 = 8

IF EX参照 == 1
	CALL RITCAN(決闘者,召喚カード,51061) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_51061(種類)
#DIM DYNAMIC 種類

PRINTL 「벤데드」儀式魔法カードにより降臨。
PRINTL このカード名の③の効果は１ターンに１度しか使用できない。
CALL CARD_ABLE(0,永続効果)
PRINTL ①：このカードはモンスターゾーンに存在する限り、カード名を「리벤데드 슬레이어」として扱う。
CALL CARD_ABLE(0,永続効果)
PRINTL ②：儀式召喚したこのカードがモンスターゾーンに存在する限り、自分フィールドの他のカードを相手は効果の対象にできない。
CALL CARD_ABLE(0,戦破壊→墓地)
PRINTL ③：フィールドのこのカードが戦闘・効果で破壊された場合に発動できる。
PRINTL デッキから「벤데드」カード１枚を手札に加える。


@CARDCAN_51061(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 発動不可,2

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,51061)
SIF RESULT == 1
	RETURN 0

IF 種類 == 戦破壊→墓地
	SIF 発動済_3(決闘者,51061)
		RETURN 0
	SIF !GROUPMATCH(墓地送り種類,"戦闘破壊","破壊")
		RETURN 0
	CALL FULLRESET
	効果範囲デッキ = 1
	効果条件カテゴリ = 벤데드
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 種類 == 永続効果
	SIF ゾーン != "戦場"
		RETURN 0
	RETURN 1

ELSEIF 種類 == 戦場起動 || 種類 == フリー誘発
	SIF ゾーン != "戦場"
		RETURN 0
	SIF GETVAR(@"%決闘者%_除外NG置換") >= 1
		RETURN 0

	;レヴナント
	SIF GETBIT(GETVAR(@"%決闘者%_MZ_汎数:場所"),1) != 1
		発動不可:0 = 1
	SIF 発動済_1(決闘者,51061)
		発動不可:0 = 1
	CALL FULLRESET
	効果範囲相手戦場 = 1
	効果条件召種 = 召種全特殊
	効果条件除去種 = 除外
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		発動不可:0 = 1

	;ヘルハン
	SIF GETBIT(GETVAR(@"%決闘者%_MZ_汎数:場所"),2) != 1
		発動不可:1 = 1
	SIF 発動済_2(決闘者,51061)
		発動不可:1 = 1
	CALL FULLRESET
	効果範囲相手魔罠 = 1
	効果条件除去種 = 除外
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		発動不可:1 = 1

	SIF 発動不可:0 == 1 && 発動不可:1 == 1
		RETURN 0
	RETURN 1

;ストリゲスの付与効果
ELSEIF 種類 == 攻撃後誘発
	SIF ゾーン != "戦場"
		RETURN 0
	SIF 戦闘場所(決闘者) != 場所
		RETURN 0
	SIF 戦闘場所(決闘者,1) == 5
		RETURN 0
	SIF GETBIT(GETVAR(@"%決闘者%_MZ_汎数:場所"),3) != 1
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1
ENDIF

@CARDEFFECT_51061(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 発動不可,2
#DIMS DYNAMIC 対面者

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 戦破壊→墓地
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,51061,3)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲デッキ = 1
		効果条件カテゴリ = 벤데드
		CALL CARD_ADD_ADV(決闘者,ゾーン,場所)
	ENDIF

ELSEIF 種類 == 永続効果
	CALL STATCHANGE_EFFECT(決闘者,"自身",場所,"変化元ID",51060)
	IF GETVAR(@"%決闘者%_MZ_召種:場所") == 召種EXデッキ
		CALL FULLRESET
		効果条件他所 = 1
		CALL STATCHANGE_EFFECT(決闘者,"戦場",場所,"対耐")
		CALL STATCHANGE_EFFECT(決闘者,"魔罠",場所,"対耐")
	ENDIF


ELSEIF 種類 == 戦場起動 || 種類 == フリー誘発
	SIF GETBIT(GETVAR(@"%決闘者%_MZ_汎数:場所"),1) != 1
		発動不可:0 = 1
	SIF 発動済_1(決闘者,51061)
		発動不可:0 = 1
	CALL FULLRESET
	効果範囲相手戦場 = 1
	効果条件召種 = 召種全特殊
	効果条件除去種 = 除外
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		発動不可:0 = 1

	SIF GETBIT(GETVAR(@"%決闘者%_MZ_汎数:場所"),2) != 1
		発動不可:1 = 1
	SIF 発動済_2(決闘者,51061)
		発動不可:1 = 1
	CALL FULLRESET
	効果範囲相手魔罠 = 1
	効果条件除去種 = 除外
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		発動不可:1 = 1

	IF 発動不可:0 != 1 && 発動不可:1 != 1
		PRINTL 使用する効果を選択してください
		CALL YN_CHOICE(決闘者,"●벤데드 레버넌트","●벤데드 헬하운드")
	ELSEIF 発動不可:0 != 1
		RESULT = 0
	ELSEIF 発動不可:1 != 1
		RESULT = 1
	ENDIF

	IF RESULT == 0
		CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,51061,1)
		IF RESULT == 0
			CALL FULLRESET
			効果条件召種 = 召種全特殊
			CALL CARD_REMOVAL_BASIC(決闘者,"戦場単体",場所,"除外")
		ENDIF
	ELSEIF RESULT == 1
		CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,51061,2)
		IF RESULT == 0
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(決闘者,"魔罠単体",場所,"除外")
		ENDIF
	ENDIF

ELSEIF 種類 == 攻撃後誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,51061)
	IF RESULT == 0
		CALL URARA_CHANCE(決闘者,処理中_効果ID,"雑多")
		IF RESULT == 0
			CALL CARDDRAW_BASIC(決闘者,2)
			CALL FULLRESET
			効果範囲手札 = 1
			CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
		ENDIF
	ENDIF
ENDIF