@CARDNAME_92281,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 벤데드 데이브레이크
	CASE "略称"
		RESULTS = VD.Daybreak
	CASE "カテゴリ"
		RESULTS:0 = 벤데드
ENDSELECT


@CARD_92281(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 通常罠
	CASE "追加日"
		RETURN 250707
ENDSELECT

RETURN 0

@CARD_EXPLANATION_92281(種類)
#DIM DYNAMIC 種類

PRINTL このカード名の①の効果は１ターンに１度しか使用できない。
PRINTL このカードは自分の墓地の「벤데드」儀式モンスター３種類を除外し、セットしたターンに発動できる。
CALLFORM CARD_ABLE(1,魔罠起動,フリー誘発)
PRINTL ①：相手フィールドのカードの数が自分フィールドのカードより多い場合に発動できる。
PRINTL 自分フィールドの「벤데드」儀式モンスター１体を選び、
PRINTL そのモンスター以外のフィールドのカードを全て破壊する。


@CARDCAN_92281(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,92281)
SIF RESULT == 1
	RETURN 0

IF 種類 == 魔罠起動 || 種類 == フリー誘発
	SIF ゾーン != "魔罠"
		RETURN 0
	SIF 発動済_1(決闘者,92281)
		RETURN 0

	IF GETVAR(@"%決闘者%_魔_表:場所") == 3
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件カテゴリ = 벤데드
		効果条件種類 = 儀式
		効果条件固名前起動 = 1
		効果条件CNT = 1
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT >= 3 && GETVAR(@"%決闘者%_除外NG置換") < 1
			RETURN 1
	ELSEIF GETVAR(@"%決闘者%_魔_表:場所") == 1
		RETURN 0
	ENDIF

	SIF モンスター数(決闘者) + 魔罠数(決闘者) >= モンスター数(対面者) + 魔罠数(対面者)
		RETURN 0

	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件カテゴリ = 벤데드
	効果条件種類 = 儀式
	効果条件表示 = 表側表示
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_92281(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 魔罠起動 || 種類 == フリー誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,92281,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所)
		IF GETVAR(@"%決闘者%_魔_表:場所") == 3
			CALL FULLRESET
			効果範囲墓地 = 1
			効果条件カテゴリ = 벤데드
			効果条件種類 = 儀式
			効果条件行先フラグ = 除外行き
			効果条件固名前選択 = 1
			効果条件複数 = 3
			CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
			SIF RESULT != 1
				RETURN 0
		ENDIF
		CALL FULLRESET
		効果条件カテゴリ = 벤데드
		効果条件種類 = 儀式
		効果条件表示 = 表側表示
		CALL CARD_CHOOSE_BASIC(決闘者,"戦場",ゾーン,場所)
		IF RESULT != 9999
			選択カード = RESULT
			CALL FULLRESET
			効果条件他所 = 1
			CALL CARD_REMOVAL_BASIC(決闘者,"両全場全体",選択カード,"墓地")
		ENDIF
	ENDIF
ENDIF

