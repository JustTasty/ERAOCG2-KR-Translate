@CARDNAME_92281,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 벤데드 데이브레이크
	CASE "略称"
		RESULTS = VD.Daybreak
	CASE "カテゴリ"
		RESULTS:0 = 벤데드
ENDSELECT


@CARD_92281(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
	CASE "추가日"
		RETURN 250707
ENDSELECT

RETURN 0

@CARD_EXPLANATION_92281(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①의 효과는 １턴에 １번밖에 사용할 수 없다。
PRINTL 이 카드는 자신 묘지의 "벤데드"의식 몬스터 ３종류를 제외하고、세트한 턴에 발동할 수 있다。
CALLFORM CARD_ABLE(1,마함起動,フリー誘発)
PRINTL ①：상대 필드의 카드의 수가 자신 필드의 카드보다 많은 경우에 발동할 수 있다。
PRINTL 자신 필드의 "벤데드" 의식 몬스터 １장을 골라、
PRINTL 그 몬스터 이외의 필드의 카드를 전부 파괴한다。


@CARDCAN_92281(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,92281)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == フリー誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF 발동済_1(듀얼리스트,92281)
		RETURN 0

	IF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 벤데드
		효과条件종류 = 의식
		효과条件固名前起動 = 1
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT >= 3 && GETVAR(@"%듀얼리스트%_제외NG置換") < 1
			RETURN 1
	ELSEIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
		RETURN 0
	ENDIF

	SIF モンスター数(듀얼리스트) + 마함数(듀얼리스트) >= モンスター数(対面者) + 마함数(対面者)
		RETURN 0

	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 벤데드
	효과条件종류 = 의식
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_92281(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,92281,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		IF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件カテゴリ = 벤데드
			효과条件종류 = 의식
			효과条件行先플래그 = 제외行き
			효과条件固名前選択 = 1
			효과条件複数 = 3
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			SIF RESULT != 1
				RETURN 0
		ENDIF
		CALL FULLRESET
		효과条件カテゴリ = 벤데드
		효과条件종류 = 의식
		효과条件표시 = 表側표시
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		IF RESULT != 9999
			選択カード = RESULT
			CALL FULLRESET
			효과条件他所 = 1
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"両全場全体",選択カード,"묘지")
		ENDIF
	ENDIF
ENDIF

