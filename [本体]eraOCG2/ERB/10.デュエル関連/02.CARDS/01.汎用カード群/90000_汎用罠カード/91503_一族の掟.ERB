@CARDNAME_91503,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 일족의 법도
	CASE "略称"
		RESULTS = 일족의 법도
	CASE "カテゴリ"
		RESULTS:0 = 없음
ENDSELECT


@CARD_91503(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 永続罠
	CASE "追加日"
		RETURN 250709
ENDSELECT

RETURN 0

@CARD_EXPLANATION_91503(種類)
#DIM DYNAMIC 種類

CALLFORM CARD_ABLE(0,魔罠起動 ,フリー誘発)
PRINTL ①：種族を１つ宣言して発動できる。
PRINTL 宣言した種族のフィールドのモンスターは攻撃および効果の発動を行えない。
CALLFORM CARD_ABLE(0,ターン開始時強制)
PRINTL ②：自分スタンバイフェイズに、自分フィールドのモンスター１体をリリースできる。
PRINTL リリースしなかった場合、このカードは破壊される。

@CARDCAN_91503(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,91503)
SIF RESULT == 1
	RETURN 0

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 魔罠起動 || 種類 == フリー誘発
	SIF ゾーン != "魔罠"
		RETURN 0
	SIF GETVAR(@"%決闘者%_魔_表:場所") == 3	
		RETURN 0
	SIF GETVAR(@"%決闘者%_魔_表:場所") == 1
		RETURN 0
	RETURN 1

ELSEIF 種類 == ターン開始時強制
	SIF ゾーン != "魔罠"
		RETURN 0
	SIF GETVAR(@"%決闘者%_魔_表:場所") != 1
		RETURN 0
	RETURN 1

ELSEIF 種類 == 永続効果
	SIF ゾーン != "魔罠"
		RETURN 0
	SIF GETVAR(@"%決闘者%_魔_表:場所") != 1
		RETURN 0
	SIF GETVAR(@"%決闘者%_魔_汎数:場所") < 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_91503(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 宣言種族
#DIM DYNAMIC 最多モンスター数

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 魔罠起動 || 種類 == フリー誘発
	;カード発動＆設置まで
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,91503)
	IF RESULT == 0
		IF 決闘者 == "自分"
			PRINTL 種族を宣言してください
			PRINTL
			PRINT [01] - 魔法使い族　
			PRINT [02] - ドラゴン族　
			PRINTL [03] - アンデット族
			PRINT [04] - 戦士族　　　
			PRINT [05] - 機械族　　　
			PRINTL [06] - 獣族
			PRINT [07] - 鳥獣族　　　
			PRINT [08] - 鱗獣族　　　
			PRINTL [09] - 悪魔族
			PRINT [10] - 天使族　　　
			PRINT [11] - 昆虫族　　　
			PRINTL [12] - 元素霊族
			PRINT [13] - 植物族　　　
			PRINT [14] - サイキック族
			PRINTL [15] - サイバース族
			PRINT [16] - 海生族　　　
			PRINT [17] - 未確認族　　
			PRINTL [20] - 幻神獣族
			SETCOLOR C_D_GRAY
			PRINTL [21] - 나체족
			RESETCOLOR
			BINPUT
			宣言種族 = RESULT
		ELSE
			FOR カウンタ,1,21
				CALL FULLRESET
				効果範囲相手戦場 = 1
				効果条件表示 = 表側表示
				効果条件種族 = カウンタ
				効果条件CNT = 1
				CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
				IF 最多モンスター数 < RESULT
					最多モンスター数 = RESULT
					宣言種族 = カウンタ
				ENDIF
			NEXT
			IF 宣言種族 == 0
				宣言種族 = RAND:17
			ENDIF
		ENDIF
		PRINTL
		IF 宣言種族 == 21
			PRINTFORML %決闘者%は裸族を宣言しました
		ELSE
			PRINTFORML %決闘者%は%GET_TALENTNAME(191,宣言種族)%を宣言しました
		ENDIF
		CALL SET_FIELD_INFO(決闘者,"魔罠",場所,"汎数",宣言種族)
	ENDIF

ELSEIF 種類 == ターン開始時強制
	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件任意 = 1
	CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
	SIF RESULT != 1
		CALL CARD_DISPOSE(決闘者,ゾーン,場所,破壊行き)
	
ELSEIF 種類 == 永続効果
	宣言種族 = GETVAR(@"%決闘者%_魔_汎数:場所")
	IF 宣言種族 == 21
		FOR カウンタ,0,5
			SIF GETVAR(@"%決闘者%_戦場:カウンタ") == -1
				CONTINUE
			SIF GETVAR(@"%決闘者%_MZ_表:カウンタ") == 3
				CONTINUE
			SIF GETVAR(@"%決闘者%_MZ_服状:カウンタ") < 2
				CONTINUE
			CALL STATCHANGE_EFFECT(決闘者,"自身",カウンタ,"戦無")
			CALL STATCHANGE_EFFECT(決闘者,"自身",カウンタ,"発無")
		NEXT
		FOR カウンタ,0,5
			SIF GETVAR(@"%対面者%_戦場:カウンタ") == -1
				CONTINUE
			SIF GETVAR(@"%対面者%_MZ_服状:カウンタ") < 2
				CONTINUE
			CALL STATCHANGE_EFFECT(決闘者,"対面自身",カウンタ,"戦無",,91503)
			CALL STATCHANGE_EFFECT(決闘者,"対面自身",カウンタ,"発無",,91503)
		NEXT
	ELSE
		CALL FULLRESET
		効果条件種族 = 宣言種族
		CALL STATCHANGE_EFFECT(決闘者,"戦場",場所,"戦無")
		CALL STATCHANGE_EFFECT(決闘者,"戦場",場所,"発無")
		CALL STATCHANGE_EFFECT(決闘者,"相手戦場",場所,"戦無",,91503)
		CALL STATCHANGE_EFFECT(決闘者,"相手戦場",場所,"発無",,91503)
	ENDIF
ENDIF

