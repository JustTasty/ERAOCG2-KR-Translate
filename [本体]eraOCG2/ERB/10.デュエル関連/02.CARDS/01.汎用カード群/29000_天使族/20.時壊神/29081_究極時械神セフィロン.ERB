@CARDNAME_29081,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 究極時械神セフィロン
	CASE "略称"
		RESULTS = セフィロン
	CASE "カテゴリ"
		RESULTS:0 = 時械神
ENDSELECT


@CARD_29081(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 4000
	CASE "守備力"
		RETURN 4000
	CASE "추가日"
		RETURN 250630
	CASE "소환불가"
		RETURN 1
	CASE "특수불가"
		RETURN 1
ENDSELECT
RETURN 0

@CARD_EXPLANATION_29081(종류)
#DIM DYNAMIC 종류

PRINTL このカードは通常소환できない。
CALLFORM CARD_ABLE(0,손패起動)
PRINTL 자신フィールドに「無限光アイン・ソフ・オウル」が存在する場合のみ손패から특수소환できる。
CALLFORM CARD_ABLE(10,필드起動)
PRINTL ①：１ターンに１度、자신の손패・덱・묘지から攻撃力０の「時械神」モンスターを
PRINTL 攻撃力・守備力４０００にして可能な限り특수소환できる。
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ②：このカードの攻撃力は、このカードを除く자신フィールドの「時械神」モンスターの攻撃力の合計となり、
PRINTL このカードの戦闘で発生する자신への戦闘ダメージは０になる。
CALLFORM CARD_ABLE(0,身代효과)
PRINTL ③：モンスターゾーンのこのカードが破壊される場合、代わりに자신フィールドの「時械神」モンスター１体を제외できる。



@CARDCAN_29081(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 選択カード名前
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,29081)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,29081,ゾーン)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件名前 = 無限光アイン・ソフ・オウル
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲덱 = 1
	효과範囲묘지 = 1
	효과条件攻撃力 = 0
	효과条件カテゴリ = 時械神
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃時強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	RETURN 1
ELSEIF 종류 == 身代효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	SIF 破壊대상ゾーン != "필드"
		RETURN 0
	SIF 破壊대상場所 != 場所
		RETURN 0
	SIF !GROUPMATCH(破壊종류,"효과","戦闘")
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件他所 = 1
	효과条件カテゴリ = 時械神
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_29081(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,29081)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,29081,10)
	IF RESULT == 0
		REPEAT 5
			CALL FULLRESET
			효과範囲손패 = 1
			효과範囲덱 = 1
			효과範囲묘지 = 1
			효과条件攻撃力 = 0
			효과条件カテゴリ = 時械神
			효과추가종류:0 = 104000
			효과추가종류:1 = 154000
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			SIF モンスター数(듀얼리스트) >= 5
				BREAK
		REND
	ENDIF
ELSEIF 종류 == 攻守変動
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件他所 = 1
	효과条件カテゴリ = 時械神
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	攻撃力修正 = RESULT:3
	CALL STATCHANGE_BASIC(듀얼리스트,"自身",場所,攻撃力修正)
ELSEIF 종류 == 攻撃時強制誘発
	CALL SET_PLAYER_INFO(듀얼리스트,"ダメージ無効",1)
ELSEIF 종류 == 身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,29081)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 時械神
		효과条件他所 = 1
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"필드",ゾーン,場所)
		SIF RESULT == 1
			RETURN 1
	ENDIF
	RETURN 0
ENDIF

