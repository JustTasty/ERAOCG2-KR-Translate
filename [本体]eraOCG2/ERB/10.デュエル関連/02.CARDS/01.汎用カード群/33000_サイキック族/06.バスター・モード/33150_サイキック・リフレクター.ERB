@CARDNAME_33150,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 사이킥 리플렉터
	CASE "略称"
		RESULTS = Psi리플렉터
	CASE "カテゴリ"
		RESULTS:0 = 버스터
ENDSELECT


@CARD_33150(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 効果モン
	CASE "属性"
		RETURN 闇属性
	CASE "種族"
		RETURN サイキック族
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 400
	CASE "守備力"
		RETURN 300
	CASE "性別"
		RETURN 雄性
	CASE "チューナー"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_33150(種類)
#DIM DYNAMIC 種類

PRINTL このカード名の①②の効果はそれぞれ１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(1,召喚時 ,特殊召喚時)
PRINTL ①：このカードが召喚・特殊召喚した場合に発動できる。
PRINTL このカード名を除く、「버스터」カード１枚をデッキから手札に加える。
CALLFORM CARD_ABLE(2,戦場起動)
PRINTL ②：手札・魔法＆罠ゾーンの「버스터 모드」１枚を相手に見せ、
PRINTL 自分の墓地の「사이킥 리플렉터」以外の「버스터」モンスター１体を対象として発動できる。
PRINTL そのモンスターを特殊召喚し、そのレベルを４つまで上げる。

@CARDCAN_33150(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所


CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,33150)
SIF RESULT == 1
	RETURN 0

IF 種類 == 召喚時 || 種類 == 特殊召喚時
	SIF 発動済_1(決闘者,33150)
		RETURN 0
	CALL FULLRESET
	効果範囲デッキ = 1
	効果条件カテゴリ = 버스터
	効果条件固名前 = 사이킥 리플렉터
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 戦場起動
	SIF 発動済_2(決闘者,33150)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL FULLRESET
	効果範囲手札 = 1
	効果範囲魔罠 = 1
	効果条件表示 = 0
	効果条件名前 = 버스터 모드
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件カテゴリ = 버스터
	効果条件固名前 = 사이킥 리플렉터
	効果条件召喚可 = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_33150(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 設定レベル

IF 種類 == 召喚時 || 種類 == 特殊召喚時
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,33150,1)
	IF RESULT == 0
		CALL FULLRESET
		効果条件カテゴリ = 버스터
		効果条件固名前 = 사이킥 리플렉터
		CALL CARD_ADD_BASIC(決闘者,"デッキ",ゾーン,場所)
	ENDIF
ELSEIF 種類 == 戦場起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,33150,2)
	IF RESULT == 0

		CALL FULLRESET
		効果範囲手札 = 1
		効果範囲魔罠 = 1
		効果条件名前 = 버스터 모드
		CALL CARD_CHOOSE_ADV(決闘者,ゾーン,場所)
		SIF RESULT == 9999
			RETURN 0

		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件カテゴリ = 버스터
		効果条件固名前 = 사이킥 리플렉터
;		効果追加種類 = 140 + 設定レベル
		CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
		IF RESULT == 1
			IF 決闘者 == "自分"
				PRINTFORML レベルを上げる：
				PRINTFORML [0] [1] [2] [3] [4]
				BINPUT
				設定レベル = RESULT
			ELSEIF 決闘者 == "相手"
				設定レベル = 3
			ENDIF
			CALL SET_FIELD_INFO(決闘者,"戦場",カード召喚先,"永星",設定レベル)
		ENDIF
	ENDIF
ENDIF