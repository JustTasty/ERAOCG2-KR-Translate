@CARDNAME_73581,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 헤카톤케일 타르타로스
	CASE "略称"
		RESULTS = 타르타로스
	CASE "カテゴリ"
		RESULTS:0 = 헤카톤케일
ENDSELECT


@CARD_73581(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 速攻마법
	CASE "レア度"
		RETURN RARE
	CASE "추가日"
		RETURN 250823
ENDSELECT

RETURN 0

@CARD_EXPLANATION_73581(종류)
#DIM DYNAMIC 종류

PRINTL ①：이하의 효과에서 １개를 선택해서 적용한다。
PRINTL （이 카드명의 이하의 효과는 각각 １턴에 １번밖에 실행할 수 없다）
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ●자신 필드에 "헤카톤케일" 몬스터가 존재하는 경우、
PRINTL 상대 필드의 마법 / 함정 카드 １장을 패에 넣는다。
CALLFORM CARD_ABLE(2,마함起動,손패起動)
PRINTL ●자신 필드의 원래 주인이 상대인 몬스터 전부를 "헤카톤케일" 몬스터로서 취급한다。
PRINTL 그 후、자신의 필드 / 묘지의 몬스터를 융합소재로 하고 제외하고、
PRINTL "헤카톤케일" 융합몬스터 １장을 융합 소환한다。


@CARDCAN_73581(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%



CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,73581)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	SIF !GROUPMATCH(ゾーン,"손패","마함")
		RETURN 0
	IF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 2
			RETURN 0
	ELSEIF ゾーン == "손패"
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
	ENDIF
	IF 발동済_1(듀얼리스트,73581) == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = 헤카톤케일
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲상대마함 = 1
			효과条件除去種 = 손패
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				条件달성++
		ENDIF
	ENDIF
	IF 발동済_2(듀얼리스트,73581) == 0 && GETVAR(@"%듀얼리스트%_제외NG置換") == 0
		CALL FULLRESET
		효과範囲EX덱 = 1
		효과条件종류 = 융합
		효과条件カテゴリ = 헤카톤케일
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲손패 = 1
			효과範囲필드 = 1
			효과条件종류 = モン全般
			효과条件CNT = 1
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT >= 2
				条件달성++
		ENDIF
	ENDIF
	SIF 条件달성 == 0
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_73581(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성1
#DIM DYNAMIC 条件달성2
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,73581)
	IF RESULT == 0
		IF 발동済_1(듀얼리스트,73581) == 0
			CALL FULLRESET
			효과範囲필드 = 1
			효과条件カテゴリ = 헤카톤케일
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				효과範囲상대마함 = 1
				효과条件除去種 = 손패
				CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
				SIF RESULT == 1
					条件달성1 = 1
			ENDIF
		ENDIF
		IF 발동済_2(듀얼리스트,73581) == 0 && GETVAR(@"%듀얼리스트%_제외NG置換") == 0
			CALL FULLRESET
			효과範囲EX덱 = 1
			효과条件종류 = 융합
			효과条件カテゴリ = 헤카톤케일
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				효과範囲손패 = 1
				효과範囲필드 = 1
				효과条件종류 = モン全般
				효과条件CNT = 1
				CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
				SIF RESULT >= 2
					条件달성2 = 1
			ENDIF
		ENDIF
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		IF 条件달성1 == 1 && 条件달성2 == 1
			PRINTFORML
			PRINTFORML 어느 효과를 사용하시겠습니까？
			PRINTFORML
			PRINTFORML [1] - 상대 필드의 마법 / 함정 카드 １장을 패로 되돌린다。
			PRINTFORML [2] - "헤카톤케일" 융합 몬스터 １장을 융합 소환한다。
			BINPUT
		ELSEIF 条件달성1 == 1 && 条件달성2 == 0
			RESULT = 1
		ELSEIF 条件달성1 == 0 && 条件달성2 == 1
			RESULT = 2
		ENDIF
		IF RESULT == 1
			CALL CARD_EFFECT_USE(듀얼리스트,73581,1)
			CALL FULLRESET
			효과範囲상대마함 = 1
			CALL CARD_REMOVAL_ADV(듀얼리스트,"単体除去",場所,"손패")
		ELSEIF RESULT == 2
			CALL CARD_EFFECT_USE(듀얼리스트,73581,2)
			;자신フィールドの元々の持ち主が상대であるモンスター全てを「헤카톤케일」モンスターとして扱う。
			FOR カウンタ,0,5
				SIF GETVAR(@"%듀얼리스트%_필드:カウンタ") == -1
					CONTINUE
				SIF GETVAR(@"%듀얼리스트%_MZ_表:カウンタ") == 3
					CONTINUE
				IF 듀얼리스트 == "자신"
					SIF GETVAR(@"%듀얼리스트%_MZ_所有:カウンタ") == 1
						CONTINUE
				ELSEIF 듀얼리스트 == "상대"
					SIF GETVAR(@"%듀얼리스트%_MZ_所有:カウンタ") == 2
						CONTINUE
				ENDIF
				IF GETVARS(@"%듀얼리스트%_MZ_追カテ:カウンタ") == ""
					CALL CARD_SET_CATEGORY(듀얼리스트,"필드",カウンタ,"헤카톤케일")
				ELSE
					SETVAR @"%듀얼리스트%_MZ_追カテ2:カウンタ", "헤카톤케일"
				ENDIF
			NEXT
			CALL RESETAREA_MAT
			素材範囲필드 = 1
			素材範囲묘지 = 1
			CALL FULLRESET
			효과条件종류 = 융합
			효과条件カテゴリ = 헤카톤케일
			효과条件소환可 = 1
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
				CALL FUSSUMMON(듀얼리스트,選択カード,제외行き)
			ENDIF
		ENDIF

	ENDIF
ENDIF
