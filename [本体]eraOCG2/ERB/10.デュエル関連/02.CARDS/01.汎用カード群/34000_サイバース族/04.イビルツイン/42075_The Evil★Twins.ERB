@CARDNAME_42075,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = The Evil★Twins！키스킬 & 리일라
	CASE "略称"
		RESULTS = Evil★Twins

	CASE "カテゴリ"
		RESULTS:0 = Evil★Twin
		RESULTS:1 = 키스킬
		RESULTS:2 = 리일라
		RESULTS:3 = 게임 오리지널
ENDSELECT


@CARD_42075(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 링크
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 2200
	CASE "守備力"
		RETURN 0
	CASE "특수불가"
		RETURN 1
	CASE "성별"
		RETURN 牝性
	CASE "連動ID"
;	RETURN 2075
		SELECTCASE RAND:2
			CASE 0
				RESULT:1 = 2076
				RETURN 2075
			CASE 1
				RESULT:1 = 2075
				RETURN 2076
		ENDSELECT
ENDSELECT

RETURN 0



@CARD_EXPLANATION_42075(종류)
#DIM DYNAMIC 종류

PRINTL "Evil★Twin" 몬스터를 포함한 몬스터 2장 이상
PRINTL 이 카드는 링크 소환 이외의 방법으로는 특수 소환할 수 없다。
PRINTL 이 카드명의  ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.。
CALLFORM CARD_ABLE(1,필드起動,フリー誘発)
PRINTL ①：자신 / 상대 턴에, 이카드를 릴리스하고 발동할 수 있다。
PRINTL 자신 묘지에서 "키스킬" 몬스터 와 "리일라" 몬스터를,
PRINTL 각각 1장까지 선택해 특수 소환한다。

CALLFORM CARD_ABLE(2,특수소환時,EX소환時)
PRINTL ②：이 카드를 특수 소환했을 경우에 발동할 수 있다。
PRINTL 상대의 덱의 맨 위 카드를 확인하고 그 종류에따라 다음과 같은 효과를 적용한다。
PRINTL 그 후 확인한 카드를 제외한다。
PRINTL ● 몬스터：상대의 패를 무작위 1장을 골라 파괴한다。
PRINTL ● 마법：자신은 덱에서 1장 드로우한다。
PRINTL ● 함정：이 카드는 수비표시가 되며, 턴 종료시까지 효과를 발동할 수 없다。


@CARDSUMMON_42075(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要링크数 = 4

IF EX参照 == 0 || EX参照 == 1
	素材条件カテゴリ = Evil★Twin
ENDIF

IF EX参照 == 1
	CALL LINCAN(듀얼리스트,소환カード,42075) 
	RETURN RESULT
ENDIF



@CARDCAN_42075(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,42075)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_1(듀얼리스트,42075)
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ:0 = 리일라
	효과条件カテゴリ:1 = 키스킬
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	RETURN 1

ELSEIF 종류 == 특수소환時 || 종류 == EX소환時
	SIF 발동済_2(듀얼리스트,42075)
		RETURN 0
	SIF 現山札数(対面者) == 0
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1
ENDIF

@CARDEFFECT_42075(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 효과用소환先
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード,2
#DIM DYNAMIC 選択カード_ID


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 특수소환時 || 종류 == EX소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,42075,2)
	IF RESULT == 0
		TRYCALLFORM CARD_UI(GETVAR(@"%対面者%_덱:0"))
		PRINTW
		CALLFORM CARD_{GETVAR(@"%対面者%_덱:0")}, "종류"
		IF RESULT < 10
			;モンスター효과
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"손패破壊",場所,"묘지")
		ELSEIF RESULT > 30
			;トラップ효과
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"無効",1,1)
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"表",2,1)
		ELSE
			CALL CARDDRAW_BASIC(듀얼리스트,1)
		ENDIF
		CALL CARD_DISPOSE(対面者,"덱",0,제외行き)
	ENDIF


ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,42075,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 리일라
		효과条件任意 = 1
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)

		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 키스킬
		효과条件任意 = 1
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF


