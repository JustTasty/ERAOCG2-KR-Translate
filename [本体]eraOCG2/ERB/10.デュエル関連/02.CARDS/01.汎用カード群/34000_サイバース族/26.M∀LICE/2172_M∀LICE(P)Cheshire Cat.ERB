@CARDNAME_2172,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = M∀LICE＜P＞Cheshire Cat
	CASE "略称"
		RESULTS = 체셔 캣
	CASE "カテゴリ"
		RESULTS:0 = 맬리스
ENDSELECT


@CARD_2172(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 사이버스족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 1500
	CASE "守備力"
		RETURN 300
	CASE "성별"
		RETURN 牝性
ENDSELECT
RETURN 0

@CARD_EXPLANATION_2172(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①③의 효과는 각각 1턴에 1번 밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,필드起動)
PRINTL ①：자신 메인 페이즈에 발동할 수 있다.
PRINTL 패로부터 "M∀LICE" 1장을 선택해 제외한다.
PRINTL 그 후, 자신은 2장 드로우할 수 있다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ②：자신 필드에 존재하는 "M∀LICE" L 몬스터가 전투로 파괴한 몬스터는 묘지로 가지 않고 제외된다.
CALLFORM CARD_ABLE(3,제외時)
PRINTL ③：이 카드가 제외되었을 경우, 300 LP를 지불하고 발동할 수 있다.
PRINTL 이 카드를 특수 소환한다.
PRINTL 이 턴, 자신은 L 몬스터 밖에 EX덱으로부터 특수 소환할 수 없다.


@CARDSUMMON_AA_2172


@CARDCAN_2172(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,2172)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動
	SIF 발동済_1(듀얼리스트,2172)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件カテゴリ = 맬리스
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 제외時
	SIF 발동済_3(듀얼리스트,2172)
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 300
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,2172,"제외")
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_2172(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードID

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,2172,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件行先플래그 = 제외行き
		효과条件カテゴリ = 맬리스
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			PRINTL 덱으로부터 2장 드로우 합니까?
			CALL YN_CHOICE(듀얼리스트)
			IF RESULT == 0
				CALL CARDDRAW_BASIC(듀얼리스트,2)
			ELSE
				PRINTW 드로우 하지 않았습니다.
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	효과条件カテゴリ = 맬리스
	효과条件종류 = 링크
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"戦除")
ELSEIF 종류 == 제외時
	CALL CARD_CHOICE(듀얼리스트,종류,"제외",場所,2172,3)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",300)
		CALL FULLRESET
		효과範囲제외 = 1
		효과条件名前 = M∀LICE＜P＞Cheshire Cat
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)

		EX소환可能종류 = 링크
		CALL CARD_EX_RESTRICTION(듀얼리스트)
	ENDIF
ENDIF

