@CARDNAME_24322,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 그랜드 코어
	CASE "略称"
		RESULTS = 그랜드 코어
	CASE "カテゴリ"
		RESULTS:0 = 기황
ENDSELECT


@CARD_24322(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
	CASE "맥시멈"
		RETURN 124235
	CASE "추가日"
		RETURN 250802
ENDSELECT
RETURN 0

@CARD_EXPLANATION_24322(종류)
#DIM DYNAMIC 종류

PRINTL 【MAXIMUM！！】기계족 몬스터×２
PRINTL 패에 존재하는 다른 기계족 몬스터 ２장을 덮어씌워、맥시멈 소환한다。
PRINTL 맥시멈 소환은 무효화할 수 없다。
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ①：이 카드는 전투 내성을 갖는다。
CALLFORM CARD_ABLE(0,戦破壊→묘지)
PRINTL ②：필드의 이 카드가 효과로 파괴되었을 경우 발동할 수 있다。
PRINTL 게임 밖에서 "그랜드 파츠" 2장을 패에 넣고, 묘지 / 제외 상태인 "그랜드 코어" 1장을 패에 넣는다。
PRINTL 그 후, 맥시멈 소환을 한다。


@CARDSUMMON_24322(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 2
素材条件종족 = 기계족

IF EX参照 == 1
	CALL MAXCAN(듀얼리스트,소환カード,24322)
	RETURN RESULT

ENDIF

@CARDCAN_24322(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,24322)
SIF RESULT == 1
	RETURN 0

IF 종류 == 戦破壊→묘지
	SIF !GROUPMATCH(묘지送り종류,"破壊")
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_24322(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 候補カードID,紙束最大値


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 戦破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24322)
	IF RESULT == 0
		CALL CARD_GENERATE_HAND(듀얼리스트,224235)
		CALL CARD_GENERATE_HAND(듀얼리스트,224235)

		CALL FULLRESET
		효과範囲묘지 = 1
		효과範囲제외 = 1
		효과条件名前 = 그랜드 코어
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			VARSET 候補カードID,-1
			FOR カウンタ, 0, 紙束最大値
				選択カードID = GETVAR(@"%듀얼리스트%_손패:カウンタ")
				SIF 選択カードID == -1
					BREAK
				TRYCALLFORM CARD_{選択カードID}("맥시멈")
				SIF RESULT < 1
					CONTINUE
				TRYCALLFORM CARDSUMMON_{選択カードID}(듀얼리스트,カウンタ,1)
				IF RESULT >= 1
					候補カードID:カウンタB = 選択カードID
					カウンタB++
					SIF カウンタB >= VARSIZE("효과条件ID")
						BREAK
				ENDIF
			NEXT
			IF カウンタB > 0
				PRINTL 패에서 맥시멈 소환할 몬스터를 고를 수 있습니다。
				CALL FULLRESET
				효과範囲손패 = 1
				효과条件任意 = 1
				FOR カウンタ, 0, VARSIZE("효과条件ID")
					SIF 候補カードID == -1
						BREAK
					효과条件ID:カウンタ = 候補カードID:カウンタ
				NEXT
				CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
				IF RESULT != 9999
					CALL CARD_SELECT(듀얼리스트,RESULT)
					CALL MAXSUMMON(듀얼리스트,選択カード（CS）,"손패")
				ENDIF
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == 지속효과
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"戦耐")
ENDIF


;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
@CARDNAME_224235,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 그랜드 파츠
	CASE "略称"
		RESULTS = 그랜드 파츠
	CASE "カテゴリ"
		RESULTS:0 = 기황
ENDSELECT

@CARD_224235(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常モン
	CASE "トークン"
		RETURN 1
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
ENDSELECT


@CARD_EXPLANATION_224235(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드는 "그랜드 코어"의 효과로 생성된다。

@CARDCAN_224235(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

RETURN 0



;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
@CARDNAME_124235,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 기황제 그란엘MAX
	CASE "略称"
		RESULTS = 그란엘MAX！
	CASE "カテゴリ"
		RESULTS:0 = 기황
		RESULTS:1 = 기황제
		RESULTS:2 = 그란엘
	CASE "맥시멈"
		RESULTS = 기황제 코어
ENDSELECT


@CARD_124235(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
ENDSELECT
RETURN 0


@CARD_EXPLANATION_124235(종류)
#DIM DYNAMIC 종류
CALL TEXT_DECORATION("MAXIMUM")
PRINTL 이 카드명의 ②의 효과는 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ①：이 카드가 존재하는 한、자신 필드의 "그란엘" 몬스터、"와이젤" 몬스터、"스키엘" 몬스터는 일시적 파괴 내성을 얻고、
PRINTL 이 카드의 공격력 / 수비력은 자신ＬＰ의 절반 올리고、이 카드의 공격력은 자신의 효과로 장착한 몬스터의 공격력 만큼 올린다。
CALLFORM CARD_ABLE(0,バトル開始時強制)
PRINTL ②：자신 / 상대의 배틀 페이즈 개시시에 발동할 수 있다。
PRINTL 자신의 "A" 몬스터 및 "G" 몬스터의 공격력은 ０이 된다。
CALLFORM CARD_ABLE(1,필드起動,フリー誘発)
PRINTL ③：자신 / 상대 턴에、상대 필드의 Ｓ몬스터 １장을 대상으로 하고 발동할 수 있다。
PRINTL 그 상대 Ｓ몬스터를 이 카드에 장착한다。
CALLFORM CARD_ABLE(10,발동誘発)
PRINTL ④：１턴에 １번、상대가 몬스터 카드의 효과를 발동했을 때에 발동할 수 있다。
PRINTL 상대 필드의 앞면 표시 몬스터 １장의 효과를 턴 종료시까지 무효로 한다。
CALLFORM CARD_ABLE(0,戦破壊→묘지)
PRINTL ⑤：필드의 이 카드가 파괴될 경우에 발동할 수 있다。
PRINTL 덱 / 묘지에서 "기황제 그란엘∞" １장을 특수 소환한다。


@CARDCAN_124235(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,124235)
SIF RESULT == 1
	RETURN 0

IF 종류 == 攻守変動 || 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 발동誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	CALL FULLRESET
	효과条件종류 = モン全般
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件除去種 = 無効
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所,124235)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 맥시멈条件
	CALL CARD_CHECK_MAXX(듀얼리스트,ゾーン,場所,"기황제 파츠")
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == バトル開始時強制
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 戦破壊→묘지
	SIF !GROUPMATCH(묘지送り종류,"破壊","戦闘破壊")
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲묘지 = 1
	효과条件名前 = 기황제 그란엘∞
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_1(듀얼리스트,124235)
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과条件종류 = 싱크로
	효과範囲상대필드 = 1
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ENDIF


@CARDEFFECT_124235(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 攻守変動
	攻撃力修正 = GETVAR(@"ライフ_%듀얼리스트%") / 2
	CALL STATCHANGE_BASIC(듀얼리스트,"自身",場所,攻撃力修正,攻撃力修正)

ELSEIF 종류 == バトル開始時強制
	FOR カウンタ,0,5
		SIF GETVAR(@"%듀얼리스트%_필드:{カウンタ}") == -1
			CONTINUE
		TRYCALLFORM CARDNAME_{GETVAR(@"%듀얼리스트%_필드:{カウンタ}")}, "カテゴリ"
		IF !MATCH(RESULTS, "A")
			SIF !MATCH(RESULTS, "G")
			CONTINUE
		ENDIF 
		攻撃力修正 = GETVAR(@"%듀얼리스트%_MZ_攻:カウンタ")
		CALL SET_FIELD_INFO(듀얼리스트,"필드",カウンタ,"一攻",-攻撃力修正)
	NEXT


ELSEIF 종류 == 지속효과
	CALL FULLRESET
	효과条件カテゴリ = 와이젤
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"一破")

	CALL FULLRESET
	효과条件カテゴリ = 스키엘
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"一破")

	CALL FULLRESET
	효과条件カテゴリ = 그란엘
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"一破")


ELSEIF 종류 == 발동誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,124235,10)
	IF RESULT == 0
		CALL FULLRESET
		효과条件표시 = 表側표시
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"無効")
	ENDIF

ELSEIF 종류 == 戦破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,124235)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과範囲묘지 = 1
		효과条件名前 = 기황제 그란엘∞
		효과条件소환可 = 1
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)	
	ENDIF


ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,124235,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과条件종류 = 싱크로
		효과条件표시 = 表側표시
		CALL CARD_EQUIP_MONSTER_ADV(듀얼리스트,ゾーン,場所,1,124235)
	ENDIF

ENDIF

;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
@CARDNAME_124236,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 그란엘A
	CASE "略称"
		RESULTS = 그란엘A
	CASE "カテゴリ"
		RESULTS:0 = 기황
		RESULTS:1 = 그란엘
		RESULTS:2 = A
	CASE "맥시멈"
		RESULTS = 기황제 파츠
ENDSELECT

@CARD_124236(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 1600
	CASE "守備力"
		RETURN 0
	CASE "パーツ"
		RETURN 1
ENDSELECT
RETURN 0


@CARD_EXPLANATION_124236(종류)
#DIM DYNAMIC 종류

PRINTL （맥시멈 상태인 이 카드는 공격할 수 없다）
CALLFORM CARD_ABLE(10,攻撃時誘発)
PRINTL ①：１턴에 １번、"기황제" 몬스터가 상대 몬스터를 공격한 경우에 발동할 수 있다。
PRINTL 이 턴、자신의 "기황제" 몬스터 １장은 １번 더 몬스터를 공격할 수 있다。
CALLFORM CARD_ABLE(0,필드起動,묘지起動)
PRINTL ②：자신 메인 페이즈에、이 카드가 필드 / 묘지에 존재하고、
PRINTL 필드에 다른 "A" 몬스터가 존재하지 않는 경우에 발동할 수 있다。
PRINTL 이 카드가 필드에 존재한다면、이 카드를 릴리스한다。
PRINTL 패에서 바람속성 몬스터、땅속성 몬스터、어둠속성 몬스터중 어느 쪽이든１장을 버리고、
PRINTL 대응하는 몬스터 １장을 게임 밖에서 특수 소환한다。

@CARDCAN_124236(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,124236)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動 || 종류 == 묘지起動
	SIF メインフェイズ != 1
		RETURN 0

	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = A
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0

	CALL FULLRESET
	효과範囲손패 = 1
	효과条件속성:0 = 바람속성
	효과条件속성:1 = 땅속성
	효과条件속성:2 = 어둠속성
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 맥시멈条件
	SIF ゾーン != "필드"
		RETURN 0
	CALL CARD_CHECK_MAXX(듀얼리스트,ゾーン,場所,"기황제 코어")
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻撃後誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	TRYCALLFORM CARDNAME_{戦闘カードID(듀얼리스트)}("カテゴリ")
	SIF !MATCH(RESULTS,"기황제")
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_124236(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 選択カード名前


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動 || 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,124236)
	IF RESULT == 0
		SIF 종류 == 필드起動
			CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)

		CALL FULLRESET
		효과範囲손패 = 1
		효과条件속성:0 = 바람속성
		효과条件속성:1 = 땅속성
		효과条件속성:2 = 어둠속성
		효과条件종류 = モン全般
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			選択カード_ID = RESULT:1
			IF 選択カード_ID == 24234
				CALL CARD_GENERATE(듀얼리스트,324236)

			ELSE
				TRYCALLFORM CARD_{選択カード_ID},"속성"
				IF RESULT == 바람속성
					CALL CARD_GENERATE(듀얼리스트,124231)
				ELSEIF RESULT == 땅속성
					SIF 종류 == 필드起動
						CALL CARD_GENERATE(듀얼리스트,224236)
					SIF 종류 == 묘지起動
						CALL CARD_GENERATE(듀얼리스트,124236)
				ELSEIF RESULT == 어둠속성
					CALL CARD_GENERATE(듀얼리스트,124321)
				ENDIF
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,124236,10)
	IF RESULT == 0
		CALL FULLRESET
		효과条件표시 = 1
		효과条件カテゴリ = 기황제
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"戦",1,1)
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"戦無",0,1)
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"直無",1,1)
		ENDIF
	ENDIF

ELSEIF 종류 == 맥시멈条件
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"戦無")
ENDIF


;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――

@CARDNAME_224236,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 그란엘A３
	CASE "略称"
		RESULTS = 그란엘A３
	CASE "カテゴリ"
		RESULTS:0 = 기황
		RESULTS:1 = 그란엘
		RESULTS:2 = A
	CASE "맥시멈"
		RESULTS = 기황제 파츠
ENDSELECT


@CARD_224236(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 1800
	CASE "守備力"
		RETURN 500
	CASE "パーツ"
		RETURN 1
ENDSELECT
RETURN 0


@CARD_EXPLANATION_224236(종류)
#DIM DYNAMIC 종류

PRINTL （맥시멈 상태인 이 카드는 공격할 수 없다）
PRINTL ①：이 카드가 맥시멈 상태라면、자신 필드의 "기황제" 몬스터와 Ｓ몬스터는 관통 공격을 얻는다。
CALLFORM CARD_ABLE(10,필드起動,フリー誘発)
PRINTL ②：１턴에 １번、자신 / 상대 배틀 페이즈에 발동할 수 있다。
PRINTL 자신의 마법＆함정 존에 존재하는 Ｓ몬스터 １장을 특수 소환한다。
CALLFORM CARD_ABLE(0,ターン終了時)
PRINTL ③：자신・상대 엔드 페이즈에 발동할 수 있다。
PRINTL 자신 필드의 Ｓ몬스터 １장을 "기황제" 몬스터 １장에 장착한다。
PRINTL 이 효과로 몬스터를 장착하고 있는 한、그 공격력 만큼、장착 몬스터의 공격력을 올린다。


@CARDCAN_224236(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,224236)
SIF RESULT == 1
	RETURN 0

IF 종류 == 맥시멈条件
	SIF ゾーン != "필드"
		RETURN 0

	CALL CARD_CHECK_MAXX(듀얼리스트,ゾーン,場所,"기황제 코어")
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF バトルフェイズ != 1
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件종류 = 싱크로
	효과条件표시 = 表側표시
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == ターン終了時
	SIF ゾーン != "필드"
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과条件カテゴリ = 기황제
	효과範囲필드 = 1
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과条件종류 = 싱크로
	효과範囲필드 = 1
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_224236(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択場所

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 맥시멈条件
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"戦無")
	CALL FULLRESET
	효과条件カテゴリ = 기황제
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"貫通")
	CALL FULLRESET
	효과条件종류 = 싱크로
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"貫通")

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,224236,10)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件종류 = 싱크로
		효과条件표시 = 表側표시
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == ターン終了時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,224236)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 기황제
		효과条件표시 = 表側표시
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所,224236)
		選択場所 = RESULT
		IF 選択場所 != 9999
			CALL FULLRESET
			효과範囲필드 = 1
			효과条件표시 = 表側표시
			효과条件종류 = 싱크로
			CALL CARD_EQUIP_MONSTER_ADV(듀얼리스트,ゾーン,選択場所,1,224236)
		ENDIF
	ENDIF
ENDIF

;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――

@CARDNAME_324236,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 기황병장 그란엘 아인A
	CASE "略称"
		RESULTS = 그란엘１A
	CASE "カテゴリ"
		RESULTS:0 = 기황
		RESULTS:1 = 기황병
		RESULTS:2 = 그란엘
		RESULTS:3 = A
	CASE "맥시멈"
		RESULTS = 기황제 파츠
ENDSELECT


@CARD_324236(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1600
	CASE "守備力"
		RETURN 1000
	CASE "パーツ"
		RETURN 1
ENDSELECT
RETURN 0


@CARD_EXPLANATION_324236(종류)
#DIM DYNAMIC 종류

PRINTL （맥시멈 상태인 이 카드는 공격할 수 없다）
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ①：이 카드의 공격력은、이 카드 이외의 필드의 기계족 몬스터의 수×１００ 올린다。
CALLFORM CARD_ABLE(10,필드起動,フリー誘発)
PRINTL ②：자신 / 상대 턴에 １번、묘지 / 제외 상태의 "기황" 카드 ３장을 덱으로 되돌리고、
PRINTL 상대 필드의 앞면 표시 몬스터 １장을 대상으로 하고 발동할 수 있다。
PRINTL 그 몬스터의（표시 형식이 수비 표시인 경우엔 공격 표시가 된다。）、
PRINTL 턴 종료시까지、공격력을 절반으로 하고、효과를 무효로 한다。

@CARDCAN_324236(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,324236)
SIF RESULT == 1
	RETURN 0

IF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 맥시멈条件
	SIF ゾーン != "필드"
		RETURN 0

	CALL CARD_CHECK_MAXX(듀얼리스트,ゾーン,場所,"기황제 코어")
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件除去種 = 無効
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所,324236)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과範囲제외 = 1
	효과条件カテゴリ = 기황
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 3
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_324236(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 攻守変動
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲상대필드 = 1
    효과条件他所 = 1
	효과条件종족 = 기계족
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	攻撃力修正 = RESULT * 100
	CALL STATCHANGE_BASIC(듀얼리스트,"自身",場所,攻撃力修正)

ELSEIF 종류 == 맥시멈条件
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"戦無")

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,324236,10)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과範囲제외 = 1
		효과条件カテゴリ = 기황
		효과条件複数 = 3
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과条件표시 = 表側표시
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"選択")
			選択カード = RESULT:1
			IF 選択カード != 9999
				CALL FULLRESET
				CALL SET_FIELD_INFO(対面者,"필드",選択カード,"表",1,上書きモード)
				CALL FULLRESET
				효과추가종류 = 12
				CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",選択カード,"無効")
			ENDIF
		ENDIF
	ENDIF
ENDIF

;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
@CARDNAME_124237,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 그란엘G
	CASE "略称"
		RESULTS = 그란엘G
	CASE "カテゴリ"
		RESULTS:0 = 기황
		RESULTS:1 = 그란엘
		RESULTS:2 = G
	CASE "맥시멈"
		RESULTS = 기황제 파츠
ENDSELECT

@CARD_124237(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 500
	CASE "守備力"
		RETURN 1000
	CASE "パーツ"
		RETURN 1
ENDSELECT
RETURN 0


@CARD_EXPLANATION_124237(종류)
#DIM DYNAMIC 종류

PRINTL （맥시멈 상태인 이 카드는 공격할 수 없다）
PRINTL ①：이 카드가 맥시멈 상태라면、이하의 효과를 얻는다。
PRINTL ●자신 필드에 Ｓ몬스터가 존재한다：자신 필드의 Ｓ몬스터는 공격 유도를 얻는다。
PRINTL ●자신 필드에 Ｓ몬스터가 존재하지 않는다：이 카드는 공격 유도를 얻는다。
CALLFORM CARD_ABLE(0,身代효과)
PRINTL ②：이 카드가 전투 / 효과로 파괴될 경우、
PRINTL 대신에 패 또는 마법＆함정 존에서 몬스터 카드 １장을 묘지로 보낼 수 있다。
CALLFORM CARD_ABLE(0,필드起動,묘지起動)
PRINTL ③：자신 메인 페이즈에、이 카드가 필드 / 묘지에 존재하고、
PRINTL 필드에 다른 "G" 몬스터가 존재하지 않는 경우에 발동할 수 있다。
PRINTL 이 카드가 필드에 존재한다면、이 카드를 릴리스한다。
PRINTL 패에서 바람속성 몬스터、땅속성 몬스터、어둠속성 몬스터중 어느 쪽이든 １장을 버리고、
PRINTL 대응하는 몬스터 １장을 게임 밖에서 특수 소환한다。

@CARDCAN_124237(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,124237)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動 || 종류 == 묘지起動
	SIF メインフェイズ != 1
		RETURN 0

	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = G
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0

	CALL FULLRESET
	효과範囲손패 = 1
	효과条件속성:0 = 바람속성
	효과条件속성:1 = 땅속성
	효과条件속성:2 = 어둠속성
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 身代효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF 破壊대상ゾーン != "필드"
		RETURN 0
	SIF 破壊대상場所 != 場所
		RETURN 0
	SIF !GROUPMATCH(破壊종류,"효과","戦闘")
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲마함 = 1
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 맥시멈条件
	SIF ゾーン != "필드"
		RETURN 0
	CALL CARD_CHECK_MAXX(듀얼리스트,ゾーン,場所,"기황제 코어")
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_124237(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 選択カード名前


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動 || 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,124237)
	IF RESULT == 0
		SIF 종류 == 필드起動
			CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)

		CALL FULLRESET
		효과範囲손패 = 1
		효과条件속성:0 = 바람속성
		효과条件속성:1 = 땅속성
		효과条件속성:2 = 어둠속성
		효과条件종류 = モン全般
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			選択カード_ID = RESULT:1
			IF 選択カード_ID == 24234
				CALL CARD_GENERATE(듀얼리스트,324237)

			ELSE
				TRYCALLFORM CARD_{選択カード_ID},"속성"
				IF RESULT == 바람속성
					CALL CARD_GENERATE(듀얼리스트,124232)
				ELSEIF RESULT == 땅속성
					SIF 종류 == 필드起動
						CALL CARD_GENERATE(듀얼리스트,224237)
					SIF 종류 == 묘지起動
						CALL CARD_GENERATE(듀얼리스트,124237)
				ELSEIF RESULT == 어둠속성
					CALL CARD_GENERATE(듀얼리스트,124322)
				ENDIF
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == 身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,124237)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲마함 = 1
		효과条件종류 = モン全般
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			RETURN 1
		ENDIF
	ENDIF
	RETURN 0

ELSEIF 종류 == 맥시멈条件
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"戦無")

	CALL FULLRESET
	효과条件종류 = 싱크로
	효과範囲필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT != 1
		CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"攻誘")
	ELSE
		CALL FULLRESET
		효과条件종류 = 싱크로
		CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"攻誘")
	ENDIF
ENDIF


;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
@CARDNAME_224237,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 그란엘G３
	CASE "略称"
		RESULTS = 그란엘G３
	CASE "カテゴリ"
		RESULTS:0 = 기황
		RESULTS:1 = 그란엘
		RESULTS:2 = G
	CASE "맥시멈"
		RESULTS = 기황제 파츠
ENDSELECT

@CARD_224237(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 800
	CASE "守備力"
		RETURN 2000
	CASE "パーツ"
		RETURN 1
ENDSELECT
RETURN 0


@CARD_EXPLANATION_224237(종류)
#DIM DYNAMIC 종류

PRINTL （맥시멈 상태인 이 카드는 공격할 수 없다）
PRINTL ①：이 카드가 맥시멈 상태라면、이하의 효과를 얻는다。
PRINTL ●자신 필드에 Ｓ몬스터가 존재한다：자신 필드의 Ｓ몬스터는 공격 유도를 얻는다。
PRINTL ●자신 필드에 Ｓ몬스터가 존재하지 않는다：이 카드는 공격 유도를 얻는다。
CALLFORM CARD_ABLE(0,身代효과)
PRINTL ②：이 카드가 전투 / 효과로 파괴될 경우、
PRINTL 대신에 패 또는 마법＆함정 존에서 몬스터 카드 １장을 묘지로 보낼 수 있다。
CALLFORM CARD_ABLE(10,발동誘発)
PRINTL ③：１턴에 １번、상대가 몬스터의 효과를 발동한 경우에 발동할 수 있다。
PRINTL 그 효과를 무효로 한다。

@CARDCAN_224237(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,224237)
SIF RESULT == 1
	RETURN 0

IF 종류 == 身代효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF 破壊대상ゾーン != "필드"
		RETURN 0
	SIF 破壊대상場所 != 場所
		RETURN 0
	SIF !GROUPMATCH(破壊종류,"효과","戦闘")
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲마함 = 1
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 맥시멈条件
	SIF ゾーン != "필드"
		RETURN 0
	CALL CARD_CHECK_MAXX(듀얼리스트,ゾーン,場所,"기황제 코어")
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 발동誘発 
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과条件종류 = モン全般
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_224237(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 選択カード名前


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,224237)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲마함 = 1
		효과条件종류 = モン全般
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			RETURN 1
		ENDIF
	ENDIF
	RETURN 0

ELSEIF 종류 == 맥시멈条件
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"戦無")
	CALL FULLRESET
	효과条件종류 = 싱크로
	효과範囲필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT != 1
		CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"攻誘")
	ELSE
		CALL FULLRESET
		효과条件종류 = 싱크로
		CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"攻誘")
	ENDIF

ELSEIF 종류 == 발동誘発 
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,224237,10)
	IF RESULT == 0
		RETURN 1
	ENDIF
	RETURN 0
ENDIF

;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――

@CARDNAME_324237,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 기황병장 그란엘 아인G
	CASE "略称"
		RESULTS = 그란엘１G
	CASE "カテゴリ"
		RESULTS:0 = 기황
		RESULTS:1 = 기황병
		RESULTS:2 = 그란엘
		RESULTS:3 = G
	CASE "맥시멈"
		RESULTS = 기황제 파츠
ENDSELECT

@CARD_324237(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1200
	CASE "守備力"
		RETURN 1600
	CASE "パーツ"
		RETURN 1
ENDSELECT
RETURN 0

@CARD_EXPLANATION_324237(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ②의 효과는 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ①：이 카드의 수비력은、이 카드 이외의 필드의 기계족 몬스터의 수×１００ 올린다。
CALLFORM CARD_ABLE(1,필드起動,フリー誘発)
PRINTL ②：자신 / 상대 턴에、묘지 / 제외 상태인 "기황" 카드 ３장을 덱으로 되돌리고、
PRINTL 상대 필드의 앞면 표시 몬스터 １장을 대상으로 하고 발동할 수 있다。
PRINTL 그 몬스터의 효과를 턴 종료시까지 무효로 하고、공격력을 절반으로 한다。
CALLFORM CARD_ABLE(10,攻撃時誘発)
PRINTL ②：１턴에 １번、상대 몬스터의 공격 선언시에 발동할 수 있다。
PRINTL 그 공격 몬스터의 공격력을 절반으로 한다。
 
@CARDCAN_324237(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,324237)
SIF RESULT == 1
	RETURN 0

IF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻撃時誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF バトルフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_1(듀얼리스트,324237)
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件除去種 = 無効
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所,324237)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과範囲제외 = 1
	효과条件カテゴリ = 기황
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 3
		RETURN 0
	RETURN 1

ELSEIF 종류 == 맥시멈条件
	SIF ゾーン != "필드"
		RETURN 0

	CALL CARD_CHECK_MAXX(듀얼리스트,ゾーン,場所,"기황제 코어")
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF




@CARDEFFECT_324237(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 攻守変動
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲상대필드 = 1
    효과条件他所 = 1
	효과条件종족 = 기계족
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	攻撃力修正 = RESULT * 100
	CALL STATCHANGE_BASIC(듀얼리스트,"自身",場所,0,攻撃力修正)

ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,324237,10)
	IF RESULT == 0
		攻撃力修正 = GETVAR(@"%対面者%_MZ_攻:%対面者%_戦闘カード") / 2
		選択カード = 戦闘場所(듀얼리스트,1)
		CALL SET_FIELD_INFO(対面者,"필드",選択カード,"一攻",-攻撃力修正)
	ENDIF

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,324237,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과範囲제외 = 1
		효과条件カテゴリ = 기황
		효과条件複数 = 3
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과条件표시 = 表側표시
			효과추가종류 = 12
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"無効")
		ENDIF
	ENDIF

ELSEIF 종류 == 맥시멈条件
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"戦無")
ENDIF


