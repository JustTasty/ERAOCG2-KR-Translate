@CARDNAME_31234,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 뇌겁룡－썬더 드래곤
	CASE "略称"
		RESULTS = 뇌겁룡
	CASE "カテゴリ"
		RESULTS:0 = 썬더 드래곤
ENDSELECT


@CARD_31234(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 원소정령족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 2800
	CASE "守備力"
		RETURN 0
	CASE "소환불가"
		RETURN 1
	CASE "추가日"
		RETURN 250707
ENDSELECT

RETURN 0

@CARD_EXPLANATION_31234(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드는 일반 소환할 수 없다。
CALLFORM CARD_ABLE(0,손패起動)
PRINTL 자신의 묘지에서 빛속성과 어둠속성의 몬스터를 １장씩 제외한 경우에 특수소환할 수 있다。
CALLFORM CARD_ABLE(1,효과後誘発)
PRINTL ①：１턴에 １번、몬스터의 효과가 손패에서 발동한 경우에 발동한다。
PRINTL 이 카드의 공격력은 턴 종료시 까지 ３００올린다。
CALLFORM CARD_ABLE(2,攻撃後誘発)
PRINTL ②：이 카드가 전투로 상대 몬스터를 파괴했을 때에 발동할 수 있다。
PRINTL 자신의 묘지에서 카드 １장을 제외하고、덱에서 원소정령족 몬스터 １장을 패에 넣는다。
CALLFORM CARD_ABLE(3,ターン終了時)
PRINTL ③：상대 엔드 페이즈에 발동할 수 있다。
PRINTL 제외되어있는 자신의 카드 １장을 패에 넣는다。


@CARDCAN_31234(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,31234)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,31234,"손패")
	SIF RESULT == 1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件속성 = 빛속성
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件속성 = 어둠속성
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종류 = モン全般
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"효과발동",2)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃後誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 直前戦闘破壊ID(듀얼리스트,1) == 0
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종족 = 원소정령족
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == ターン終了時
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲제외 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_31234(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件달성
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,31234)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件속성 = 빛속성
		효과条件종류 = モン全般
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件속성 = 어둠속성
			효과条件종류 = モン全般
			효과条件行先플래그 = 제외行き
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				CALL CARD_SP_SUMMON(듀얼리스트,"손패",場所)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,31234)
	IF RESULT == 0
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"一攻",300)
	ENDIF
ELSEIF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,31234)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件종족 = 원소정령족
			효과条件종류 = モン全般
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == ターン終了時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,31234)
IF RESULT == 0
		CALL FULLRESET
		효과範囲제외 = 1
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF



