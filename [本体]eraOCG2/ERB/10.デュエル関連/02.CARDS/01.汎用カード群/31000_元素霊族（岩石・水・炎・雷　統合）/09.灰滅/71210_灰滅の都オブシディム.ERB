@CARDNAME_71210,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 회멸의 도 옵시딤
	CASE "略称"
RESULTS = 회멸의 도
	CASE "カテゴリ"
RESULTS = 0 = 회멸
ENDSELECT


@CARD_71210(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN フィールド
	CASE "추가日"
		RETURN 250630
ENDSELECT

RETURN 0

@CARD_EXPLANATION_71210(종류)
#DIM DYNAMIC 종류

PRINTL 자신 필드는 「회멸의 도」가 된다.
PRINTL 이 카드명의 ②③의 효과는 각각 １턴에 １번만 사용할 수 있다.
CALLFORM CARD_ABLE(0,지속효과)
;期間と範囲をアッパー
PRINTL ①：필드의 특수 소환된 앞면 표시 몬스터는 원소정령족이 된다.
CALLFORM CARD_ABLE(2,ターン終了時)
PRINTL ②：자신 엔드 페이즈에 발동할 수 있다.
PRINTL 자신 묘지의 「회멸」 카드 １장을 덱의 맨 아래로 되돌린다. 그 후, 자신은 １장 드로우한다.
CALLFORM CARD_ABLE(3,필드→戦離)
PRINTL ③：필드의 이 카드가, 파괴된 경우 또는 제외된 경우에 발동할 수 있다.
PRINTL 덱에서 「회멸」 몬스터 １체를 특수 소환한다.

@CARDCAN_71210(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,71210)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	RETURN 1
ELSEIF 종류 == ターン終了時
	SIF ゾーン != "마함"
		RETURN 0
	SIF 발동済_2(듀얼리스트,71210)
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 회멸
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1
ELSEIF 종류 == 필드→戦離
	SIF 발동済_3(듀얼리스트,71210)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 회멸
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_71210(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71210)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF
ELSEIF 종류 == 지속효과
	CALL SET_FIELD_MAGIC_INFO(듀얼리스트,"회멸の都")
	CALL FULLRESET
	효과条件召種 = 召種全특수
	CALL STATCHANGE_OTHER(듀얼리스트,"필드",場所,"種",원소정령족)
	CALL FULLRESET
	효과条件召種 = 召種全특수
	CALL STATCHANGE_OTHER(듀얼리스트,"상대필드",場所,"種",원소정령족,71210)
ELSEIF 종류 == ターン終了時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71210,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 회멸
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL CARDDRAW_BASIC(듀얼리스트,1)
		ENDIF
	ENDIF
ELSEIF 종류 == 필드→戦離
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71210,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 회멸
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF

