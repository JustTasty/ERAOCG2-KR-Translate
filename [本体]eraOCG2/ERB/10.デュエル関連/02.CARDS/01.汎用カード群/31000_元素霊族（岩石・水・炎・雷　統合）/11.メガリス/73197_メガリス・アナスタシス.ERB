@CARDNAME_73197,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 메가리스 아나스타시스
	CASE "略称"
		RESULTS = 메가리스 Ａ
	CASE "カテゴリ"
		RESULTS:0 = 메가리스
ENDSELECT


@CARD_73197(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 永続魔法
	CASE "追加日"
		RETURN 250726
ENDSELECT

RETURN 0

@CARD_EXPLANATION_73197(種類)
#DIM DYNAMIC 種類

PRINTL このカード名の①の効果は１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(1,魔罠起動)
PRINTL ①：手札を１枚捨てて発動できる。
PRINTL デッキからレベル４以下と８以上の「메가리스」モンスターをそれぞれ１体ずつ手札に加える。
CALLFORM CARD_ABLE(10,効果後誘発)
PRINTL ②：１ターンに１度、自分が「메가리스」モンスターを特殊召喚した場合、
PRINTL 以下の効果から１つを選択して発動できる。
PRINTL （このカード名の以下の効果はそれぞれ１ターンに１度しか選択できない）
CALLFORM CARD_ABLE(2,効果後誘発)
PRINTL ●自分は２枚ドローし、手札を１枚選んで捨てる。
CALLFORM CARD_ABLE(3,効果後誘発)
PRINTL ●相手フィールドのモンスター１体をリリースする。


@CARDCAN_73197(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 発動不可,2

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,73197)
SIF RESULT == 1
	RETURN 0

IF 種類 == 魔罠起動 && GETVAR(@"%決闘者%_魔_表:場所") == 1
	SIF 発動済_1(決闘者,73197)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	SIF 手札数(決闘者) < 1
		RETURN 0
	CALL FULLRESET
	効果範囲デッキ = 1
	効果条件カテゴリ = 메가리스
	効果条件種類 = モン全般
	CALLFORM 効果条件レベル_ADV(4,"以下")
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	効果範囲デッキ = 1
	効果条件カテゴリ = 메가리스
	効果条件種類 = モン全般
	CALLFORM 効果条件レベル_ADV(8,"以上")
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 種類 == 魔罠起動 || 種類 == 手札起動
	IF 種類 == 魔罠起動
		SIF GETVAR(@"%決闘者%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	RETURN 1

ELSEIF 種類 == 効果後誘発
	SIF ゾーン != "魔罠"
		RETURN 0
	SIF 効果発動済(決闘者,場所,ゾーン)
		RETURN 0
	SIF GETVAR(@"%決闘者%_魔_表:場所") >= 2
		RETURN 0

	CALL FULLRESET
	効果条件カテゴリ = 메가리스
	CALL CARD_CHECK_FIN_DATA(決闘者,"特殊召喚")
	SIF RESULT != 1
		RETURN 0
	
	SIF 発動済_2(決闘者,73197)
		発動不可:0 = 1
	SIF 発動不可:0 != 1
		CALL CARD_DRAW_CHECK
	
	SIF 発動済_3(決闘者,73197)
		発動不可:1 = 1
	CALL FULLRESET
	効果範囲相手戦場 = 1
	効果条件除去種 = リリース
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		発動不可:1 = 1
	
	SIF 発動不可:0 == 1 && 発動不可:1 == 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_73197(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 発動不可,2

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 魔罠起動 && GETVAR(@"%決闘者%_魔_表:場所") == 1
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,73197,1)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲手札 = 1
		CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			効果範囲デッキ = 1
			効果条件カテゴリ = 메가리스
			効果条件種類 = モン全般
			CALLFORM 効果条件レベル_ADV(4,"以下")
			CALL CARD_ADD_ADV(決闘者,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				効果範囲デッキ = 1
				効果条件カテゴリ = 메가리스
				効果条件種類 = モン全般
				CALLFORM 効果条件レベル_ADV(8,"以上")
				CALL CARD_ADD_ADV(決闘者,ゾーン,場所)
			ENDIF
		ENDIF
	ENDIF

ELSEIF 種類 == 魔罠起動 || 種類 == 手札起動
	;カード発動＆設置まで
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,73197)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所,魔罠行き)
	ENDIF

ELSEIF 種類 == 効果後誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,73197,10)
	IF RESULT == 0
		SIF 発動済_2(決闘者,73197)
			発動不可:0 = 1
		SIF 発動不可:0 != 1
			CALL CARD_DRAW_CHECK
		
		SIF 発動済_3(決闘者,73197)
			発動不可:1 = 1
		CALL FULLRESET
		効果範囲相手戦場 = 1
		効果条件除去種 = リリース
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT != 1
			発動不可:1 = 1
		
		IF 発動不可:0 != 1 && 発動不可:1 != 1
			IF 決闘者 == "自分"
				CALL YN_CHOICE(決闘者,"●２枚ドローし、手札を１枚捨てる。","●相手フィールドのモンスター１体をリリースする。")
			ELSE
				RESULT = 1
			ENDIF
		ELSEIF 発動不可:0 != 1
			RESULT = 0
		ELSEIF 発動不可:1 != 1
			RESULT = 1
		ENDIF

		IF RESULT == 0
			CALL CARD_EFFECT_USE(決闘者,73197,2)
			CALL URARA_CHANCE(決闘者,処理中_効果ID,"雑多")
			IF RESULT == 0
				CALL CARDDRAW_BASIC(決闘者,2)
				CALL FULLRESET
				効果範囲手札 = 1
				CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
			ENDIF
		ELSEIF RESULT == 1
			CALL CARD_EFFECT_USE(決闘者,73197,3)
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(決闘者,"戦場単体",場所,"リリース")
		ENDIF
	ENDIF
ENDIF
