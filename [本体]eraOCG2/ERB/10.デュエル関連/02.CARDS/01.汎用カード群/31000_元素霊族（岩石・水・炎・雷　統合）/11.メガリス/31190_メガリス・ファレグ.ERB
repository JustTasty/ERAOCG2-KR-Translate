@CARDNAME_31190,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 메가리스 파레그
	CASE "略称"
		RESULTS = 파레그
	CASE "カテゴリ"
		RESULTS:0 = 메가리스
ENDSELECT


@CARD_31190(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 의식
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 원소정령족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 1200
	CASE "성별"
		RETURN 牝性
	CASE "추가日"
		RETURN 250703
ENDSELECT

RETURN 0

@CARDSUMMON_31190(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 出処
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照

CALL RESETCONDITION_MAT
必要レベル数 = 8

IF EX参照 == 1
	CALL RITCAN(듀얼리스트,소환カード,31190) 
	RETURN RESULT
ENDIF

@CARD_EXPLANATION_31190(종류)
#DIM DYNAMIC 종류

PRINTL 「메가리스」カードにより降臨。
PRINTL 이 카드명의 ①의 효과는 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①：이 카드를 패에서 捨てて발동할 수 있다。
PRINTL レベルの合計が의식소환する몬스터의 レベル以上になるように、
PRINTL 자신의 패 / 필드의 몬스터를 リリースし、
PRINTL 패에서 「메가리스」의식몬스터 １장을 의식소환する。
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ②：자신 필드의 몬스터의 공격력 / 수비력은 、
PRINTL 자신의 묘지의 의식몬스터의 数×３００올린다。


@CARDCAN_31190(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 対面者


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,31190)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,31190)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL RESETAREA_MAT
	素材範囲손패 = 1
	素材範囲필드 = 1
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종류 = 의식
	효과条件カテゴリ = 메가리스
	효과条件소환可 = 1
	효과条件他所 = 1
	強制素材禁止場所 = 100 + 場所
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_31190(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,31190,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL RESETAREA_MAT
		素材範囲손패 = 1
		素材範囲필드 = 1
		CALL FULLRESET
		효과条件종류 = 의식
		효과条件カテゴリ = 메가리스
		효과条件소환可 = 1
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"손패",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL RESETAREA_MAT
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_손패:選択カード")}(듀얼리스트,選択カード)
			素材範囲필드 = 1
			素材範囲손패 = 1
			TRYCALLFORM RITSUMMON(듀얼리스트, 選択カード,"손패")
		ENDIF
	ENDIF

ELSEIF 종류 == 攻守変動
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종류 = 의식
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	攻撃力修正 = RESULT * 300
	CALL FULLRESET
	CALL STATCHANGE_BASIC(듀얼리스트,"필드",場所,攻撃力修正,攻撃力修正)
ENDIF

