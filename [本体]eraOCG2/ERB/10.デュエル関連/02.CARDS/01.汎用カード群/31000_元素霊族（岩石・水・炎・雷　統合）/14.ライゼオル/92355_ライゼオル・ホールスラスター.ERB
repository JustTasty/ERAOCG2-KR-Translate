@CARDNAME_92355,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 라이제올 홀스러스터
	CASE "略称"
		RESULTS = RZL함정속으로
	CASE "カテゴリ"
		RESULTS:0 = 라이제올
		RESULTS:1 = 함정 속으로
ENDSELECT


@CARD_92355(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"

		RETURN 通常함정
ENDSELECT

RETURN 0

@CARD_EXPLANATION_92355(종류)
#DIM DYNAMIC 종류

PRINTL このカード名の①②の효과はそれぞれ１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(1,마함起動, フリー誘発)
PRINTL ①：자신フィールドの「라이제올」Ｘモンスターの数まで、
PRINTL 상대フィールドの表側표시カードを대상として발동できる。そのカードを破壊する。
PRINTL その後、자신フィールドのランク４のＸモンスター１体のＸ素材を１つ増やす。
CALLFORM CARD_ABLE(2,묘지起動, フリー誘発)
PRINTL ②：묘지のこのカードを제외して발동できる。
PRINTL 자신フィールドの「라이제올」モンスター１体を대상として발동できる。
PRINTL そのカード名を含む자신フィールドのモンスターを素材としてX소환を行う。


@CARDCAN_92355(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,92355)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,92355)
		RETURN 0
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
		RETURN 0

	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 라이제올
	효과条件종류 = 엑시즈
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	CALL FULLRESET
	효과範囲상대필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動 || (종류 == フリー誘発 && ゾーン == "묘지")
	SIF ゾーン != "묘지"
		RETURN 0
	SIF 발동済_2(듀얼리스트,92355)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0

	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 라이제올
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 엑시즈
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_92355(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 選択カード名前
#DIMS DYNAMIC 対面者
#DIM DYNAMIC リピート数

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == フリー誘発
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,92355,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = 라이제올
		효과条件종류 = 엑시즈
		효과条件표시 = 表側표시
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		リピート数 = RESULT
		CALL FULLRESET
		효과条件複数 = 100 -リピート数
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"묘지")

		CALL FULLRESET
		효과条件レベル = 4
		효과条件종류 = 엑시즈
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"Ｘ",1)
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動 || (종류 == フリー誘発 && ゾーン == "묘지")
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,92355,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = 라이제올
		효과条件종류 = モン全般
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			選択カードID = RESULT:99
			TRYCALLFORM CARDNAME_{選択カードID}, "名前"
			選択カード名前 = %RESULTS%
			CALL FULLRESET
			효과条件종류 = 엑시즈
			強制素材場所 = 지정場所(選択カード,"필드")
			효과条件소환可 = 1
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
				CALL XYZSUMMON(듀얼리스트, 選択カード)
			ENDIF
		ENDIF
	ENDIF
ENDIF