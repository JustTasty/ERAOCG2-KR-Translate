@CARDNAME_93115,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 트라미드 펄스
	CASE "略称"
		RESULTS = 고대유적의 각성
	CASE "カテゴリ"
		RESULTS:0 = 트라미드
ENDSELECT


@CARD_93115(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 永続罠
	CASE "追加日"
		RETURN 250706
ENDSELECT

RETURN 0

@CARD_EXPLANATION_93115(種類)
#DIM DYNAMIC 種類

CALLFORM CARD_ABLE(10,魔罠起動,フリー誘発)
PRINTL ①：１ターンに１度、自分の墓地から、
PRINTL 地属性・元素霊族モンスターまたはフィールド魔法カードを合計２枚除外し、
PRINTL 以下の効果から１つを選択して発動できる。
PRINTL ●相手フィールドの表側表示カード１枚を破壊する。
PRINTL ●自分の墓地の地属性・元素霊族モンスター１体を守備表示で特殊召喚する。


@CARDCAN_93115(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC 発動不可,2
#DIM DYNAMIC カウンタ

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,93115)
SIF RESULT == 1
	RETURN 0

IF 種類 == 魔罠起動 && GETVAR(@"%決闘者%_魔_表:場所") == 1 || 種類 == フリー誘発 && ゾーン == "魔罠"
	SIF 効果発動済(決闘者,場所,ゾーン)
		RETURN 0
	SIF GETVAR(@"%決闘者%_除外NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件スーパー種類 = フィールド
	効果条件属性 = 地属性
	効果条件種族 = 元素霊族
	効果条件CNT = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0

	CALL FULLRESET
	効果範囲相手戦場 = 1
	効果範囲相手魔罠 = 1
	効果条件表示 = 表側表示
	効果条件除去種 = 墓地
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		発動不可:0 = 1
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件属性 = 地属性
	効果条件種族 = 元素霊族
	効果条件召喚可 = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		発動不可:1 = 1
	
	SIF 発動不可:0 == 1 && 発動不可:1 == 1
		RETURN 0
	RETURN 1
	
ELSEIF 種類 == 魔罠起動
	SIF GETVAR(@"%決闘者%_魔_表:場所") == 3	
		RETURN 0
	SIF GETVAR(@"%決闘者%_魔_表:場所") == 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_93115(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC 発動不可,2
#DIM DYNAMIC カウンタ

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 魔罠起動 && GETVAR(@"%決闘者%_魔_表:場所") == 1 || 種類 == フリー誘発 && GETVAR(@"%決闘者%_魔_表:場所") == 1
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,93115,10)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件スーパー種類 = フィールド
		効果条件属性 = 地属性
		効果条件種族 = 元素霊族
		効果条件行先フラグ = 除外行き
		効果条件複数 = 2
		CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			効果範囲相手戦場 = 1
			効果範囲相手魔罠 = 1
			効果条件表示 = 表側表示
			効果条件除去種 = 墓地
			CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
			SIF RESULT != 1
				発動不可:0 = 1
			CALL FULLRESET
			効果範囲墓地 = 1
			効果条件属性 = 地属性
			効果条件種族 = 元素霊族
			効果条件召喚可 = 1
			CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
			SIF RESULT != 1
				発動不可:1 = 1
			
			IF 発動不可:0 != 1 && 発動不可:1 != 1
				CALL YN_CHOICE(決闘者,"●相手の表側カードを破壊する。","●墓地の地属性・元素霊族を特殊召喚する。")
			ELSEIF 発動不可:0 != 1
				RESULT = 0
			ELSEIF 発動不可:1 != 1
				RESULT = 1
			ELSEIF 発動不可:0 == 1 && 発動不可:1 == 1
				PRINTW 効果の対象が存在しないため処理を終了します
				RETURN 0
			ENDIF

			IF RESULT == 0
				CALL FULLRESET
				効果条件表示 = 表側表示
				CALL CARD_REMOVAL_BASIC(決闘者,"全場単体",場所,"墓地")
			ELSEIF RESULT == 1
				CALL FULLRESET
				効果範囲墓地 = 1
				効果条件属性 = 地属性
				効果条件種族 = 元素霊族
				CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所,"守備表示")
			ENDIF
		ENDIF
	ENDIF

ELSEIF 種類 == 魔罠起動 || 種類 == フリー誘発 && GETVAR(@"%決闘者%_魔_表:場所") == 2
	;カード発動＆設置まで
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,93115)
	IF RESULT == 0
		TRYCALLFORM CARDCAN_93115(決闘者,魔罠起動,"魔罠",場所)
		IF RESULT == 1
			PRINTFORML 
			PRINTFORML 今、効果を使用しますか？
			PRINTFORML 
			CALL YN_CHOICE(決闘者)
			IF RESULT == 0
				TRYCALLFORM CARDEFFECT_93115(決闘者,魔罠起動,"魔罠",場所)
			ENDIF
		ENDIF
	ENDIF
ENDIF
