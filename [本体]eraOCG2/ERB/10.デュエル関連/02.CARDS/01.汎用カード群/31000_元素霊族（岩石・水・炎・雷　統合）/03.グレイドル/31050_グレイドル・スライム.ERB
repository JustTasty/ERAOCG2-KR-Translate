@CARDNAME_31050,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 그레이돌 슬라임
	CASE "略称"
		RESULTS = 그레이돌 슬라임
	CASE "カテゴリ"
		RESULTS:0 = 그레이돌
		RESULTS:1 = 슬라임 
ENDSELECT


@CARD_31050(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
	
		RETURN 효과モン
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 원소정령족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 2000
	CASE "튜너"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_31050(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의  ①의 효과는 1턴에 1번밖에 사용할 수 없다
CALLFORM CARD_ABLE(1,손패起動,묘지起動)
PRINTL ①：이 카드가 패 / 묘지에 존재할 경우,
PRINTL 자신 필드의 「그레이돌」 카드 2장을 대상으로 발동할 수 있다 。
PRINTL 그 카드를 파괴하고, 이 카드를 특수 소환한다。
CALLFORM CARD_ABLE(0,특수소환時)
PRINTL ②：이 카드가 특수 환에 성공했을 경우에,
PRINTL 자신 묘지의 「그레이돌」 몬스터 1장을 대상으로 발동 할 수 있다。
PRINTL 그 몬스터를 수비 표시로 특수 소환한다。
PRINTL 

@CARDCAN_31050(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,31050)
SIF RESULT == 1
	RETURN 0

;패 / 묘지에서 특수 소환한다효과
;参考：月光黄유、魔導法士 ジュノン
IF 종류 == 손패起動 || 종류 == 묘지起動
	SIF 발동済_1(듀얼리스트,31050)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과範囲필드 = 1
	효과条件カテゴリ = 그레이돌
	;CNT使えば数をかぞえられるよ！
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0	
	CALL CARD_SS_CHECK(듀얼리스트,31050,ゾーン,1)
	SIF RESULT == 1
		RETURN 0
	RETURN 1
;특수소환時に발동する효과
;参考：LL－タ-コイズ ワ-ブラ-
ELSEIF 종류 == 특수소환時
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 그레이돌
	효과条件종류 = モン全般
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_31050(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 選択カード
#DIM DYNAMIC セット先

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

;自己SS효과
IF 종류 == 손패起動 || 종류 == 묘지起動
	;PRINTFORMW ※파괴하는 그레이돌 카드가 부족해도 발동할 수 있습니다, 파괴하는 카드가 1장의 경우에는 특수 소환할 수 없다。※
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,31050,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲마함 = 1
		효과範囲필드 = 1
		효과条件カテゴリ = 그레이돌
		효과条件複数 = 2;これ最強
		효과条件行先플래그 = 破壊行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		SIF RESULT == 1
			CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,31050,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 그레이돌
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"守備표시")
	ENDIF
ENDIF