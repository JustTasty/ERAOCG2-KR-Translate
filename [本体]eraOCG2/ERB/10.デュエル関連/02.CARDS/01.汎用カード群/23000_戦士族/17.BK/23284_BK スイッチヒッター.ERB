@CARDNAME_23284,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = BK 스위치히터
	CASE "略称"
		RESULTS = BK Sw히터
	CASE "カテゴリ"
		RESULTS:0 = BK
ENDSELECT


@CARD_23284(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 効果モン
	CASE "属性"
		RETURN 炎属性
	CASE "種族"
		RETURN 戦士族 
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1500
	CASE "守備力"
		RETURN 1400
	CASE "追加日"
		RETURN 250707
ENDSELECT

RETURN 0

@CARD_EXPLANATION_23284(種類)
#DIM DYNAMIC 種類

PRINTL このカード名の②の効果はデュエル中に１度しか使用できない。
CALLFORM CARD_ABLE(1,召喚時)
PRINTL ①：このカードが召喚した場合、自分の墓地の「ＢＫ」モンスター１体を対象として発動できる。
PRINTL そのモンスターを特殊召喚する。
CALLFORM CARD_ABLE(20,攻撃時誘発)
PRINTL ②：相手モンスターの直接攻撃宣言時に発動できる。
PRINTL このカードを特殊召喚し、その攻撃を無効にする。
PRINTL その後、ＥＸデッキから「ＢＫ」モンスター１体をこのカードの上に重ねてＸ召喚扱いで特殊召喚する。

@CARDCAN_23284(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,23284)
SIF RESULT == 1
	RETURN 0

IF 種類 == 召喚時
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件カテゴリ = BK
	効果条件召喚可 = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 種類 == 攻撃時誘発
	SIF ゾーン != "墓地"
		RETURN 0
	SIF 発動済_D1(決闘者,23284)
		RETURN 0
	SIF ターンプレイヤー == 決闘者
		RETURN 0
	SIF 戦闘場所(決闘者) != 5
		RETURN 0
	CALL CARD_EX_CHECK(決闘者,0)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	効果範囲EXデッキ = 1
	効果条件カテゴリ = BK	
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_23284(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者判定(決闘者)
対面者 = %RESULTS%


IF 種類 == 召喚時
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,23284,1)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件種類 = モン全般
		効果条件カテゴリ = BK
		CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
	ENDIF
ELSEIF 種類 == 攻撃時誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,23284,20)
	IF RESULT == 0
		CALL FULLRESET
		CALL EVADE_ATTACK(決闘者)
		CALL CARD_SP_SUMMON(決闘者,ゾーン,場所)
		CALL FULLRESET
		効果条件カテゴリ = BK
		効果条件種類 = エクシーズ
		CALL CARD_CHOOSE_BASIC(決闘者,"EXデッキ",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL FULLRESET
			CALL RESETCONDITION_MAT
			必要素材数 = 1
			重ねてＸ = 1
			素材条件固定 = カード召喚先
			効果追加種類 = 効追Ｘ素
			TRYCALLFORM XYZSUMMON(決闘者,選択カード)
		ENDIF
	ENDIF
ENDIF