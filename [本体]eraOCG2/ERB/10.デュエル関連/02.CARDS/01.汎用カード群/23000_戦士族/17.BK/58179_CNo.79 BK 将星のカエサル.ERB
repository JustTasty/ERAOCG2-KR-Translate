@CARDNAME_58179,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = CNo.79 BK 장성의 카이사르
	CASE "略称"
		RESULTS = C#79 카이사르
	CASE "カテゴリ"
		RESULTS:0 = BK
		RESULTS:1 = 카오스
		RESULTS:2 = 넘버즈
ENDSELECT

@CARD_58179(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN エクシーズ
	CASE "属性"
		RETURN 炎属性
	CASE "種族"
		RETURN 戦士族 
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 2600
	CASE "守備力"
		RETURN 2000
	CASE "追加日"
		RETURN 250707
ENDSELECT

RETURN 0
@CARDSUMMON_58179(決闘者,召喚カード,EX参照)
#DIMS DYNAMIC 決闘者
#DIM DYNAMIC 召喚カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード

CALL RESETCONDITION_MAT
必要素材数 = 3
素材条件レベル = 5
指定素材名前 = No.79 BK 항성의 카이저

IF EX参照 == 1
	CALL XYZCAN(決闘者,召喚カード,58179) 
	RETURN RESULT
ENDIF



@CARD_EXPLANATION_58179(種類)
#DIM DYNAMIC 種類

PRINTL レベル５モンスター×３
PRINTL このカード名の②の効果は１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ①：このカードの攻撃力は、このカードのＸ素材の数×２００アップする。
CALLFORM CARD_ABLE(1,召喚誘発)
PRINTL ②：相手がモンスターを特殊召喚する際に発動できる。
PRINTL このカードのＸ素材を２つ取り除き、それを無効にし破壊する。
CALLFORM CARD_ABLE(10,攻撃時誘発)
PRINTL ③：このカードが「Ｎｏ.７９ ＢＫ 항성의 카이저」を素材としてＸ召喚したこのカードは以下の効果を得る。
PRINTL ●１ターンに１度、自分の「ＢＫ」モンスターが相手モンスターと戦闘を行う攻撃宣言時に
PRINTL 手札・デッキから「ＢＫ」モンスター１体を墓地へ送って発動できる。
PRINTL その相手モンスターを除外し、このカードのＸ素材を１つ増やす。

@CARDCAN_58179(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,58179)
SIF RESULT == 1
	RETURN 0

IF 種類 == 召喚誘発
	SIF 発動済_1(決闘者,58179)
		RETURN 0
	SIF ゾーン != "戦場"
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_Ｘ:場所") <= 0
		RETURN 0
	SIF 最終召喚決者 == 決闘者
		RETURN 0
	RETURN 1

ELSEIF 種類 == 攻撃時誘発
	SIF 効果発動済(決闘者,場所,ゾーン)
		RETURN 0
	SIF ゾーン != "戦場"
		RETURN 0
	SIF バトルフェイズ != 1
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_条達:場所") == 0
		RETURN 0
	SIF 戦闘場所(決闘者) == 5
		RETURN 0
	SIF 戦闘場所(決闘者,1) == 5
		RETURN 0
	TRYCALLFORM CARDNAME_{戦闘カードID(決闘者)},"カテゴリ"
	SIF RESULTS != "BK"
		RETURN 0
	CALL FULLRESET
	効果範囲手札 = 1
	効果範囲デッキ = 1
	効果条件カテゴリ = BK
	効果条件種類 = モン全般
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 種類 == 攻守変動
	SIF ゾーン != "戦場"
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_Ｘ:場所") < 0
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_58179(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 除外カード
#DIM DYNAMIC 効果先

CALL 対面者判定(決闘者)
対面者 = %RESULTS%


IF 種類 == 召喚誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,58179,1,X消費2)
	IF RESULT == 0
		効果先 = 最終召喚場所
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(決闘者,"自動処理",効果先,"永続無効")
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(決闘者,"自動処理",効果先,"墓地")
	ENDIF

ELSEIF 種類 == 攻撃時誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,58179,10)
	IF RESULT == 0
		除外カード = 戦闘場所(決闘者,1)

		CALL FULLRESET
		効果範囲手札 = 1
		効果範囲デッキ = 1
		効果条件カテゴリ = BK
		効果条件種類 = モン全般
		CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(決闘者,"自動処理",除外カード,"除外")
			CALL FULLRESET
			CALL SET_FIELD_INFO(決闘者,"戦場",場所,"Ｘ",1)
			PRINTL 
			SETCOLOR C_YELLOW
			PRINTL カエサルのオーバーレイユニットが追加された！
			RESETCOLOR
			PRINTL 
		ENDIF
	ENDIF

ELSEIF 種類 == 攻守変動
	攻撃力修正 = (GETVAR(@"%決闘者%_MZ_Ｘ:場所") + 1) * 200
	CALL STATCHANGE_BASIC(決闘者,"自身",場所,攻撃力修正)
ENDIF