@CARDNAME_58205,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = CNo.105 BK 혜성의 카에스토스
	CASE "略称"
		RESULTS = C#105 카에스토스
	CASE "カテゴリ"
		RESULTS:0 = BK
		RESULTS:1 = 카오스
		RESULTS:2 = 넘버즈
		RESULTS:3 = 오버 헌드레드
ENDSELECT


@CARD_58205(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN エクシーズ
	CASE "属性"
		RETURN 炎属性
	CASE "種族"
		RETURN 戦士族 
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 2800
	CASE "守備力"
		RETURN 2000
	CASE "追加日"
		RETURN 250707
ENDSELECT

RETURN 0
@CARDSUMMON_58205(決闘者,召喚カード,EX参照)
#DIMS DYNAMIC 決闘者
#DIM DYNAMIC 召喚カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード

CALL RESETCONDITION_MAT
必要素材数 = 4
素材条件レベル = 5
指定素材名前 = No.105 BK 유성의 세스터스

IF EX参照 == 1
	CALL XYZCAN(決闘者,召喚カード,58205) 
	RETURN RESULT
ENDIF



@CARD_EXPLANATION_58205(種類)
#DIM DYNAMIC 種類

PRINTL レベル５モンスター×４
PRINTL このカード名の①の効果は１ターンに１度しか使用できない。
CALL CARD_ABLE(1,EX召喚時)
PRINTL ①：このカードがＸ召喚に成功した場合に発動できる。
PRINTL このカードのＸ素材の数まで、自分の墓地から通常罠カードを選んで自分フィールドにセットする。
PRINTL この効果で「カウンター」罠カードをセットした場合、そのカードはセットしたターンに発動できる。
CALL CARD_ABLE(2,攻撃後誘発)
PRINTL ②：このカードが戦闘でモンスターを破壊した場合に発動できる。
PRINTL 相手フィールドの攻撃表示モンスターを全て破壊し、破壊したモンスターの攻撃力の合計の半分のダメージを相手に与える。
CALL CARD_ABLE(3,発動誘発)
PRINTL ③：「Ｎｏ.１０５ ＢＫ 유성의 세스터스」をＸ素材としているこのカードがフィールドに存在し、バトルフェイズ中にカードの効果が発動した時、このカードのＸ素材を１つ取り除いて発動できる。
PRINTL 相手フィールドのカード１枚を破壊し、相手に５００ダメージを与える。

@CARDCAN_58205(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,58205)
SIF RESULT == 1
	RETURN 0

IF 種類 == EX召喚時
	SIF 発動済_1(決闘者,58205)
		RETURN 0
	SIF ゾーン != "戦場"
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_Ｘ:場所") <= -1
		RETURN 0
	SIF 魔罠数(決闘者) >= 5
		RETURN 0
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件種類 = 通常罠
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 種類 == 攻撃後誘発
	SIF ゾーン != "戦場"
		RETURN 0
	SIF 戦闘場所(決闘者) != 場所
		RETURN 0
	SIF 直前戦闘破壊ID(決闘者,1) == 0
		RETURN 0
	SIF モンスター数(対面者) == 0
		RETURN 0
	RETURN 1

ELSEIF 種類 == 発動誘発
	SIF ゾーン != "戦場"
		RETURN 0
	SIF バトルフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_条達:場所") == 0
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_Ｘ:場所") <= -1
		RETURN 0
	SIF モンスター数(対面者) == 0 && 魔罠数(対面者) == 0
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_START_DATA(決闘者)
	SIF RESULT == 1
		RETURN 1
	CALL CARD_CHECK_START_DATA(決闘者,1)
	SIF RESULT == 1
		RETURN 1
	RETURN 0
ENDIF


@CARDEFFECT_58205(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 最大選択数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == EX召喚時
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,58205,1)
	IF RESULT == 0
		最大選択数 = GETVAR(@"%決闘者%_MZ_Ｘ:場所") + 1
		SIF 最大選択数 > 5 - 魔罠数(決闘者) 
			最大選択数 = 5 - 魔罠数(決闘者) 
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件種類 = 通常罠
		効果条件CNT = 1
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF 最大選択数 > RESULT
			最大選択数 = RESULT
		FOR カウンタ,0,最大選択数
			CALL FULLRESET
			効果範囲墓地 = 1
			効果条件種類 = 通常罠
			CALL CARD_CHOOSE_ADV(決闘者,ゾーン,場所)
			選択カード = RESULT
			選択カード_ID = RESULT:99
			IF 選択カード_ID != 9999
				CALL CARD_SELECT(決闘者,選択カード)
				選択カード = RESULT
				TRYCALLFORM CARDNAME_{選択カード_ID},"カテゴリ"
				IF MATCH(RESULTS,"カウンター")
					CALL FULLRESET
					PRINTL 
					SETCOLOR C_YELLOW
					PRINTL カエストスは反撃の構えを取った！
					RESETCOLOR
					PRINTL 
					CALL CARD_ADD(決闘者,"墓地","魔罠", 選択カード, "使化裏側表示")
				ELSE
					CALL FULLRESET
					CALL CARD_ADD(決闘者,"墓地","魔罠", 選択カード, "裏側表示")
				ENDIF
			ENDIF
			CALL FULLRESET
			IF 決闘者 == "自分" && 最大選択数 - カウンタ > 1
				PRINTL さらにカードをセットしますか？
				CALL YN_CHOICE(決闘者)
				SIF RESULT == いいえ
					BREAK
			ENDIF
		NEXT
	ENDIF
ELSEIF 種類 == 攻撃後誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,58205,2)
	IF RESULT == 0
		効果追加種類 = 2
		CALL CARD_REMOVAL_BASIC(決闘者,"戦場全体",場所,"墓地")
	ENDIF
ELSEIF 種類 == 発動誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,58205,3,X消費1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(決闘者,"全場単体",場所,"墓地")
		CALL LIFE_CHANGE(決闘者,"減少","効果",500,1)
	ENDIF
ENDIF