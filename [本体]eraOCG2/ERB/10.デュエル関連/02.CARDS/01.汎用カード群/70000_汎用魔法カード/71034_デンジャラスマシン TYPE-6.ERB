@CARDNAME_71034,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 데인저러스 머신 TYPE－6
	CASE "略称"
		RESULTS = 데인저러스머신６
	CASE "カテゴリ"
		RESULTS:0 = 없음
ENDSELECT


@CARD_71034(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 지속마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_71034(종류)
#DIM DYNAMIC 종류

CALLFORM CARD_ABLE(0,ターン開始時強制)
PRINTL ①：자신 스탠바이 페이즈에 발동한다。
PRINTL 주사위를 1회 던지고, 나온 눈의 효과를 적용한다。
PRINTL ●１：자신의 패를 1장 고르고 버린다。
PRINTL ●２：상대는 패를 1장 고르고 버린다。
PRINTL ●３：자신은 1장 드로우한다。
PRINTL ●４：상대는 1장 드로우한다。
PRINTL ●５：상대 필드의 몬스터 1장을 파괴한다。
PRINTL ●６：필드의 카드를 모두 파괴하고, 서로 1000 데미지를 받는다。


@CARDCAN_71034(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,71034)
SIF RESULT == 1
	RETURN 0

IF 종류 == ターン開始時強制
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 1
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	RETURN 1

ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_71034(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == ターン開始時強制
	PRINTW 不穏な音を立てながらスロットが回り始める・・・
	CALL DICE_ROLL(듀얼리스트,1)
	IF RESULT == 1
		PRINTW マシンがすごい勢いで周りの物を吸い込み始めた！
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
	ELSEIF RESULT == 2
		PRINTW マシンがすごい勢いで周りの物を吸い込み始めた！
		CALL CARD_ADD_GY_BASIC(対面者,"손패",ゾーン,場所)
	ELSEIF RESULT == 3
		PRINTW マシンから카드가 吐き出された！
		CALL CARDDRAW_BASIC(듀얼리스트,1)
	ELSEIF RESULT == 4
		PRINTW マシンから카드가 吐き出された！
		CALL CARDDRAW_BASIC(対面者,1)
	ELSEIF RESULT == 5
		PRINTW マシンから電撃が迸る！
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		CALL CARD_REMOVAL_ADV(듀얼리스트,"単体除去",場所,"묘지")
	ELSEIF RESULT == 6
		PRINTW まずい！　爆発するっ────！
		SETBGCOLOR 95,95,95
		SETBGCOLOR 145,145,145
		SETBGCOLOR 198,198,198
		SETBGCOLOR 255,255,255
		WAIT
		RESETBGCOLOR
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"両필드全体",場所,"묘지")
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"両마함全体",場所,"묘지")
		CALL LIFE_CHANGE(듀얼리스트,"減少","효과",1000)
		CALL LIFE_CHANGE(듀얼리스트,"減少","효과",1000,1)
	ENDIF

ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동＆설치まで
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71034)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
		
	ENDIF
ENDIF
