@CARDNAME_38371,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 문장수 유니콘
	CASE "略称"
		RESULTS = 문장수 유니콘
	CASE "カテゴリ"
		RESULTS:0 = 문장수
ENDSELECT


@CARD_38371(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	;効果モン/通常モン/儀式/融合/シンクロ/エクシーズ/リンク/超次元
	CASE "種類"
		RETURN 効果モン
	;闇属性/光属性/地属性/水属性/炎属性/風属性
	CASE "属性"
		RETURN 光属性
	CASE "種族"
		RETURN 獣族
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1100
	CASE "守備力"
		RETURN 1600
	CASE "追加日"
		RETURN 250713
ENDSELECT
RETURN 0

@CARD_EXPLANATION_38371(種類)
#DIM DYNAMIC 種類

PRINTL このカード名の効果は１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(1,墓地起動)
PRINTL ①：墓地のこのカードを除外し、自分の墓地の「문장수」Xモンスター１体を対象として発動できる。
PRINTL そのモンスターを効果を無効にして特殊召喚する。



@CARDCAN_38371(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,38371)
SIF RESULT == 1
	RETURN 0

IF 種類 == 墓地起動
	SIF ゾーン != "墓地"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	SIF 発動済_1(決闘者,38371)
		RETURN 0
	SIF GETVAR(@"%決闘者%_除外NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	効果範囲墓地 = 1	
    効果条件カテゴリ = 문장수
	効果条件種類 = エクシーズ
	効果条件召喚可 = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF	

@CARDEFFECT_38371(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カード

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 墓地起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,38371,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所)
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件カテゴリ = 문장수
	    効果条件種類 = エクシーズ
		効果追加種類 = 効追無効
		CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
	ENDIF
ENDIF


