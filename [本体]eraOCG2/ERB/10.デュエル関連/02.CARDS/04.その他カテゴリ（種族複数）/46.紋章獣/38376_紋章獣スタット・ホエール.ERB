@CARDNAME_38376,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 문장수 스탓트 웨일
	CASE "略称"
		RESULTS = 스탓트 웨일
	CASE "カテゴリ"
		RESULTS:0 = 문장수
ENDSELECT


@CARD_38376(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	;効果モン/通常モン/儀式/融合/シンクロ/エクシーズ/リンク/超次元
	CASE "種類"
		RETURN 効果モン
	;闇属性/光属性/地属性/水属性/炎属性/風属性
	CASE "属性"
		RETURN 水属性
	CASE "種族"
		RETURN 海生族
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 900
	CASE "守備力"
		RETURN 1900
	CASE "追加日"
		RETURN 250713
ENDSELECT
RETURN 0

@CARD_EXPLANATION_38376(種類)
#DIM DYNAMIC 種類


PRINTL このカード名の①②の効果はそれぞれ１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(1,召喚時,特殊召喚時)
PRINTL ①：このカードが召喚・特殊召喚した場合、手札を１枚捨てて発動できる。
PRINTL デッキから「메달리온」魔法・罠カードを２枚まで手札に加える。
CALLFORM CARD_ABLE(2,墓地起動)
PRINTL ②：墓地のこのカードを除外して発動できる。
PRINTL 自分の墓地の同名の「문장수」モンスター２体を墓地から守備表示で特殊召喚する。
PRINTL この効果の発動後、ターン終了時まで自分は「문장수」モンスターしかEXデッキから特殊召喚できない。


@CARDCAN_38376(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,38376)
SIF RESULT == 1
	RETURN 0

IF 種類 == 召喚時 || 種類 == 特殊召喚時
	SIF 発動済_1(決闘者,38376)
		RETURN 0
	SIF 手札数(決闘者) < 1
		RETURN 0
	CALL FULLRESET
	効果範囲デッキ = 1
	効果条件カテゴリ = 메달리온
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 種類 == 墓地起動
	SIF 発動済_2(決闘者,38376)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	SIF ゾーン != "墓地"
		RETURN 0
	SIF GETVAR(@"%決闘者%_除外NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件CNT = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT >= 5
		RETURN 0

	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件カテゴリ = 문장수
	効果条件種類 = モン全般
	効果条件他所 = 1
	効果条件召喚可 = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ENDIF


@CARDEFFECT_38376(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件達成
#DIMS DYNAMIC カード名

CALL 対面者判定(決闘者)
対面者 = %RESULTS%


IF 種類 == 召喚時 || 種類 == 特殊召喚時
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,38376,1)
	IF RESULT == 0
	    CALL FULLRESET
		CALL CARD_ADD_GY_BASIC(決闘者,"手札",ゾーン,場所)
		IF RESULT == 1
		    CALL FULLRESET
		    効果範囲デッキ = 1
		    効果条件カテゴリ = 메달리온
			効果条件種類 = 魔罠全般
			効果条件複数 = 98
		    CALL CARD_ADD_ADV(決闘者,ゾーン,場所)
		ENDIF
	ENDIF

ELSEIF 種類 == 墓地起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,38376,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所)
		CALL FULLRESET
		効果範囲墓地 = 1
	    効果条件カテゴリ = 문장수
	    効果条件種類 = モン全般
		CALL CARD_CHOOSE_BASIC(決闘者, "墓地",ゾーン,場所)	
		選択カード = RESULT
		IF 選択カード != 9999
			CALLFORM CARDNAME_{GETVAR(@"%決闘者%_墓地:選択カード")}, "名前"
			カード名 = %RESULTS%

			CALL FULLRESET
			効果範囲墓地 = 1
			効果条件名前 = %カード名%
			効果条件複数 = 2
			CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所,"守備表示")
			CALL CARD_EX_RESTRICTION(決闘者,"문장수")
		ENDIF
	ENDIF
ENDIF