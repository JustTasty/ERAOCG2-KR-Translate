@CARDNAME_58521,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 마스크드 히어로 아토믹
	CASE "略称"
		RESULTS = 아토믹
	CASE "カテゴリ"
		RESULTS:0 = HERO
		RESULTS:1 = M•HERO
ENDSELECT


@CARD_58521(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 融合
	CASE "属性"
		RETURN 炎属性
	CASE "種族"
		RETURN 戦士族
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 2600
	CASE "守備力"
		RETURN 2000
	CASE "特殊不可"
		RETURN 1
	CASE "性別"
		RETURN 雄性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_58521(種類)
#DIM DYNAMIC 種類

PRINTL このカードは「マスク・チェンジ」の効果でのみ特殊召喚できる。
PRINTL このカード名の①③の効果はそれぞれ１ターンに１度しか使用できない。
CALL CARD_ABLE(1,EX召喚時,特殊召喚時)
PRINTL ①：このカードが特殊召喚した場合、
PRINTL 炎属性以外の自分の墓地・除外状態の「M•HERO」モンスター１体を対象として発動できる。
PRINTL そのモンスターを召喚条件を無視して守備表示で特殊召喚する。
CALL CARD_ABLE(0,永続効果)
PRINTL ②：このカードは１ターンに１度だけ戦闘・効果では破壊されない。
CALL CARD_ABLE(3,ターン終了時)
PRINTL ③：自分エンドフェイズに、自分の墓地の「チェンジ」速攻魔法カード１枚を対象として発動できる。
PRINTL そのカードを自分フィールドにセットする。


@CARDSUMMON_58521(決闘者,召喚カード,EX参照)
#DIMS DYNAMIC 決闘者
#DIM DYNAMIC 召喚カード
#DIM DYNAMIC EX参照

@CARDCAN_58521(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件達成

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,58521)
SIF RESULT == 1
	RETURN 0

IF 種類 == EX召喚時 || 種類 == 特殊召喚時
	SIF 発動済_1(決闘者,58521)
		RETURN 0
	CALL FULLRESET
	効果範囲墓地 = 1
	効果範囲除外 = 1
	CALL 効果条件属性_ADV(炎属性, "以外")
	効果条件カテゴリ = M•HERO
	効果条件種類 = モン全般
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 永続効果
	SIF ゾーン != "戦場"
		RETURN 0
	RETURN 1
ELSEIF 種類 == ターン終了時
	SIF 発動済_3(決闘者,58521)
		RETURN 0
	SIF ゾーン != "戦場"
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件カテゴリ = チェンジ
	効果条件種類 = 速攻魔法
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF




@CARDEFFECT_58521(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件達成

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == EX召喚時 || 種類 == 特殊召喚時
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,58521,1)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲墓地 = 1
		効果範囲除外 = 1
		CALL 効果条件属性_ADV(炎属性, "以外")
		効果条件カテゴリ = M•HERO
		効果条件種類 = モン全般
		効果条件特不無視 = 1
		CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
	ENDIF
ELSEIF 種類 == 永続効果
	CALL STATCHANGE_EFFECT(決闘者,"自身",場所,"一破")
ELSEIF 種類 == ターン終了時
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,58521,3)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件カテゴリ = チェンジ
		効果条件種類 = 速攻魔法
		CALL CARD_SET_ADV(決闘者,ゾーン,場所,"裏側表示")
	ENDIF
ENDIF
