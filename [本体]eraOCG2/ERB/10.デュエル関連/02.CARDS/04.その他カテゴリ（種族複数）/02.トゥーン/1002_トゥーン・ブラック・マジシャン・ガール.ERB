@CARDNAME_1002,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 툰 블랙 매지션 걸
	CASE "略称"
		RESULTS = ＴＢＭＧ
	CASE "カテゴリ"
		RESULTS:0 = 툰
		RESULTS:1 = 블랙 매지션 걸
		RESULTS:2 = 매지션 걸
		RESULTS:3 = 블랙 매지션
ENDSELECT


@CARD_1002(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 마법사족
	CASE "レベル"
		RETURN 6
	CASE "攻撃力"
		RETURN 2000
	CASE "守備力"
		RETURN 1700
	CASE "성별"
		RETURN 牝性
ENDSELECT
RETURN 0

@CARD_EXPLANATION_1002(종류)
#DIM DYNAMIC 종류
PRINTL 이 카드명의 ③의 효과는 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(0,손패起動)
PRINTL ①：이 카드 이외의 자신의 패를 １장 버리고 발동할 수 있다。
PRINTL 이 카드를 패에서 특수 소환한다。
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ②：이 카드의 공격력은、묘지의 "툰" 몬스터의 수×３００올리고、
CALLFORM CARD_ABLE(0,지속효과)
PRINTL 상대 필드에 "툰" 몬스터가 존재하지 않을 경우、직접 공격할 수 있다。
CALLFORM CARD_ABLE(3,필드起動)
PRINTL ③：자신 메인 페이즈에 발동할 수 있다。
PRINTL 덱에서 "툰" 마법 / 함정 카드 １장을 패에 넣는다。

@CARDSUMMON_AA_1002


@CARDCAN_1002(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,1002)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,1002,"손패",1)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 0
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件カテゴリ = 툰
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 발동済_3(듀얼리스트,1002)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 툰
	효과条件종류 = 마함全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 0
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_1002(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1002)
	IF RESULT == 0
		CALL FULLRESET
		효과条件他所 = 1
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 攻守変動
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 툰
	효과条件종류 = モン全般
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	攻撃力修正 = RESULT * 300
	CALL STATCHANGE_BASIC(듀얼리스트,"자신",場所,攻撃力修正)
ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"자신",場所,"直攻")
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1002,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 툰
		효과条件종류 = 마함全般
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF


