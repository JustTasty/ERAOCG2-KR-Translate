@CARDNAME_1527,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 현람와려 바를룬
	CASE "略称"
		RESULTS = 바를룬
	CASE "カテゴリ"
		RESULTS:0 = 현람
ENDSELECT


@CARD_1527(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 効果モン
	CASE "属性"
		RETURN 風属性
	CASE "種族"
		RETURN 元素霊族
	CASE "レベル"
		RETURN 9
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 3000
	CASE "性別"
		RETURN 牝性
	CASE "追加日"
		RETURN 250726
ENDSELECT
RETURN 0

@CARD_EXPLANATION_1527(種類)
#DIM DYNAMIC 種類

PRINTL このカード名の①②③の効果はそれぞれ１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(1,効果後誘発)
PRINTL ①：速攻魔法カードが発動した場合に発動できる。
PRINTL このカードを手札から特殊召喚する。
CALLFORM CARD_ABLE(2,発動誘発)
PRINTL ②：自分の墓地に「サイクロン」が存在し、相手がモンスターの効果を発動した時に発動できる。
PRINTL その効果を無効にする。
PRINTL 自分の墓地に「サイクロン」が２枚以上存在する場合、さらにそのモンスターを破壊できる。
CALLFORM CARD_ABLE(3,効果後誘発)
PRINTL ③：このカードが墓地に存在する状態で「サイクロン」が発動した場合に発動できる。
PRINTL このカードを特殊召喚する。


@CARDCAN_1527(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,1527)
SIF RESULT == 1
	RETURN 0

IF 種類 == 効果後誘発
	IF ゾーン == "手札"
		SIF 発動済_1(決闘者,1527)
			RETURN 0
		CALL CARD_SS_CHECK(決闘者,1527,ゾーン)
		SIF RESULT == 1
			RETURN 0
		CALL FULLRESET
		効果条件種類 = 速攻魔法
		CALL CARD_CHECK_FIN_DATA(決闘者,"効果発動",2)
		SIF RESULT != 1
			RETURN 0

	ELSEIF ゾーン == "墓地"
		SIF 発動済_3(決闘者,1527)
			RETURN 0
		CALL CARD_SS_CHECK(決闘者,1527,ゾーン)
		SIF RESULT == 1
			RETURN 0
		CALL FULLRESET
		効果条件名前 = サイクロン
		CALL CARD_CHECK_FIN_DATA(決闘者,"効果発動",2)
		SIF RESULT != 1
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	RETURN 1

ELSEIF 種類 == 発動誘発
	SIF ゾーン != "戦場"
		RETURN 0
	SIF 発動済_2(決闘者,1527)
		RETURN 0
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件名前 = サイクロン
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	効果条件種類 = モン全般
	CALL CARD_CHECK_START_DATA(決闘者,1)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_1527(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カード

IF 種類 == 効果後誘発
	IF ゾーン == "手札"
		CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,1527,1)
	ELSEIF ゾーン == "墓地"
		CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,1527,3)
	ENDIF
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(決闘者,ゾーン,場所)
	ENDIF

ELSEIF 種類 == 発動誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,1527,2)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件名前 = サイクロン
		効果条件CNT = 1
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		IF RESULT >= 2
			PRINTL モンスターを破壊しますか？
			CALL YN_CHOICE(決闘者)
		ELSE
			RESULT = 1
		ENDIF

		IF RESULT == 0
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(決闘者,"発動無効",場所,"墓地")
		ELSEIF RESULT == 1
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(決闘者,"発動無効",場所,"無効")
		ENDIF
		RETURN 1
	ENDIF
	RETURN 0
ENDIF
