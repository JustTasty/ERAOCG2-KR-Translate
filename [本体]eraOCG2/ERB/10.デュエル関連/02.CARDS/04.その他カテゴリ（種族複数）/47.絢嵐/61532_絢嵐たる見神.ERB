@CARDNAME_61532,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 현람한 견신
	CASE "略称"
		RESULTS = 현람한 견신
	CASE "カテゴリ"
		RESULTS:0 = 현람
ENDSELECT


@CARD_61532(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 速攻魔法
	CASE "追加日"
		RETURN 250726
ENDSELECT

RETURN 0

@CARD_EXPLANATION_61532(種類)
#DIM DYNAMIC 種類
PRINTL ①：以下の効果から１つを選択して発動できる
PRINTL （このカード名の以下の効果はそれぞれ１ターンに１度しか選択できない）。
CALL CARD_ABLE(1,魔罠起動 ,手札起動,フリー誘発)
PRINTL ●自分は２枚ドローし、手札の「현람」カードか速攻魔法カード１枚を捨てる。
PRINTL 捨てられなかった場合、自分の手札を全て捨てる。
CALL CARD_ABLE(2,魔罠起動 ,手札起動,フリー誘発)
PRINTL ●自分のデッキ・墓地から「サイクロン」１枚を手札に加える。
CALL CARD_ABLE(0,破壊→墓地)
PRINTL ②：このカードが「サイクロン」の効果で破壊された場合に発動できる。
PRINTL このカードを自分の魔法＆罠ゾーンにセットする。

@CARDCAN_61532(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC 発動不可,2
#DIM DYNAMIC カウンタ

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,61532)
SIF RESULT == 1
	RETURN 0
IF 種類 == 魔罠起動 || 種類 == 手札起動 || 種類 == フリー誘発
	IF ゾーン == "手札"
		SIF ターンプレイヤー != 決闘者
			RETURN 0
	ELSEIF ゾーン == "魔罠"
		SIF GETVAR(@"%決闘者%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	SIF 発動済_1(決闘者,61532)
		発動不可:0 = 1
	SIF 発動済_2(決闘者,61532)
		発動不可:1 = 1

	SIF 発動済_1(決闘者,61532) == 0
		CALL CARD_DRAW_CHECK
	CALL FULLRESET
	効果範囲デッキ = 1
	効果範囲墓地 = 1
	効果条件名前 = サイクロン
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		発動不可:1 = 1
	RETURN 1

ELSEIF 種類 == 破壊→墓地
	SIF ゾーン != "墓地"
		RETURN 0
	TRYCALLFORM CARDNAME_{処理中_効果ID},"名前"
	SIF RESULTS != "サイクロン"
		RETURN 0
	SIF 魔罠数(決闘者) >= 5
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_61532(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 発動不可,2

CALL 対面者判定(決闘者)
対面者 = %RESULTS%


IF 種類 == 魔罠起動 || 種類 == 手札起動 || 種類 == フリー誘発
	SIF 発動済_1(決闘者,61532)
		発動不可:0 = 1
	SIF 発動済_2(決闘者,61532)
		発動不可:1 = 1

	CALL FULLRESET
	効果範囲デッキ = 1
	効果範囲墓地 = 1
	効果条件名前 = サイクロン
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		発動不可:1 = 1
	
	IF 発動不可:0 != 1 && 発動不可:1 != 1
		CALL YN_CHOICE(決闘者,"●自分は２枚ドローし、「현람」カードか速攻魔法カード１枚を捨てる。","●自分のデッキ・墓地から「サイクロン」１枚を手札に加える。")
	ELSEIF 発動不可:0 != 1
		RESULT = 0
	ELSEIF 発動不可:1 != 1
		RESULT = 1
	ENDIF
	
	IF RESULT == 0
		CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,61532,1)
		IF RESULT == 0
			CALL CARD_DISPOSE(決闘者,ゾーン,場所)
			CALL URARA_CHANCE(決闘者,処理中_効果ID,"雑多")
			IF RESULT == 0
				CALL CARDDRAW_BASIC(決闘者,2)
				CALL FULLRESET
				効果範囲手札 = 1
				効果条件スーパー種類 = 速攻魔法
				効果条件カテゴリ = 현람
				CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
				IF RESULT != 1
					FOR カウンタ,0,紙束最大値
						SIF GETVAR(@"%決闘者%_手札:カウンタ") == -1
							CONTINUE
						CALL CARD_DISPOSE(決闘者,"手札",カウンタ,効果墓地行き)
					NEXT
				ENDIF
			ENDIF
		ENDIF
	
	ELSEIF RESULT == 1
		CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,61532,2)
		IF RESULT == 0
			CALL CARD_DISPOSE(決闘者,ゾーン,場所)
			CALL FULLRESET
			効果範囲デッキ = 1
			効果範囲墓地 = 1
			効果条件名前 = サイクロン
			CALL CARD_ADD_ADV(決闘者,ゾーン,場所)
		ENDIF
	ENDIF

ELSEIF 種類 == 破壊→墓地
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,61532)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件名前 = 현람한 견신
		CALL CARD_SET_ADV(決闘者,ゾーン,場所,"裏側表示")
	ENDIF
ENDIF
