@CARDNAME_1525,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 현람한 메가라
	CASE "略称"
		RESULTS = 메가라
	CASE "カテゴリ"
		RESULTS:0 = 현람
ENDSELECT


@CARD_1525(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 効果モン
	CASE "属性"
		RETURN 風属性
	CASE "種族"
		RETURN 天使族
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 500
	CASE "守備力"
		RETURN 1500
	CASE "性別"
		RETURN 牝性
	CASE "追加日"
		RETURN 250726
ENDSELECT
RETURN 0

@CARD_EXPLANATION_1525(種類)
#DIM DYNAMIC 種類

PRINTL このカード名の①②の効果はそれぞれ１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(1,手札起動)
PRINTL ①：自分の墓地に「サイクロン」が存在するか、相手フィールドに魔法・罠カードが存在しない場合に発動できる。
PRINTL このカードを手札から特殊召喚する。
CALLFORM CARD_ABLE(2,効果後誘発)
PRINTL ②：「현람」速攻魔法カードか「サイクロン」が発動した場合に発動できる。
PRINTL 同名モンスターが自分フィールドに存在しない「현람」モンスター１体をデッキから特殊召喚する。
PRINTL この効果の発動後、ターン終了時まで自分は風属性モンスターしか特殊召喚できない。


@CARDCAN_1525(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 選択カード名前,6
#DIMS DYNAMIC 対面者

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,1525)
SIF RESULT == 1
	RETURN 0

IF 種類 == 手札起動
	SIF 発動済_1(決闘者,1525)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL CARD_SS_CHECK(決闘者,1525,ゾーン)
	SIF RESULT == 1
		RETURN 0
	SIF 魔罠数(対面者) == 0
		RETURN 1
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件名前 = サイクロン
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT == 1
		RETURN 1
	RETURN 0

ELSEIF 種類 == 効果後誘発
	SIF ゾーン != "戦場"
		RETURN 0
	SIF 発動済_2(決闘者,1525)
		RETURN 0
	CALL FULLRESET
	効果条件スーパー名前 = サイクロン
	効果条件カテゴリ = 현람
	効果条件種類 = 速攻魔法
	CALL CARD_CHECK_FIN_DATA(決闘者,"効果発動",2)
	SIF RESULT != 1
		RETURN 0

	FOR カウンタ,0,5
		SIF GETVAR(@"%決闘者%_戦場:カウンタ") == -1
			CONTINUE
		SIF GETVAR(@"%決闘者%_MZ_表:カウンタ") == 3
			CONTINUE
		TRYCALLFORM CARDNAME_{GETVAR(@"%決闘者%_戦場:カウンタ")},"カテゴリ"
		SIF !MATCH(RESULTS,"현람")
			CONTINUE
		TRYCALLFORM CARDNAME_{GETVAR(@"%決闘者%_戦場:カウンタ")},"名前"
		選択カード名前:(カウンタ+1) = %RESULTS%
	NEXT
	CALL FULLRESET
	効果範囲デッキ = 1
	効果条件カテゴリ = 현람
	効果条件固名前:0 = 仮置き
	FOR カウンタ,1,6
		効果条件固名前:カウンタ = %選択カード名前:カウンタ%
	NEXT
	効果条件召喚可 = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_1525(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 選択カード名前,6

IF 種類 == 手札起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,1525,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(決闘者,ゾーン,場所)
	ENDIF

ELSEIF 種類 == 効果後誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,1525,2)
	IF RESULT == 0
		FOR カウンタ,0,5
			SIF GETVAR(@"%決闘者%_戦場:カウンタ") == -1
				CONTINUE
			SIF GETVAR(@"%決闘者%_MZ_表:カウンタ") == 3
				CONTINUE
			TRYCALLFORM CARDNAME_{GETVAR(@"%決闘者%_戦場:カウンタ")},"カテゴリ"
			SIF !MATCH(RESULTS,"현람")
				CONTINUE
			TRYCALLFORM CARDNAME_{GETVAR(@"%決闘者%_戦場:カウンタ")},"名前"
			選択カード名前:(カウンタ+1) = %RESULTS%
		NEXT
		CALL FULLRESET
		効果範囲デッキ = 1
		効果条件カテゴリ = 현람
		効果条件固名前:0 = 仮置き
		FOR カウンタ,1,6
			効果条件固名前:カウンタ = %選択カード名前:カウンタ%
		NEXT
		CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
		SS召喚可能属性 = 風属性
		CALL CARD_SS_RESTRICTION(決闘者)
	ENDIF
ENDIF
