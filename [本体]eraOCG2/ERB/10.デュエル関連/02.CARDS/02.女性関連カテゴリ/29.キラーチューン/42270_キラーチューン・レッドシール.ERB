@CARDNAME_42270,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 킬러튠 레드씰
	CASE "略称"
		RESULTS = 레드씰
	CASE "カテゴリ"
		RESULTS:0 = 킬러튠
		RESULTS:5 = 튜너소재가능
ENDSELECT


@CARD_42270(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 싱크로
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 1700
	CASE "守備力"
		RETURN 2400
	CASE "성별"
		RETURN 牝性
	CASE "튜너"
		RETURN 1
	CASE "추가日"
		RETURN 250823
ENDSELECT

RETURN 0

@CARDSUMMON_42270(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要レベル数 = 5
IF EX参照 == 0 || EX参照 == 1
	素材条件名前 = 킬러튠 레코
ELSEIF EX参照 >= 10
	素材条件튜너 = 1
ENDIF

IF EX参照 == 1
	CALL SYNCAN(듀얼리스트,소환カード,42270) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_42270(종류)
#DIM DYNAMIC 종류

PRINTL "킬러튠 레코"＋튜너 １장 이상
PRINTL 이 카드명의 ③의 효과는 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ①：이 카드의 공격력은、서로의 묘지의 튜너의 수×３００ 올린다。
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ②：원래 공격력이 １７００ 이하인 상대 필드의 몬스터의 레벨은 １개 오른다。
CALLFORM CARD_ABLE(3,필드起動,フリー誘発)
PRINTL ③：자신 / 상대의 메인 페이즈에、자신의 묘지에서 튜너 １장을 제외하고、
PRINTL 상대 필드의 앞면 표시 카드 １장을 대상으로 발동할 수 있다。
PRINTL 그 카드의 효과를 턴 종료시까지 무효로 한다。


@CARDCAN_42270(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,42270)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_3(듀얼리스트,42270)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件튜너 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	효과条件표시 = 表側표시
	효과条件除去種 = 無効
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_42270(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,42270,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件튜너 = 1
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲상대필드 = 1
			효과範囲상대마함 = 1
			효과条件표시 = 表側표시
			CALL CARD_REMOVAL_ADV(듀얼리스트,"単体除去",場所,"無効")
		ENDIF
	ENDIF

ELSEIF 종류 == 攻守変動
	CALL FULLRESET
	효과範囲묘지 = 1
	효과範囲상대묘지 = 1
	효과条件튜너 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	攻撃力修正 = RESULT * 300
	CALL STATCHANGE_BASIC(듀얼리스트,"自身",場所,攻撃力修正)
	
	FOR カウンタ,0,5
		SIF GETVAR(@"%対面者%_필드:カウンタ") == -1
			CONTINUE
		SIF GETVAR(@"%対面者%_MZ_モ耐:カウンタ") >= 1
			CONTINUE
		SIF GETVAR(@"%対面者%_MZ_無差耐:カウンタ") >= 1
			CONTINUE
		TRYCALLFORM CARD_{GETVAR(@"%対面者%_필드:カウンタ")},"攻撃力"
		SIF RESULT > 1700
			CONTINUE
		CALL STATCHANGE_OTHER(対面者,"自身",カウンタ,"星",1)
	NEXT
ENDIF
