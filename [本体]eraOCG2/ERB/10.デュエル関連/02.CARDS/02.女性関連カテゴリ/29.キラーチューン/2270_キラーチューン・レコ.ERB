@CARDNAME_2270,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 킬러튠 레코
	CASE "略称"
		RESULTS = ＫＴ 레코
	CASE "カテゴリ"
		RESULTS:0 = 킬러튠
ENDSELECT


@CARD_2270(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 1500
	CASE "守備力"
		RETURN 800
	CASE "성별"
		RETURN 牝性
	CASE "튜너"
		RETURN 1
	CASE "추가日"
		RETURN 250823
ENDSELECT

RETURN 0

@CARD_EXPLANATION_2270(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,소환時,특수소환時)
PRINTL ①：이 카드가 소환 / 특수소환된 경우에 발동할 수 있다。
PRINTL 자신의 덱 / 묘지에서 레벨 ３몬스터 이외의 "킬러튠" 몬스터 １장을 패에 넣는다。
CALLFORM CARD_ABLE(2,S素材時효과)
PRINTL ②：이 카드가 Ｓ소재로서 묘지로 보내진 경우、
PRINTL 상대 필드의 마법 / 함정 카드 １장을 대상으로 하고 발동할 수 있다。
PRINTL 그 카드를 파괴한다。
CALLFORM CARD_ABLE(10,필드起動)
PRINTL ③：１턴에 １번、자신 메인 페이즈에 발동할 수 있다。
PRINTL 패의 튜너 １장과 필드의 이 카드를 Ｓ소재로서 묘지로 보내고、
PRINTL ＥＸ덱에서 Ｓ몬스터 １장을 Ｓ소환 한다。
PRINTL 이 효과는、이 카드의 효과가 무효가 되어있거나 발동할수 없는 상태에서도 발동가능하며、
PRINTL 상대는 이 효과에 대해 카드의 효과를 발동할 수 없다。


@CARDCAN_2270(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

IF 종류 == 필드起動
	;裏だったらダメ
	SIF GETVAR(@"%듀얼리스트%_MZ_表:場所") == 3
		RETURN 0
	;素材불가だったらダメ
	SIF GETVAR(@"%듀얼리스트%_MZ_不材:場所") >= 1
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL KILLERTUNE_CHECK(듀얼리스트,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,2270)
SIF RESULT == 1
	RETURN 0

IF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,2270)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 킬러튠
	CALLFORM 효과条件レベル_ADV(3,"以外")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == S素材時효과
	SIF 발동済_2(듀얼리스트,2270)
		RETURN 0
	CALL FULLRESET
	효과範囲상대마함 = 1
	효과条件除去種 = 묘지
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_2270(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC EXカードID,50
#DIM DYNAMIC 必要レベル

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,2270,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과範囲묘지 = 1
		효과条件カテゴリ = 킬러튠
		CALLFORM 효과条件レベル_ADV(3,"以外")
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == S素材時효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,2270,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲상대마함 = 1
		CALL CARD_REMOVAL_ADV(듀얼리스트,"単体除去",場所,"묘지")
	ENDIF

ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,EX덱起動,ゾーン,場所,2270,10)
	IF RESULT == 0
		CALL KILLERTUNE_CHECK(듀얼리스트,場所)
		IF RESULT == 1
			FOR カウンタ,0,50
				EXカードID:カウンタ = RESULT:(カウンタ+1)
			NEXT

			CALL FULLRESET
			FOR カウンタ,0,50
				효과条件ID:カウンタ = EXカードID:カウンタ
			NEXT
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			IF RESULT != 9999
				選択カード = RESULT
				CALL KILLERTUNE_SYNSUMMON(듀얼리스트,ゾーン,場所,選択カード)
			ENDIF
		ENDIF
	ENDIF
ENDIF

@KILLERTUNE_CHECK(듀얼리스트,場所,튜너플래그)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 場所
#DIM DYNAMIC 튜너플래그
#DIM DYNAMIC EXカードID
#DIM DYNAMIC SS可能カードID,99
#DIM DYNAMIC 싱크로レベル
#DIM DYNAMIC 必要レベル
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 素材条件달성
#DIM DYNAMIC カウンタ

;カード無くなってたら終わり
SIF GETVAR(@"%듀얼리스트%_필드:場所") == -1
	RETURN 0

FOR カウンタ,0,現EX山札数(듀얼리스트)
	VARSET EXカードID

	EXカードID = GETVAR(@"%듀얼리스트%_EX덱:カウンタ")
	SIF EXカードID == -1
		CONTINUE
	TRYCALLFORM CARD_{EXカードID},"종류"
	SIF RESULT != 싱크로
		CONTINUE
	TRYCALLFORM CARDNAME_{EXカードID},"カテゴリ"
	SIF !MATCH(RESULTS,"튜너소재가능")
		CONTINUE
	CALL CARD_SS_CHECK(듀얼리스트,EXカードID,"EX덱",1)
	SIF RESULT == 1
		CONTINUE
	IF 튜너플래그 == 1
		TRYCALLFORM CARD_{EXカードID},"튜너"
		SIF RESULT < 1
			CONTINUE
	ENDIF

	;素材지정
	SELECTCASE EXカードID
		CASE 42270
			TRYCALLFORM CARDNAME_{GETVAR(@"%듀얼리스트%_필드:場所")},"名前"
			SIF RESULTS == "킬러튠 레코"
				素材条件달성 = 1
			IF 素材条件달성 != 1 && GETVAR(@"%듀얼리스트%_MZ_星:場所") == 2
				CALL FULLRESET
				효과範囲손패 = 1
				효과条件名前 = 킬러튠 레코
				CALL CARD_CHECK_ADV(듀얼리스트,0,"",場所)
				IF RESULT == 1
					素材条件달성 = 1
				ENDIF
			ENDIF
		CASE 42271
			TRYCALLFORM CARDNAME_{GETVAR(@"%듀얼리스트%_필드:場所")},"名前"
			SIF RESULTS == "킬러튠 믹스"
				素材条件달성 = 1
			IF 素材条件달성 != 1 && GETVAR(@"%듀얼리스트%_MZ_星:場所") == 3
				CALL FULLRESET
				효과範囲손패 = 1
				효과条件名前 = 킬러튠 믹스
				CALL CARD_CHECK_ADV(듀얼리스트,0,"",場所)
				IF RESULT == 1
					素材条件달성 = 1
				ENDIF
			ENDIF
		CASE 41949
			SIF GETVAR(@"%듀얼리스트%_MZ_属:場所") == 빛속성
				素材条件달성 = 1
			IF 素材条件달성 != 1
				CALL FULLRESET
				효과範囲손패 = 1
				효과条件속성 = 빛속성
				효과条件튜너 = 1
				CALL CARD_CHECK_ADV(듀얼리스트,0,"",場所)
				IF RESULT == 1
					素材条件달성 = 1
				ENDIF
			ENDIF
		CASEELSE
			素材条件달성 = 1
	ENDSELECT
	SIF 素材条件달성 != 1
		CONTINUE

	;Ｓモンスターのレベル취득
	TRYCALLFORM CARD_{EXカードID},"レベル"
	싱크로レベル = RESULT

	;손패の素材にできるレベルを算出
	必要レベル = 싱크로レベル - GETVAR(@"%듀얼리스트%_MZ_星:場所")
	;マイナス行ったらおしまい
	SIF 必要レベル < 1
		CONTINUE

	;손패チェック
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종류 = モン全般
	효과条件튜너 = 1
	효과条件レベル = 必要レベル
	CALL CARD_CHECK_ADV(듀얼리스트,0,"",場所)
	SIF RESULT != 1
		CONTINUE
	
	SS可能カードID:カウンタ = EXカードID
	条件달성 ++
NEXT

SIF 条件달성 == 0
	RETURN 0

FOR カウンタ,0,現EX山札数(듀얼리스트)
	RESULT:(カウンタ+1) = SS可能カードID:カウンタ
NEXT
RETURN 1



@KILLERTUNE_SYNSUMMON(듀얼리스트,ゾーン,場所,選択カード)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 起動カードID
#DIM DYNAMIC 必要レベル
#DIMS DYNAMIC 必要素材名前
#DIM DYNAMIC 必要素材속성

TRYCALLFORM CARD_{GETVAR(@"%듀얼리스트%_EX덱:選択カード")},"レベル"
必要レベル = RESULT - GETVAR(@"%듀얼리스트%_MZ_星:場所")
;マイナス行ったらおしまい
SIF 必要レベル < 1
	RETURN 0

起動カードID = GETVAR(@"%듀얼리스트%_필드:場所")

;素材지정
SELECTCASE GETVAR(@"%듀얼리스트%_EX덱:選択カード")
	CASE 42270
		TRYCALLFORM CARDNAME_{起動カードID},"名前"
		SIF RESULTS != "킬러튠 레코"
			必要素材名前 = 킬러튠 레코
	CASE 42271
		TRYCALLFORM CARDNAME_{起動カードID},"名前"
		SIF RESULTS != "킬러튠 믹스"
			必要素材名前 = 킬러튠 믹스
	;アムリターラ
	CASE 41949
		SIF GETVAR(@"%듀얼리스트%_MZ_属:場所") != 빛속성
			必要素材속성 = 빛속성
ENDSELECT

CALL FULLRESET
효과範囲손패 = 1
효과条件レベル = 必要レベル
효과条件튜너 = 1
SIF 必要素材名前 != ""
	효과条件名前 = %必要素材名前%
SIF 必要素材속성 != 0
	효과条件속성 = 必要素材속성
CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
SIF RESULT != 1
	RETURN 0
CALLFORM CARD_STANBY(RESULT:1,듀얼리스트,S素材時효과,"묘지")
CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
CALLFORM CARD_STANBY(起動カードID,듀얼리스트,S素材時효과,"묘지")

CALL FULLRESET
CALL CARD_EX_SUMMON(듀얼리스트,"EX덱",選択カード)
リザルト：SYN소환 = 1

RETURN 1
