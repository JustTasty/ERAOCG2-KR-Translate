@CARDNAME_2273,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 킬러튠 큐
	CASE "略称"
		RESULTS = ＫＴ 큐
	CASE "カテゴリ"
		RESULTS:0 = 킬러튠
ENDSELECT


@CARD_2273(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 900
	CASE "守備力"
		RETURN 1500
	CASE "성별"
		RETURN 牝性
	CASE "튜너"
		RETURN 1
	CASE "추가日"
		RETURN 250823
ENDSELECT

RETURN 0

@CARD_EXPLANATION_2273(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,소환時)
PRINTL ①：이 카드가 소환된 경우에 발동할 수 있다。
PRINTL 자신의 패 / 덱 / 묘지에서 "킬러튠 큐" 이외의 튜너 １장을 특수 소환한다。
PRINTL 이 턴、자신은 튜너밖에 특수 소환할 수 없다。
CALLFORM CARD_ABLE(2,S素材時효과)
PRINTL ②：이 카드가 Ｓ소재로서 묘지로 보내진 경우에 발동할 수 있다。
PRINTL 상대의 덱 위에서 카드 ２장을 뒤집어、
PRINTL 그 안에서 １장을 제외하고、１장 더 덱 맨 위나 맨 아래로 되돌린다。
CALLFORM CARD_ABLE(10,필드起動)
PRINTL ③：１턴에 １번、자신 메인 페이즈에 발동할 수 있다。
PRINTL 패의 튜너 １장과 필드의 이 카드를 Ｓ소재로서 묘지로 보내고、
PRINTL ＥＸ덱에서 Ｓ몬스터 １장을 Ｓ소환한다。
PRINTL 이 효과는、이 카드의 효과가 무효가 되어있거나 발동할 수 없는 상태에서도 발동 가능하며、
PRINTL 상대는 이 효과에 대해서 카드의 효과를 발동할 수 없다。


@CARDCAN_2273(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動
	;裏だったらダメ
	SIF GETVAR(@"%듀얼리스트%_MZ_表:場所") == 3
		RETURN 0
	;素材불가だったらダメ
	SIF GETVAR(@"%듀얼리스트%_MZ_不材:場所") >= 1
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL KILLERTUNE_CHECK(듀얼리스트,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,2273)
SIF RESULT == 1
	RETURN 0

IF 종류 == 소환時
	SIF 발동済_1(듀얼리스트,2273)
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲묘지 = 1
	효과範囲덱 = 1
	효과条件튜너 = 1
	효과条件固名前 = 킬러튠 큐
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == S素材時효과
	SIF 발동済_2(듀얼리스트,2273)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	SIF 現山札数(対面者) < 2
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_2273(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC EXカードID,50
#DIM DYNAMIC 必要レベル
#DIM DYNAMIC 발동불가,2
#DIM DYNAMIC 덱カードID,2
#DIM DYNAMIC 選択肢
#DIM DYNAMIC カード추가先

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,2273,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲묘지 = 1
		효과範囲덱 = 1
		효과条件튜너 = 1
		효과条件固名前 = 킬러튠 큐
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		CALL SET_PLAYER_INFO(듀얼리스트,"튜너制限",1)
	ENDIF

ELSEIF 종류 == S素材時효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,2273,2)
	IF RESULT == 0
		덱カードID:0 = GETVAR(@"%対面者%_덱:0")
		덱カードID:1 = GETVAR(@"%対面者%_덱:1")
		TRYCALLFORM CARD_UI(덱カードID:0)
		TRYCALLFORM CARD_UI(덱カードID:1)

		IF 듀얼리스트 == "자신"
			PRINTL 제외할 카드를 선택해주세요。
			PRINTL
			TRYCALLFORM CARDNAME_{덱カードID:0},"名前"
			PRINTFORML [0] - %RESULTS%
			TRYCALLFORM CARDNAME_{덱カードID:1},"名前"
			PRINTFORML [1] - %RESULTS%
			BINPUT
			選択肢 = RESULT
		ELSE
			選択肢 = RAND:2
		ENDIF

		CALL CARD_DISPOSE(対面者,"덱",選択肢,제외行き)

		;選択肢を修正
		SELECTCASE 選択肢
			CASE 0
				選択肢 = 1
			CASE 1
				選択肢 = 0
		ENDSELECT

		IF 듀얼리스트 == "자신"
			TRYCALLFORM CARDNAME_{덱カードID:選択肢},"名前"
			PRINTFORML %RESULTS%를 덱 맨 아래로 되돌리시겠습니까？
			CALL YN_CHOICE(듀얼리스트)
		ELSE
			RESULT = RAND:2
		ENDIF

		IF RESULT == はい
			カード추가先 = 現山札数(対面者)
			SETVAR @"%対面者%_덱:カード추가先", 덱カードID:選択肢
			SETVAR @"%対面者%_덱:0", -1
			IF 対面者 == "상대"
				ARRAYREMOVE 상대_덱, 0, 1
			ELSE
				ARRAYREMOVE 자신_덱, 0, 1
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == 필드起動
	;悪いことします
	CALL CARD_CHOICE(듀얼리스트,EX덱起動,ゾーン,場所,2273,10)
	IF RESULT == 0
		CALL KILLERTUNE_CHECK(듀얼리스트,場所)
		IF RESULT == 1
			FOR カウンタ,0,50
				EXカードID:カウンタ = RESULT:(カウンタ+1)
			NEXT

			CALL FULLRESET
			FOR カウンタ,0,50
				효과条件ID:カウンタ = EXカードID:カウンタ
			NEXT
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			IF RESULT != 9999
				選択カード = RESULT
				CALL KILLERTUNE_SYNSUMMON(듀얼리스트,ゾーン,場所,選択カード)
			ENDIF
		ENDIF
	ENDIF
ENDIF
