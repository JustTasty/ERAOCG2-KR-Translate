@CARDNAME_42105,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 엑소시스터 마니피카
	CASE "略称"
		RESULTS = 마니피카
	CASE "カテゴリ"
		RESULTS:0 = 엑소시스터
ENDSELECT


@CARD_42105(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 전사족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 2800
	CASE "守備力"
		RETURN 2800
	CASE "성별"
		RETURN 牝性
	CASE "連動ID"
		SELECTCASE RAND:2
			CASE 0
				RESULT:1 = 2101
				RETURN 2100
			CASE 1
				RESULT:1 = 2100
				RETURN 2101
		ENDSELECT
ENDSELECT
RETURN 0
@CARDSUMMON_42105(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 2
素材条件レベル = 4
素材条件종류 = 엑시즈
素材条件カテゴリ = 엑소시스터
なんでもＸ = 1

IF EX参照 == 1
	CALL XYZCAN(듀얼리스트,소환カード,42105) 
	RETURN RESULT
ENDIF



@CARD_EXPLANATION_42105(종류)
#DIM DYNAMIC 종류

PRINTL 랭크 4"엑소시스터" 몬스터 ×２
PRINTL 이 카드명의  ②③의 효과는 각각 １턴에 １번만 사용할 수 있다。
CALLFORM CARD_ABLE(1,지속효과)
PRINTL ①：이 카드는 1번의 배틀 페이즈 중에 2회 공격할 수 있다。
CALLFORM CARD_ABLE(2,필드起動,フリー誘発)
PRINTL ②：자신 / 상대의 턴에 1번、이 카드의 엑시즈 소재를 1개 제거하고 발동할 수 있다。
PRINTL 모든 내성을 무시하고 상대 필드의 카드 1장을 고르고 제외한다。
CALLFORM CARD_ABLE(3,발동誘発)
PRINTL ③：상대가 효과를 발동했을 때 발동할 수 있다.。
PRINTL 묘지의"엑소시스터"X몬스터 １장을 EX덱으로 되돌린다。
PRINTL 그 후、자신 필드의 이 카드 위에"엑소시스터"X몬스터 １장을 겹쳐 X소환 취급으로 EX덱에서 특수 소환할 수 있다。

@CARDCAN_42105(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,42105)
SIF RESULT == 1
	RETURN 0

IF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,42105)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") <= -1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 발동誘発
	SIF 발동済_3(듀얼리스트,42105)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종류 = 엑시즈
	효과条件カテゴリ = 엑소시스터
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	RETURN 1	
ENDIF

@CARDEFFECT_42105(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"追戦",1)
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,42105,2,X消費1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 발동誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,42105,3)
	IF RESULT == 0
		CALL FULLRESET
		효과条件종류 = 엑시즈
		효과条件カテゴリ = 엑소시스터
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"묘지",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과条件종류 = 엑시즈
			효과条件カテゴリ = 엑소시스터
			효과条件任意 = 1	
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
				CALL RESETCONDITION_MAT(1)
				代替素材数 = 1
				重ねてＸ = 1
				素材条件固定 = 場所
				CALL XYZSUMMON(듀얼리스트, 選択カード)
			ENDIF
		ENDIF
	ENDIF
	RETURN 0
ENDIF

