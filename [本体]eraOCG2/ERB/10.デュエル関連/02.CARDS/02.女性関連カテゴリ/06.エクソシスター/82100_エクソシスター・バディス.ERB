@CARDNAME_82100,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 엑소시스터 바디스
	CASE "略称"
		RESULTS = EXS 바디스
	CASE "カテゴリ"
		RESULTS:0 = 엑소시스터
ENDSELECT


@CARD_82100(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
ENDSELECT

RETURN 0

@CARD_EXPLANATION_82100(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의  카드는 1턴에 1장밖에 발동할 수 없다。
CALLFORM CARD_ABLE(1,마함起動,フリー誘発)
PRINTL ①：자신 / 상대의 메인 페이즈에 800LP를 지불하고 발동할 수 있다。
PRINTL 덱에서"엑소시스터" 몬스터 １장을 고르고、
PRINTL 추가로 그 몬스터에 카드명이 쓰여진"엑소시스터" 몬스터 １장을 덱에서 고른다。
PRINTL 그 몬스터 2장을 특수 소환한다。
PRINTL 이 효과로 특수 소환한 몬스터는 엔드 페이즈에 주인의 덱으로 되돌아간다。
PRINTL 이 카드의 발동 후、턴 종료시까지 자신은"엑소시스터" 몬스터밖에 EX덱에서 특수 소환할 수 없다。

@CARDCAN_82100(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,82100)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == フリー誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF 발동済_1(듀얼리스트,82100)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 800
		RETURN 0

	SIF モンスター数(듀얼리스트) >= 4
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 엑소시스터
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_82100(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID


IF 종류 == 마함起動 || 종류 == フリー誘発
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,82100,1)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",800)
		;通常마법汎用処理
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 엑소시스터
		효과추가종류 = 10 + 덱行き
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT
			CALL FULLRESET
			효과範囲덱 = 1
			효과추가종류 = 10 + 덱行き
			選択カード_ID = GETVAR(@"%듀얼리스트%_필드:カード소환先")
			CALLFORM CARDNAME_{選択カード_ID}, "名前"
			SELECTCASE RESULTS
				CASE "엑소시스터 에리스"
					효과条件名前:0 = 엑소시스터 스텔라
					효과条件名前:1 = 엑소시스터 마르파
				CASE "엑소시스터 스텔라"
					효과条件名前:0 = 엑소시스터 에리스
				CASE "엑소시스터 이렌느"
					효과条件名前:0 = 엑소시스터 소피아
				CASE "엑소시스터 소피아"
					효과条件名前:0 = 엑소시스터 이렌느
				CASE "엑소시스터 마르파"
					효과条件名前:0 = 엑소시스터 에리스
			ENDSELECT
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			CALL STATCHANGE_EFFECT(듀얼리스트,"자신",カード소환先,"終処",덱行き)
			CALL CARD_EX_RESTRICTION(듀얼리스트,"엑소시스터")
		ENDIF
	ENDIF
ENDIF
