@CARDNAME_1377,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 알로메루스의 충혹마
	CASE "略称"
		RESULTS = 알로메루스
	CASE "カテゴリ"
		RESULTS:0 = 충혹마
ENDSELECT


@CARD_1377(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 곤충족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 2200
	CASE "守備力"
		RETURN 600
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARDSUMMON_1377(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 2
素材条件종족:0 = 곤충족
素材条件종족:1 = 식물족
素材条件レベル:0 = 4

IF EX参照 == 1
	CALL XYZCAN(듀얼리스트,소환カード,1377) 
	RETURN RESULT
ENDIF

@CARD_EXPLANATION_1377(종류)
#DIM DYNAMIC 종류

PRINTL 식물족 / 곤충족 레벨 4 몬스터 × 2
PRINTL 이 카드명의  ②③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ①：엑시즈 소재를 가진 이 카드는 함정 내성을 가진다.
CALLFORM CARD_ABLE(1,필드起動)
PRINTL ②：이 카드의 엑시즈 소재를 2개 제거하고 발동할 수 있다.
PRINTL 자신의 묘지에서 곤충족 / 식물족 레벨 4 몬스터 1장을 특수 소환한다.
CALLFORM CARD_ABLE(2,효과後誘発)
PRINTL ③：자신의 일반 함정 카드의 효과로 상대 몬스터가 필드를 벗어났을 경우,
PRINTL 이 카드의 엑시즈 소재를 1개 제거하고, 상대 묘지의 몬스터 1장을 대상으로 발동할 수 있다.
PRINTL 그 몬스터를 자신 필드에 특수 소환한다.



@CARDCAN_1377(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,1377)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動
	SIF 발동済_1(듀얼리스트,1377)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") < 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종족:0 = 곤충족
	효과条件종족:1 = 식물족
	효과条件レベル = 4
	효과条件종류 = レベル持ち
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF 발동済_2(듀얼리스트,1377)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") <= -1
		RETURN 0
	CALL FULLRESET
	효과条件종류 = 함정全般
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"除去成功")
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대묘지 = 1
	효과条件종류 = モン全般
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1	
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") <= -1
		RETURN 0
	RETURN 1
ENDIF




@CARDEFFECT_1377(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1377,1,X消費2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件종족:0  = 곤충족
		효과条件종족:1  = 식물족
		효과条件レベル = 4
		효과条件종류 = レベル持ち
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1377,2,X消費1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲상대묘지 = 1
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"자신",場所,"함정耐")
ENDIF