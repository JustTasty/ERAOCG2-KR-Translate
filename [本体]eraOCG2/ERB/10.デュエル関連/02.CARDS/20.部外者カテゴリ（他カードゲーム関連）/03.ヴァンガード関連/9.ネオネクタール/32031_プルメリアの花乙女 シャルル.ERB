@CARDNAME_32031,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = プルメリアの花乙女 シャルル
	CASE "略称"
		RESULTS = シャルル
	CASE "カテゴリ"
		RESULTS:0 = 네오 넥타르
		RESULTS:4 = 뱅가드
ENDSELECT


@CARD_32031(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 식물족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1700
	CASE "守備力"
		RETURN 800
ENDSELECT

RETURN 0

@CARD_EXPLANATION_32031(종류)
#DIM DYNAMIC 종류


PRINTL このカード名の①②の효과はそれぞれ１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①：このカードが손패に存在する場合、EX덱の「アーシャ」モンスター１体を상대に見せて발동できる。
PRINTL このカードを손패から특수소환する。このターン、자신は식물족モンスターしか특수소환できない。
CALLFORM CARD_ABLE(2,소환時,특수소환時)
PRINTL ②：このカードが소환・특수소환した場合に발동できる。
PRINTL 자신の덱の上からカードを７枚めくる。
PRINTL その中から同名カードが자신フィールドに存在する「네오 넥타르」モンスター１体を특수소환できる。
PRINTL 残りのカードは덱に戻す。

@CARDCAN_32031(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,32031)
SIF RESULT == 1
	RETURN 0

IF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,32031)
		RETURN 0
	RETURN 1

ELSEIF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,32031)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,32031,"손패")
	SIF RESULT == 1
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件カテゴリ = アーシャ
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_32031(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 選択カード名
#DIMS DYNAMIC 存在カード名,100

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,32031,2)
	IF RESULT == 0
		FOR カウンタ,0,5
			SIF GETVAR(@"%듀얼리스트%_필드:カウンタ") == -1
				CONTINUE
			TRYCALLFORM CARDNAME_{GETVAR(@"%듀얼리스트%_필드:カウンタ")},"カテゴリ"
			SIF !MATCH(RESULTS,"네오 넥타르")
				CONTINUE
			TRYCALLFORM CARD_{GETVAR(@"%듀얼리스트%_필드:カウンタ")},"종류"
			SIF !INRANGE(RESULT,0,9)
				CONTINUE
			TRYCALLFORM CARDNAME_{GETVAR(@"%듀얼리스트%_필드:カウンタ")},"名前"
			選択カード名 = %RESULTS%
			SIF MATCH(存在カード名,選択カード名)
				CONTINUE
			存在カード名:カウンタB = %選択カード名%
			カウンタB ++
			SIF カウンタB >= 5
				BREAK
		NEXT
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 네오 넥타르
		효과条件종류 = モン全般
		효과条件任意 = 1
		효과条件名前:0 = 仮置き
		FOR カウンタ,0,20
			효과条件名前:カウンタ = %存在カード名:カウンタ%
		NEXT
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,,7)
	ENDIF
ELSEIF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,32031,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = アーシャ
		CALL CARD_CHOOSE_BASIC(듀얼리스트, "EX덱",ゾーン,場所)	
		IF RESULT != 9999
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
			SS소환可能종족 = 식물족
			CALL CARD_SS_RESTRICTION(듀얼리스트)
		ENDIF
	ENDIF
ENDIF

