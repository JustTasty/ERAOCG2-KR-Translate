@CARDNAME_71091,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 여신재림
	CASE "略称"
		RESULTS = 여신재림
	CASE "カテゴリ"
		RESULTS:0 = 제네시스
		RESULTS:1 = 신기
		RESULTS:4 = 뱅가드
ENDSELECT


@CARD_71091(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
		RETURN 速攻마법
	CASE "生成カード"
		RETURN 171091
ENDSELECT

RETURN 0

@CARD_EXPLANATION_71091(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 1턴에 각각 1번만 사용할 수 있다.
CALL CARD_ABLE(1,마함起動 ,손패起動 ,フリー誘発)
PRINTL ①: 패 / 필드에서 '신기' 카드 1장을 제외하고,
PRINTL 이하의 효과 중에서 1개를 선택하여 발동할 수 있다.
PRINTL  ●자신의 제외 상태의 '신기' 몬스터 1장을 선택하여 특수 소환한다.
PRINTL  ●상대 필드의 몬스터 1장을 선택하여 파괴한다.
PRINTL  ●'신기' S 몬스터 1장의 S 소환을 한다.
CALLFORM CARD_ABLE(2,제외時)
PRINTL ②: 이 카드가 제외되었을 경우에 발동할 수 있다.
PRINTL 자신의 필드에 '신기' 토큰 1장을 특수 소환한다.
PRINTL
PRINTL '신기 토큰'
PRINTL (천사족 / 튜너 / 빛 / 레벨 1 / 공격력 0 / 수비력 0)

@CARDCAN_71091(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,71091)
SIF RESULT == 1
	RETURN 0


IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,71091)
		RETURN 0
	IF ゾーン == "손패"
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 신기
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 제외時
	SIF 발동済_2(듀얼리스트,71091)
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,171091,"")
	SIF RESULT == 1
		RETURN 0	
	RETURN 1
ENDIF

@CARDEFFECT_71091(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件달성,3
#DIM DYNAMIC 選択효과
#DIM DYNAMIC 소환カード
#DIM DYNAMIC 소환カードID

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71091,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件他所 = 1
		효과条件カテゴリ = 신기
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲제외 = 1
			효과条件カテゴリ = 신기
			효과条件종류 = モン全般
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				条件달성:0 = 1
			CALL FULLRESET
			효과範囲상대필드 = 1
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				条件달성:1 = 1
			CALL FULLRESET
			효과範囲EX덱 = 1
			효과条件カテゴリ = 신기
			효과条件종류 = 싱크로
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
			条件달성:2 = 1

			IF 듀얼리스트 == "자신"
				PRINTL 효과를 選択してください
				PRINTL 
				IF 条件달성:0 == 1
					PRINTL [1] - 자신의 제외상태인 「신기」몬스터 １장을 選んで특수 소환한다。
				ELSE
					SETCOLOR C_GRAY
					PRINTL ［X］- 제외상태인 「신기」몬스터가 いません。
					RESETCOLOR
				ENDIF
				IF 条件달성:1 == 1
					PRINTL [2] - 상대 필드의 몬스터 １장을 選んで파괴한다。
				ELSE
					SETCOLOR C_GRAY
					PRINTL ［X］- 상대 필드에 몬스터가 いません。
					RESETCOLOR
				ENDIF
				IF 条件달성:2 == 1
					PRINTL [3] - 「신기」S몬스터 １장의 S소환을 한다。
				ELSE
					SETCOLOR C_GRAY
					PRINTL ［X］- EX덱に「신기」S몬스터가 いません。
					RESETCOLOR
				ENDIF
				BINPUT
				選択효과 = RESULT
			ELSE
				CALL FULLRESET
				選択효과 = 1
			ENDIF
			IF 選択효과 == 1
				CALL FULLRESET
				효과範囲제외 = 1
				효과条件カテゴリ = 신기
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			ELSEIF 選択효과 == 2
				CALL FULLRESET
				CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"묘지",71091)
			ELSEIF 選択효과 == 3
				CALL FULLRESET
				효과範囲EX덱 = 1
				효과条件カテゴリ = 신기
				효과条件종류 = 싱크로
				효과条件소환可 = 1
				CALL CARD_CHOOSE_BASIC(듀얼리스트, "EX덱",ゾーン,場所)	
				소환カード = RESULT
				IF 소환カード != 9999
					소환カードID = RESULT:99
					CALLFORM CARD_{소환カードID}, "종류"
					IF RESULT == 싱크로
						TRYCALLFORM CARDSUMMON_{소환カードID}(듀얼리스트,소환カード)
						CALL SYNSUMMON(듀얼리스트, 소환カード)	
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 제외時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71091,2)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_GENERATE(듀얼리스트,171091)
	ENDIF
ENDIF




@CARDNAME_171091,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 신기 토큰
	CASE "略称"
		RESULTS = 신기 토큰
	CASE "カテゴリ"
		RESULTS:0 = 제네시스
		RESULTS:1 = 신기
		RESULTS:4 = 뱅가드
ENDSELECT


@CARD_171091(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 通常モン
	CASE "トークン"
		RETURN 1
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
	CASE "성별"
		RETURN 牝性
	CASE "튜너"
		RETURN 1
ENDSELECT

@CARD_EXPLANATION_171091(종류)
#DIM DYNAMIC 종류

PRINTL ①: 필드를 벗어났을 때에 소멸한다.


@CARDCAN_171091(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,171091)
SIF RESULT == 1
	RETURN 0

IF 종류 == 지속효과
	SIF ゾーン == "필드"
		RETURN 0
	RETURN 1
ENDIF

RETURN 0



@CARDEFFECT_171091(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード


IF 종류 == 지속효과
	CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,소멸行き)
ENDIF