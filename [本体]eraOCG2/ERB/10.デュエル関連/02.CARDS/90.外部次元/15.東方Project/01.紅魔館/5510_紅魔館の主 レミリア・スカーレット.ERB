@CARDNAME_5510,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 홍마관의 주인 레밀리아 스칼렛
	CASE "略称"
		RESULTS = 홍마관의레밀리아
	CASE "カテゴリ"
		RESULTS:0 = 홍마관
		RESULTS:1 = 환상소녀
		RESULTS:2 = 뱀파이어
		RESULTS:4 = 동방 Project
ENDSELECT

@CARD_5510(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
		RETURN 링크
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 언데드족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 2000
	CASE "성별"
		RETURN 牝性
	CASE "추가日"
		RETURN 250705
ENDSELECT
RETURN 0

@CARD_EXPLANATION_5510(종류)
#DIM DYNAMIC 종류

PRINTL "홍마관" 몬스터를 포함하는 몬스터 ２장 이상
PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALL CARD_ABLE(1,EX소환時,특수소환時)
PRINTL ①：이 카드가 특수 소환에 성공한 경우에 발동할 수 있다。
PRINTL 자신의 패 / 묘지의 "홍마관" 몬스터 １장을 특수 소환한다。
CALL CARD_ABLE(2,필드起動,フリー誘発)
PRINTL ②：자신 / 상대 턴에 발동할 수 있다。덱의 위에서 ６장을 보고、그 안에서 １장을 고른다。
PRINTL 그 카드를 덱의 위에 둔다。다음 드로우 페이즈의 일반 드로우는 ２장이 된다。
PRINTL 이렇게해서 "홍마관" 카드를 선택한 경우、자신 / 상대 필드의 카드를 １장 파괴할 수 있다。
CALL CARD_ABLE(0,攻撃時強制誘発)
PRINTL ③：이 카드가 몬스터를 공격한 경우、
PRINTL 그 수비력을 공격력이 넘었다면、상대 몬스터를 파괴하고、이 카드의 공격력 만큼의 데미지를 준다。




@CARDCAN_5510(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,5510)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX소환時 || 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,5510)
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲묘지 = 1
	효과条件종류 = モン全般
	효과条件カテゴリ = 홍마관
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF 발동済_2(듀얼리스트,5510)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃時強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	SIF GETVAR(@"%対面者%_MZ_守:戦闘場所(듀얼리스트,1)") >= GETVAR(@"%듀얼리스트%_MZ_攻:戦闘場所(듀얼리스트)")
		RETURN 0
	RETURN 1
ENDIF
@CARDSUMMON_5510(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要링크数 = 3

IF EX参照 == 0 || EX参照 == 1
	素材条件カテゴリ = 홍마관
ELSE
ENDIF

IF EX参照 == 1
	CALL LINCAN(듀얼리스트,소환カード,5510) 
	RETURN RESULT
ENDIF


@CARDEFFECT_5510(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 合計ダメージ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == EX소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,5510,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲묘지 = 1
		효과条件종류 = モン全般
		효과条件カテゴリ = 홍마관
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,5510,2)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"덱",ゾーン,場所,6)
		選択カード = RESULT
		IF 選択カード != 9999
			選択カード_ID = RESULT:99
			CALL CARD_ADD_DECK_SPC(듀얼리스트,"덱",選択カード)
			CALL SET_PLAYER_INFO(듀얼리스트,"ドロー倍化",1)
			TRYCALLFORM CARDNAME_{選択カード_ID}("カテゴリ")
			IF MATCH(RESULTS,"홍마관")
				CALL FULLRESET
				효과条件任意 = 1
				CALL CARD_REMOVAL_BASIC(듀얼리스트,"両全場単体",場所,"묘지")
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 攻撃時強制誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,5510)
	IF RESULT == 0
		選択カード = 戦闘場所(듀얼리스트,1)
		合計ダメージ = GETVAR(@"%듀얼리스트%_MZ_攻:戦闘場所(듀얼리스트)")
		CALL FULLRESET
		효과추가종류 = 3
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",選択カード,"묘지",合計ダメージ)
	ENDIF
ENDIF


