@CARDNAME_65520,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 백옥루-Haunted Jade Shrine-
	CASE "略称"
		RESULTS = 『백옥루』
	CASE "カテゴリ"
		RESULTS:0 = 백옥루
		RESULTS:4 = 동방 Project
ENDSELECT


@CARD_65520(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "種類"
		RETURN フィールド
	CASE "追加日"
		RETURN 250808
ENDSELECT

RETURN 0

@CARD_EXPLANATION_65520(種類)
#DIM DYNAMIC 種類

PRINTL 自分フィールドは「백옥루」となる。
PRINTL このカード名の①の効果は１ターンに１度しか使用できない。
;気まぐれでアンデットや友達を呼ぶ
CALLFORM CARD_ABLE(1,魔罠起動)
PRINTL ①：自分メインフェイズに手札からアンデット族モンスターを墓地に送って発動できる。
PRINTL デッキからレベル４のアンデット族モンスター１体を手札に加える。
PRINTL 自分フィールドにレベル８以上のアンデット族モンスターが存在する場合、代わりに「환상소녀」モンスター１体を手札に加えてもよい。
CALLFORM CARD_ABLE(2,ターン終了時)
PRINTL ②：お互いのターン終了時に発動できる。
PRINTL 自分フィールドのアンデット族モンスター１体を選ぶ。
PRINTL 自分の墓地に存在する、そのカードの元々のレベル未満のアンデット族モンスター１体を特殊召喚する。
;邪魔するお化けたち
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ③：相手フィールドのモンスターの攻撃力は、お互いの墓地のアンデット族モンスターの数の合計×１００ポイントダウンする。



@CARDCAN_65520(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 効果使用不可,3
CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,65520)
SIF RESULT == 1
	RETURN 0


IF 種類 == 魔罠起動 && GETVAR(@"%決闘者%_魔_表:場所") == 1
	SIF 発動済_1(決闘者,65520)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL FULLRESET
	効果範囲手札 = 1
	効果条件種族 = アンデット族
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件種族 = アンデット族
	CALL 効果条件レベル_ADV(8, "以上")
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	IF RESULT == 1
		CALL FULLRESET
		効果範囲デッキ = 1
		効果条件スーパーカテゴリ = 환상소녀
		効果条件スーパー種類 = モン全般
		効果条件レベル = 4
		効果条件種族 = アンデット族
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ELSE
		CALL FULLRESET
		効果範囲デッキ = 1
		効果条件レベル = 4
		効果条件種族 = アンデット族
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ENDIF
	RETURN 1
ELSEIF 種類 == 魔罠起動 || 種類 == 手札起動
	IF 種類 == 魔罠起動
		SIF GETVAR(@"%決闘者%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	RETURN 1
ELSEIF 種類 == ターン終了時
	SIF 発動済_2(決闘者,65520)
		RETURN 0
	SIF ゾーン != "魔罠"
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件種族 = アンデット族
	CALL 効果条件レベル_ADV(2, "以上")
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件種族 = アンデット族
	効果条件種類 = レベル持ち
	効果条件召喚可 = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 永続効果 || 種類 == 攻守変動
	SIF ゾーン != "魔罠"
		RETURN 0
	SIF GETVAR(@"%決闘者%_魔_表:場所") >= 2
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_65520(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件達成
#DIM DYNAMIC 効果使用不可,3
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択レベル


IF 種類 == 魔罠起動 && GETVAR(@"%決闘者%_魔_表:場所") == 1
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,65520,1)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲戦場 = 1
		効果条件種族 = アンデット族
		CALL 効果条件レベル_ADV(8, "以上")
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT == 1
			条件達成 = 1
		CALL FULLRESET
		効果範囲手札 = 1
		効果条件種族 = アンデット族
		CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			効果範囲デッキ = 1
			効果条件レベル = 4
			効果条件種族 = アンデット族
			IF 条件達成 == 1
				効果条件スーパーカテゴリ = 환상소녀
				効果条件スーパー種類 = モン全般
			ENDIF
			CALL CARD_ADD_BASIC(決闘者,"デッキ",ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 種類 == 魔罠起動 || 種類 == 手札起動
	;カード発動＆設置まで
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,65520)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所,魔罠行き)
	ENDIF
ELSEIF 種類 == ターン終了時
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,65520,2)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲戦場 = 1
		効果条件種族 = アンデット族
		CALL 効果条件レベル_ADV(2, "以上")
		CALL CARD_CHOOSE_ADV(決闘者,ゾーン,場所)	
		選択カード = RESULT
		IF 選択カード != 9999
			TRYCALLFORM CARD_{RESULT:99},"レベル"
			選択レベル = RESULT
			CALL FULLRESET
			効果範囲墓地 = 1
			効果条件種族 = アンデット族
			効果条件種類 = レベル持ち
			CALL 効果条件レベル_ADV(選択レベル - 1, "以下")
			CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 種類 == 攻守変動
	CALL FULLRESET
	効果範囲墓地 = 1
	効果範囲相手墓地 = 1
	効果条件種族 = アンデット族
	効果条件CNT = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	攻撃力修正 = RESULT * 100

	CALL FULLRESET
	CALL STATCHANGE_BASIC(決闘者,"相手戦場",場所,-攻撃力修正,0,65520)
ELSEIF 種類 == 永続効果
	CALL SET_FIELD_MAGIC_INFO(決闘者, "백옥루")

ENDIF
