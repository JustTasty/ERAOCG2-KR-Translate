@CARDNAME_4547,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 연환마대람진 호연작
	CASE "略称"　
		RESULTS = 호연작
	CASE "カテゴリ"
		RESULTS:0 = 영령
		RESULTS:1 = 어쌔신
		RESULTS:2 = FATE
ENDSELECT


@CARD_4547(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "種類"
		RETURN 効果モン
	CASE "属性"
		RETURN 闇属性
	CASE "種族"
		RETURN 悪魔族
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 2300
	CASE "守備力"
		RETURN 2000
	CASE "性別"
		RETURN 牝性
	CASE "追加日"
		RETURN 250815
ENDSELECT


@CARD_EXPLANATION_4547(種類)
#DIM DYNAMIC 種類

PRINTL このカード名の①の効果は１ターンに１度しか使用できない。
;承認欲求の塊なので、1番が好き。雑に扱われると除外される。
CALLFORM CARD_ABLE(1,手札起動)
PRINTL ①：このターンほかのカードの効果を発動していなかった場合に発動できる。
PRINTL 手札のこのカードを特殊召喚する。
PRINTL この方法で特殊召喚したこのカードはフィールドを離れた場合除外される。
;宝具パワー、条件としては好感度
CALLFORM CARD_ABLE(2,攻撃時強制誘発)
PRINTL ②：このカードが攻撃した場合に強制的に発動する。
PRINTL このカードの攻撃力は倍になる……事がある。
;早すぎる距離の詰め方！　好感度UP！
CALLFORM CARD_ABLE(2,攻撃後強制誘発)
PRINTL ③：このカードの直接攻撃によって相手ライフを０にした場合に強制的に発動する。
PRINTL このカードの好感度は１０００アップする。




@CARDCAN_4547(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC 効果制限達成
#DIM DYNAMIC カウンタ

CALL 対面者判定(決闘者)
対面者 = %RESULTS%



IF 種類 == 攻撃後強制誘発
	SIF 決闘者 != "自分"
		RETURN 0
	SIF ゾーン != "戦場"
		RETURN 0
	SIF 戦闘場所(決闘者) != 場所
		RETURN 0
	SIF 戦闘場所(決闘者,1) != 5
		RETURN 0
	SIF ライフ_相手 >= 1
		RETURN 0
	RETURN 1
ENDIF

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,4547)
SIF RESULT == 1
	RETURN 0
IF 種類 == 手札起動
	SIF 発動済_1(決闘者,4547)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	SIF GETVAR(@"%決闘者%_発動数") != 0
		RETURN 0
	CALL CARD_SS_CHECK(決闘者,4547,"手札")
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 攻撃時強制誘発
	SIF ゾーン != "戦場"
		RETURN 0
	SIF 戦闘場所(決闘者) != 場所
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_4547(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 破壊カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 失敗率
#DIM DYNAMIC キャラ_ID
#DIM DYNAMIC 攻撃力修正
CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 手札起動
	;カード発動確認
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,4547,1)
	IF RESULT == 0
		CALL FULLRESET
		効果追加種類 = 2
		CALL CARD_SP_SUMMON(決闘者, ゾーン, 場所)
	ENDIF
ELSEIF 種類 == 攻撃時強制誘発
	キャラ_ID = NO_TO_CHARANUM(4547)
	;素で80％くらいの失敗率があり、好感度分下がる。
	失敗率 = 8000 - CFLAG:キャラ_ID:好感度
	IF 失敗率 >= 10000
		失敗率 = 10000
	ELSEIF 失敗率 <= 0
		失敗率 = 0
	ENDIF
	IF RAND:10000 + 1 >= 失敗率
		SETCOLOR C_GOLD
		PRINTFORML >>——MESSAGE:——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————>>>
		PRINTL
		PRINTFORML 「天に威星！　地に鉄鎖！　今、私が輝く時が来た～！
		PRINTFORML 　　私ってば最高ーーー！！！……ですよね？」
		PRINTL
		CUSTOMDRAWLINE ―
		RESETCOLOR
		攻撃力修正 = GETVAR(@"%決闘者%_MZ_攻:場所")
		CALL SET_FIELD_INFO(決闘者,"戦場",場所,"永攻",攻撃力修正)
	ELSE
		SETCOLOR C_GOLD
		PRINTFORML >>——MESSAGE:——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————>>>
		PRINTL
		PRINTFORML 「あっ、無理そう……出ない……ひぃ！　叱らないでマスター！　過程を褒めて！」
		PRINTL
		CUSTOMDRAWLINE ―
		RESETCOLOR
	ENDIF
ELSEIF 種類 == 攻撃後強制誘発
	キャラ_ID = NO_TO_CHARANUM(4547)
	CALL EVENT_FAVUP(キャラ_ID,1000)
	SETCOLOR C_GOLD
	PRINTFORML >>——MESSAGE:——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————>>>
	PRINTL
	PRINTFORML 「私が、トドメ……くぅ～～！！　褒めて！　もっと褒めて！
	PRINTFORML 　　かっこいい、호연작を応援よろしくお願いします！」
	PRINTL
	CUSTOMDRAWLINE ―
	RESETCOLOR
ENDIF
