@CARDNAME_4538,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 강철의 간호사 나이팅게일
	CASE "略称"
		RESULTS = 나이팅게일
	CASE "カテゴリ"
		RESULTS:0 = 영령
		RESULTS:1 = 버서커
		RESULTS:2 = FATE
ENDSELECT


@CARD_4538(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "種類"
		RETURN 効果モン
	CASE "属性"
		RETURN 地属性
	CASE "種族"
		RETURN 戦士族
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1800
	CASE "守備力"
		RETURN 1300
	CASE "性別"
		RETURN 牝性
	CASE "追加日"
		RETURN 250810
ENDSELECT

RETURN 0



@CARD_EXPLANATION_4538(種類)
#DIM DYNAMIC 種類

PRINTL このカード名の②の効果は１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(1,効果後誘発)
PRINTL ①：モンスターがフィールドから破壊された場合に発動できる。
PRINTL このカードを手札から特殊召喚する。
PRINTL その後、そのカードを守備表示で自分フィールドに特殊召喚する。
PRINTL そのカードはターン終了時まで表示形式を変更できない。

CALLFORM CARD_ABLE(0,永続効果)
PRINTL ②：このカードが自分メインフェイズにいる限り発動する。
PRINTL 自分のほかのモンスターから不調をすべて取り除く。
CALLFORM CARD_ABLE(3, ターン終了時強制)
PRINTL ③：自分ターン終了時に強制的に発動する。
PRINTL このカードは手札に戻る。

@CARDCAN_4538(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所

IF 種類 == 効果後強制誘発
	SIF ゾーン != "手札"
		RETURN 0
	CALL CARD_SS_CHECK(決闘者,4538,"手札")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	効果条件種類 = モン全般
	CALL CARD_CHECK_FIN_DATA(決闘者,"破壊全般",2)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 戦場起動
	SIF 発動済_2(決闘者,4538)
		RETURN 0
	SIF ゾーン != "戦場"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	RETURN 1
ELSEIF 種類 == ターン終了時強制
	SIF ゾーン != "戦場"
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_4538(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード_ID,10
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 対象カード名
CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 効果後強制誘発
	;カード発動確認
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,4538)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(決闘者,ゾーン, 場所)

		選択カード_ID:0 =  GETVAR(@"%決闘者%_直前破壊")
		選択カード_ID:1 =  GETVAR(@"%対面者%_直前破壊")
		CALL FULLRESET
		効果範囲墓地 = 1
		効果範囲相手墓地 = 1
		効果条件ID:0 = 999999
		効果条件ID:1 = 選択カード_ID:0
		効果条件ID:2 = 選択カード_ID:1
		効果追加種類 = 38
		CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所,"守備表示")
	ENDIF
ELSEIF 種類 == 戦場起動
	;カード発動確認
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,4538,2)
	IF RESULT == 0
		FOR カウンタ,0,5
			SIF カウンタ == 場所
				CONTINUE
			SIF GETVAR(@"%決闘者%_戦場:カウンタ") < 1
				CONTINUE
			VARSET 対象カード名
			TRYCALLFORM CARD_{GETVAR(@"%決闘者%_戦場:カウンタ")}, "性別"
			IF RESULT == 牝性 || RESULT == ふたなり
				対象カード名 = 미스
			ELSE
				対象カード名 = 미스터
			ENDIF
			TRYCALLFORM CARDNAME_{GETVAR(@"%決闘者%_戦場:カウンタ")}, "略称"
			対象カード名 = %対象カード名%・%RESULTS%
			IF RESULTS != ""
				SETCOLOR C_Ne_PINK
				PRINTFORML >>——MESSAGE:——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————>>>
				PRINTL
				PRINTFORMW 「では診察を開始します、速やかに終わらせましょう%対象カード名%」
				PRINTFORMW ・・・・・・
				IF GETVAR(@"%決闘者%_MZ_悪Ｃ:カウンタ") >= 1
					PRINTFORML 「悪性腫瘍確認、コレは緊急摘出手術を要します、こらっ逃げるな！」
					CALL SET_FIELD_INFO(決闘者,"戦場",カウンタ,"悪Ｃ",0,1)
				ENDIF
				IF GETVAR(@"%決闘者%_MZ_無効:カウンタ") >= 1
					PRINTFORML 「麻痺効果ですか、ではマッサージで対処としましょう……ふんっ！！　はぁッ！！」
					CALL SET_FIELD_INFO(決闘者,"戦場",カウンタ,"無効",0,1)
				ENDIF
				IF GETVAR(@"%決闘者%_MZ_終処:カウンタ") >= 1
					PRINTFORML 「時間がありませんね、その死を断つ――！」
					CALL SET_FIELD_INFO(決闘者,"戦場",カウンタ,"終処",0,1)
				ENDIF
				IF GETVAR(@"%決闘者%_MZ_戦狂:カウンタ") >= 1
					PRINTFORML 「――どうでしょう、暴れまわって落ち着きましたか？」
					CALL SET_FIELD_INFO(決闘者,"戦場",カウンタ,"戦狂",0,1)
				ENDIF
				IF GETVAR(@"%決闘者%_MZ_戦無:カウンタ") >= 1
					PRINTFORML 「――戦えない？　腑抜けたことを！」（ﾊﾞｷｯ
					CALL SET_FIELD_INFO(決闘者,"戦場",カウンタ,"戦無",0,1)
				ENDIF
				IF GETVAR(@"%決闘者%_MZ_直無:カウンタ") >= 1
					PRINTFORML 「マスター、%対象カード名%の自信回復のために一度砂袋になってください」
					CALL SET_FIELD_INFO(決闘者,"戦場",カウンタ,"直無",0,1)
				ENDIF
				IF GETVAR(@"%決闘者%_MZ_不材:カウンタ") >= 1
					PRINTFORML 「衛生的に特殊召喚の素材に不適切なようですね。さぁこちらへ……薬毒風呂を用意しました」
					CALL SET_FIELD_INFO(決闘者,"戦場",カウンタ,"不材",0,1)
				ENDIF
				IF GETVAR(@"%決闘者%_MZ_除処:カウンタ") >= 1
					PRINTFORML 「貴方の命を奪ってでも救――と除外されますね……別の手で対処します……」
					CALL SET_FIELD_INFO(決闘者,"戦場",カウンタ,"除処",0,1)
				ENDIF
				IF GETVAR(@"%決闘者%_MZ_表固:カウンタ") >= 1 && GETVAR(@"%決闘者%_MZ_表固:カウンタ") != 2
					PRINTFORML 「体が硬いですね、では……はッ！　はあっ！！」（ﾊﾞｷｯﾎﾞｷｯ！
					CALL SET_FIELD_INFO(決闘者,"戦場",カウンタ,"表固",0,1)
				ELSEIF GETVAR(@"%決闘者%_MZ_表固:カウンタ") >= 1 && GETVAR(@"%決闘者%_MZ_表固:カウンタ") == 2
					PRINTFORML 「包帯を取ってほしい？　まだまだ安静にする必要がありますので許可はできません」
				ENDIF
				PRINTFORMW ・・・・
				IF GETVAR(@"%決闘者%_MZ_薬Ｃ:カウンタ") >= 1
					PRINTFORML 「毒はより強い薬毒で中和します。大丈夫です、苦痛は伴いますが依存症は無いので」
					CALL SET_FIELD_INFO(決闘者,"戦場",カウンタ,"薬Ｃ",0,1)
				ENDIF
				IF GETVAR(@"%決闘者%_MZ_調教:カウンタ") >= 1
					PRINTFORML 「調教？　バカげたことを、惚けてないでサーヴァントの自覚なさい！」（ﾊﾞｷｯ
					CALL SET_FIELD_INFO(決闘者,"戦場",カウンタ,"調教",0,1)
				ENDIF
				IF GETVAR(@"%決闘者%_MZ_発情:カウンタ") >= 1
					PRINTFORML 「――強力な媚薬が霊基をめぐっている、除去します」
					CALL SET_FIELD_INFO(決闘者,"戦場",カウンタ,"発情",0,1)
				ENDIF
				IF GETVAR(@"%決闘者%_MZ_胸追:カウンタ") >= 1
					PRINTFORML 「胸部の異常な肥大化、絞り出し器で処置します――」
					CALL SET_FIELD_INFO(決闘者,"戦場",カウンタ,"胸追",0,1)
				ENDIF
				IF GETVAR(@"%決闘者%_MZ_服状:カウンタ") >= 1
					PRINTFORML 「服の損傷、速やかに新たな服装の準備を――」
					CALL SET_FIELD_INFO(決闘者,"戦場",カウンタ,"服状",0,1)
				ENDIF
				PRINTFORMW ・・
				PRINTL 
				PRINTFORMW 「終了しました、次！」
				PRINTL 
				CUSTOMDRAWLINE ―
				RESETCOLOR
			ENDIF
		NEXT
	ENDIF
ELSEIF 種類 == ターン終了時強制
	CALL CARD_DISPOSE(決闘者,ゾーン,場所,手札行き)
ENDIF

