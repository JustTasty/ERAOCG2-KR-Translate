@CARDNAME_4718,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 칠흑의종자 클로에
	CASE "略称"
		RESULTS = 종자클로에
	CASE "カテゴリ"
		RESULTS:0 = 쿠로이누
ENDSELECT

@CARD_4718(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 전사족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1800
	CASE "守備力"
		RETURN 0
	CASE "성별"
		RETURN 牝性
	CASE "추가日"
		RETURN 250707
ENDSELECT
RETURN 0

@CARD_EXPLANATION_4718(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①③ 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALL CARD_ABLE(1,소환時,특수소환時)
PRINTL ①：이 카드가 소환 / 특수 소환된 경우에 발동할 수 있다.
PRINTL 이 카드의 공격력을 턴 종료시까지 400 올린다.
PRINTL 그 후, 자신 필드 / EX덱에 "칠흑의여왕 올가"가 존재하지 않는 경우,
PRINTL 덱 / 묘지에서 "칠흑의여왕 올가" 1장을 EX덱에 보낼 수 있다.
CALL CARD_ABLE(0,지속효과)
PRINTL ②：자신 필드의 "칠흑의여왕 올가"는 대상 내성을 얻는다.
CALL CARD_ABLE(2,필드→묘지 ,손패→묘지 ,덱→묘지 ,破壊→묘지 ,戦闘→묘지)
PRINTL ③：이 카드가 묘지로 보내졌을 때, 자신 필드 / EX덱에 "칠흑의여왕 올가"가 존재한다면 발동할 수 있다.
PRINTL 손패를 1장 버리고, 묘지의 이 카드를 특수 소환한다.


@CARDCAN_4718(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4718)
SIF RESULT == 1
	RETURN 0

IF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과範囲EX덱 = 1
	효과条件名前 = 칠흑의여왕 올가
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,4718)
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드→묘지 || 종류 == 손패→묘지 || 종류 == 덱→묘지 || 종류 == 破壊→묘지 || 종류 == 戦闘→묘지
	SIF 발동済_2(듀얼리스트,4718)
		RETURN 0
	SIF 손패数(듀얼리스트) < 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과範囲EX덱 = 1
	효과条件名前 = 칠흑의여왕 올가
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件名前 = 칠흑의종자 클로에
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_4718(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

#DIM DYNAMIC 条件Ａ
#DIM DYNAMIC 条件Ｂ
#DIM DYNAMIC 選択효과

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 지속효과
	CALL FULLRESET
	효과条件名前 = 칠흑의여왕 올가
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"対耐")
	CALL STATCHANGE_EFFECT(듀얼리스트,"마함",場所,"対耐")
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4718,1)
	IF RESULT == 0
		CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"一攻",400)
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과範囲EX덱 = 1
		효과条件名前 = 칠흑의여왕 올가
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT != 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과範囲묘지 = 1
			효과条件名前 = 칠흑의여왕 올가
			효과条件行先플래그 = EX덱行き
			효과条件任意 = 1
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 필드→묘지 || 종류 == 손패→묘지 || 종류 == 덱→묘지 || 종류 == 破壊→묘지 || 종류 == 戦闘→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4718,3)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件名前 = 칠흑의종자 클로에
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ENDIF