@DUEL_SYSTEM(이벤트플래그,이벤트起動,連戦플래그,先攻설정)
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 勝利판정
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 덱選択
#DIM DYNAMIC 상대덱選択
#DIM DYNAMIC 攻撃誘導플래그
#DIM DYNAMIC 이벤트플래그
#DIM DYNAMIC 이벤트起動
#DIM DYNAMIC 連戦플래그
#DIM DYNAMIC 발동カード_ID
#DIM DYNAMIC 裏側플래그
#DIM DYNAMIC 先攻설정
#DIMS DYNAMIC 名前ボタン
#DIM DYNAMIC 選択カード_ID
;PRINTFORML 덱選択

IF TARGET >= 1
	OPPOSER = TARGET
ELSE
	OPPOSER = -1
ENDIF

$MYDECK_INPUT
SIF 詰め플래그
	GOTO GAMESTART

IF 連戦플래그 >= 1

	덱選択 = 連戦덱
	GOTO SKIPDECK
ENDIF

IF 이벤트起動 >= 1
	VARSET RESULT
	TRYCALLFORM IRAI_DECK_{이벤트起動}("자신")
	IF RESULT
		덱選択 = RESULT
		GOTO SKIPDECK
	ENDIF
ENDIF

CALL DECK_LIST(1)
IF RESULT == 9999
	IF 이벤트플래그 == 1
		PRINTL 듀얼을 포기하시겠습니까？
		PRINTL [0] - 예
		PRINTL [1] - 아니오
		BINPUT
		IF RESULT == 1
			GOTO MYDECK_INPUT
		ELSE
			RETURN 2
		ENDIF
	ENDIF
	RETURN 0
ELSE
	덱選択 = RESULT
	連戦덱 = RESULT
ENDIF



$SKIPDECK

IF 이벤트起動 >= 1
	VARSET RESULT
	TRYCALLFORM IRAI_DECK_{이벤트起動}("상대")
	IF RESULT
		상대덱選択 = RESULT
		GOTO GAMESTART
	ENDIF
ENDIF

CALL ENEMYDECK_CHECK(OPPOSER)
IF RESULT == 0
	IF GROUPMATCH(이벤트플래그,0,2)
		CALL DECK_LIST(2)
		IF RESULT == 9999
			RETURN 0
		ELSEIF RESULT == 8888
			GOTO MYDECK_INPUT
		ELSE
			상대덱選択 = RESULT
		ENDIF
	ELSEIF 이벤트플래그 == 1
		CALL CHOOSE_DECK_RANDOM
		상대덱選択 = RESULT
	ELSEIF 이벤트플래그 == 3
		CALL CHOOSE_DECK_ONLINE_RATEMATCH
		상대덱選択 = RESULT
	ENDIF
ELSE
	상대덱選択 = RESULT	
	IF 이벤트플래그 == 0 && CFLAG:OPPOSER:호감도 >= 1000 && オプション_덱변경 == 1
		PRINTFORML 대전상대에게 자신의 덱을 쥐어주겠습니까？
		PRINTL 
		PRINTL [0] - 예
		PRINTL [1] - 아니오
		PRINTL [2] - 아니오＋이 옵션을 OFF로한다
		PRINTL 
		BINPUT
		IF RESULT == 0
			CALL DECK_LIST(2)
			IF RESULT == 9999
				PRINTFORML 원래 덱 그대로 개시합니다。
			ELSEIF RESULT == 8888
				GOTO MYDECK_INPUT
			ELSE
				상대덱選択 = RESULT
			ENDIF
		ELSEIF RESULT == 2
			オプション_덱변경 = 0
		ENDIF
	ENDIF
ENDIF


;-------------------------ゲーム開始-------------------------
$GAMESTART
ALIGNMENT CENTER
PRINTL 
CUSTOMDRAWLINE —
PRINTL 
CALLFORM CHARA_CUTIN("","D U E L   S T A R T ! !")
CUSTOMDRAWLINE —
PRINTW
ALIGNMENT LEFT

$FULLRESET_LOOP
CALL CARD_GAMELOADUP

IF 詰め플래그
	ターンプレイヤー = 상대
	ターン数 = 2
	GOTO PUZZLE_SET
ENDIF
IF 덱選択 >= 1000
	CALLFORM DUELDECK_{덱選択}("자신")
ELSE
	CALLFORM MYDECK("자신",덱選択)
ENDIF

IF 상대덱選択 >= 1000
	CALLFORM DUELDECK_{상대덱選択}("상대")
ELSE
	CALLFORM MYDECK("상대",상대덱選択)
ENDIF
;ザポルグ追記20241016
IF 先攻설정 != 0
	IF 先攻설정 == 1
		PRINTW 상대의 선공입니다
		リザルト：後攻 = 1
		ターンプレイヤー = 자신
	ELSEIF 先攻설정 == 2
		PRINTW 당신의 선공입니다
		ターンプレイヤー = 상대
	ENDIF
ELSE
	IF RAND:2 == 0
		PRINTW 당신의 선공입니다
		ターンプレイヤー = 상대
	ELSEIF RAND:1 == 0
		PRINTW 상대의 선공입니다
		リザルト：後攻 = 1
		ターンプレイヤー = 자신
	ENDIF
ENDIF

;シャッフル＆5ドロー
CALL SHUFFLE("자신")
CALL SHUFFLE("상대")
REPEAT 5
;	PRINTFORML 체크1카운트:{COUNT}
REND

REPEAT 5
;	PRINTFORML 체크2카운트:{COUNT}
	CALL CARD_ADD, "자신", "덱", "손패", 0
REND
REPEAT 5
	CALL CARD_ADD, "상대", "덱", "손패", 0
REND


$PUZZLE_SET
CALL FULLRESET
효과範囲덱 = 1
효과範囲손패 = 1
효과条件カテゴリ = E.R.A
CALL CARD_CHECK_ADV("자신")
IF RESULT == 1
	SIF TARGET >= 1
		CALL EVILTY_POINT(5)
	SETCOLOR C_RED
	CUSTOMDRAWLINE —
	PRINTL 
	PRINTFORML 　＊덱 안의E.R.A카드의 효력에 의해、당신 이외 듀얼중의 위화감에 둔감해졌다。
	PRINTL 
	CUSTOMDRAWLINE —
	PRINTW
	RESETCOLOR
	ERA起動 = 1
ENDIF


IF 이벤트起動 >= 1
	CUSTOMDRAWLINE —
	PRINTL 
	TRYCALLFORM IRAI_DUEL_INTRO_{이벤트起動}
	PRINTL 
	CUSTOMDRAWLINE —
ELSE
	CALL KOJO_MESSAGE_SEND("DUEL",OPPOSER,0)
ENDIF


; PREPARE PLAYBOARD
; CALL PREPARE_DUEL_THUMBNAIL(0)
;①アトラ:ここでHTMLを作成し、
CALL PREPARE_DUEL_PLAYBOARD(0)


$INPUTTURN_LOOP
CALL CARD_STARTUP

IF 이벤트起動 >= 1
	TRYCALLFORM IRAI_DUEL_INTERMISSION_{이벤트起動}
	TRYCALLFORM IRAI_DUEL_CONDSET_{이벤트起動}
ENDIF


$INPUT_LOOP
VARSET 処理中_효과ID, -1
CALL RESET_FIELD_CONDITION(ターンプレイヤー,6,1)
CALL CHECK_PLAYERS_VIT

SIF 待機_효과ID_保 > 0
	CALL CARD_FINAL(ターンプレイヤー);효과残り香の処理

IF ターンプレイヤー == "자신"
	CALL ENEMY_RUTIN_FRCHAIN
ENDIF

;CALL ENSLAVE_RELEASE

;リセット
자신_戦闘カード = 5
상대_戦闘カード = 5

$RECHECK_VICTORY
CALL CARD_LOADUP

;패배処理
CALL CHECK_VICTORY
IF RESULT != 0
	勝利판정 = RESULT
	VARSET RESULT
	IF 이벤트起動 >= 1 && 勝利판정 == 1
		CUSTOMDRAWLINE —
		PRINTL 
		VARSET RESULT
		TRYCALLFORM IRAI_DUEL_VIC_{이벤트起動}
		PRINTL 
		CUSTOMDRAWLINE —
		PRINTW
		IF RESULT == 1
			GOTO RECHECK_VICTORY
		ENDIF
	ENDIF

	IF 詰め플래그 == 0
		PRINTL
		IF ERA起動 != 2
			CALL CHECK_DP_GET(勝利판정,이벤트플래그)
			SIF 상대_듀얼패배플래그 >= 1 && オプション_カード友 == 1
				CALL DUEL_RESULT_BOND(덱選択,RESULT)		
		ENDIF
		PRINTBUTTON "　[듀얼종료]",0
		;이벤트はOFF/ERA起動中/대상상대は-1/ライフダメージによる패배
		IF 이벤트起動 == 0 && ERA起動 == 1 && OPPOSER != -1 && 상대_듀얼패배플래그 == 1
			SETCOLOR C_RED
			PRINTBUTTON "　　E.R.A　[강제적으로 속행한다]",999
			RESETCOLOR
		ENDIF
		BINPUT
		IF RESULT == 999
			ERA起動 = 2
			勝利판정 = 0
		ENDIF
	ELSE
		詰め플래그 = 0
	ENDIF
	
	IF 勝利판정 == 1
		IF 이벤트起動 >= 1
			CUSTOMDRAWLINE —
			PRINTL 
			TRYCALLFORM IRAI_DUEL_ENDING_{이벤트起動}
			PRINTL 
			CUSTOMDRAWLINE —
			PRINTW
		ENDIF
		RETURN 1
	ELSEIF 勝利판정 == 2
		RETURN 2
	ELSEIF 勝利판정 == 3
		RETURN 3
	ENDIF
ENDIF



IF デバッグコマンド == 1
	CUSTOMDRAWLINE —
	PRINTFORML 디버그 표시：
	CUSTOMDRAWLINE —
	PRINTFORM 발동됨_1 ID:
	FOR カウンタ,0,자신_발동カウンタ_1
		PRINTFORM 「{자신_발동済효과_1:カウンタ}」
	NEXT
	PRINTL
	PRINTFORM 발동됨_2 ID:
	FOR カウンタ,0,자신_발동カウンタ_2
		PRINTFORM 「{자신_발동済효과_2:カウンタ}」
	NEXT
	PRINTL
	PRINTFORM 발동됨_3 ID:
	FOR カウンタ,0,자신_발동カウンタ_3
		PRINTFORM 「{자신_발동済효과_3:カウンタ}」
	NEXT
	PRINTL
	PRINTFORM 발동됨_4 ID:
	FOR カウンタ,0,자신_발동カウンタ_4
		PRINTFORM 「{자신_발동済효과_4:カウンタ}」
	NEXT

	PRINTL
	PRINTFORM 상대발동됨_1 ID:
	FOR カウンタ,0,상대_발동カウンタ_1
		PRINTFORM 「{상대_발동済효과_1:カウンタ}」
	NEXT
	PRINTL
	PRINTFORM 상대발동됨_2 ID:
	FOR カウンタ,0,상대_발동カウンタ_2
		PRINTFORM 「{상대_발동済효과_2:カウンタ}」
	NEXT
	PRINTL
	PRINTFORM 상대발동됨_3 ID:
	FOR カウンタ,0,상대_발동カウンタ_3
		PRINTFORM 「{상대_발동済효과_3:カウンタ}」
	NEXT
	PRINTL
	PRINTFORM 상대발동됨_4 ID:
	FOR カウンタ,0,상대_발동カウンタ_4
		PRINTFORM 「{상대_발동済효과_4:カウンタ}」
	NEXT

	PRINTL
	PRINTFORM 자신금지중 ID:
	FOR カウンタ,0,자신_禁カウンタ
		PRINTFORM 「{자신_禁止令:カウンタ}」
	NEXT

	PRINTL
	PRINTFORM 상대금지중 ID:
	FOR カウンタ,0,상대_禁カウンタ
		PRINTFORM 「{상대_禁止令:カウンタ}」
	NEXT

	PRINTL
	CUSTOMDRAWLINE —

	SETCOLOR C_L_RED
	PRINTFORM 상대 산패수：「{現山札数("상대"),2}」
	PRINTFORM 상대 몬스터 수：「{モンスター数("상대"),2}」
	PRINTFORM 상대 마함수：「{現마함("상대"),2}」
	PRINTFORML 상대 손패수：「{손패数("상대"),2}」
	PRINTFORM 상대 묘지수：「{묘지数("상대"),2}」
	PRINTFORML 상대 제외수：「{제외数("상대"),2}」
	PRINTL
	RESETCOLOR
	SETCOLOR C_L_BLUE
	PRINTFORM 현재산패 수：「{現山札数("자신"),2}」
	PRINTFORM 몬스터 수：「{モンスター数("자신"),2}」
	PRINTFORM 현마함수：「{現마함("자신"),2}」
	PRINTFORML 현 손패수：「{손패数("자신"),2}」
	PRINTFORM 현 묘지수：「{묘지数("자신"),2}」
	PRINTFORML 현 제외수：「{제외数("자신"),2}」
	RESETCOLOR
ENDIF

; PRINT IMAGES THUMBNAIL
; CALL PREPARE_DUEL_THUMBNAIL(1)
;①アトラ:ここでHTMLを출력している
CALL DISPLAY_CARDS_ONBOARD()

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
;SETCOLOR C_L_RED

PRINTPLAIN ┌─────────────────────────────────────>E<────────────────────────────────────
PRINTBUTTON "●",8888
RESETCOLOR

CALL CARD_MAGINFO("상대")

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
;SETCOLOR C_L_BLUE
PRINTPLAIN └┬─────────────────────────────────>
;SETCOLOR C_L_RED
PRINTPLAIN MAGIC / TRAPS<────────────────────────────────┬┘
RESETCOLOR

CALL CARD_SUMINFO("상대")

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
;SETCOLOR C_L_BLUE
PRINTFORM 　｜

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
PRINTFORM 　┏──%"",20%
SETCOLOR C_L_BLUE
名前ボタン = 【%CALLNAME:MASTER,17,LEFT%】
PRINTBUTTON @"%名前ボタン%",1101

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
;8削減
PRINTFORM ——————— ／
IF 자신_フィールド名 != "" && 상대_フィールド名 != ""
	SETCOLOR C_L_BLUE
	SUBSTRING 자신_フィールド名, 0, 12
	PRINTFORM %RESULTS,12,LEFT%
	IF ターンプレイヤー == "자신"
		SETCOLOR C_L_BLUE
	ELSE
		SETCOLOR C_L_RED
	ENDIF
	PRINTFORM ／
	SETCOLOR C_L_RED
	SUBSTRING 상대_フィールド名, 0, 12
	PRINTFORM %RESULTS,12,LEFT%
ELSEIF 자신_フィールド名 != ""
	SETCOLOR C_L_BLUE
	PRINTFORM %자신_フィールド名,26,LEFT%
ELSEIF 상대_フィールド名 != ""
	SETCOLOR C_L_RED
	PRINTFORM %상대_フィールド名,26,RIGHT%
ELSE
	PRINTFORM %"",26%
ENDIF
	
PRINTFORM ＼──%"",20%


RESETCOLOR

PRINTFORM 

SETCOLOR C_L_RED
IF OPPOSER == -1
	IF OPPOSER名 != ""
		名前ボタン = 【%OPPOSER名,17,LEFT%】
	ELSE
		名前ボタン = 【OPPOSER　　　　　】
	ENDIF
	PRINTBUTTON @"%名前ボタン%",1100
ELSE
	名前ボタン = 【%CALLNAME:OPPOSER,17,LEFT%】
	PRINTBUTTON @"%名前ボタン%",1100
ENDIF

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
PRINTFORM —————————┓　

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
;SETCOLOR C_L_RED
PRINTFORML ｜
RESETCOLOR

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
;SETCOLOR C_L_BLUE
PRINTFORM 　｜

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
PRINTFORM 　┣──%"",20%
;PRINTFORM 　┣———————————————

SETCOLOR C_L_BLUE
IF ライフ_자신 >= 99999
	PRINTBUTTON @"【PLAYER  LP:99999+】",1101
;	PRINTFORM 【PLAYER  LP:99999+】
ELSE
	SIF ライフ_자신 <= 2000
		SETCOLOR C_RED
	PRINTBUTTON @"【PLAYER  LP:{ライフ_자신,6}】",1101
;	PRINTFORM 【PLAYER  LP: {ライフ_자신,5}】
ENDIF


RESETCOLOR
IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
;	PRINTFORM ——————————————— │
	PRINTFORM —————— │

	FONTSTYLE 8
	PRINTFORM  TURN【{ターン数,2}】/【PLAYER  TURN】
	FONTSTYLE 0
	PRINTFORM │———%"",20%
ELSE
	SETCOLOR C_L_RED
;	PRINTFORM ——————————————— │
	PRINTFORM —————— │
	FONTSTYLE 8	
	PRINTFORM  TURN【{ターン数,2}】/【OPPOSER TURN】
	FONTSTYLE 0
	PRINTFORM │———%"",20%
ENDIF
RESETCOLOR
SETCOLOR C_L_RED
IF ライフ_상대 >= 99999
	PRINTBUTTON @"【OPPOSER LP:99999+】",1100
ELSEIF ライフ_상대 <= -99999
	PRINTBUTTON @"【OPPOSER LP:-99999】",1100
	
ELSE
	SIF ライフ_상대 <= 2000
		SETCOLOR C_RED

	PRINTBUTTON @"【OPPOSER LP:{ライフ_상대,6}】",1100
ENDIF
IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
;PRINTFORM ———————————┫　
PRINTFORM —————————┫　

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
;SETCOLOR C_L_RED
PRINTFORML ｜
RESETCOLOR

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
;SETCOLOR C_L_BLUE
PRINTFORM 　｜

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF

PRINTFORM 　┗──%"",20%
;PRINTFORM 　┗———————————————

SETCOLOR C_L_BLUE
PRINTFORM 【De:{現山札数("자신"),2}/Ha:{손패数("자신"),2}/Gy:{묘지数("자신"),2}】



IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF

;PRINTFORM ———————————————— ＼
PRINTFORM ——————— ＼

FONTSTYLE 8
PRINTFORM —————【

IF メインフェイズ２ == 1
	PRINTFORM MAIN  PHASE2
ELSEIF メインフェイズ == 1
	PRINTFORM MAIN  PHASE1
ELSEIF バトルフェイズ == 1
	SETCOLOR C_RED
	PRINTFORM BATTLE PHASE
	IF ターンプレイヤー == "자신"
		SETCOLOR C_L_BLUE
	ELSE
		SETCOLOR C_L_RED
	ENDIF
ENDIF
PRINTFORM 】—————
FONTSTYLE 0
PRINTFORM ／──%"",20%


SETCOLOR C_L_RED
PRINTFORM 【De:{現山札数("상대"),2}/Ha:{손패数("상대"),2}/Gy:{묘지数("상대"),2}】
RESETCOLOR

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
PRINTFORM —————————┛　

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
;SETCOLOR C_L_RED
PRINTFORML ｜

RESETCOLOR

CALL CARD_SUMINFO("자신")

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
;SETCOLOR C_L_BLUE
PRINTPLAIN ┌┴─────────────────────────────────>MAGIC / TRAPS
;SETCOLOR C_L_RED
PRINTPLAIN <────────────────────────────────┴┐
RESETCOLOR

CALL CARD_MAGINFO("자신")

IF ターンプレイヤー == "자신"
	SETCOLOR C_L_BLUE
ELSE
	SETCOLOR C_L_RED
ENDIF
;SETCOLOR C_L_BLUE
PRINTBUTTON "◎",8888
PRINTPLAIN ─────────────────────────────────────>P<────────────────────────────────────┘
RESETCOLOR

CALL DUEL_PLAYER_UI(0,이벤트起動)
;패의 カード발동・소환処理――――――――――――――――――――――――
IF INRANGE (RESULT, 0, 100)
	選択カード = RESULT
	選択カード_ID = 자신_손패:選択カード
	$INPUT_TEFUDA_LOOP
	CALL CARD_UI_RESET
	確認듀얼리스트 = 자신
	確認종류 = 손패起動
	確認ゾーン = 손패
	確認場所 = 選択カード
	フリー確認 = 1
	TRYCALLFORM CARD_UI(選択カード_ID,2); 自由確認での손패確認（受け渡し必要：듀얼리스트情報のみ）
	TRYCALLFORM CARDCAN_{選択カード_ID}("자신",손패起動,"손패",選択カード)
	IF RESULT == 1 
		SETCOLOR C_G_YELLOW
		PRINT 　[3] - 효과발동　　　　
		RESETCOLOR
	ENDIF
	VARSET RESULT

	IF メインフェイズ == 1 && ターンプレイヤー == "자신"
		TRYCALLFORM CARD_{選択カード_ID}, "종류"
		IF RESULT <= 通常モン
			SETCOLOR C_GRAY
			CALL CARD_NS_CHECK_OK("자신",選択カード)
			IF RESULT != 0				
				IF RESULT == 1 || RESULT == 3
					SETCOLOR C_AQUA_2
					PRINT 　[1] - 통상소환　　　　
					SIF 소환権 >= 1
						PRINT 　[2] - 카드 세트　　
				ENDIF
				IF RESULT == 2 || RESULT == 3
					VARSET RESULT
					TRYCALLFORM CARDCAN_{選択カード_ID}("자신",효과소환,"손패",選択カード)
					IF RESULT == 1 
						SETCOLOR C_AQUA_2
						PRINT 　[4] - 효과소환　　　　
					ENDIF
				ENDIF
				RESETCOLOR
			ENDIF
			VARSET RESULT
			TRYCALLFORM CARD_{選択カード_ID}, "맥시멈"
			IF RESULT >= 1
				;PRINTFORML 選択カード:{選択カード}/選択カード_ID:{選択カード_ID}
				TRYCALLFORM CARDSUMMON_{選択カード_ID}("자신",選択カード,1)
				IF RESULT >= 1
					SETCOLOR C_RED
					PRINT 　[5] - 맥시멈 소환！！
					RESETCOLOR
				ELSE
					SETCOLOR C_GRAY
					PRINT 　<5> - 맥시멈 소환　
					RESETCOLOR
				ENDIF
			ENDIF
		ENDIF
		IF RESULT > 마법全般
			SIF 現마함("자신") == 5
				SETCOLOR C_GRAY
			PRINT 　[2] - 카드 세트　　
			RESETCOLOR
		ENDIF


	ENDIF
	RESETCOLOR
	PRINTL
	PRINTL
	PRINTL 　[100] - 돌아간다
	BINPUT
	IF RESULT == 100
		PRINTL 
	ELSEIF RESULT == 1000
		CALL PRINT_STATE_DUEL(選択カード_ID,2)
		WAIT
		GOTO INPUT_TEFUDA_LOOP
	ELSEIF RESULT == 1
		IF 자신_手_召可:選択カード >= 1
			CALL CARD_SUMMON_SHORI("자신",選択カード,"攻撃표시",3)
		ELSE
			CALL CARD_SUMMON_SHORI("자신",選択カード,"攻撃표시")
		ENDIF
	ELSEIF RESULT == 2
		CALLFORM CARD_{選択カード_ID}("종류")
		IF RESULT > 20
			IF 現마함("자신") == 5
				PRINTFORMW 마함존이 가득차 세트할 수 없습니다。
			ELSE
				PRINTFORMW 리버스카드 세트！
				CALL CARD_ADD("자신", "손패", "마함", 選択カード,"裏側표시")
			ENDIF
		ELSEIF RESULT < 20
			CALL CARD_SUMMON_SHORI("자신",選択カード,"裏側守備표시")
		ENDIF
	ELSEIF RESULT == 4
		IF 자신_手_召可:選択カード >= 1
			CALL CARD_SUMMON_SHORI("자신",選択カード,"攻撃표시",4)
		ELSE	
			CALL CARD_SUMMON_SHORI("자신",選択カード,"攻撃표시",2)
		ENDIF
	ELSEIF RESULT == 3
		발동カード_ID = 選択カード_ID
		CALLFORM CARDNAME_{발동カード_ID}("名前")
		PRINTFORMW 당신은 패에서 %RESULTS%의 효과발동선언을 했습니다！
		CALLFORM CARD_STANBY(발동カード_ID,"자신",손패起動,"손패",選択カード)
		CALL CARD_FINAL(ターンプレイヤー);자신ターン패의 起動後のチェック
		
	ELSEIF RESULT == 5
		CALLFORM CARDNAME_{選択カード_ID}("名前")
		PRINTFORMW 당신은 패에서『{選択カード}』%RESULTS%의 맥시멈선언을 했습니다！
		CALL MAXSUMMON("자신", 選択カード, "손패")



	ENDIF
;フィールド上のモンスター발동・攻撃処理・状態변경処理――――――――――――――――――――――――
ELSEIF INRANGE (RESULT, 2000, 2005)
	選択カード = RESULT -2000
	CALL CARD_UI_RESET
	確認듀얼리스트 = 자신
	確認종류 = 필드起動
	確認ゾーン = 필드
	確認場所 = 選択カード
	フリー確認 = 1
	TRYCALLFORM CARD_UI_FIELD("자신",選択カード);필드의 情報（受け渡し必要：듀얼리스트、필드（ゾーン）、場所）
	IF RESULT == 100
		PRINTL
	ELSEIF RESULT == 1
		IF 자신_MZ_表:選択カード == 1
			PRINTW 수비표시로 했습니다
			CALL SET_FIELD_INFO("자신","필드",選択カード,"表",2,1)
		ELSEIF 자신_MZ_表:選択カード >= 2
			PRINTW 공격표시로 했습니다
			CALL SET_FIELD_INFO("자신","필드",選択カード,"表",1,1)
		ENDIF
		자신_MZ_表変:選択カード = 0
	ELSEIF RESULT == 3
		;효과발동処理
		발동カード_ID = 자신_필드:選択カード
		CALLFORM CARDNAME_{발동カード_ID}("名前")
		PRINTFORMW 당신은 몬스터 존에서 %RESULTS%의 효과발동선언을 했습니다！
		CALLFORM CARD_STANBY(발동カード_ID,"자신",필드起動,"필드",選択カード)
		CALL CARD_FINAL(ターンプレイヤー);자신ターン필드의 起動後のチェック
	ELSEIF RESULT == 4
		;효과발동処理
		TRYCALLFORM CARDEFFECT_{자신_MZ_특수状態:選択カード}("자신",필드起動付与,"필드",選択カード)

	ELSEIF RESULT == 69
		CALL CARD_REACT("자신",選択カード,"필드")

;―――――――――――――――――――【戦闘】――――――――――――――――――――――
	ELSEIF RESULT == 5
		CALL BATTLE_SETUP("자신",選択カード)
		VARSET 攻撃誘導플래그

		;攻撃モンスター選択
		CUSTOMDRAWLINE —
		PRINTFORML 「공격 몬스터 선택 화면」
		CUSTOMDRAWLINE —
		PRINTL
		FOR カウンタ,0,5
			SIF 상대_필드:カウンタ == -1
				CONTINUE
			SIF 상대_MZ_攻誘:カウンタ == 0
				CONTINUE
			攻撃誘導플래그 ++
		NEXT

		IF 攻撃誘導플래그 == 0
			FOR カウンタ,0,5
				VARSET 裏側플래그
				SIF 상대_필드:カウンタ == -1
					CONTINUE
				SIF 상대_필드:カウンタ == 使用불가ゾーン
					CONTINUE

				裏側플래그 = 상대_MZ_表:カウンタ
				IF 상대_MZ_攻避:カウンタ >= 1
					SETCOLOR C_GRAY
					TRYCALLFORM CARDNAME_{상대_필드:カウンタ}, "名前"
					CALLFORM CARD_KANPRINT("필드",カウンタ,상대_필드:カウンタ,6,裏側플래그)
				ELSE
					CALL CARD_SETCOLOR("상대","필드",カウンタ,裏側플래그)
					CALLFORM CARD_KANPRINT("필드",カウンタ,상대_필드:カウンタ,,裏側플래그)
				ENDIF
				RESETCOLOR
			NEXT
		ELSEIF 攻撃誘導플래그 == 1
			FOR カウンタ,0,5
				VARSET 裏側플래그
				裏側플래그 = 상대_MZ_表:カウンタ
				SIF 상대_필드:カウンタ == -1
					CONTINUE
				SIF 상대_필드:カウンタ == 使用불가ゾーン
					CONTINUE

				IF 상대_MZ_攻誘:カウンタ == 0 || 상대_MZ_攻避:カウンタ >= 1
					SETCOLOR C_GRAY
					CALLFORM CARD_KANPRINT("필드",カウンタ,상대_필드:カウンタ,6,裏側플래그)
				ELSE
					SETCOLOR C_RED
					CALLFORM CARD_KANPRINT("필드",カウンタ,상대_필드:カウンタ,,裏側플래그)
					RESETCOLOR
				ENDIF
				RESETCOLOR
			NEXT
		ENDIF
		PRINTL
		IF 자신_直接無効 >= 1
			SETCOLOR C_GRAY
			PRINTFORML 전투『{99,2}』: 직접공격무효중。
		ELSEIF 자신_MZ_直無:자신_戦闘カード >= 1
			SETCOLOR C_GRAY
			PRINTFORML 전투『{99,2}』: 직접공격불가。
		ELSE
			IF  자신_MZ_直攻:자신_戦闘カード >= 1
				PRINTFORML 전투[{99,2}] : 다이렉트 어택
			ELSE
				CALL FULLRESET
				효과範囲상대필드 = 1
				효과条件攻撃可 = 1
				CALL CARD_CHECK_ADV("자신")
				IF RESULT == 1
					SETCOLOR C_GRAY
					PRINTFORML 전투『{99,2}』: 몬스터가 존재합니다。
				ELSE
					PRINTFORML 전투[{99,2}] : 다이렉트 어택
				ENDIF
			ENDIF
		ENDIF
		RESETCOLOR
		PRINTL
		CUSTOMDRAWLINE —
		PRINTL
		PRINTL [100] - 돌아간다
		BINPUT
		CALL CARD_FIGHT("자신",選択カード,RESULT)
		IF バトル強制終了 == 1
			バトル強制終了 = 0
			GOTO INPUT_MAIN2
		ENDIF
	ENDIF

;――――――――――――フィールド上の마법함정발동・状態변경処理――――――――――――
ELSEIF INRANGE (RESULT, 3000, 3005)
	選択カード = RESULT -3000
	CALL CARD_UI_RESET
	確認듀얼리스트 = 자신
	確認종류 = 마함起動
	確認ゾーン = 마함
	確認場所 = 選択カード
	フリー確認 = 1
	TRYCALLFORM CARD_UI_MAGIC("자신",選択カード);필드의 情報（受け渡し必要：듀얼리스트、마함（ゾーン）、場所）
	IF RESULT == 100
		PRINTL
	ELSEIF RESULT == 3
		발동カード_ID = 자신_마함:選択カード
		CALLFORM CARDNAME_{발동カード_ID}("名前")
		PRINTFORMW 당신은 마・함존에서 %RESULTS%의 효과발동선언을 했습니다！
		CALLFORM CARD_STANBY(발동カード_ID,"자신",마함起動,"마함",選択カード)
		CALL CARD_FINAL(ターンプレイヤー);자신ターン마함の起動後のチェック

	ELSEIF RESULT == 69
		CALL CARD_REACT("자신",選択カード,"마함")
	ENDIF
;――――――――――――상대 필드上のカード状態確認――――――――――――
ELSEIF INRANGE (RESULT, 5000, 5005)
	選択カード = RESULT -5000
	IF デバッグコマンド == 1
		PRINTL [200] - 표시형식변경
	ENDIF
	CALL CARD_UI_RESET
	確認듀얼리스트 = 상대
	確認ゾーン = 필드
	確認場所 = 選択カード
	フリー確認 = 1
	TRYCALLFORM CARD_UI_FIELD("상대",選択カード);필드의 情報（受け渡し必要：듀얼리스트、필드（ゾーン）、場所）
	IF RESULT == 100
		PRINTL
	ELSEIF RESULT == 200
		IF 상대_MZ_表:選択カード == 1
			PRINTW 수비표시로 했습니다
			상대_MZ_表:選択カード = 2
		ELSEIF 상대_MZ_表:選択カード >= 2
			PRINTW 공격표시로 했습니다
			상대_MZ_表:選択カード = 1
		ENDIF
	ELSEIF RESULT == 4
		TRYCALLFORM CARDEFFECT_{상대_MZ_특수状態:選択カード}("상대",상대起動付与,"필드",選択カード)
	ELSEIF RESULT == 69
		CALL CARD_REACT("상대",選択カード,"필드")
	ENDIF
;――――――――――――상대 필드上の마함状態변경処理――――――――――――
ELSEIF INRANGE (RESULT, 6000, 6005)
	選択カード = RESULT -6000
	CALL CARD_UI_RESET
	確認듀얼리스트 = 자신
	確認ゾーン = 마함
	確認場所 = 選択カード
	フリー確認 = 1
	TRYCALLFORM CARD_UI_MAGIC("상대",選択カード);필드의 情報（受け渡し必要：듀얼리스트、마함（ゾーン）、場所）
	IF RESULT == 100
		PRINTL
	ENDIF
;――――――――――――묘지のカード確認――――――――――――
ELSEIF RESULT == 400
	CUSTOMDRAWLINE =
	PRINTFORML 「（자신）묘지확인화면」
	CUSTOMDRAWLINE =
	PRINTL
	FOR カウンタ,0,紙束最大値
		SIF 자신_묘지:カウンタ == -1
			CONTINUE
		VARSET RESULT
		TRYCALLFORM CARDCAN_{자신_묘지:カウンタ}("자신",묘지起動,"묘지",カウンタ)
		IF RESULT == 1
			SETCOLOR C_G_YELLOW
		ELSE
			CALL CARD_SETCOLOR("자신","묘지",カウンタ)
		ENDIF

		CALLFORM CARD_KANPRINT("묘지",カウンタ,자신_묘지:カウンタ)
		RESETCOLOR
	NEXT
	CUSTOMDRAWLINE —
	PRINTL
	PRINTL [100] - 돌아간다
	BINPUT
	選択カード = RESULT
	;돌아간다
	IF 選択カード != 100
		CALL CARD_UI_RESET
		確認듀얼리스트 = 자신
		確認종류 = 묘지起動
		確認ゾーン = 묘지
		確認場所 = 選択カード
		フリー確認 = 1
		TRYCALLFORM CARD_UI(자신_묘지:選択カード,2); 自由確認での묘지確認（受け渡し必要：듀얼리스트情報のみ）
		TRYCALLFORM CARDCAN_{자신_묘지:選択カード}("자신",묘지起動,"묘지",選択カード)
		SETCOLOR C_G_YELLOW
		SIF RESULT == 1
			PRINT [3] - 효과발동
		RESETCOLOR	
		PRINTL
		PRINTL [100] - 돌아간다
		BINPUT
		IF RESULT == 100
			PRINTL 
		ELSEIF RESULT == 3
			발동カード_ID = 자신_묘지:選択カード
			CALLFORM CARDNAME_{발동カード_ID}("名前")
			PRINTFORMW 당신은 묘지에서 %RESULTS%의 효과발동선언을 했습니다！
			CALLFORM CARD_STANBY(발동カード_ID,"자신",묘지起動,"묘지",選択カード)
			CALL CARD_FINAL(ターンプレイヤー);자신ターン묘지の起動後のチェック
		ENDIF
	ENDIF
ELSEIF RESULT == 499
	CUSTOMDRAWLINE =
	PRINTFORML 「（상대）묘지확인화면」
	CUSTOMDRAWLINE =
	PRINTL
	FOR カウンタ,0,紙束最大値
		SIF 상대_묘지:カウンタ == -1
			CONTINUE
		VARSET RESULT	
		CALL CARD_SETCOLOR("상대","묘지",カウンタ)
		CALLFORM CARD_KANPRINT("묘지",カウンタ,상대_묘지:カウンタ)
		RESETCOLOR	
	NEXT
	CUSTOMDRAWLINE —
	PRINTL
	PRINTL [100] - 돌아간다
	BINPUT
	選択カード = RESULT
	;돌아간다
	IF 選択カード != 100
		CALL CARD_UI_RESET
		確認듀얼리스트 = 상대
		確認ゾーン = 묘지
		確認場所 = 選択カード
		フリー確認 = 1
		TRYCALLFORM CARD_UI(상대_묘지:選択カード,2); 自由確認での상대묘지確認（受け渡し必要：듀얼리스트情報のみ）
		RESETCOLOR	
		PRINTL
		PRINTL [100] - 돌아간다
		BINPUT
		IF RESULT == 100
			PRINTL 
		ENDIF
	ENDIF

;――――――――――――제외のカード確認――――――――――――
ELSEIF RESULT == 600
	CUSTOMDRAWLINE =
	PRINTFORML 「（자신）제외확인화면」
	CUSTOMDRAWLINE =
	PRINTL
	FOR カウンタ,0,紙束最大値
		SIF 자신_제외:カウンタ == -1
			CONTINUE

		VARSET RESULT		
		CALL CARD_SETCOLOR("자신","제외",カウンタ)
		CALLFORM CARD_KANPRINT("제외",カウンタ,자신_제외:カウンタ)
		RESETCOLOR	
	NEXT
	CUSTOMDRAWLINE —
	PRINTL
	PRINTL [100] - 돌아간다
	BINPUT
	選択カード = RESULT
	;돌아간다
	IF 選択カード != 100
		CALL CARD_UI_RESET
		確認듀얼리스트 = 자신
		確認ゾーン = 제외
		確認場所 = 選択カード
		フリー確認 = 1
		TRYCALLFORM CARD_UI(자신_제외:選択カード,2); 自由確認での자신제외確認（受け渡し必要：듀얼리스트情報のみ）
		PRINTL
		PRINTL [100] - 돌아간다
		BINPUT
		IF RESULT == 100
			PRINTL 
		ENDIF
	ENDIF
ELSEIF RESULT == 699
	CUSTOMDRAWLINE =
	PRINTFORML 「（상대）제외확인화면」
	CUSTOMDRAWLINE =
	PRINTL
	FOR カウンタ,0,紙束最大値
		SIF 상대_제외:カウンタ == -1
			CONTINUE
		VARSET RESULT	
		CALL CARD_SETCOLOR("상대","제외",カウンタ)
		TRYCALLFORM CARDNAME_{상대_제외:カウンタ}, "名前"
		PRINTFORM 제외[{カウンタ,2}] %RESULTS%
		PRINTL
		RESETCOLOR	
	NEXT
	CUSTOMDRAWLINE —
	PRINTL
	PRINTL [100] - 돌아간다
	BINPUT
	選択カード = RESULT
	;돌아간다
	IF 選択カード != 100
		CALL CARD_UI_RESET
		確認듀얼리스트 = 상대
		確認ゾーン = 제외
		確認場所 = 選択カード
		フリー確認 = 1
		TRYCALLFORM CARD_UI(상대_제외:選択カード,2); 自由確認での상대제외確認（受け渡し必要：듀얼리스트情報のみ）
		RESETCOLOR	
		PRINTL
		PRINTL [100] - 돌아간다
		BINPUT
		IF RESULT == 100
			PRINTL 
		ENDIF
	ENDIF
;――――――――――――ターン進行――――――――――――
ELSEIF RESULT == 700
	;GOTO INPUT_LOOP_ENEMY
	CALL ENEMYTURN_BASIC

;――――――――――――EX덱確認――――――――――――
ELSEIF RESULT == 800
	CALL CARD_EXDECK("자신")

;――――――――――――バトルフェイズ移行――――――――――――
ELSEIF RESULT == 900
	メインフェイズ = 0
	バトルフェイズ = 1
	バトルフェイズ移行 = 0
	CALL CARD_REACTION_EFFECT
	CALL RESET_FIELD_CONDITION(ターンプレイヤー,5,1)

	IF GETVAR(@"%ターンプレイヤー%_バトルスキップ") >= 1
		$INPUT_MAIN2
		SETVAR @"%ターンプレイヤー%_バトルスキップ", 0
		PRINTFORMW 『배틀 페이즈』는 스킵되었습니다。
		メインフェイズ = 1
		メインフェイズ２ = 1
		バトルフェイズ = 0
		GOTO INPUT_LOOP
	ENDIF
	PRINTFORMW 『배틀 페이즈』
	CALL CARD_TIMERLOOP("バトル開始時")
	CALL CARD_FINAL(ターンプレイヤー);バトルフェイズ移行のフリーチェック
	IF バトル強制終了 == 1
		PRINTFORMW 배틀 페이즈가 강제종료 되었습니다。
		バトル強制終了 = 0
		メインフェイズ = 1
		メインフェイズ２ = 1
		バトルフェイズ = 0
		GOTO INPUT_LOOP
	ENDIF
ELSEIF RESULT == 950
	CALL CARD_CHECK_ATK_ABLE("자신")
	IF ターンプレイヤー == "자신" && メインフェイズ２ != 1
		IF RESULT == 2
			CUSTOMDRAWLINE —
			PRINTL
			PRINTL 아직 공격하지 않은 전투광이 된 몬스터가 존재하고 있습니다。
			PRINTL
			CUSTOMDRAWLINE —
			PRINTW
			GOTO INPUT_LOOP
		ELSEIF RESULT == 573
			CUSTOMDRAWLINE —
			PRINTL
			PRINTL 아직 공격하지 않은 몬스터가 존재하고 있습니다만 턴을 종료하시겠습니까？
			PRINTL
			CUSTOMDRAWLINE —
			PRINTL [0] - 예
			PRINTL [1] - 아니오
			BINPUT
			SIF RESULT == 1
				GOTO INPUT_LOOP
		ENDIF
	ENDIF
	バトルフェイズ = 0
	メインフェイズ = 1
	メインフェイズ２ = 1
	メインフェイズ２移行 = 0
	CALL CARD_REACTION_EFFECT
	CALL RESET_FIELD_CONDITION(ターンプレイヤー,5,1)
	PRINTFORMW 『메인 페이즈２』
	CALL CARD_TIMERLOOP("バトル終了時")
	CALL CARD_FINAL(ターンプレイヤー);メインフェイズ２移行のフリーチェック


;――――――――――――ターン終了――――――――――――
ELSEIF RESULT == 1000
	CALL CARD_REACTION_EFFECT
	CALL CARD_CHECK_ATK_ABLE("자신")
	IF ターンプレイヤー == "자신" && メインフェイズ２ != 1
		IF RESULT == 2
			CUSTOMDRAWLINE —
			PRINTL
			PRINTL 아직 공격하지 않은 전투광이 된 몬스터가 존재합니다。
			PRINTL
			CUSTOMDRAWLINE —
			PRINTW
			GOTO INPUT_LOOP
		ELSEIF RESULT == 573
			CUSTOMDRAWLINE —
			PRINTL
			PRINTL 아직 공격하지 않은 몬스터가 존재하고 있습니다만 턴을 종료하시겠습니까？
			PRINTL
			CUSTOMDRAWLINE —
			PRINTL [0] - 예
			PRINTL [1] - 아니오
			BINPUT
			SIF RESULT == 1
				GOTO INPUT_LOOP
		ENDIF
	ENDIF
	
	CALL CARD_ENDPHASE
	GOTO INPUTTURN_LOOP
ELSEIF RESULT == 1100
	$INPUT_LOOP_OPCHECK
	IF OPPOSER != -1
		CALL PRINT_STATE(OPPOSER,3)
		IF 상대_接可 >= 1 || オプション_いつでも触れる == 1
			SETCOLOR C_PINK
			PRINT [69] - 상대 플레이어를 만진다
			RESETCOLOR
		ENDIF
	ELSE
		PRINTFORML 【랜덤한 플레이어】
	ENDIF
	PRINTL
	IF 상대_薬Ｃ > 0
		SETCOLOR C_FUSE_PUR
		RESULTS = 【약물카운터】:{상대_薬Ｃ}
		PRINTBUTTON @"%RESULTS,12,LEFT%", 9071
		RESETCOLOR
	ENDIF
	PRINTL
	PRINTL [100] - 돌아간다
	BINPUT
	IF RESULT == 100
		PRINTL 
	ELSEIF RESULT == 69
		CALL CARD_REACT("자신",OPPOSER,"プレイヤー")
		GOTO INPUT_LOOP_OPCHECK
	ELSEIF 9010
		CALL PRINT_STATE_DUEL(NO:OPPOSER,2,1)
		WAIT
	ELSEIF 9011
		CALL PRINT_STATE_DUEL(NO:OPPOSER,2)
		WAIT
	ELSEIF RESULT == 9071
		PRINTFORML 【드러그카운터】:{상대_薬Ｃ}
		PRINTL 약에 대한 중독도・오염도。
		PRINTL 
		PRINTW
		GOTO INPUT_LOOP_OPCHECK
	ENDIF
ELSEIF RESULT == 1101
	$INPUT_LOOP_MASCHECK
	CALL PRINT_STATE(MASTER,3)
	PRINTL
	IF 상대_薬Ｃ > 0
		SETCOLOR C_FUSE_PUR
		RESULTS = 【약물카운터】:{자신_薬Ｃ}
		PRINTBUTTON @"%RESULTS,12,LEFT%", 9071
		RESETCOLOR
	ENDIF
	PRINTL
	PRINTL [100] - 돌아간다
	BINPUT
	IF RESULT == 100
		PRINTL 
	ELSEIF RESULT == 69
		CALL CARD_REACT("자신",OPPOSER,"プレイヤー")
		GOTO INPUT_LOOP_OPCHECK
	ELSEIF RESULT == 9071
		PRINTFORML 【드러그카운터】:{상대_薬Ｃ}
		PRINTL 약에 대한 중독도・오염도。
		PRINTL 
		PRINTW
		GOTO INPUT_LOOP_MASCHECK
	ENDIF
;――――――――――――リセット処理――――――――――――
ELSEIF RESULT == 9006
	PRINTFORML 듀얼을 리셋하시겠습니까？
	PRINTL [1] - 예
	PRINTL [2] - 예（덱 선택부터）
	PRINTL [3] - 아니오
	BINPUT
	IF RESULT == 1
		PRINTFORML 강제리셋
		GOTO FULLRESET_LOOP
	ELSEIF RESULT == 2
		GOTO MYDECK_INPUT
	ENDIF
ENDIF

GOTO INPUT_LOOP