@COM312
#DIM DYNAMIC 환락강도
;키스한다
;-------------------------------------------------
;COM別FLAGの설정
;-------------------------------------------------
;ボディタッチで발기UP
IF BASE:MASTER:발기 <= 1000
	TCVAR:MASTER:사정쾌락강도 = 500
	TCVAR:MASTER:사정대상캐릭터_1 = TARGET
	TCVAR:MASTER:사정부위플래그 = 8
ENDIF


LOCAL = RAND:110
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 109
	LOCAL:1 = 109
IF LOCAL <= 9
	;10％で大成功
	TFLAG:SELECTCOM분기 = 1
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	TFLAG:SELECTCOM분기 = -1
ELSE
	;残りは成功
	TFLAG:SELECTCOM분기 = 0
ENDIF
;先制発生時は失敗しない
SIF TFLAG:COMABLE관리 == 3
	TFLAG:SELECTCOM분기 = MAX(TFLAG:SELECTCOM분기,0)


DOWNBASE:기력 += 80
DOWNBASE:MASTER:기력 += 50

;환락강도
환락강도 = 10
IF TFLAG:SELECTCOM분기 == -1
	환락강도 = 1
ELSEIF TFLAG:SELECTCOM분기 == 0
	환락강도 = 10
ELSE
	환락강도 = 20
ENDIF

SIF TALENT:MASTER:혀사용자
	CALL SOURCE_CALC_접촉(TARGET, MASTER, 50)
SIF TALENT:TARGET:혀사용자
	CALL SOURCE_CALC_접촉(MASTER, TARGET, 50)
CALL SOURCE_CALC_접촉(TARGET, MASTER, 15 * 환락강도)
CALL SOURCE_CALC_노출(TARGET, MASTER, 15 * 환락강도)
CALL SOURCE_CALC_쾌Ｍ(TARGET, MASTER, 10 * 환락강도)
CALL SOURCE_CALC_쾌Ｍ(MASTER, TARGET, 10 * 환락강도)
TIME += 10

SIF BASE:TARGET:술기운 >= 500
	CALL DRUNKEN(MASTER)

;키스상실
IF TALENT:MASTER:키스미경험
	CALL SET_FIRSTKISS(MASTER, TARGET, 1)
	EXP:TARGET:애정경험 += 10
ENDIF
IF TALENT:TARGET:키스미경험
	CALL SET_FIRSTKISS(TARGET, MASTER, 1)
	EXP:TARGET:애정경험 += 10
ENDIF

EXP:키스경험 += 1
EXP:MASTER:키스경험 += 1

IF HETEROSEX(TARGET,MASTER)
	EXP:(50 + HETEROSEX(TARGET,MASTER)) += 1
	EXP:MASTER:(50 + HETEROSEX(TARGET,MASTER)) += 1
ENDIF
IF TALENT:TARGET:스위트바디
	CALL RECOVER_PERMIL(MASTER,50,"체력",1)
	CALL RECOVER_PERMIL(MASTER,100,"기력",1)
ENDIF
RETURN 1

;-------------------------------------------------
;実行판정
;-------------------------------------------------
;키스한다
@COM_ABLE312
;키스한다実行판정
SIF !TFLAG:100
	RETURN 0
;一括관리
SIF GLOBAL_COMABLE(312)
	RETURN RESULT
;친밀감강도1
;PRINTFORML TCVAR:MASTER:친밀감({TCVAR:MASTER:친밀감})
SIF TCVAR:MASTER:친밀감 < 1
	RETURN 0
;수면中
SIF CFLAG:수면
	RETURN 0
;기력0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
;입사용중
SIF TFLAG:60 && MASTER_POSE(4,4) != TARGET
	RETURN 0
SIF GETBIT (TCVAR:26,2)
	RETURN 0
;식스나인자세
SIF TEQUIP:식스나인 >= 1
	RETURN 0
RETURN 1