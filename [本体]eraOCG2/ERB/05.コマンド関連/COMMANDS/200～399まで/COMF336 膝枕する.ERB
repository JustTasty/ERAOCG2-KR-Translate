;-------------------------------------------------
;무릎베개 받는다
;日常계커맨드、レベル2
;-------------------------------------------------
@COM336
#DIM 회복前체력
#DIM 회복前기력
#DIM 회복後체력
#DIM 회복後기력
#DIM DYNAMIC 정애강도
#DIM DYNAMIC 수동강도
#DIM DYNAMIC 노출강도
#DIM DYNAMIC 접촉강도

;무릎베개 받는다
IF TIME_PROGRESS(TFLAG:무릎베개했다) < 45
	CUSTOMDRAWLINE —
	PRINTFORMW 잠시 시간을 두지 않으면 다리가 저린다
	RETURN 0
ENDIF
;IF TALENT:MASTER:체형 < TALENT:체형 - 1
;	CUSTOMDRAWLINE —
;	PRINTFORMW 체격차이적으로 힘들다
;	RETURN -1
;ENDIF
LOCAL = ABL:친밀 * 10 + TALENT:연모 * 50 + TALENT:유치 * 30 - TALENT:수치심 * 20 - TALENT:프라이드 * 20
SIF BASE:체력 > MAXBASE:체력 * 4 / 5
	LOCAL -= 30
SIF BASE:체력 < MAXBASE:체력 * 2 / 5
	LOCAL += 30
SELECTCASE TFLAG:192
	;通常
	CASE 0
		LOCAL = MAX(LOCAL,10)
		IF RAND:LOCAL > 120
			TFLAG:SELECTCOM분기 = 1
		ELSEIF RAND:LOCAL < 30
			TFLAG:SELECTCOM분기 = -1
		ELSE
			TFLAG:SELECTCOM분기 = 0
		ENDIF
ENDSELECT


DOWNBASE:기력 += 50

DOWNBASE:MASTER:기력 += 100


;固定で획득するソース
정애강도 = 250
수동강도 = 70


;ABL:친밀をみる
IF ABL:친밀 <= 5
	정애강도 += (ABL:친밀 * 25)
ELSEIF ABL:친밀 <= 8
	정애강도 += 600 + (ABL:친밀 * 30)
	노출강도 += 200 + (ABL:욕망 * 15)
ELSEIF ABL:친밀 <= 11
	정애강도 += 800 + (ABL:친밀 * 30)
	접촉강도 += ABL:친밀 * 40
	노출강도 += 300 + (ABL:욕망 * 20)
ELSE
	정애강도 += 1000 + (ABL:친밀 * 40)
	접촉강도 += ABL:친밀 * 50
	노출강도 += 700 + (ABL:욕망 * 25)
ENDIF



수동강도 = 150 + 300 * ABL:순종

IF TFLAG:SELECTCOM분기 == -1
	TIMES 정애강도 , 0.50
	TIMES 수동강도 , 0.50
	TIMES 노출강도 , 0.40
ELSEIF TFLAG:SELECTCOM분기 == 0
	TIMES 정애강도 , 1.00
	TIMES 수동강도 , 1.00
	TIMES 노출강도 , 0.90
ELSE
	TIMES 정애강도 , 1.50
	TIMES 수동강도 , 2.00
	TIMES 노출강도 , 1.80
	TIME += 20
	DOWNBASE:체력 -= 150
	DOWNBASE:기력 -= 300
	DOWNBASE:MASTER:기력 -= 50
ENDIF

CALL SOURCE_CALC_정애(TARGET, MASTER, 정애강도)
CALL SOURCE_CALC_수동(TARGET, MASTER, 수동강도)
CALL SOURCE_CALC_노출(TARGET, MASTER, 노출강도)
CALL SOURCE_CALC_접촉(TARGET, MASTER, 접촉강도)

TIME += 20
;무릎베개플래그
TFLAG:무릎베개했다 = 1440 * DAY + TIME
RETURN 1


;-------------------------------------------------
;実行판정
;-------------------------------------------------
;무릎베개 받는다
@COM_ABLE336
;무릎베개実行판정
SIF !TFLAG:100
	RETURN 0
;一括관리
SIF GLOBAL_COMABLE(336)
	RETURN RESULT
;친밀감강도1
SIF TCVAR:MASTER:친밀감 < 1
	RETURN 0
;室内のみ
SIF CFLAG:MASTER:현재위치 > 自宅部屋最大値
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
;수면中
SIF CFLAG:수면
	RETURN 0
;기력0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
RETURN 1
