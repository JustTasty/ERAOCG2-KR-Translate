;차를 끓인다
@COM301
LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	EXP:MASTER:대화경험 ++
	TFLAG:SELECTCOM분기 = 1
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	TFLAG:SELECTCOM분기 = -1
ELSE
	;残りは成功
	TFLAG:SELECTCOM분기 = 0
ENDIF
;先制発生時は失敗しない
SIF TCVAR:TARGET:CT행동
	TFLAG:SELECTCOM분기 = MAX(TFLAG:SELECTCOM분기,0)
DOWNBASE:MASTER:기력 += 200
DOWNBASE:MASTER:체력 += 100

;その場に居るTARGET全員で回す
LOCAL:1 = TARGET
FOR LOCAL,1,CHARANUM
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):수면
		CONTINUE
	TARGET = TARGET:LOCAL
	CALL COM301_1
NEXT
TARGET = LOCAL:1

TIME += 30
RETURN 1

@COM301_1

DOWNBASE:기력 += 10

;환락강도
LOCAL:0 = 700
;정복강도
LOCAL:1 = 100

IF TFLAG:SELECTCOM분기 == -1
	TIMES LOCAL:0 , 0.10
	TIMES LOCAL:1 , 0.50
ELSEIF TFLAG:SELECTCOM분기 == 0
	TIMES LOCAL:0 , 1.00
	TIMES LOCAL:1 , 1.00
ELSE
	TIMES LOCAL:0 , 2.00
	TIMES LOCAL:1 , 2.00
ENDIF

CALL SOURCE_CALC_환락(TARGET, MASTER, LOCAL:0)
CALL SOURCE_CALC_정복(TARGET, MASTER, LOCAL:1)


;お茶を飲んだ플래그
TCVAR:300 = 1440 * DAY + TIME

;-------------------------------------------------
;実行판정
;-------------------------------------------------
;차를 끓인다
@COM_ABLE301
;차를 끓인다実行판정
SIF !TFLAG:100
	RETURN 0
;一括관리
SIF GLOBAL_COMABLE(301)
	RETURN RESULT
;二時間間隔
SIF TIME_PROGRESS(TCVAR:300) < 120
	RETURN 0
;수면中
SIF CFLAG:수면
	RETURN 0
;浴場
SIF BATHROOM(CFLAG:MASTER:현재위치)
	RETURN 0
;기력0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
RETURN 1