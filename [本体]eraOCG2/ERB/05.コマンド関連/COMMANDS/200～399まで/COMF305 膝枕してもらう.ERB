;-------------------------------------------------
;무릎베개 해준다
;日常계커맨드　レベル2
;-------------------------------------------------
@COM305
#DIM DYNAMIC 정애강도
#DIM DYNAMIC 노출강도
#DIM DYNAMIC 수동강도
#DIM DYNAMIC 접촉강도
IF TALENT:MASTER:체형 > TALENT:체형 + 2
	BASE:체력 -= 500
	BASE:기력 -= 500
	TFLAG:SELECTCOM분기 = -5
	RETURN 0
ELSE
	SELECTCASE TFLAG:192
		;通常
		CASE 0
			LOCAL = RAND:110
			LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
			SIF LOCAL:1 > 109
				LOCAL:1 = 109
			IF LOCAL <= 9
				;10％で大成功
				TFLAG:SELECTCOM분기 = 1
			ELSEIF LOCAL >= LOCAL:1
				;10～1％で失敗
				TFLAG:SELECTCOM분기 = -1
			ELSE
				;残りは成功
				TFLAG:SELECTCOM분기 = 0
			ENDIF
	ENDSELECT
	;先制発生時は失敗しない
	SIF TFLAG:COMABLE관리 == 3 && TFLAG:192 != -1
		TFLAG:SELECTCOM분기 = MAX(TFLAG:SELECTCOM분기,0)
	DOWNBASE:기력 += 100
	DOWNBASE:MASTER:기력 += 50

	;固定で획득するソース
	정애강도 = 170
	노출강도 = 55
	IF ABL:친밀 <= 1
		정애강도 += (ABL:친밀 * 20)
	ELSEIF ABL:친밀 <= 3
		정애강도 += 200 + (ABL:친밀 * 20)
	ELSEIF ABL:친밀 <= 5
		정애강도 += 400 + (ABL:친밀 * 20)
	ELSEIF ABL:친밀 <= 8
		정애강도 += 600 + (ABL:친밀 * 30)
		노출강도 += 200 + (ABL:욕망 * 10)
	ELSEIF ABL:친밀 <= 11
		정애강도 += 800 + (ABL:친밀 * 30)
		노출강도 += 300 + (ABL:욕망 * 10)
	ELSE
		정애강도 += 1800 + (ABL:친밀 * 20)
		노출강도 += 700 + (ABL:욕망 * 10)
	ENDIF

	;호감도をみる
	IF CFLAG:호감도 <= 500
		접촉강도 = CFLAG:호감도 / 5
	ELSEIF CFLAG:호감도 <= 5000
		접촉강도 = 150 + CFLAG:호감도 / 20
	ELSE
		접촉강도 = 400 + CFLAG:호감도 / 100
	ENDIF
	SIF 접촉강도 < 0
		접촉강도 = 0

	LOCAL = 0
	;호감도が低いと호감도低下
	IF CFLAG:호감도 <= 50
		LOCAL -= 3
	ELSEIF CFLAG:호감도 <= 100
		LOCAL -= 2
	ELSEIF CFLAG:호감도 <= 250
		LOCAL -= 1
	ENDIF


	수동강도 = 120 + 240 * ABL:순종

	;호감도変化
	TFLAG:97 += LOCAL

	LOCAL:1 = 100
	IF TFLAG:SELECTCOM분기 == -1
		TIMES LOCAL:0 , 0.10
		TIMES LOCAL:1 , 0.50
	ELSEIF TFLAG:SELECTCOM분기 == 0
		TIMES LOCAL:0 , 1.00
		TIMES LOCAL:1 , 1.00
	ELSE
		TIMES LOCAL:0 , 2.00
		TIMES LOCAL:1 , 2.00
	ENDIF

	CALL SOURCE_CALC_환락(TARGET, MASTER, LOCAL:0)
	CALL SOURCE_CALC_정복(TARGET, MASTER, LOCAL:1)

	CALL SOURCE_CALC_정애(TARGET, MASTER, 정애강도)
	CALL SOURCE_CALC_수동(TARGET, MASTER, 수동강도)
	CALL SOURCE_CALC_노출(TARGET, MASTER, 노출강도)
	CALL SOURCE_CALC_접촉(TARGET, MASTER, 접촉강도)

	TIME += 20
	;무릎베개플래그
	TCVAR:310 = 1440 * DAY + TIME
	RETURN 1
ENDIF



;-------------------------------------------------
;実行판정
;-------------------------------------------------
;무릎베개される
@COM_ABLE305
;무릎베개実行판정
SIF !TFLAG:100
	RETURN 0
;ターゲットの足しびれ
SIF TIME_PROGRESS(TCVAR:310) < 45
	RETURN 0
;一括관리
SIF GLOBAL_COMABLE(305)
	RETURN RESULT
;친밀감강도1
SIF TCVAR:MASTER:친밀감 < 1
	RETURN 0
;室内のみ
SIF CFLAG:MASTER:현재위치 > 自宅部屋最大値
	RETURN 0
;수면中
SIF CFLAG:수면
	RETURN 0
;기력0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
RETURN 1
