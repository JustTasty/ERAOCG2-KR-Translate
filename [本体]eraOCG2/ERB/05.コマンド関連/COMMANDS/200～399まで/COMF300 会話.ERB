@COM300
;대화

;대화누적値が閾値を超えると失敗
IF TCVAR:302 && !TALENT:연모
	PRINTFORML %CALLNAME:TARGET%에게 말을 걸려 했지만 단어가 생각나지 않는다
	PRINTW 주위에 불편한 공기가 흐른다…
	TIME += 10
	LOSEBASE:기력 += 100
	DOWNBASE:MASTER:기력 += 100
	RETURN 0
ENDIF

LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	TFLAG:SELECTCOM분기 = 1
	EXP:MASTER:대화경험 ++
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	TFLAG:SELECTCOM분기 = -1
ELSE
	;残りは成功
	TFLAG:SELECTCOM분기 = 0
ENDIF
;先制発生時は失敗しない
SIF TCVAR:TARGET:CT행동
	TFLAG:SELECTCOM분기 = MAX(TFLAG:SELECTCOM분기,0)

;환락강도
LOCAL:0 = 300
;정복강도
LOCAL:1 = 50

IF TFLAG:SELECTCOM분기 == -1
	TIMES LOCAL:0 , 0.10
	TIMES LOCAL:1 , 0.50
ELSEIF TFLAG:SELECTCOM분기 == 0
	TIMES LOCAL:0 , 1.00
	TIMES LOCAL:1 , 1.00
ELSE
	TIMES LOCAL:0 , 2.00
	TIMES LOCAL:1 , 2.00
ENDIF
SELECTCASE ABL:MASTER:화술기능
	CASE 0
		TIMES LOCAL:0 , 0.20
		TIMES LOCAL:1 , 0.20
	CASE 1
		TIMES LOCAL:0 , 0.40
		TIMES LOCAL:1 , 0.40
	CASE 2
		TIMES LOCAL:0 , 0.70
		TIMES LOCAL:1 , 0.70
	CASE 3
		TIMES LOCAL:0 , 1.00
		TIMES LOCAL:1 , 1.00
	CASE 4
		TIMES LOCAL:0 , 1.50
		TIMES LOCAL:1 , 1.50
	CASEELSE
		TIMES LOCAL:0 , 2.00
		TIMES LOCAL:1 , 2.00
ENDSELECT

DOWNBASE:기력 += 10
DOWNBASE:MASTER:기력 += 10
CALL SOURCE_CALC_환락(TARGET, MASTER, LOCAL:0)
CALL SOURCE_CALC_정복(TARGET, MASTER, LOCAL:1)


;대화누적値
TCVAR:301 += 200 / (3 + ABL:MASTER:화술기능)

TIME += 5
EXP:MASTER:대화경험 ++
RETURN 1

;-------------------------------------------------
;実行판정
;-------------------------------------------------
;대화
@COM_ABLE300
;대화実行판정
SIF !TFLAG:100
	RETURN 0
;수면中
SIF CFLAG:수면
	RETURN 0

;기력0
SIF BASE:MASTER:기력 <= 0
	RETURN 0

;エッチしている状態で喋りたい
SIF CFLAG:우후후 == 1
	RETURN 1

;一括관리
SIF GLOBAL_COMABLE(300)
	RETURN RESULT
RETURN 1